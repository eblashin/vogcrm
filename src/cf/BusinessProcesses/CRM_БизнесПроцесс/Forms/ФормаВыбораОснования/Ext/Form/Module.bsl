
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.СписокСсылок.Количество() > 0 Тогда
		
		Если Параметры.Свойство("БизнесПроцесс") Тогда
			// Добавляем в корень элемент Бизнес-Процесс
			КоллекцияЭлементов = ДеревоОснований.ПолучитьЭлементы();
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			НоваяСтрока.Объект	= Параметры.БизнесПроцесс;
			НоваяСтрока.ЭтоЭтап	= Ложь;
			НоваяСтрока.ЭтоБизнесПроцесс = Истина;
		КонецЕсли;
		
		Для Каждого ЭлементСписка ИЗ Параметры.СписокСсылок Цикл
			
			КоллекцияЭлементов = ДеревоОснований.ПолучитьЭлементы();
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			НоваяСтрока.Объект	= ЭлементСписка.Представление;
			НоваяСтрока.ЭтоЭтап	= Истина;
			
			ПодчиненнаяКоллекция = НоваяСтрока.ПолучитьЭлементы();
			Для Каждого ЭлементМассива ИЗ ЭлементСписка.Значение Цикл
				ПодчиненнаяСтрока = ПодчиненнаяКоллекция.Добавить();
				ПодчиненнаяСтрока.Объект	= ЭлементМассива;
				ПодчиненнаяСтрока.ЭтоЭтап	= Ложь;
				ПодчиненнаяСтрока.Гиперссылка = НСтр("ru = 'Открыть'")
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ИнформационнаяНадпись.Заголовок = НСтр("ru = 'Выберите документ, на основании которого будет создан документ %ИмяДокумента%'");
	Элементы.ИнформационнаяНадпись.Заголовок = СтрЗаменить(Элементы.ИнформационнаяНадпись.Заголовок, "%ИмяДокумента%", Параметры.ИмяДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОснованийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыборПроизведенИзСписка = (Элемент.ТекущийЭлемент.Имя = "ДеревоОснованийГиперссылка");
	
	Если ВыборПроизведенИзСписка И НЕ Элементы.ДеревоОснований.ТекущиеДанные.ЭтоЭтап Тогда
		ПоказатьЗначение(, Элементы.ДеревоОснований.ТекущиеДанные.Объект);
	ИначеЕсли НЕ Элементы.ДеревоОснований.ТекущиеДанные.ЭтоЭтап Тогда
		СтандартнаяОбработка = Ложь;
		Закрыть(Элементы.ДеревоОснований.ТекущиеДанные.Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОснованийВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка	= ВыборПроизведенИзСписка;
	ВыборПроизведенИзСписка	= Ложь;
	Если НЕ СтандартнаяОбработка И НЕ Элементы.ДеревоОснований.ТекущиеДанные.ЭтоЭтап Тогда
		Закрыть(Элементы.ДеревоОснований.ТекущиеДанные.Объект);
	КонецЕсли;
	
КонецПроцедуры
