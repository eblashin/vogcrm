#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Типы = ТипЗначения.Типы();
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Типы[0]);
	ТипЗначения = Новый ОписаниеТипов(МассивТипов);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Типы = ТипЗначения.Типы();
	Если Типы.Количество() > 1 Тогда
	  	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Составной тип респондента запрещен.'"),, "Объект.ТипЗначения",, Отказ)
			
	КонецЕсли;
	
	Если УказатьНаправлениеДеятельности Тогда
		ПроверяемыеРеквизиты.Добавить("НаправлениеДеятельности");
	КонецЕсли;
	
	Если ИспользоватьДополнительныеРеквизиты Тогда
		ПроверяемыеРеквизиты.Добавить("НаборДополнительныхРеквизитов");
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Заполнение служебной ТЧ "ТипыРеспондента" на основании типа значения
	ТипыРеспондента.Очистить();
	
	Типы = ТипЗначения.Типы();
	Для Инд = 1 По Типы.Количество() Цикл
		НоваяСтрока = ТипыРеспондента.Добавить();
		НоваяСтрока.ТипРеспондента = ОбщегоНазначения.ИмяТаблицыПоСсылке(Новый(Типы[Инд - 1]));
		// ++ VOG Солодов В.В. 17.06.2020 CRM-516
		ИдентификаторОбъектаМетаданных 	= ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Типы[Инд - 1]);
		ОбъектМетаданных 				= ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторОбъектаМетаданных, Ложь);
		НоваяСтрока.ПредставлениеТипа 	= ОбщегоНазначения.ПредставлениеОбъекта(ОбъектМетаданных);
		// -- VOG Солодов В.В. 17.06.2020
	КонецЦикла;		
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Флаги наличия данных
	ИспользоватьВопросы = НаборВопросов.Количество();
		
	ИспользоватьКлассификаторы = НаборКлассификаторов.Количество();
	
	Если НЕ УказатьНаправлениеДеятельности Тогда
		НаправлениеДеятельности = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьИзмененениеПринадлежности(Отказ);	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьИзмененениеПринадлежности(Отказ)
	
	Если Не ТипыРеспондента.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CRM_КлассификаторыПринадлежность.Ссылка КАК Классификатор,
		|	МАКСИМУМ(вогВариантыОпросовТипыРеспондента.Ссылка) КАК Вариант
		|ИЗ
		|	ПланВидовХарактеристик.CRM_Классификаторы.Принадлежность КАК CRM_КлассификаторыПринадлежность
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.вогВариантыОпросов.ТипыРеспондента КАК вогВариантыОпросовТипыРеспондента
		|		ПО CRM_КлассификаторыПринадлежность.ИмяТаблицы = вогВариантыОпросовТипыРеспондента.ТипРеспондента
		|			И (вогВариантыОпросовТипыРеспондента.Ссылка = &Ссылка)
		|ГДЕ
		|	CRM_КлассификаторыПринадлежность.Ссылка В
		|			(ВЫБРАТЬ
		|				вогВариантыОпросовНаборКлассификаторов.Классификатор КАК Классификатор
		|			ИЗ
		|				ПланВидовХарактеристик.вогВариантыОпросов.НаборКлассификаторов КАК вогВариантыОпросовНаборКлассификаторов
		|			ГДЕ
		|				вогВариантыОпросовНаборКлассификаторов.Ссылка = &Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	CRM_КлассификаторыПринадлежность.Ссылка
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(вогВариантыОпросовТипыРеспондента.Ссылка) ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Отказ = Истина;
		Сообщить(СтрШаблон("В списке вопросов останется классификатор %1, у которого не соответствует тип респондента. Запись невозможна.", ВыборкаДетальныеЗаписи.Классификатор));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
