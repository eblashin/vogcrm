#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Заполнение служебной ТЧ "Принадлежность" на основании типов
	Принадлежность.Очистить();
	
	Типы = ТипЗначения.Типы();
	Для Инд = 1 По Типы.Количество() Цикл
		НоваяСтрока = Принадлежность.Добавить();
		НоваяСтрока.ИмяТаблицы = Метаданные.НайтиПоТипу(Типы[Инд - 1]).ПолноеИмя();
		
	КонецЦикла;		
	
	Если ПометкаУдаления Тогда
		ИспользоватьРегламентноеЗадание = Ложь;
		
	КонецЕсли;
	
	// ++ VOG Солодов В.В. 18.07.2019 task 425
	// Обновим элементы справочника НаборыДополнительныхРеквизитовИСведений
	УправлениеСвойствами.ПередЗаписьюВидаОбъекта(ЭтотОбъект, "Справочник_CRM_ЗначенияКлассификаторов");
	// -- VOG Солодов В.В. 18.07.2019
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Удаляем регламентное задание при необходимости.
	Если ПометкаУдаления Тогда
		
		УдалитьРегламентноеЗадание(Отказ);
		
	КонецЕсли;
	
	Если СпособФормирования = Перечисления.СпособыФормированияСегментов.ФормироватьВручную Тогда
		ИспользоватьРегламентноеЗадание = Ложь;
		УдалитьРегламентноеЗадание(Отказ);
		
	КонецЕсли;
	
	// Обновляем параметр сеанса "CRM_ИспользуемаяАналитикаКлассификаторов"
	//CRM_КлассификаторыВызовСервера.ОбновитьЗначенияИспользуемыхАналитик();
	
	// Обновляем кэш платформы для зачитывания актуальных настроек
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РегламентноеЗаданиеGUID = "";
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьРегламентноеЗадание(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет удаление регламентного задания.
//
// Параметры:
//  Отказ                     - Булево - флаг отказа. Если в процессе выполнения процедуры были обнаружены ошибки,
//                                       то флаг отказа устанавливается в значение Истина.
//  РегламентноеЗаданиеОбъект - объект регламентного задания, которое необходимо удалить.
// 
Процедура УдалитьРегламентноеЗадание(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Определяем регламентное задание.
	РегламентноеЗаданиеОбъект = CRM_КлассификаторыВызовСервера.НайтиРегламентноеЗадание(РегламентноеЗаданиеGUID);
	
	Если РегламентноеЗаданиеОбъект <> Неопределено Тогда
		
		Попытка
			РегламентноеЗаданиеОбъект.Удалить();
		Исключение
			СтрокаСообщения = НСтр("ru = 'Ошибка при удалении регламентного задания: %1'");
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбменДаннымиСервер.СообщитьОбОшибке(СтрокаСообщения, Отказ);
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
