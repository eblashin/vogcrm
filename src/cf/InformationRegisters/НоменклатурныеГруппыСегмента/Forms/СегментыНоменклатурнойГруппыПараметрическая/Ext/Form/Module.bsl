
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("НоменклатурнаяГруппа") Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатурнаяГруппа = Параметры.НоменклатурнаяГруппа;
	Заголовок = Заголовок + " (" + НоменклатурнаяГруппа.Наименование + ")";
	
	ЕстьПравоНаИзменение = СегментыСервер.ЕстьПравоИзмененияСоставаСегментов("НоменклатурныеГруппы");
	Элементы.ВключитьВСегмент.Видимость  = ЕстьПравоНаИзменение;
	Элементы.УдалитьИзСегмента.Видимость = ЕстьПравоНаИзменение;
	
	ЗаполнитьТаблицуСегментов("Ручные");
	
КонецПроцедуры 

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСегменты

&НаКлиенте
Процедура СегментыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Сегменты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "СегментыСегмент" Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.Сегмент);
		
	ИначеЕсли Поле.Имя = "СегментыОтветственный" Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.Ответственный);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВСегмент(Команда)
	
	ВключитьУдалитьВСегментКлиент(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзСегмента(Команда)
	
	ВключитьУдалитьВСегментКлиент(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТаблицуСегментов("Ручные");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуСегментов(ТипТаблицы)
	
	Если ТипТаблицы = "Ручные" Тогда
		
		ИмяТаблицы = "Сегменты";
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	ЭтотОбъект[ИмяТаблицы].Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	вогСегментыНоменклатурныхГрупп.Ссылка КАК Сегмент,
	               |	ВЫБОР
	               |		КОГДА НоменклатурныеГруппыСегмента.НоменклатурнаяГруппа ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ВходитВСегмент,
	               |	вогСегментыНоменклатурныхГрупп.Ответственный КАК Ответственный
	               |ИЗ
	               |	Справочник.вогСегментыНоменклатурныхГрупп КАК вогСегментыНоменклатурныхГрупп
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеГруппыСегмента КАК НоменклатурныеГруппыСегмента
	               |		ПО (вогСегментыНоменклатурныхГрупп.Ссылка = НоменклатурныеГруппыСегмента.Сегмент)
	               |			И (НоменклатурныеГруппыСегмента.НоменклатурнаяГруппа = &НоменклатурнаяГруппа)
	               |ГДЕ
	               |	НЕ вогСегментыНоменклатурныхГрупп.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	
	ЭтотОбъект[ИмяТаблицы].Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ЗаполнитьТаблицуСегментов()

&НаКлиенте
Процедура ВключитьУдалитьВСегментКлиент(Включить)
	
	ВыделенныеСтроки = Элементы.Сегменты.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбрабатываемыхСегментов = Новый Массив;
	
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.Сегменты.ДанныеСтроки(ВыделеннаяСтрока);
		Если ДанныеСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.ВходитВСегмент = Включить Тогда
			Продолжить;
		Иначе
			МассивОбрабатываемыхСегментов.Добавить(ДанныеСтроки.Сегмент);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивОбрабатываемыхСегментов.Количество() > 0 Тогда
		
		ВключитьУдалитьВСегментСервер(МассивОбрабатываемыхСегментов, Включить);
		
	КонецЕсли;
	
КонецПроцедуры // ВключитьУдалитьВСегментКлиент()

&НаСервере
Процедура ВключитьУдалитьВСегментСервер(МассивОбрабатываемыхСегментов,Включить);
	
	Для каждого ЭлементМассива Из МассивОбрабатываемыхСегментов Цикл
		
		Если Включить Тогда
			
			СегментыСервер.ДобавитьНоменклатурнуюГруппуВСегмент(ЭлементМассива,НоменклатурнаяГруппа);
			
		Иначе
			
			СегментыСервер.УдалитьНоменклатурнуюГруппуИзСегмента(ЭлементМассива,НоменклатурнаяГруппа);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьТаблицуСегментов("Ручные");
	
КонецПроцедуры // ВключитьУдалитьВСегментСервер()

#КонецОбласти
