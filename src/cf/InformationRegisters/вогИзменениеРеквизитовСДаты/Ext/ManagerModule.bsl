#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗаписатьИзмененияРеквизитов(МассивРеквизитов) Экспорт

	//++ Бей 148 - добавлен привилегированный режим
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ЭлементМассива Из МассивРеквизитов Цикл
		
		Если ЭлементМассива.ТипПоля <> Неопределено Тогда
			
			МенеджерЗаписи = РегистрыСведений.вогИзменениеРеквизитовСДаты.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период 		= ЭлементМассива.Период;
			МенеджерЗаписи.ОбъектСвязи 	= ЭлементМассива.ОбъектСвязи;
			МенеджерЗаписи.ТипПоля 		= ЭлементМассива.ТипПоля;
			МенеджерЗаписи.Значение 	= ЭлементМассива.Значение;
			
			Попытка
				
				МенеджерЗаписи.Записать();
				
			Исключение
				
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Ошибка записи истории изменения реквизитов.'") 
					+ Символы.ПС + ТекстОшибки);
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

Функция СформироватьМассивСтруктурРеквизитовОбъекта(ОбъектИзменений, ДатаИзменения = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивРеквизитов 	= Новый Массив;
	Ссылка 				= ОбъектИзменений.Ссылка;
	ОбъектМетаданных 	= Метаданные.НайтиПоТипу(ТипЗнч(Ссылка));
	
	Если ДатаИзменения = Неопределено Тогда
		ДатаИзменения = ТекущаяДата();
	КонецЕсли;
	
	БлокируемыеРеквизиты 	= ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка).ПолучитьБлокируемыеРеквизитыОбъекта();
	РеквизитыОбъекта 		= Ссылка.Метаданные().Реквизиты;
	
	Для Каждого ИмяРеквизита Из БлокируемыеРеквизиты Цикл
		
		МетаданныеРеквизита = РеквизитыОбъекта.Найти(ИмяРеквизита);
		
		Если МетаданныеРеквизита <> Неопределено Тогда
			
			ЗначениеРеквизита = ОбъектИзменений[ИмяРеквизита];
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				
				Если Метаданные.Перечисления.вогТипыПолейКонтрагента.ЗначенияПеречисления.Найти(ИмяРеквизита) <> Неопределено Тогда
					ТипПоля = Перечисления.вогТипыПолейКонтрагента[ИмяРеквизита];
				ИначеЕсли ИмяРеквизита = "КПП" И ТипЗнч(Ссылка) = Тип("СправочникСсылка.вогТорговыеТочки") Тогда
					ТипПоля = Перечисления.вогТипыПолейКонтрагента.CRM_КПП;
				Иначе
					Продолжить;
				КонецЕсли;
				
				СтруктураЗаписи = Новый Структура;
				СтруктураЗаписи.Вставить("Период", 		ДатаИзменения);
				СтруктураЗаписи.Вставить("ОбъектСвязи", Ссылка);
				СтруктураЗаписи.Вставить("ТипПоля",		ТипПоля);
				СтруктураЗаписи.Вставить("Значение", 	ЗначениеРеквизита);
				
				МассивРеквизитов.Добавить(СтруктураЗаписи);
				
			КонецЕсли;
			
		Иначе
			
			ИмяПредопределенныхДанных = "Справочник" + ОбъектМетаданных.Имя;
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
			|ГДЕ
			|	ВидыКонтактнойИнформации.Родитель.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных
			|	И ВидыКонтактнойИнформации.Наименование = &Наименование
			|	И ВидыКонтактнойИнформации.ЭтоГруппа = ЛОЖЬ
			|	И ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ";
			
			Запрос.УстановитьПараметр("Наименование", 				ИмяРеквизита);
			Запрос.УстановитьПараметр("ИмяПредопределенныхДанных", 	ИмяПредопределенныхДанных);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				СтруктураОтбора = Новый Структура("Вид", Выборка.Ссылка);
				
				МассивКИ = ОбъектИзменений.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
				
				Если МассивКИ.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ТипПоля = Перечисления.вогТипыПолейКонтрагента.ОпределитьТипПоляКонтактнойИнформации(Выборка.Ссылка);
				
				Если ТипПоля = Неопределено Тогда					
					Продолжить;					
				КонецЕсли;
				
				СтруктураЗаписи = Новый Структура;
				СтруктураЗаписи.Вставить("Период", 		ДатаИзменения);
				СтруктураЗаписи.Вставить("ОбъектСвязи", Ссылка);
				СтруктураЗаписи.Вставить("ТипПоля",		ТипПоля);
				СтруктураЗаписи.Вставить("Значение", 	МассивКИ[0].Представление);
				
				МассивРеквизитов.Добавить(СтруктураЗаписи);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МассивРеквизитов;
	
КонецФункции

#КонецОбласти

#КонецЕсли