
&НаКлиенте
// Функция проверяет заполнение полей.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Строка	- Сообщение об ошибке.
//
Функция ПроверитьЗаполнениеПолей()
	Рез = Истина;
	Если НЕ ЗначениеЗаполнено(ПутьКФайлуМакета) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран файл макета'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуМакета");
		Рез = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВладелецМакета) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран документ/справочник для которого создаем макет'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВладелецМакета");
		Рез = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПредставлениеМакета) Тогда
		ТекстСообщения = НСтр("ru = 'Не указано представление макета'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПредставлениеМакета");
		Рез = Ложь;
	КонецЕсли;
	
	ИндексМакета = СписокМакетов.Количество() + 1;
	
	ИмяОбъектаМетаданныхМакета = ВладелецМакета + "." + "ПФ_" + ТипМакета + "_Макет" + Строка(ИндексМакета);
	НайденныйМакет = СписокМакетов.НайтиПоЗначению(ИмяОбъектаМетаданныхМакета);
	
	Если НЕ (НайденныйМакет = Неопределено) Тогда
		Ном = 1;
		Пока НЕ (НайденныйМакет = Неопределено) Цикл
			ИмяОбъектаМетаданныхМакета	= ВладелецМакета + "." + "ПФ_" + ТипМакета + "_Макет" + Строка(ИндексМакета + Ном);
			НайденныйМакет				= СписокМакетов.НайтиПоЗначению(ИмяОбъектаМетаданныхМакета);
			Ном = Ном + 1;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ПроверитьЗаполнениеПолей()

&НаКлиенте
// Процедура выполняет заполнение полей "ПредставлениеМакета" и "ИмяОбъектаМетаданныхМакета".
//
// Парамеры:
//	Нет.
//
Процедура СформироватьИмяОбъектаМетаданныхМакета()
	Если ТипМакета = "DOC" Тогда
		ПредставлениеМакета = ПредставлениеВладельца + " (Microsoft Word)";
	ИначеЕсли ТипМакета = "ODT" Тогда
		ПредставлениеМакета = ПредставлениеВладельца + " (OpenOffice.org Writer)";
	Иначе
		ПредставлениеМакета = "";
	КонецЕсли;
КонецПроцедуры // СформироватьИмяОбъектаМетаданныхМакета()

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля формы "ВладелецМакета".
//
Процедура ВладелецМакетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецМакета"			, ВладелецМакета);
	ПараметрыФормы.Вставить("ПредставлениеВладельца"	, ПредставлениеВладельца);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВладелецМакетаНачалоВыбораЗавершение", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.ПользовательскиеМакетыПечати.Форма.CRM_ФормаВыбораПринадлежности", ПараметрыФормы, Элемент,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры // ВладелецМакетаНачалоВыбора()

&НаКлиенте
Процедура ВладелецМакетаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ (Результат = Неопределено) Тогда
		ВладелецМакета			= Результат.Значение;
		ПредставлениеВладельца	= Результат.Представление;
		СформироватьИмяОбъектаМетаданныхМакета();
	КонецЕсли;
КонецПроцедуры // ВладелецМакетаНачалоВыбора()

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля формы "ПутьКФайлуМакета".
//
Процедура ПутьКФайлуМакетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКФайлуМакетаНачалоВыбораПродолжение", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

&НаКлиенте
Процедура ПутьКФайлуМакетаНачалоВыбораПродолжение(Подключено, ДополнительныеПараметры) Экспорт
	Если Подключено Тогда
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогОткрытияФайла.Фильтр             = "Файл макета (*.doc,*.odt)|*.doc;*.odt";
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите путь к файлу макета'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКФайлуМакетаНачалоВыбораЗавершение", ЭтотОбъект);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Для выбора каталога необходимо установить расширение для работы с файлами в Веб-клиенте.'"));
	КонецЕсли;
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

&НаКлиенте
Процедура ПутьКФайлуМакетаНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		ПутьКФайлуМакета	= ВыбранныеФайлы[0];
		ТипМакета			= ВРег(ВзаимодействияКлиентСервер.РасширениеФайла(ПутьКФайлуМакета));
		СформироватьИмяОбъектаМетаданныхМакета();
	КонецЕсли;
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

&НаКлиенте
// Процедура - обработчик команды формы "ОК".
//
Процедура ОК(Команда)
	Если ПроверитьЗаполнениеПолей() Тогда
		Результат = Новый Структура;
		Результат.Вставить("ВладелецМакета",				ВладелецМакета);
		Результат.Вставить("ИмяОбъектаМетаданныхМакета",	ИмяОбъектаМетаданныхМакета);
		Результат.Вставить("ПредставлениеМакета",			ПредставлениеМакета);
		Результат.Вставить("ПредставлениеВладельца",		ПредставлениеВладельца);
		Результат.Вставить("ТипМакета",						ТипМакета);
		Результат.Вставить("ПутьКФайлуМакета",				ПутьКФайлуМакета);
		Результат.Вставить("CRM_ВнешнийМакет",				Истина);
		Закрыть(Результат);
	КонецЕсли;
КонецПроцедуры // ОК()

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("СписокМакетов") Тогда
		СписокМакетов = Параметры.СписокМакетов;
	Иначе
		СписокМакетов = Новый СписокЗначений;
	КонецЕсли;	
КонецПроцедуры // ПриСозданииНаСервере()
