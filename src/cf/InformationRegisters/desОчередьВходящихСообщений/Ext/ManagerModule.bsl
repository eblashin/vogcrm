#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс


Процедура ЗарегистрироватьЗапрос(СтруктураЗапроса) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураЗапроса.Сообщение);
	
	ЗапросJSON = ЗаписьJSON.Закрыть();
	
	
	ОтключитьОбработку 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураЗапроса, "ОтключитьОбработку", Ложь);
	Идентификатор 		= Неопределено;
	
	Если Не СтруктураЗапроса.Свойство("Идентификатор" , Идентификатор) Тогда
		
		ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
		ХешированиеДанных.Добавить(ЗапросJSON);
		Идентификатор = НРег(СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", ""));
		
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдентификаторНастройки	= СтруктураЗапроса.ИдентификаторНастройки;
	МенеджерЗаписи.ИмяОчереди				= СтруктураЗапроса.ИмяОчереди;
	МенеджерЗаписи.Идентификатор 			= Идентификатор;
	МенеджерЗаписи.ДатаРегистрации 			= ТекущаяУниверсальнаяДата();
	МенеджерЗаписи.Хранилище = Новый ХранилищеЗначения(СтруктураЗапроса.Сообщение);
	
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ОтключитьОбработку Тогда 
		
		СтруктураПараметров = Новый Структура("ИдентификаторНастройки,ИмяОчереди,ИдентификаторСообщения", СтруктураЗапроса.ИдентификаторНастройки,СтруктураЗапроса.ИмяОчереди,Идентификатор);
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(СтруктураЗапроса.ИдентификаторНастройки);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтруктураЗапроса.ИмяОчереди;
		ПараметрыВыполнения.ОжидатьЗавершение	= 0;
		
		Результат = ДлительныеОперации.ВыполнитьВФоне("desОбменДаннымиВызовСервера.ОбработатьСообщениеОчереди",СтруктураПараметров,ПараметрыВыполнения);
		
		//desОбменДаннымиВызовСервера.ОбработатьСообщениеОчереди(СтруктураЗапроса.ИдентификаторНастройки,СтруктураЗапроса.ИмяОчереди,Идентификатор);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли