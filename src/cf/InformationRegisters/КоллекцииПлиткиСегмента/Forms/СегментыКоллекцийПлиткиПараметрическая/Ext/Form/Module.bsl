
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("КоллекцияПлитки") Тогда
		Возврат;
	КонецЕсли;
	
	КоллекцияПлитки = Параметры.КоллекцияПлитки;
	Заголовок = Заголовок + " (" + КоллекцияПлитки.Наименование + ")";
	
	ЕстьПравоНаИзменение = СегментыСервер.ЕстьПравоИзмененияСоставаСегментов("КоллекцияПлитки");
	Элементы.ВключитьВСегмент.Видимость  = ЕстьПравоНаИзменение;
	Элементы.УдалитьИзСегмента.Видимость = ЕстьПравоНаИзменение;
	
	ЗаполнитьТаблицуСегментов("Ручные");
	
КонецПроцедуры 

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСегменты

&НаКлиенте
Процедура СегментыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Сегменты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "СегментыСегмент" Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.Сегмент);
		
	ИначеЕсли Поле.Имя = "СегментыОтветственный" Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.Ответственный);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВСегмент(Команда)
	
	ВключитьУдалитьВСегментКлиент(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзСегмента(Команда)
	
	ВключитьУдалитьВСегментКлиент(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТаблицуСегментов("Ручные");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуСегментов(ТипТаблицы)
	
	Если ТипТаблицы = "Ручные" Тогда
		
		ИмяТаблицы = "Сегменты";
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	ЭтотОбъект[ИмяТаблицы].Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	вогСегментыКоллекцийПлитки.Ссылка КАК Сегмент,
	               |	ВЫБОР
	               |		КОГДА КоллекцииПлиткиСегмента.КоллекцияПлитки ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ВходитВСегмент,
	               |	вогСегментыКоллекцийПлитки.Ответственный КАК Ответственный
	               |ИЗ
	               |	Справочник.вогСегментыКоллекцийПлитки КАК вогСегментыКоллекцийПлитки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоллекцииПлиткиСегмента КАК КоллекцииПлиткиСегмента
	               |		ПО вогСегментыКоллекцийПлитки.Ссылка = КоллекцииПлиткиСегмента.Сегмент
	               |			И (КоллекцииПлиткиСегмента.КоллекцияПлитки = &КоллекцияПлитки)
	               |ГДЕ
	               |	НЕ вогСегментыКоллекцийПлитки.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("КоллекцияПлитки", КоллекцияПлитки);
	
	ЭтотОбъект[ИмяТаблицы].Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ЗаполнитьТаблицуСегментов()

&НаКлиенте
Процедура ВключитьУдалитьВСегментКлиент(Включить)
	
	ВыделенныеСтроки = Элементы.Сегменты.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбрабатываемыхСегментов = Новый Массив;
	
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.Сегменты.ДанныеСтроки(ВыделеннаяСтрока);
		Если ДанныеСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.ВходитВСегмент = Включить Тогда
			Продолжить;
		Иначе
			МассивОбрабатываемыхСегментов.Добавить(ДанныеСтроки.Сегмент);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивОбрабатываемыхСегментов.Количество() > 0 Тогда
		
		ВключитьУдалитьВСегментСервер(МассивОбрабатываемыхСегментов, Включить);
		
	КонецЕсли;
	
КонецПроцедуры // ВключитьУдалитьВСегментКлиент()

&НаСервере
Процедура ВключитьУдалитьВСегментСервер(МассивОбрабатываемыхСегментов,Включить);
	
	Для каждого ЭлементМассива Из МассивОбрабатываемыхСегментов Цикл
		
		Если Включить Тогда
			
			СегментыСервер.ДобавитьКоллекциюПлиткиВСегмент(ЭлементМассива,КоллекцияПлитки);
			
		Иначе
			
			СегментыСервер.УдалитьКоллекциюПлиткиИзСегмента(ЭлементМассива,КоллекцияПлитки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьТаблицуСегментов("Ручные");
	
КонецПроцедуры // ВключитьУдалитьВСегментСервер()

#КонецОбласти
