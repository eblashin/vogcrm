
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогВиджетыРабочегоСтола.Наименование КАК Наименование,
		|	вогВиджетыРабочегоСтола.Ссылка КАК Виджет,
		|	ЕСТЬNULL(вогНастройкиНачальнойСтраницы.Порядок, вогВиджетыРабочегоСтола.Порядок) КАК Порядок,
		|	ЕСТЬNULL(вогНастройкиНачальнойСтраницы.Отображать, ИСТИНА) КАК Отображать
		|ИЗ
		|	Справочник.вогВиджетыРабочегоСтола КАК вогВиджетыРабочегоСтола
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогНастройкиНачальнойСтраницы КАК вогНастройкиНачальнойСтраницы
		|		ПО (вогНастройкиНачальнойСтраницы.Виджет = вогВиджетыРабочегоСтола.Ссылка)
		|			И (вогНастройкиНачальнойСтраницы.Пользователь = &Пользователь)
		|ГДЕ
		|	НЕ вогВиджетыРабочегоСтола.Отключен
		|	И НЕ вогВиджетыРабочегоСтола.ПометкаУдаления
		|	И вогВиджетыРабочегоСтола.Предопределенный
		// +++ VOG Кулаков П.Л. 18.02.2021 DEV-177
		|	И ВЫБОР
		|			КОГДА &ВыводитьВиджетПродажи
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ вогВиджетыРабочегоСтола.Ссылка <> ЗНАЧЕНИЕ(Справочник.вогВиджетыРабочегоСтола.МоиПродажи)
		|					И вогВиджетыРабочегоСтола.Ссылка <> ЗНАЧЕНИЕ(Справочник.вогВиджетыРабочегоСтола.МоиПродажиКвартал)
		|		КОНЕЦ
		// --- VOG Кулаков П.Л.
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	Запрос.УстановитьПараметр("Пользователь",Пользователи.ТекущийПользователь());
	// +++ VOG Кулаков П.Л. 18.02.2021 DEV-177
	Запрос.УстановитьПараметр("ВыводитьВиджетПродажи",Обработки.вогНачальнаяСтраницаПользователя.ВыводитьВиджетПродажи());
	// --- VOG Кулаков П.Л.
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокВиджетов.Добавить(ВыборкаДетальныеЗаписи.Виджет,ВыборкаДетальныеЗаписи.Наименование,ВыборкаДетальныеЗаписи.Отображать);
		// +++ VOG Кулаков П.Л. 17.02.2021 DEV-179
		СписокВиджетовНачальный.Добавить(ВыборкаДетальныеЗаписи.Виджет,ВыборкаДетальныеЗаписи.Наименование,ВыборкаДетальныеЗаписи.Отображать);
		// --- VOG Кулаков П.Л.
	КонецЦикла;
	
		
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	// +++ VOG Кулаков П.Л. 17.02.2021 DEV-179
	ЗаписьВРегистрПроизведена =	ГотовоНаСервере();
	Если ЗаписьВРегистрПроизведена Тогда
		Оповестить("ОбновитьПриветственнуюИнформацию");
	КонецЕсли;
	// --- VOG Кулаков П.Л.
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ГотовоНаСервере()
	
	// +++ VOG Кулаков П.Л. 17.02.2021 DEV-179
	Если ОбщегоНазначения.КоллекцииИдентичны(СписокВиджетов,СписокВиджетовНачальный,,,Истина) Тогда
		Возврат Ложь;
	КонецЕсли;
	// --- VOG Кулаков П.Л.
	
	Порядок = 1;
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Для Каждого Строка Из СписокВиджетов Цикл
		МенеджерЗаписи = РегистрыСведений.вогНастройкиНачальнойСтраницы.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = ТекущийПользователь;
		МенеджерЗаписи.Виджет = Строка.Значение;
		МенеджерЗаписи.Отображать = Строка.Пометка;
		МенеджерЗаписи.Порядок = Порядок;
		МенеджерЗаписи.Записать();
		Порядок = Порядок + 1;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
