
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПустаяКартинка = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.вогПросмотрНедоступен);
	ВариантОбразец = ПустаяКартинка;
	
	ПрочитатьДанныеОбразца();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбразец = Новый ХранилищеЗначения(Неопределено);
	
	Если ЭтоАдресВременногоХранилища(ВариантОбразец) И Не ВариантОбразец = ПустаяКартинка Тогда
		
		ДанныеВариантОбразец = ПолучитьИзВременногоХранилища(ВариантОбразец);
		
		Если ТипЗнч(ДанныеВариантОбразец) = Тип("Картинка") Тогда
			ТекущийОбразец = Новый ХранилищеЗначения(ДанныеВариантОбразец);
		ИначеЕсли ТипЗнч(ДанныеВариантОбразец) = Тип("ДвоичныеДанные") ТОгда
			ТекущийОбразец = Новый ХранилищеЗначения(Новый Картинка(ДанныеВариантОбразец));
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущийОбъект.Образец = ТекущийОбразец;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область Образец

&НаКлиенте
Процедура ВариантОбразецНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьКартинкуЗавершение", ЭтотОбъект);
	
	ПараметрыДиалога = Новый Структура;
	ПараметрыДиалога.Вставить("Заголовок", НСтр("ru = 'Выберите файл изображения'"));
	ПараметрыДиалога.Вставить("ПредварительныйПросмотр", Истина);
	ПараметрыДиалога.Вставить("Фильтр", НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;
		|*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf'"));
	
	СтандартныеПодсистемыКлиент.ПоказатьПомещениеФайла(ОписаниеОповещения, УникальныйИдентификатор, "", ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКартинкуЗавершение(Файлы, ДополнительныеДанные) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Файлы) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(ВариантОбразец) И НЕ ВариантОбразец = ПустаяКартинка Тогда
		УдалитьИзВременногоХранилища(ВариантОбразец);
	КонецЕсли;
	ВариантОбразец = Файлы[0].Хранение;
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьДанныеОбразца()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(вогНастройкиОтображенияОтчетов.Образец, НЕОПРЕДЕЛЕНО) КАК Образец
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогНастройкиОтображенияОтчетов КАК вогНастройкиОтображенияОтчетов
	|		ПО ВариантыОтчетов.Ссылка = вогНастройкиОтображенияОтчетов.Вариант
	|ГДЕ
	|	ВариантыОтчетов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Запись.Вариант);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ТипЗнч(Выборка.Образец) = Тип("ХранилищеЗначения") Тогда
			Образец = Выборка.Образец.Получить();
			Если ТипЗнч(Образец)=Тип("Картинка") Тогда
				ВариантОбразец = ПоместитьВоВременноеХранилище(Образец, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти