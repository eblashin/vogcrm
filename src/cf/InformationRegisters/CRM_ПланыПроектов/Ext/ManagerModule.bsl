#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция СоздатьКонтрольнуюТочку(Проект, Этап, Исполнитель, Подразделение, ДатаНачала, ДатаОкончания, НаВесьДень, ЭтоКТНачала = Истина) Экспорт
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Проект",				Проект);
	Если ЗначениеЗаполнено(Этап) Тогда
		ДанныеЗаполнения.Вставить("Этап",			Этап);
	КонецЕсли;
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ДанныеЗаполнения.Вставить("Подразделение",	Подразделение);
	КонецЕсли;
	ДанныеЗаполнения.Вставить("Дата",				ДатаНачала);
	ДанныеЗаполнения.Вставить("ДатаЗакрытия",		ДатаОкончания);
	ДанныеЗаполнения.Вставить("НаВесьДень",			НаВесьДень);
	
	ДокументОбъект = Документы.CRM_КонтрольнаяТочкаПроекта.СоздатьДокумент();
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	ДокументОбъект.Дата			= ДатаНачала;
	ДокументОбъект.ДатаЗакрытия	= ДатаОкончания;
	ДокументОбъект.НаВесьДень	= НаВесьДень;
	
	Тема = Строка(Проект);
	Если ЗначениеЗаполнено(Этап) Тогда
		Тема = Тема + ?(ЗначениеЗаполнено(Тема), ", ", "") + Строка(Этап);
	КонецЕсли;
	Если ЭтоКТНачала Тогда
		Тема = НСтр("ru = 'Начало'") + " """ + Тема + """";
	Иначе
		Тема = НСтр("ru = 'Окончание'") + " """ + Тема + """";
	КонецЕсли;
	
	ДокументОбъект.Тема = Тема;
	
	ДокументОбъект.Проект = Проект;
	
	Если ЗначениеЗаполнено(Этап) Тогда
		ДокументОбъект.Этап = Этап;
	Иначе
		ДокументОбъект.Этап = Справочники.CRM_ЭтапыПроектов.ПустаяСсылка();
	КонецЕсли;
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ДокументОбъект.Подразделение = Подразделение;
	Иначе
		ДокументОбъект.Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	бИсполнительОтветственный = Истина;
	Если ЗначениеЗаполнено(Проект.Ответственный) Тогда
		НоваяСтрока = ДокументОбъект.ПользователиКТ.Добавить();
		НоваяСтрока.Пользователь	= Проект.Ответственный;
		НоваяСтрока.Ответственный	= Истина;
		
		бИсполнительОтветственный = Ложь;
	Иначе
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Исполнитель) И Исполнитель <> Проект.Ответственный Тогда
		НоваяСтрока = ДокументОбъект.ПользователиКТ.Добавить();
		НоваяСтрока.Пользователь	= Исполнитель;
		НоваяСтрока.Ответственный	= бИсполнительОтветственный;
	КонецЕсли;
	
	Попытка
		ДокументОбъект.Записать();
		Возврат ДокументОбъект.Ссылка;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		СтрОшибка = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		Возврат СтрОшибка;
	КонецПопытки;
КонецФункции

#КонецЕсли