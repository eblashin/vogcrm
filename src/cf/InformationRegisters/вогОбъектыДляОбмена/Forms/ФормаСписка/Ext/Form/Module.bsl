
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("Подключаемый_СписокПриАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_СписокПриАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеПроцедурыФункции

&НаКлиенте
Процедура Подключаемый_СписокПриАктивизацииСтроки()

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.вогСостоянияОбмена.Успешно")
		Или ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.вогСостоянияОбмена.Ошибка") Тогда
		
		ПолучитьДанныеПротоколаОбменаСервер(ТекущиеДанные.Объект, ПротоколОбменаВыгрузки, ПротоколОбменаЗагрузки);
		
	Иначе
		
		Если ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.вогСостоянияОбмена.Зарегистрирован") Тогда
			ТекстПротокола = Символы.ПС + НСтр("ru = 'Объект зарегистрирован к обмену.'");
		ИначеЕсли ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.вогСостоянияОбмена.ОшибкаПроверки") Тогда
			ТекстПротокола = Символы.ПС + НСтр("ru = 'Объект не прошел проверку заполнения.'");
		Иначе
			ТекстПротокола = "";
		КонецЕсли;
		
		ПротоколОбменаВыгрузки.УстановитьТекст(ТекстПротокола);
		ПротоколОбменаЗагрузки.УстановитьТекст(ТекстПротокола);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПолучитьДанныеПротоколаОбменаСервер(ОбъектОбмена, ПротоколОбменаВыгрузки, ПротоколОбменаЗагрузки)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогЖурналОбменаСУчетнойСистемойСрезПоследних.ХранилищеПротоколаОбмена КАК ХранилищеПротоколаОбмена,
		|	вогЖурналОбменаСУчетнойСистемойСрезПоследних.ДействиеПриОбмене КАК ДействиеПриОбмене
		|ИЗ
		|	РегистрСведений.вогЖурналОбменаСУчетнойСистемой.СрезПоследних(
		|			,
		|			Объект = &Объект) КАК вогЖурналОбменаСУчетнойСистемойСрезПоследних";
	
	Запрос.УстановитьПараметр("Объект", ОбъектОбмена);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДанные = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДанные.Следующий() Цикл
		Если ВыборкаДанные.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ВыгрузкаДанных Тогда
			ПротоколОбменаВыгрузки = ВыборкаДанные.ХранилищеПротоколаОбмена.Получить();
		ИначеЕсли ВыборкаДанные.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ЗагрузкаДанных Тогда
			ПротоколОбменаЗагрузки = ВыборкаДанные.ХранилищеПротоколаОбмена.Получить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти