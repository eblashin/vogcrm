
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЭтотОбъект.Отбор.Владелец.Значение) <> Тип("СправочникСсылка.вогТорговыеТочки") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не ЗначениеЗаполнено(Запись.ДатаОкончания) Тогда
			УстановитьПривилегированныйРежим(Истина);
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	вогМенеджерыОбъектов.Менеджер КАК Менеджер
			|ИЗ
			|	РегистрСведений.вогМенеджерыОбъектов КАК вогМенеджерыОбъектов
			|ГДЕ
			|	вогМенеджерыОбъектов.Владелец = &Владелец
			|	И вогМенеджерыОбъектов.Подразделение = &Подразделение
			|	И вогМенеджерыОбъектов.Роль = &Роль
			|	И вогМенеджерыОбъектов.НаправлениеДеятельности = &НаправлениеДеятельности
			|	И вогМенеджерыОбъектов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
			|	И вогМенеджерыОбъектов.Менеджер <> &Менеджер
			|	И НЕ вогМенеджерыОбъектов.БрендМенеджер";
			
			Запрос.УстановитьПараметр("Владелец", 					Запись.Владелец);
			Запрос.УстановитьПараметр("НаправлениеДеятельности", 	Запись.НаправлениеДеятельности);
			Запрос.УстановитьПараметр("Подразделение", 				Запись.Подразделение);
			Запрос.УстановитьПараметр("Роль", 						Запись.Роль);
			Запрос.УстановитьПараметр("Менеджер", 					Запись.Менеджер);
			
			ВыборкаДанных = Запрос.Выполнить().Выбрать();
			
			Если ВыборкаДанных.Следующий() Тогда
				
				ШаблонСообщения = НСтр("ru = 'У ""%1"" уже есть %2 %3 по направлению ""%4"".'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Запись.Владелец, НРег(Запись.Роль),ВыборкаДанных.Менеджер ,Запись.НаправлениеДеятельности);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
				
			КонецЕсли;
			//Кайдашов DEV-133 +++
			Если НЕ Запись.БрендМенеджер и не Запись.ЕстьБрендМенеджерВТТ тогда
				Запрос.Текст =
				"ВЫБРАТЬ
				|	вогМенеджерыОбъектов.Менеджер КАК Менеджер
				|ИЗ
				|	РегистрСведений.вогМенеджерыОбъектов КАК вогМенеджерыОбъектов
				|ГДЕ
				|	вогМенеджерыОбъектов.Владелец = &Владелец
				|	И вогМенеджерыОбъектов.Роль = &Роль
				|	И вогМенеджерыОбъектов.НаправлениеДеятельности = &НаправлениеДеятельности
				|	И вогМенеджерыОбъектов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
				|	И вогМенеджерыОбъектов.Менеджер <> &Менеджер
				|	И вогМенеджерыОбъектов.БрендМенеджер";
				ВыборкаДанных = Запрос.Выполнить().Выбрать();
				Если ВыборкаДанных.Следующий() Тогда
					//Есть бренд-менеджер на этой ТТ
					Запись.ЕстьБрендМенеджерВТТ = Истина;
				Иначе
					Запись.ЕстьБрендМенеджерВТТ = Ложь;
				КонецЕсли;
			Иначе
				Запрос.Текст =
				"ВЫБРАТЬ
				|	вогМенеджерыОбъектов.Менеджер КАК Менеджер,
				|	вогМенеджерыОбъектов.Владелец КАК Владелец,
				|	вогМенеджерыОбъектов.Подразделение КАК Подразделение,
				|	вогМенеджерыОбъектов.Роль КАК Роль,
				|	вогМенеджерыОбъектов.НаправлениеДеятельности КАК НаправлениеДеятельности,
				|	вогМенеджерыОбъектов.БрендМенеджер КАК БрендМенеджер,
				|	вогМенеджерыОбъектов.ДатаНачала КАК ДатаНачала,
				|	вогМенеджерыОбъектов.ДатаОкончания КАК ДатаОкончания,
				|	вогМенеджерыОбъектов.ЕстьБрендМенеджерВТТ КАК ЕстьБрендМенеджерВТТ
				|ИЗ
				|	РегистрСведений.вогМенеджерыОбъектов КАК вогМенеджерыОбъектов
				|ГДЕ
				|	вогМенеджерыОбъектов.Владелец = &Владелец
				|	И вогМенеджерыОбъектов.Роль = &Роль
				|	И вогМенеджерыОбъектов.НаправлениеДеятельности = &НаправлениеДеятельности
				|	И вогМенеджерыОбъектов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
				|	И вогМенеджерыОбъектов.Менеджер <> &Менеджер
				|	И НЕ вогМенеджерыОбъектов.БрендМенеджер";
				ВыборкаДанных = Запрос.Выполнить().Выбрать();
				Пока ВыборкаДанных.Следующий() цикл
					МЗ = РегистрыСведений.вогМенеджерыОбъектов.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МЗ,ВыборкаДанных);
					МЗ.ЕстьБрендМенеджерВТТ = Истина;
					МЗ.Записать(Истина);
				КонецЦикла;
			КонецЕсли;
			//Кайдашов DEV-133 ---
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

	
#КонецЕсли