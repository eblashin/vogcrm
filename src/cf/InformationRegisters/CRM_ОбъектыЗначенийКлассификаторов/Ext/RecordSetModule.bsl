
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для каждого Запись из ЭтотОбъект цикл
		Если Запись.ЗначениеКлассификатора.Владелец.ХранитьИсториюИзменения тогда
			Запись.ХранитьИсториюИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	
	ТаблицаНабора = ЭтотОбъект.Выгрузить();
	Если ТаблицаНабора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	
	
	
	Если Не ДополнительныеСвойства.Свойство("ПропуститьЗаписьИстории") Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаНабора", ТаблицаНабора);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТаблицаНабора.Объект КАК Объект,
			|	ТаблицаНабора.Аналитика КАК Аналитика
			|ПОМЕСТИТЬ втТаблицаНабора
			|ИЗ
			|	&ТаблицаНабора КАК ТаблицаНабора
			|ГДЕ
			|	ТаблицаНабора.ХранитьИсториюИзменения
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Объект,
			|	Аналитика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОбъектыЗначенийКлассификаторов.Объект КАК Объект,
			|	ОбъектыЗначенийКлассификаторов.Аналитика КАК Аналитика,
			|	ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
			|	ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора.ДополнительныйРеквизит КАК ДополнительныйРеквизит,
			|	ОбъектыЗначенийКлассификаторов.ЗначениеРеквизита КАК ЗначениеРеквизита,
			|	ОбъектыЗначенийКлассификаторов.ДатаВключения КАК ДатаВключения,
			|	ОбъектыЗначенийКлассификаторов.ОбъектРегистратор КАК ОбъектРегистратор
			|ИЗ
			|	РегистрСведений.CRM_ОбъектыЗначенийКлассификаторов КАК ОбъектыЗначенийКлассификаторов
			|ГДЕ
			|	(ОбъектыЗначенийКлассификаторов.Объект, ОбъектыЗначенийКлассификаторов.Аналитика) В
			|			(ВЫБРАТЬ
			|				втТаблицаНабора.Объект,
			|				втТаблицаНабора.Аналитика
			|			ИЗ
			|				втТаблицаНабора КАК втТаблицаНабора)";
		
		РезультатЗапроса = Запрос.Выполнить();
		ДополнительныеСвойства.Вставить("ТекущиеЗначения", РезультатЗапроса.Выгрузить());
				
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	УстановитьПривилегированныйРежим(Истина);
	
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ТекущиеЗначения")
	  И Не ДополнительныеСвойства.Свойство("ПропуститьЗаписьИстории") Тогда
		ТекущиеЗначения = ДополнительныеСвойства.ТекущиеЗначения;
		НовыеЗначения   = ЭтотОбъект.Выгрузить();
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("ТекущиеЗначения", ТекущиеЗначения);
		Запрос.УстановитьПараметр("НовыеЗначения"  , НовыеЗначения);
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	НовыеЗначения.Объект КАК Объект,
			|	НовыеЗначения.Аналитика КАК Аналитика,
			|	НовыеЗначения.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
			|	НовыеЗначения.ЗначениеРеквизита КАК ЗначениеРеквизита,
			|	НовыеЗначения.ДатаВключения КАК ДатаВключения,
			|	НовыеЗначения.ОбъектРегистратор КАК ОбъектРегистратор
			|ПОМЕСТИТЬ втНовыеЗначения
			|ИЗ
			|	&НовыеЗначения КАК НовыеЗначения
			|ГДЕ
			|	НовыеЗначения.ХранитьИсториюИзменения
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Объект,
			|	Аналитика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТекущиеЗначения.Объект КАК Объект,
			|	ТекущиеЗначения.Аналитика КАК Аналитика,
			|	ТекущиеЗначения.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
			|	ТекущиеЗначения.ДополнительныйРеквизит КАК ДополнительныйРеквизит,
			|	ТекущиеЗначения.ЗначениеРеквизита КАК ЗначениеРеквизита,
			|	ТекущиеЗначения.ОбъектРегистратор КАК ОбъектРегистратор
			|ПОМЕСТИТЬ втТекущиеЗначения
			|ИЗ
			|	&ТекущиеЗначения КАК ТекущиеЗначения
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Объект,
			|	Аналитика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втНовыеЗначения.Объект КАК Объект,
			|	втНовыеЗначения.Аналитика КАК Аналитика,
			|	втНовыеЗначения.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
			|	втНовыеЗначения.ЗначениеРеквизита КАК ЗначениеРеквизита,
			|	втНовыеЗначения.ДатаВключения КАК ДатаВключения,
			|	втНовыеЗначения.ОбъектРегистратор КАК ОбъектРегистратор
			|ИЗ
			|	втНовыеЗначения КАК втНовыеЗначения
			|		ЛЕВОЕ СОЕДИНЕНИЕ втТекущиеЗначения КАК втТекущиеЗначения
			|		ПО втНовыеЗначения.Объект = втТекущиеЗначения.Объект
			|			И втНовыеЗначения.Аналитика = втТекущиеЗначения.Аналитика
			|			И втНовыеЗначения.ЗначениеКлассификатора = втТекущиеЗначения.ЗначениеКлассификатора
			|ГДЕ
			|	ВЫБОР
			|			КОГДА ЕСТЬNULL(втТекущиеЗначения.ДополнительныйРеквизит, ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_ДополнительныеРеквизитыКлассификаторов.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_ДополнительныеРеквизитыКлассификаторов.ПустаяСсылка)
			|				ТОГДА втТекущиеЗначения.ЗначениеКлассификатора ЕСТЬ NULL
			|			ИНАЧЕ втНовыеЗначения.ЗначениеРеквизита <> втТекущиеЗначения.ЗначениеРеквизита
			|		КОНЕЦ";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ДатаВключения = ТекущаяДата();
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаписьИстории = РегистрыСведений.CRM_ИсторияЗначенийКлассификаторовОбъектов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, Выборка);
			
			ЗаписьИстории.Период = ?(ЗначениеЗаполнено(Выборка.ДатаВключения),Выборка.ДатаВключения,ДатаВключения);
			ЗаписьИстории.Автор = ТекущийПользователь;
			
			ЗаписьИстории.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
	//Формирование данных для отправки
	Если Не ОбменДанными.Загрузка Тогда
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли