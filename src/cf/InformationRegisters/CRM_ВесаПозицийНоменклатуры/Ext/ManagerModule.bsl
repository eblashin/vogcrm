#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция ПолучитьВесПозиции(Номенклатура, Знач Дата = Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда Возврат 0; КонецЕсли;
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВесПозиции КАК ВесПозиции
	|ИЗ
	|	РегистрСведений.CRM_ВесаПозицийНоменклатуры.Срезпоследних(&Дата, Номенклатура = &Номенклатура)
	|");
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВесПозиции;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

Процедура УстановитьВесПозиции(Номенклатура, ВесПозиции, Знач Дата = Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда Возврат; КонецЕсли;
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.CRM_ВесаПозицийНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Период = Дата;
	МенеджерЗаписи.ВесПозиции = ВесПозиции;
	МенеджерЗаписи.Записать();
КонецПроцедуры

#КонецЕсли