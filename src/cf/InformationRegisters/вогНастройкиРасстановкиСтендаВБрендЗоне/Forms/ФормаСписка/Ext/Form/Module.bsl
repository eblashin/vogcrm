
&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",Элементы.Список.ТекущиеДанные.ИмяНастройки);
		Закрыть(ПараметрыЗакрытия);
	Иначе
		Сообщить("Настройка не выбрана");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Пользователь",
		ТекущийПользователь,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	Если Параметры.Свойство("Сохранение") И Параметры.Сохранение Тогда
		ЭтаФорма.Заголовок = "Сохранение настроек расстановки артикулов на стенде";
		Элементы.Список.Заголовок = "Ранее сохраненные настройки";
		Элементы.ФормаВыбрать.Видимость = Ложь;
		Элементы.ФормаЗаписать.КнопкаПоУмолчанию = Истина;
		Сохранение = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("Выбор") И Параметры.Выбор Тогда
		ЭтаФорма.Заголовок = "Выбор настроек расстановки артикулов на стенде";
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.Список.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.НаименованиеНастройки.Видимость = Ложь;
		Выбор = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройку(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		НаименованиеПовторяется = ПроверитьНаименованиеНаДубль(НаименованиеНастройки);
		Если НаименованиеПовторяется Тогда 
			ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьНастройкуЗавершение", ЭтотОбъект), "Наименование настройки совпадает с существующей. Данные будут перезаписаны. Продолжить?",РежимДиалогаВопрос.ДаНет);
		Иначе
			ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",НаименованиеНастройки);
			Закрыть(ПараметрыЗакрытия);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",НаименованиеНастройки);
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьНаименованиеНаДубль(ИмяНастройки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки КАК ИмяНастройки
		|ИЗ
		|	РегистрСведений.вогНастройкиРасстановкиСтендаВБрендЗоне КАК вогНастройкиРасстановкиСтендаВБрендЗоне
		|ГДЕ
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.Пользователь = &Пользователь
		|	И вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки = &ИмяНастройки
		|
		|СГРУППИРОВАТЬ ПО
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки";
	
	Запрос.УстановитьПараметр("ИмяНастройки", ИмяНастройки);
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Сохранение Тогда
		НаименованиеНастройки = Элемент.ТекущиеДанные.ИмяНастройки;
	КонецЕсли;
	
	Если Выбор Тогда
		ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",Элемент.ТекущиеДанные.ИмяНастройки);
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли; 
	
КонецПроцедуры
