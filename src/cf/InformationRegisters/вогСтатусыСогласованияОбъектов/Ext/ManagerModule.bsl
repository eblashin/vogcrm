#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПроверитьСогласованиеОбъектов(ОбъектСсылка, СтруктураВозврата, СлужебнаяВыгрузка = Ложь, Отказ) Экспорт
	
	Если СлужебнаяВыгрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВыводСообщений = Истина;
	
	Если СтруктураВозврата.Свойство("ВыводСообщений")
		И ТипЗнч(СтруктураВозврата.ВыводСообщений) = Тип("Булево") Тогда
		ВыводСообщений = СтруктураВозврата.ВыводСообщений;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	вогТорговыеТочки.Ссылка КАК Ссылка,
	|	вогТорговыеТочки.Партнер КАК Партнер
	|ПОМЕСТИТЬ ВТ_ИсточникПроверки
	|ИЗ
	|	Справочник.вогТорговыеТочки КАК вогТорговыеТочки
	|ГДЕ
	|	вогТорговыеТочки.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	вогЮридическиеЛица.Ссылка,
	|	вогЮридическиеЛица.Партнер
	|ИЗ
	|	Справочник.вогЮридическиеЛица КАК вогЮридическиеЛица
	|ГДЕ
	|	вогЮридическиеЛица.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	вогРаспределительныеЦентры.Ссылка,
	|	ВЫРАЗИТЬ(вогСвязиРаспределительныхЦентров.ОбъектСвязи КАК Справочник.вогТорговыеТочки).Партнер
	|ИЗ
	|	Справочник.вогРаспределительныеЦентры КАК вогРаспределительныеЦентры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСвязиРаспределительныхЦентров КАК вогСвязиРаспределительныхЦентров
	|		ПО вогРаспределительныеЦентры.Ссылка = вогСвязиРаспределительныхЦентров.РаспределительныйЦентр
	|ГДЕ
	|	вогРаспределительныеЦентры.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпрПартнеры.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ИсточникПроверки_Партнер
	|ИЗ
	|	Справочник.Партнеры КАК СпрПартнеры
	|ГДЕ
	|	СпрПартнеры.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИсточникПроверки.Ссылка КАК ИсточникСвязи,
	|	ВЫБОР
	|		КОГДА ВТ_ИсточникПроверки.Ссылка ССЫЛКА Справочник.вогЮридическиеЛица
	|			ТОГДА вогСвязиСТТ.ОбъектСвязи
	|		КОГДА ВТ_ИсточникПроверки.Ссылка ССЫЛКА Справочник.вогТорговыеТочки
	|				И ВЫРАЗИТЬ(ВТ_ИсточникПроверки.Ссылка КАК Справочник.вогТорговыеТочки).Вид = ЗНАЧЕНИЕ(Справочник.вогВидыТорговыхТочек.ТРТД)
	|			ТОГДА СвязиЮридическихЛицТРТД.ЮридическоеЛицо
	|		КОГДА ВТ_ИсточникПроверки.Ссылка ССЫЛКА Справочник.вогТорговыеТочки
	|			ТОГДА вогСвязиСЮрЛицами.ЮридическоеЛицо
	|		КОГДА ВТ_ИсточникПроверки.Ссылка ССЫЛКА Справочник.вогРаспределительныеЦентры
	|			ТОГДА вогСвязиРаспределительныхЦентров.ОбъектСвязи
	|	КОНЕЦ КАК ОбъектСвязи,
	|	СпрПартнеры.Ссылка КАК Партнер
	|ПОМЕСТИТЬ ВТ_ОбъектыПроверки
	|ИЗ
	|	ВТ_ИсточникПроверки КАК ВТ_ИсточникПроверки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСвязиЮридическихЛиц КАК вогСвязиСТТ
	|		ПО ВТ_ИсточникПроверки.Ссылка = вогСвязиСТТ.ЮридическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСвязиЮридическихЛиц КАК вогСвязиСЮрЛицами
	|		ПО ВТ_ИсточникПроверки.Ссылка = вогСвязиСЮрЛицами.ОбъектСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК СпрПартнеры
	|		ПО ВТ_ИсточникПроверки.Партнер = СпрПартнеры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСвязиРаспределительныхЦентров КАК вогСвязиРаспределительныхЦентров
	|		ПО ВТ_ИсточникПроверки.Ссылка = вогСвязиРаспределительныхЦентров.РаспределительныйЦентр
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСвязиТТСТТДистрибутором КАК вогСвязиТТСТТДистрибутором
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.вогСвязиЮридическихЛиц КАК СвязиЮридическихЛицТРТД
	|			ПО вогСвязиТТСТТДистрибутором.ТорговаяТочкаДистрибутор = СвязиЮридическихЛицТРТД.ОбъектСвязи
	|		ПО ВТ_ИсточникПроверки.Ссылка = вогСвязиТТСТТДистрибутором.ТорговаяТочка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	вогТорговыеТочки.Ссылка,
	|	вогЮридическиеЛица.Ссылка,
	|	ВТ_ИсточникПроверки_Партнер.Ссылка
	|ИЗ
	|	ВТ_ИсточникПроверки_Партнер КАК ВТ_ИсточникПроверки_Партнер
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.вогТорговыеТочки КАК вогТорговыеТочки
	|		ПО ВТ_ИсточникПроверки_Партнер.Ссылка = вогТорговыеТочки.Партнер
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.вогЮридическиеЛица КАК вогЮридическиеЛица
	|		ПО ВТ_ИсточникПроверки_Партнер.Ссылка = вогЮридическиеЛица.Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вогСтатусыСогласованияОбъектов1.Статус КАК СтатусИсточникСвязи,
	|	ВЫБОР
	|		КОГДА ВТ_ОбъектыПроверки.ИсточникСвязи ССЫЛКА Справочник.вогТорговыеТочки
	|				И ВЫРАЗИТЬ(ВТ_ОбъектыПроверки.ИсточникСвязи КАК Справочник.вогТорговыеТочки).Вид = ЗНАЧЕНИЕ(Справочник.вогВидыТорговыхТочек.ТРТД)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Согласован)
	|		ИНАЧЕ вогСтатусыСогласованияОбъектов2.Статус
	|	КОНЕЦ КАК СтатусОбъектСвязи,
	|	вогСтатусыСогласованияОбъектов3.Статус КАК СтатусПартнер
	|ПОМЕСТИТЬ ВТ_СтатусыОбъектов
	|ИЗ
	|	ВТ_ОбъектыПроверки КАК ВТ_ОбъектыПроверки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСтатусыСогласованияОбъектов.СрезПоследних КАК вогСтатусыСогласованияОбъектов1
	|		ПО ВТ_ОбъектыПроверки.ИсточникСвязи = вогСтатусыСогласованияОбъектов1.Объект
	|			И (вогСтатусыСогласованияОбъектов1.Статус = ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Согласован)
	// VOG Ульянов И.В. 20.04.2020 CRM-568
	|				ИЛИ вогСтатусыСогласованияОбъектов1.Статус = ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Заблокирован))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСтатусыСогласованияОбъектов.СрезПоследних КАК вогСтатусыСогласованияОбъектов2
	|		ПО ВТ_ОбъектыПроверки.ОбъектСвязи = вогСтатусыСогласованияОбъектов2.Объект
	|			И (вогСтатусыСогласованияОбъектов2.Статус = ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Согласован)
	// VOG Ульянов И.В. 20.04.2020 CRM-568
	|				ИЛИ вогСтатусыСогласованияОбъектов2.Статус = ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Заблокирован))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.вогСтатусыСогласованияОбъектов.СрезПоследних КАК вогСтатусыСогласованияОбъектов3
	|		ПО ВТ_ОбъектыПроверки.Партнер = вогСтатусыСогласованияОбъектов3.Объект
	|			И (вогСтатусыСогласованияОбъектов3.Статус = ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Согласован)
	// VOG Ульянов И.В. 20.04.2020 CRM-568
	|				ИЛИ вогСтатусыСогласованияОбъектов3.Статус = ЗНАЧЕНИЕ(Перечисление.вогСтатусыСогласования.Заблокирован))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СтатусыОбъектов.СтатусИсточникСвязи ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатусИсточникСвязи,
	|	ЛОЖЬ КАК СтатусОбъектСвязи,
	|	ЛОЖЬ КАК СтатусПартнер
	|ПОМЕСТИТЬ ВТ_Проверка
	|ИЗ
	|	ВТ_СтатусыОбъектов КАК ВТ_СтатусыОбъектов
	|ГДЕ
	|	НЕ ВТ_СтатусыОбъектов.СтатусИсточникСвязи ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СтатусыОбъектов.СтатусОбъектСвязи ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_СтатусыОбъектов КАК ВТ_СтатусыОбъектов
	|ГДЕ
	|	НЕ ВТ_СтатусыОбъектов.СтатусОбъектСвязи ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СтатусыОбъектов.СтатусПартнер ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	ВТ_СтатусыОбъектов КАК ВТ_СтатусыОбъектов
	|ГДЕ
	|	НЕ ВТ_СтатусыОбъектов.СтатусПартнер ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_Проверка.СтатусИсточникСвязи), ЛОЖЬ) КАК ИсточникСвязиСогласован,
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_Проверка.СтатусОбъектСвязи), ЛОЖЬ) КАК ОбъектСвязиСогласован,
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_Проверка.СтатусПартнер), ЛОЖЬ) КАК ПартнерСогласован
	|ИЗ
	|	ВТ_Проверка КАК ВТ_Проверка";
	
	Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не Выборка.ИсточникСвязиСогласован
			Или Не Выборка.ОбъектСвязиСогласован
			Или Не Выборка.ПартнерСогласован Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Отказ Тогда
		
		СтруктураВозврата.ТекстПояснения 	= НСтр("ru = 'Согласованы не все объекты!'");
		СтруктураВозврата.Состояние 		= Перечисления.вогСостоянияОбмена.ОшибкаПроверки;
		
		Если ВыводСообщений Тогда
			ТекстСообщения = НСтр("ru = 'Согласованы не все объекты. Выгрузка прервана!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли