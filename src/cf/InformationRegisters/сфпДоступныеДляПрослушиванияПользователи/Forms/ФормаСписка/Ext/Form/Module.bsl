
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
// Функция добавляет контролируемого пользователя
//
// Параметры:
//	Пользователь				- СправочникСсылка	- Пользователь
//	КонтролируемыйПользователь	- СправочникСсылка	- Контролируемый пользователь
//	ЭтоГруппа					- Булево			- Признак
//
// Возвращаемое значение:
//	Булево	- Результат добавления
//
Функция ДобавитьКонтролируемогоПользователя(Пользователь, КонтролируемыйПользователь, ЭтоГруппа)
	НоваяЗапись = РегистрыСведений.сфпДоступныеДляПрослушиванияПользователи.СоздатьМенеджерЗаписи();
	НоваяЗапись.Пользователь 				= Пользователь;
	НоваяЗапись.КонтролируемыйПользователь	= КонтролируемыйПользователь;
	НоваяЗапись.ЭтоГруппа					= ЭтоГруппа;
	Попытка
		НоваяЗапись.Записать(Истина);
		ЗаписьДобавлена = Истина;
	Исключение
		ЗаписьДобавлена = Ложь;
	КонецПопытки;
	Возврат ЗаписьДобавлена;
КонецФункции // ДобавитьКонтролируемогоПользователя()	

&НаСервереБезКонтекста
// Функция изменяет контролируемого пользователя
//
// Параметры:
//	ЗаписьРегистра				- РегистрСведенийЗапись	- Запись регистра сведений
//	КонтролируемыйПользователь	- СправочникСсылка		- Контролируемый пользователь
//
// Возвращаемое значение:
//	Булево	- Результат изменения
//
Функция ИзменитьКонтролируемогоПользователя(ЗаписьРегистра, КонтролируемыйПользователь)
	НоваяЗапись = РегистрыСведений.сфпДоступныеДляПрослушиванияПользователи.СоздатьМенеджерЗаписи();
	НоваяЗапись.Пользователь 				= ЗаписьРегистра.Пользователь;
	НоваяЗапись.КонтролируемыйПользователь	= КонтролируемыйПользователь;
	НоваяЗапись.Прочитать();
	Если НоваяЗапись.Выбран() Тогда Возврат Ложь; КонецЕсли;
	НоваяЗапись.Пользователь 				= ЗаписьРегистра.Пользователь;
	НоваяЗапись.КонтролируемыйПользователь	= ЗаписьРегистра.КонтролируемыйПользователь;
	НоваяЗапись.Прочитать();
	Если НЕ НоваяЗапись.Выбран() Тогда Возврат Ложь; КонецЕсли;
	НоваяЗапись.КонтролируемыйПользователь = КонтролируемыйПользователь;
	Попытка
		НоваяЗапись.Записать(Истина);
		ЗаписьИзменена = Истина;
	Исключение
		ЗаписьИзменена = Ложь;
	КонецПопытки;
	Возврат ЗаписьИзменена;
КонецФункции // ИзменитьКонтролируемогоПользователя()	

&НаСервере
// Процедура устанавливает текущую запись в списке
//
// Параметры:
//	Пользователь				- СправочникСсылка	- Пользователь
//	КонтролируемыйПользователь	- СправочникСсылка	- Контролируемый пользователь
//
Процедура УстановитьТекущуюЗапись(Пользователь, КонтролируемыйПользователь)
	Элементы.Список.Обновить();
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Пользователь", Пользователь);
	СтруктураОтбора.Вставить("КонтролируемыйПользователь",  КонтролируемыйПользователь);
	КлючЗаписи = РегистрыСведений.сфпДоступныеДляПрослушиванияПользователи.СоздатьКлючЗаписи(СтруктураОтбора);
	Элементы.Список.ТекущаяСтрока = КлючЗаписи;
КонецПроцедуры // УстановитьТекущуюЗапись()

&НаКлиенте
// Процедура - обработчик выбора типа пользователя
//
// Параметры:
//	ВыбранныйТип			- СправочникСсылка	- Выбранный пользователь
//	ДополнительныеПараметры	- Структура			- Структура дополнительных параметров
//
Процедура ОбработкаВыбораТипаПользователя(ВыбранныйТип, ДополнительныеПараметры) Экспорт
	Если ВыбранныйТип = Неопределено Тогда Возврат; КонецЕсли;
	Если ВыбранныйТип.Значение Тогда
		ДополнительныеПараметры.Вставить("ЭтоГруппа", Истина);
		ОписаниеВыбора = Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяОповещения, ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Справочник.ГруппыПользователей.ФормаВыбора", ДополнительныеПараметры.ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
	Иначе
		ДополнительныеПараметры.Вставить("ЭтоГруппа", Ложь);
		ОписаниеВыбора = Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяОповещения, ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ДополнительныеПараметры.ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
	КонецЕсли;	
КонецПроцедуры // ОбработкаТипаКонтролируемогоПользователя()	

&НаКлиенте
// Процедура - обработчик выбора пользователя
//
// Параметры:
//	ВыбранныйЭлемент		- СправочникСсылка	- Выбранный пользователь
//	ДополнительныеПараметры	- Структура			- Структура дополнительных параметров
//
Процедура ОбработкаВыбораПользователя(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	Если ВыбранныйЭлемент = Неопределено Тогда Возврат; КонецЕсли;
	ЗаписьИзменена = ИзменитьКонтролируемогоПользователя(Параметры.ТекущаяСтрока, ВыбранныйЭлемент);
	Если ЗаписьИзменена Тогда
		Параметры.ТекущиеДанные.КонтролируемыйПользователь = ВыбранныйЭлемент;
		ОбновитьОтображениеДанных();
	КонецЕсли;	
КонецПроцедуры // ОбработкаВыбораПользователя()	

&НаКлиенте
// Процедура - обработчик подбора пользователя
//
// Параметры:
//	МассивПолучателей	- МассивПолучателей	- Массив пользователей
//	Параметры			- Структура			- Структура дополнительных параметров
//
Процедура ОбработкаПодбораПользователя(МассивПолучателей, ДополнительныеПараметры) Экспорт
	Если МассивПолучателей = Неопределено Тогда Возврат; КонецЕсли;
	Если НЕ ТипЗнч(МассивПолучателей) = Тип("Массив") Тогда Возврат; КонецЕсли;
	ЗаписьДобавлена = Ложь;	
	ВыбранныйЭлемент = Неопределено;
	Для Каждого СтрокаМассива Из МассивПолучателей Цикл
		Если ДобавитьКонтролируемогоПользователя(Владелец, СтрокаМассива, ДополнительныеПараметры.ЭтоГруппа) Тогда
			ЗаписьДобавлена = Истина;	
			ВыбранныйЭлемент = СтрокаМассива;
		КонецЕсли;	
	КонецЦикла;
	Если ЗаписьДобавлена Тогда
		УстановитьТекущуюЗапись(Владелец, ВыбранныйЭлемент);
	КонецЕсли;	
КонецПроцедуры // ОбработкаПодбораПользователя()	

&НаКлиенте
// Процедура - обработчик добавления пользователя
//
// Параметры:
//	ВыбранныйЭлемент		- СправочникСсылка	- Выбранный пользователь
//	ДополнительныеПараметры	- Структура			- Структура дополнительных параметров
//
Процедура ОбработкаДобавленияПользователя(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	Если ВыбранныйЭлемент = Неопределено Тогда Возврат; КонецЕсли;
	ЗаписьДобавлена = ДобавитьКонтролируемогоПользователя(Владелец, ВыбранныйЭлемент, ДополнительныеПараметры.ЭтоГруппа);
	Если ЗаписьДобавлена Тогда
		Элементы.Список.Обновить();
	КонецЕсли;	
КонецПроцедуры // ОбработкаДобавленияПользователя()	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ КОМАНД ФОРМЫ

&НаКлиенте
// Процедура - обработчик команды формы "ПодборКонтролируемыхПользователей"
//
Процедура ПодборКонтролируемыхПользователей(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",			Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",	Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",	Истина);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПараметрыФормы", ПараметрыФормы);
	ДополнительныеПараметры.Вставить("ИмяОповещения", "ОбработкаПодбораПользователя");
	ОповещениеВыбораТипа = Новый ОписаниеОповещения("ОбработкаВыбораТипаПользователя", ЭтотОбъект, ДополнительныеПараметры);
	СписокВыбораТипа = Новый СписокЗначений;
	СписокВыбораТипа.Добавить(Истина,	"Группа пользователей");
	СписокВыбораТипа.Добавить(Ложь,		"Пользователь");
	Если ИспользоватьГруппыПользователей Тогда
		СписокВыбораТипа.ПоказатьВыборЭлемента(ОповещениеВыбораТипа, "Выберите тип");
	Иначе
		ОбработкаВыбораТипаПользователя(СписокВыбораТипа[1], ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры // ПодборКонтролируемыхПользователей()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "Выбор" таблицы формы "Список"
//
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НЕ (Поле.Имя = "КонтролируемыйПользователь") Тогда Возврат; КонецЕсли;
	ТД = Элемент.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ТД.КонтролируемыйПользователь);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока",	Элемент.ТекущаяСтрока);
	ДополнительныеПараметры.Вставить("ТекущиеДанные",	ТД);
	ОписаниеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораПользователя", ЭтотОбъект, ДополнительныеПараметры);
	Если ТипЗнч(ТД.КонтролируемыйПользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
		ОткрытьФорму("Справочник.ГруппыПользователей.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
	Иначе
		ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
	КонецЕсли;	
КонецПроцедуры // СписокВыбор()

&НаКлиенте
// Процедура - обработчик события "ПередНачаломДобавления" таблицы формы "Список"
//
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	Если Копирование Тогда
		ТД = Элемент.ТекущиеДанные;
		Если ТД = Неопределено Тогда Возврат; КонецЕсли;
		ПараметрыФормы.Вставить("ТекущаяСтрока", ТД.КонтролируемыйПользователь);
		ДополнительныеПараметры = Новый Структура;
		Если ТипЗнч(ТД.КонтролируемыйПользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
			ДополнительныеПараметры.Вставить("ЭтоГруппа", Истина);
			ОписаниеВыбора = Новый ОписаниеОповещения("ОбработкаДобавленияПользователя", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Справочник.ГруппыПользователей.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
		Иначе
			ДополнительныеПараметры.Вставить("ЭтоГруппа", Ложь);
			ОписаниеВыбора = Новый ОписаниеОповещения("ОбработкаДобавленияПользователя", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
		КонецЕсли;	
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыФормы", ПараметрыФормы);
		ДополнительныеПараметры.Вставить("ИмяОповещения", "ОбработкаДобавленияПользователя");
		ОповещениеВыбораТипа = Новый ОписаниеОповещения("ОбработкаВыбораТипаПользователя", ЭтотОбъект, ДополнительныеПараметры);
		СписокВыбораТипа = Новый СписокЗначений;
		СписокВыбораТипа.Добавить(Истина,	"Группа пользователей");
		СписокВыбораТипа.Добавить(Ложь,		"Пользователь");
		Если ИспользоватьГруппыПользователей Тогда
			СписокВыбораТипа.ПоказатьВыборЭлемента(ОповещениеВыбораТипа, "Выберите тип");
		Иначе
			ОбработкаВыбораТипаПользователя(СписокВыбораТипа[1], ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // СписокПередНачаломДобавления()

&НаКлиенте
// Процедура - обработчик события "ПередНачаломИзменения" таблицы формы "Список"
//
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ТД = Элемент.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ТД.КонтролируемыйПользователь);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока",	Элемент.ТекущаяСтрока);
	ДополнительныеПараметры.Вставить("ТекущиеДанные",	ТД);
	ОписаниеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораПользователя", ЭтотОбъект, ДополнительныеПараметры);
	Если ТипЗнч(ТД.КонтролируемыйПользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
		ОткрытьФорму("Справочник.ГруппыПользователей.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
	Иначе
		ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора);
	КонецЕсли;	
КонецПроцедуры // СписокПередНачаломИзменения()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМЫ

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ сфпСофтФонПроСервер.сфпИспользоватьЗаписьПереговоров() 
		И НЕ сфпСофтФонПроСервер.сфпИспользоватьОграничениеПоказаТелефонныхЗвонков() Тогда
		Отказ = Истина;
		Возврат;
	ИначеЕсли НЕ Параметры.Свойство("Владелец") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ТолькоПросмотр	= НЕ сфпСофтФонПроСервер.сфпРолиДоступны("ПолныеПрава, ДобавлениеИзменениеПользователей");
	Владелец	= Параметры.Владелец;
	Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Пользователь");
	Отбор.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение	= Владелец;
	Отбор.Использование		= Истина;
	Отбор.РежимОтображения	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ИспользоватьГруппыПользователей	= сфпСофтФонПроСервер.сфпИспользоватьГруппыПользователей();
	Если ИспользоватьГруппыПользователей Тогда
		Элементы.КонтролируемыйПользователь.Заголовок = НСтр("ru = 'Контролируемый пользователь / группа пользователей'");
	Иначе	
		Элементы.КонтролируемыйПользователь.Заголовок = НСтр("ru = 'Контролируемый пользователь'");
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("ЭтоГруппа");
		Отбор.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение	= Ложь;
		Отбор.Использование		= Истина;
		Отбор.РежимОтображения	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;	
КонецПроцедуры // ПриСозданииНаСервере()
