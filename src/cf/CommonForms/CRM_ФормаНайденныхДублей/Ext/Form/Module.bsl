#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
Процедура ПродолжитьВводНового(Команда)
	ТД	= Элементы.ДеревоПоиска.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Закрыть(Новый Структура("Результат,Объект","Продолжить",Неопределено));
	ИначеЕсли НЕ ЗначениеЗаполнено(ТД.Объект) Тогда
		Закрыть(Новый Структура("Результат,Объект","Продолжить",Неопределено));
	ИначеЕсли НЕ ТД.Доступен Тогда
		Если ТипЗнч(ТД.Объект) = Тип("СправочникСсылка.Партнеры") Тогда
			Закрыть(Новый Структура("Результат,Объект","Продолжить",ТД.Объект));
		Иначе
			Закрыть(Новый Структура("Результат,Объект","Продолжить",Неопределено));
		КонецЕсли;	
	Иначе
		Закрыть(Новый Структура("Результат,Объект","Продолжить",ТД.Объект));
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВводНового()
	
	Закрыть(Новый Структура("Результат","Продолжить"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНайденному(Команда)
	ТД	= Элементы.ДеревоПоиска.ТекущиеДанные;
	Если НЕ (ТД = Неопределено)И ТД.Доступен Тогда
		Закрыть(Новый Структура("Результат,Объект","Перейти",ТД.Объект));
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Функция НайтиДубли(СтруктураОбъекта, СтруктураПоиска)
	МассивНайденных = CRM_КлиентыСервер.НайтиДублиПартнеров(СтруктураОбъекта, СтруктураПоиска);
	Если МассивНайденных.Количество() = 0 Тогда
		ЕстьНайденные = Ложь;	
	Иначе	
		ЕстьНайденные = Истина;
		Для Каждого СтрокаМассива Из МассивНайденных Цикл
			Отбор = Новый Структура();
			Отбор.Вставить("Объект", СтрокаМассива.Ссылка);
			НайдСтрока = _ТаблицаНайдено.НайтиСтроки(Отбор);
			Если НайдСтрока.Количество() = 0 Тогда
				Строка = _ТаблицаНайдено.Добавить();
				Строка.Объект			= СтрокаМассива.Ссылка;
				Строка.Представление	= Строка(СтрокаМассива.Ссылка);
			КонецЕсли;
			Строка = ТаблицаРасшифровкаНайдено.Добавить();
			Строка.Объект = СтрокаМассива.Ссылка;
			Строка.ПредставлениеНайденоПо = СтрЗаменить(СтрокаМассива.ИмяРеквизита, "CRM_", "") + " - " + Строка(СтрокаМассива.Реквизит);
		КонецЦикла;
		_ТаблицаНайдено.Сортировать("Объект Возр");
	КонецЕсли;
	Возврат ЕстьНайденные;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОтветственногоМенеджераКлиента(Клиент);
	УстановитьПривилегированныйРежим(Истина);
	Возврат Клиент.ОсновнойМенеджер;	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ

&НаКлиенте
Процедура ДеревоПоискаПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ДеревоПоиска.ТекущиеДанные = Неопределено Тогда
		ТекОбъект = Неопределено;
	ИначеЕсли ТекОбъект = Элементы.ДеревоПоиска.ТекущиеДанные.Объект Тогда
		Возврат;
	Иначе
		ТекОбъект = Элементы.ДеревоПоиска.ТекущиеДанные.Объект;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ДеревоПоискаПриАктивизацииСтрокиОбработчик", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПоискаПриАктивизацииСтрокиОбработчик()
	ОтветственныйМенеджерКлиента					= ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	Элементы.ОтветственныйМенеджерКлиента.Видимость	= Ложь;
	Если Элементы.ДеревоПоиска.ТекущиеДанные = Неопределено Тогда
		Элементы.ПродолжитьВводНового.Заголовок 				= НСтр("ru='Продолжить ввод нового (Ctrl+Enter)'");	
		Элементы.ПерейтиКНайденному.Доступность					= Ложь;
		Элементы.ТаблицаРасшифровкаНайдено.ОтборСтрок			= Новый ФиксированнаяСтруктура("Объект", Неопределено); 
		Элементы.ДекорацияСообщениеОбОтказеВДоступе.Видимость	= Ложь;
	ИначеЕсли НЕ ЗначениеЗаполнено(Элементы.ДеревоПоиска.ТекущиеДанные.Объект) Тогда
		Элементы.ПродолжитьВводНового.Заголовок					= НСтр("ru='Продолжить ввод нового (Ctrl+Enter)'");		
		Элементы.ПерейтиКНайденному.Доступность					= Ложь;
		Элементы.ТаблицаРасшифровкаНайдено.ОтборСтрок			= Новый ФиксированнаяСтруктура("Объект", ТекОбъект); 
		Элементы.ДекорацияСообщениеОбОтказеВДоступе.Видимость	= Ложь;
	ИначеЕсли НЕ Элементы.ДеревоПоиска.ТекущиеДанные.Доступен Тогда
		Если ТипЗнч(Элементы.ДеревоПоиска.ТекущиеДанные.Объект) = Тип("СправочникСсылка.Партнеры") Тогда
			Элементы.ПродолжитьВводНового.Заголовок				= НСтр("ru='Зарегистрировать новый контакт клиента (Ctrl+Enter)'");
		Иначе	
			Элементы.ПродолжитьВводНового.Заголовок				= НСтр("ru='Продолжить ввод нового (Ctrl+Enter)'");		
		КонецЕсли;	
		Элементы.ПерейтиКНайденному.Доступность					= Ложь;
		Элементы.ТаблицаРасшифровкаНайдено.ОтборСтрок			= Новый ФиксированнаяСтруктура("Объект", ТекОбъект); 
		Элементы.ДекорацияСообщениеОбОтказеВДоступе.Видимость	= Ложь;
	Иначе	
		Если ТипЗнч(Элементы.ДеревоПоиска.ТекущиеДанные.Объект) = Тип("СправочникСсылка.Партнеры") Тогда
			Элементы.ПродолжитьВводНового.Заголовок			= НСтр("ru='Зарегистрировать новый контакт клиента (Ctrl+Enter)'");	
			ОтветственныйМенеджерКлиента					= ПолучитьОтветственногоМенеджераКлиента(ТекОбъект);
			Элементы.ОтветственныйМенеджерКлиента.Видимость	= Истина;
		Иначе
			Элементы.ПродолжитьВводНового.Заголовок			= НСтр("ru='Дополнить информацию о контакте (Ctrl+Enter)'");	
		КонецЕсли; 
		Элементы.ПерейтиКНайденному.Доступность					= Элементы.ДеревоПоиска.ТекущиеДанные.Доступен;
		Элементы.ТаблицаРасшифровкаНайдено.ОтборСтрок			= Новый ФиксированнаяСтруктура("Объект", ТекОбъект); 
		Элементы.ДекорацияСообщениеОбОтказеВДоступе.Видимость	= НЕ Элементы.ДеревоПоиска.ТекущиеДанные.Доступен;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПоискаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Если Элемент.ТекущиеДанные.Доступен Тогда
			ПоказатьЗначение(, Элемент.ТекущиеДанные.Объект);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("СтруктураПоиска") Тогда
		ДублиНайдены = НайтиДубли(Параметры.СтруктураОбъекта, Параметры.СтруктураПоиска);
		Если ДублиНайдены Тогда
			ВеткаКорень = ДеревоПоиска.ПолучитьЭлементы();
			ВеткаКорень.Очистить();
			НоваяСтрока					= ВеткаКорень.Добавить();
			НоваяСтрока.Представление	= НСтр("ru = 'Создать: Клиента / Контакт'");
			НоваяСтрока.Объект			= Справочники.Партнеры.ПустаяСсылка();
			Для Каждого СтрокаНайденого Из _ТаблицаНайдено Цикл
				Если ТипЗнч(СтрокаНайденого.Объект) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
					// Выполняем поиск партнера-владельца контактного лица
					ВеткаВладелец	= Неопределено;
					Для Каждого СтрокаВетки Из ВеткаКорень Цикл
						Если СтрокаВетки.Объект = СтрокаНайденого.Объект.Владелец Тогда
							ВеткаВладелец	= СтрокаВетки;  							
							Прервать;
					    КонецЕсли;
					КонецЦикла;
					Если ВеткаВладелец = Неопределено Тогда
						ВеткаВладелец				= ВеткаКорень.Добавить();
						ВеткаВладелец.Объект		= СтрокаНайденого.Объект.Владелец;
						ВеткаВладелец.Представление	= СтрокаНайденого.Объект.Владелец.Наименование;
						ВеткаВладелец.Доступен		=  Ложь;
					КонецЕсли;
					Строка				= ВеткаВладелец.ПолучитьЭлементы().Добавить();
				Иначе
					Строка				= ВеткаКорень.Добавить();
				КонецЕсли;	
				Строка.Объект			= СтрокаНайденого.Объект;
				Строка.Представление	= СтрокаНайденого.Представление;
				Попытка
					НаименованиеКлиента = СтрокаНайденого.Объект.Наименование;
					Строка.Доступен		=  Истина;
				Исключение
					Строка.Доступен		=  Ложь;
				КонецПопытки;
			КонецЦикла;
			
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("ПотенциальныйКлиент") Тогда
		Элементы.ПерейтиКНайденному.Заголовок	= НСтр("ru='Выбрать контакт'");
	КонецЕсли;	
	Если НЕ ПравоДоступа("Добавление", Метаданные.Справочники.Партнеры) Тогда
		Элементы.ПродолжитьВводНового.Видимость = Ложь;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ДублиНайдены Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПродолжитьВводНового", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти