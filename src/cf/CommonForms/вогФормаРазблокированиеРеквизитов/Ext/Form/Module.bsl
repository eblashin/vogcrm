
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Ссылка")
		И Параметры.Свойство("СоответствиеРеквизитовКИ") Тогда
		
		ДобавитьРеквизитыСервер(Параметры.Ссылка, Параметры.СоответствиеРеквизитовКИ);	
		
		ДатаРедактирования = ТекущаяДата();
		
		Текст = НСтр("ru = 'Реквизиты не рекомендуется изменять, если этот объект уже выбран в документах.'");
		Элементы.ДекорацияПредупреждение.Заголовок = Текст;
		
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)
	
	Если ДатаРедактирования = '00010101' Тогда
		
		ТекстСообщения = НСтр("ru = 'Не заполнена дата!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	// Для всех включенных флажков сформируем массив соответствующих им реквизитов
	МассивРеквизитов = Новый Массив;
	
	Для Каждого Реквизит Из ИменаРеквизитовФормы Цикл
		
		Если ЭтаФорма[Реквизит.Значение] Тогда
			МассивРеквизитов.Добавить(Реквизит.Представление);
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаРедактирования", 	ДатаРедактирования);
	Результат.Вставить("МассивРеквизитов", 		МассивРеквизитов);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ДобавитьРеквизитыСервер(Ссылка, СоответствиеРеквизитовКИ)

	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
	
	Если Менеджер <> Неопределено Тогда
		
		ОбъектМетаданных 			= Метаданные.НайтиПоТипу(ТипЗнч(Ссылка));
		БлокируемыеРеквизитыОбъекта = Менеджер.ПолучитьБлокируемыеРеквизитыОбъекта();
		ДобавляемыеРеквизиты 		= Новый Массив;
		ПрефиксИмениРеквизита 		= "РазрешитьРедактированиеРеквизита";
		
		Для Каждого Реквизит Из БлокируемыеРеквизитыОбъекта Цикл
			
			ПредопределенныйКИ 	= ПолучитьСсылкуКонтактнаяИнформация(Реквизит, ОбъектМетаданных.Имя);
			РеквизитКИ 			= СоответствиеРеквизитовКИ.Получить(ПредопределенныйКИ);
			
			Если РеквизитКИ <> Неопределено Тогда
				
				ИмяРеквизита 		= ПрефиксИмениРеквизита + РеквизитКИ;
				ЗаголовокРеквизита 	= ИмяВЗаголовок(ПрефиксИмениРеквизита) + " " + ИмяВЗаголовок(ПредопределенныйКИ.Наименование);
				
			Иначе
				
				МетаданныеРеквизита = ОбъектМетаданных.Реквизиты.Найти(Реквизит);
				
				Если МетаданныеРеквизита = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ИмяРеквизита 		= ПрефиксИмениРеквизита + Реквизит;
				ЗаголовокРеквизита 	= ИмяВЗаголовок(ПрефиксИмениРеквизита) + " " + ИмяВЗаголовок(МетаданныеРеквизита.Синоним);
				
			КонецЕсли;
			
			ТипыРеквизита = Новый Массив;
			ТипыРеквизита.Добавить(Тип("Булево"));
			
			ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);
			
			НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита,
											ОписаниеТиповДляРеквизита,
											,
											ЗаголовокРеквизита,
											Ложь);								
			
			ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
			ИменаРеквизитовФормы.Добавить(ИмяРеквизита, Сред(ИмяРеквизита, СтрДлина(ПрефиксИмениРеквизита) + 1));
			
		КонецЦикла;
		
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
		Для Каждого Реквизит Из ДобавляемыеРеквизиты Цикл
			
			НовыйЭлемент 					= Элементы.Добавить(Реквизит.Имя, Тип("ПолеФормы"), Элементы.ГруппаДобавить);
			НовыйЭлемент.ПутьКДанным 		= Реквизит.Имя;
			НовыйЭлемент.Вид 				= ВидПоляФормы.ПолеФлажка;
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			
			ЭтаФорма[Реквизит.Имя] 			= Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИмяВЗаголовок(Имя, Ответ = "", Стэйт = 7, б = "")
	
	Для ё = 1 По СтрДлина(Имя) Цикл
        а = Сред(Имя, ё, 1);
        Стэйт = (НРег(а) <> а) * 4 + Цел(Стэйт / 2);
        Ответ = Ответ + ?(Стэйт = 2, НРег(б), ?(ё = 2, ВРег(б), б)) + ?(Стэйт = 4 Или Стэйт = 5, " ", "");
        б = а;
    КонецЦикла;
	
	Возврат Ответ + б;
	
КонецФункции

&НаСервере
Функция ПолучитьСсылкуКонтактнаяИнформация(Реквизит, ИмяОбъектаМетаданных)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Родитель.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных
	|	И ВидыКонтактнойИнформации.Наименование = &Наименование
	|	И ВидыКонтактнойИнформации.ЭтоГруппа = ЛОЖЬ
	|	И ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Наименование", 				Реквизит);
	Запрос.УстановитьПараметр("ИмяПредопределенныхДанных", 	"Справочник" + ИмяОбъектаМетаданных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#КонецОбласти