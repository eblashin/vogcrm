
&НаКлиенте
Процедура Отменить(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Предмет") И ЗначениеЗаполнено(Параметры.Предмет) Тогда
		ЗаполнитьТаблицуЗадач(Параметры.Предмет);	
	Иначе
		Сообщить("Не указан предмет. Форма будет закрыта");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗадач(Предмет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогИсполнителиРолейИДелегаты.РольПользователь КАК РольПользователь
		|ПОМЕСТИТЬ вт_Делегирование
		|ИЗ
		|	РегистрСведений.вогИсполнителиРолейИДелегаты КАК вогИсполнителиРолейИДелегаты
		|ГДЕ
		|	вогИсполнителиРолейИДелегаты.ИсполнительДелегат = &Исполнитель
		|	И вогИсполнителиРолейИДелегаты.ИмяОбластиДелегирования В ("""", ""ПроцессыИЗадачи"")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.Ссылка КАК Задача,
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.Наименование КАК Наименование,
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.Дата КАК ДатаЗадачи,
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.БизнесПроцесс КАК БизнесПроцесс,
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.Выполнена КАК Выполнена,
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.БизнесПроцесс.Номер КАК Номер,
		|	ЗадачаИсполнителяЗадачиПоИсполнителю.БизнесПроцесс.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ЗадачаИсполнителяЗадачиПоИсполнителю.БизнесПроцесс.КартаМаршрута) КАК КартаМаршрута
		|ИЗ
		// ++ VOG Солодов В.В. 24.06.2021 DEV-249
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителяЗадачиПоИсполнителю
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.CRM_БизнесПроцесс КАК ТаблицаПроцессы
		|		ПО ЗадачаИсполнителяЗадачиПоИсполнителю.БизнесПроцесс = ТаблицаПроцессы.Ссылка
		|ГДЕ
		|	ТаблицаПроцессы.Предмет = &Предмет
		// До изменения
		//|	Задача.ЗадачаИсполнителя.ЗадачиПоИсполнителю(
		//|			,
		//|			(Исполнитель = &Исполнитель
		//|				ИЛИ РольИсполнителя В (&РолиПользователя)
		//|				ИЛИ Исполнитель В
		//|					(ВЫБРАТЬ
		//|						вт_Делегирование.РольПользователь КАК РольПользователь
		//|					ИЗ
		//|						вт_Делегирование КАК вт_Делегирование))
		//|				И БизнесПроцесс.Предмет = &Предмет) КАК ЗадачаИсполнителяЗадачиПоИсполнителю
		//|ГДЕ
		//|	ЗадачаИсполнителяЗадачиПоИсполнителю.БизнесПроцесс.Предмет = &Предмет
		// -- VOG Солодов В.В. 24.06.2021 DEV-249
		|	И (ЗадачаИсполнителяЗадачиПоИсполнителю.Исполнитель = &Исполнитель
		|			ИЛИ ЗадачаИсполнителяЗадачиПоИсполнителю.РольИсполнителя В (&РолиПользователя)
		|			ИЛИ ЗадачаИсполнителяЗадачиПоИсполнителю.Исполнитель В
		|				(ВЫБРАТЬ
		|					вт_Делегирование.РольПользователь КАК РольПользователь
		|				ИЗ
		|					вт_Делегирование КАК вт_Делегирование))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаЗадачи УБЫВ
		|ИТОГИ ПО
		|	БизнесПроцесс";
	
	Запрос.УстановитьПараметр("Исполнитель", Пользователи.ТекущийПользователь());
	РолиПользователя = CRM_БизнесПроцессыСервер.ПолучитьРолиПоПользователю(ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("РолиПользователя", РолиПользователя);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаБизнесПроцесс = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаБизнесПроцесс.Следующий() Цикл
	
		ВыборкаДетальныеЗаписи = ВыборкаБизнесПроцесс.Выбрать();
	
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			
			НоваяСтрока = СписокЗадач.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаДетальныеЗаписи);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗадачу(Команда)
	
	ТекущаяСтрока = Элементы.СписокЗадач.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	Иначе
		ОткрытьЗадачу(ТекущаяСтрока.Задача);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗадачу(СписокЗадач[ВыбраннаяСтрока].Задача);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗадачу(ВыбраннаяЗадача)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ",ВыбраннаяЗадача);
	ПараметрыФормы.Вставить("НеОткрыватьПредмет",Истина);
	ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаЗадачиНезависимыйПроцесс",
				ПараметрыФормы,
				ЭтаФорма,
				,
				,
				, Новый ОписаниеОповещения("ОткрытьЗадачуЗавершение", ЭтотОбъект),  
				РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗадачуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры



			
