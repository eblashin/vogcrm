
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
// Процедура заполняет таблицу по массиву номеров
//
// Параметры:
//	МассивНомеров	- Массив	- Массив структур с данными номеров
//
Процедура ЗаполнитьТаблицуПоМассивуНомеров(МассивНомеров)
	Для Каждого ЭлементМассива Из МассивНомеров Цикл
		Если ПустаяСтрока(ЭлементМассива.Number) Тогда Продолжить КонецЕсли;
		ОчищенныйНомер = сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(ЭлементМассива.Number);
		Если СтрДлина(ОчищенныйНомер) = СтрДлина(ЭлементМассива.Number) Тогда
			НоваяСтрока					= ТаблицаТелефонныеНомера.Добавить();
			НоваяСтрока.Состояние		= ЭлементМассива.LineState - 1;
			НоваяСтрока.ИмяЛинии		= ЭлементМассива.LineName;
			НоваяСтрока.НомерТелефона	= ЭлементМассива.Number;
		КонецЕсли;
	КонецЦикла;
	Если НЕ ПустаяСтрока(ТекущийНомер) Тогда
		НайденныеСтроки = ТаблицаТелефонныеНомера.НайтиСтроки(Новый Структура("НомерТелефона", ТекущийНомер));	
		Если НайденныеСтроки.Количество() = 1 Тогда
			Элементы.ТаблицаТелефонныеНомера.ТекущаяСтрока = ТаблицаТелефонныеНомера.Индекс(НайденныеСтроки[0]);
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры // ЗаполнитьТаблицуПоМассивуНомеров()

&НаКлиенте
// Процедура оповещеает о выборе номера и закрывает форму
//
// Параметры:
//	Нет.
//
Процедура ОповеститьОВыбореИЗакрыть()
	ТД = Элементы.ТаблицаТелефонныеНомера.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;	
	Если НЕ (ТД.НомерТелефона = ТекущийНомер) Тогда
		Оповестить("сфпВыбранТелефонныйНомерПользователя", ТД.НомерТелефона);
	КонецЕсли;	
	Закрыть();
КонецПроцедуры // ОповеститьОВыбореИЗакрыть()	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ОБРАБОТЧИКИ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "Выбор" таблицы формы "ТаблицаТелефонныеНомера"
//
Процедура ТаблицаТелефонныеНомераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыбореИЗакрыть();
КонецПроцедуры // ТаблицаТелефонныеНомераВыбор()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
// Процедура - обработчик команды формы "ОК"
//
Процедура ОК(Команда)
	ОповеститьОВыбореИЗакрыть();
КонецПроцедуры // ОК()

&НаКлиенте
// Процедура - обработчик команды формы "Отмена"
//
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры // Отмена()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события фомы "ПриСозданииНаСервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ТекущийНомер") Тогда
		ТекущийНомер = Параметры.ТекущийНомер;
	КонецЕсли;	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события фомы "ПриОткрытии"
//
Процедура ПриОткрытии(Отказ)
	Если сфпСофтФонПроКлиент.сфпПроверитьДоступностьСофтФон(Истина) Тогда
		сфпСофтФонПроКлиент.сфпПолучитьСостоянияЛиний();
	КонецЕсли;	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
// Процедура - обработчик события фомы "ОбработкаОповещения"
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "СофтФон_OnAllLinesInfo" Тогда
		ЗаполнитьТаблицуПоМассивуНомеров(Параметр);
	КонецЕсли;
КонецПроцедуры // ОбработкаОповещения()
