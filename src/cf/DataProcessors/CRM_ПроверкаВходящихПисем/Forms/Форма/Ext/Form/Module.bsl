
&НаСервереБезКонтекста
Функция ЗаменитьНедопустимыеСимволыXML(Знач Текст, СимволЗамены = " ")
	
	ПозицияНачала = 1;
	Пока Истина Цикл
		Позиция = НайтиНедопустимыеСимволыXML(Текст, ПозицияНачала);
		Если Позиция = 0 Тогда
			Прервать;
		КонецЕсли;
		
		// Если возращается позиция, больше чем должна быть, то корректируем ее.
		Если Позиция > 1 Тогда
			НедопустимыйСимвол = Сред(Текст, Позиция - 1, 1);
			Если НайтиНедопустимыеСимволыXML(НедопустимыйСимвол) > 0 Тогда
				Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
			КонецЕсли;
		КонецЕсли;
		НедопустимыйСимвол = Сред(Текст, Позиция, 1);
		Если НайтиНедопустимыеСимволыXML(НедопустимыйСимвол) > 0 Тогда
			Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
		КонецЕсли;
		ПозицияНачала = Позиция + 1;
	КонецЦикла;

	Возврат Текст;
КонецФункции

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	// Редактирование текущего периода.
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = ИнтервалДат;
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьИнтервалЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период<>Неопределено Тогда
		ИнтервалДат = Период;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьПисьма()
	
	Попытка
		ТекстСообщения = "";
		ВыборкаПисем = Документы.ЭлектронноеПисьмоВходящее.Выбрать(ИнтервалДат.ДатаНачала, КонецДня(ИнтервалДат.ДатаОкончания));
		Пока ВыборкаПисем.Следующий() Цикл
			Попытка
				ПисьмоОбъект = ВыборкаПисем.ПолучитьОбъект();
				
				ЕстьИзменения = Ложь;
				Тема						= ЗаменитьНедопустимыеСимволыXML(ПисьмоОбъект.Тема, "_");
				Текст						= ЗаменитьНедопустимыеСимволыXML(ПисьмоОбъект.Текст, "_");
				СписокПолучателейПисьма		= ЗаменитьНедопустимыеСимволыXML(ПисьмоОбъект.СписокПолучателейПисьма, "_");
				СписокПолучателейКопий		= ЗаменитьНедопустимыеСимволыXML(ПисьмоОбъект.СписокПолучателейКопий, "_");
				ОтправительПредставление    = ЗаменитьНедопустимыеСимволыXML(ПисьмоОбъект.ОтправительПредставление, "_");
								
				Если НЕ Тема = ПисьмоОбъект.Тема Тогда
					ТекстСообщения_1 = Символы.ПС + НСтр("ru = 'Скорректирована тема письма.'");
					ПисьмоОбъект.Тема = Тема;
					ЕстьИзменения = Истина;
				Иначе
					ТекстСообщения_1 = "";
				КонецЕсли;
				
				Если НЕ Текст = ПисьмоОбъект.Текст Тогда
					ТекстСообщения_2 = Символы.ПС + НСтр("ru = 'Скорректирован текст письма.'");
					ПисьмоОбъект.Текст = Текст;
					ЕстьИзменения = Истина;
				Иначе
					ТекстСообщения_2 = "";
				КонецЕсли;
				
				Если НЕ СписокПолучателейПисьма = ПисьмоОбъект.СписокПолучателейПисьма Тогда
					ТекстСообщения_3 = Символы.ПС + НСтр("ru = 'Скорректирован список получателей письма.'");
					ПисьмоОбъект.СписокПолучателейПисьма = СписокПолучателейПисьма;
					ЕстьИзменения = Истина;
				Иначе
					ТекстСообщения_3 = "";
				КонецЕсли;
				
				Если НЕ СписокПолучателейКопий = ПисьмоОбъект.СписокПолучателейКопий Тогда
					ТекстСообщения_4 = Символы.ПС + НСтр("ru = 'Скорректирован список получателей копий письма.'");
					ПисьмоОбъект.СписокПолучателейКопий = СписокПолучателейКопий;
					ЕстьИзменения = Истина;
				Иначе
					ТекстСообщения_4 = "";
				КонецЕсли;
				
				Если НЕ ОтправительПредставление = ПисьмоОбъект.ОтправительПредставление Тогда
					ТекстСообщения_5 = Символы.ПС + НСтр("ru = 'Скорректировано представление отправителя.'");
					ПисьмоОбъект.ОтправительПредставление = ОтправительПредставление;
					ЕстьИзменения = Истина;
				Иначе
					ТекстСообщения_5 = "";
				КонецЕсли;				
				
				Если ЕстьИзменения Тогда
					ПисьмоОбъект.ОбменДанными.Загрузка = Истина;
					ПисьмоОбъект.Записать();
					ТекстСообщения = ТекстСообщения + Символы.ПС + Строка(ПисьмоОбъект.Ссылка)
													+ ТекстСообщения_1
													+ ТекстСообщения_2
													+ ТекстСообщения_3
													+ ТекстСообщения_4
													+ ТекстСообщения_5;													
				КонецЕсли;
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Продолжить;
			КонецПопытки;
		КонецЦикла;
		
		Если НЕ ТекстСообщения = "" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеребратьПисьма(Команда)
	
	ВыбратьПисьма();
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Обработка завершена'"));
	
КонецПроцедуры
