&НаКлиенте
Процедура УстановитьПунктМеню()
	Если Элементы.МенюHTML.Документ <> Неопределено Тогда
		Если Элементы.МенюHTML.Документ.readyState = "complete" Тогда
			вогНачальнаяСтраницаПользователяКлиетСервер.НачальнаяСтраницаПользователяПриОткрытии(ЭтаФорма,"обои");
			//ОтключитьОбработчикОжидания("УстановитьПунктМеню");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_МенюHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ДанныеСобытия.Element <> Неопределено Тогда
		вогНачальнаяСтраницаПользователяКлиетСервер.МенюHTMLПриНажатии(ЭтаФорма,"обои");
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	вогНачальнаяСтраницаПользователяКлиетСервер.НачальнаяСтраницаПользователяПриСозданииНаСервере(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("УстановитьПунктМеню",0.1,Истина);
КонецПроцедуры


&НаКлиенте
Процедура ИмпортНоменклатурыПалитра(Команда)
	ОткрытьФорму("Обработка.ЗагрузкаНоменклатурыПалитра.Форма.Форма");
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеНоменклатурыПалитра(Команда) // VOG Ульянов И.В. CRM-1030
	ОткрытьФорму("Обработка.ОбновлениеНоменклатурыПалитра.Форма.Форма");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокОбоиПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НоменклатураКоллекции.КоллекцияПроизводителя КАК КоллекцияПроизводителя,
		|	НоменклатураКоллекции.Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура.Коллекции КАК НоменклатураКоллекции
		|ГДЕ
		|	НоменклатураКоллекции.Ссылка В(&СписокНоменклатуры)
		|ИТОГИ ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", Строки.ПолучитьКлючи());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
	
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		
		СписокКоллекций = "";
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СписокКоллекций = СписокКоллекций + ?(СписокКоллекций = "", "", "; ") + ВыборкаДетальныеЗаписи.КоллекцияПроизводителя;
		КонецЦикла;
		
		Строка = Строки[ВыборкаНоменклатура.Номенклатура];
		Строка.Данные["КоллекцияПроизводителя"] = СписокКоллекций;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобрКоллекцияПроизводителяПриИзменении(Элемент)
	
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ОтборКоллекцияПроизводителя", ОтборКоллекцияПроизводителя);
	
	УстановитьОтборСписка(СписокОбои, ПараметрыОтбора);
	
	вогОбщегоНазначенияКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, ОтборКоллекцияПроизводителя);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	КоллекцияПроизводителя = ПараметрыОтбора.Получить("ОтборКоллекцияПроизводителя");
	Если КоллекцияПроизводителя <> Неопределено Тогда 
		Если Не ЗначениеЗаполнено(КоллекцияПроизводителя) Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор,"КоллекцияПроизводителяОтбор");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
					"КоллекцияПроизводителяОтбор", КоллекцияПроизводителя, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	КонецЕсли;	
		
КонецПроцедуры