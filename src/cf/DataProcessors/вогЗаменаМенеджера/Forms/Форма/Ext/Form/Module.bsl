
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаНачала 		= ТекущаяДата();
	ДатаОкончания 	= ТекущаяДата();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если ЗначениеЗаполнено(МенеджерТекущий) Тогда
		ЗагрузитьНаСервере();
	Иначе
		ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Менеджер текущий""!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Заменить(Команда)
	ЗаменитьНаСервере(ТаблицаЗагрузки, МенеджерНовый, ДатаНачала, ДатаОкончания);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНаСервере()
	
	ТаблицаЗагрузки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогМенеджерыОбъектов.Владелец КАК Владелец,
		|	вогМенеджерыОбъектов.Период КАК Период,
		|	вогМенеджерыОбъектов.Подразделение КАК Подразделение,
		|	вогМенеджерыОбъектов.Роль КАК Роль,
		|	вогМенеджерыОбъектов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	вогМенеджерыОбъектов.Менеджер КАК Менеджер,
		|	вогМенеджерыОбъектов.ДатаНачала КАК ДатаНачала
		|ИЗ
		|	РегистрСведений.вогМенеджерыОбъектов КАК вогМенеджерыОбъектов
		|ГДЕ
		|	вогМенеджерыОбъектов.Менеджер = &ЗаменяемыйМенеджер
		|	И вогМенеджерыОбъектов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)";
	
	Запрос.УстановитьПараметр("ЗаменяемыйМенеджер", МенеджерТекущий);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ТекстСообщения = НСтр("ru = 'У менеджера нет связанных объектов'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	ТаблицаЗагрузки.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаменитьНаСервере(Знач ТаблицаЗагрузки, МенеджерНовый, ДатаНачала, ДатаОкончания)
	
	Для Каждого СтрокаТЗ Из ТаблицаЗагрузки Цикл
		УстановитьДатуОкончания(СтрокаТЗ, ДатаОкончания);
	КонецЦикла;
		
	СоздатьНовуюЗапись(ТаблицаЗагрузки, МенеджерНовый, ДатаНачала);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьДатуОкончания(Знач СтрокаТЗ, ДатаОкончания)
	
	НаборЗаписей = РегистрыСведений.вогМенеджерыОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Владелец.Установить(СтрокаТЗ.Владелец);
	НаборЗаписей.Отбор.Подразделение.Установить(СтрокаТЗ.Подразделение);
	НаборЗаписей.Отбор.Роль.Установить(СтрокаТЗ.Роль);
	НаборЗаписей.Отбор.НаправлениеДеятельности.Установить(СтрокаТЗ.НаправлениеДеятельности);
	НаборЗаписей.Отбор.Менеджер.Установить(СтрокаТЗ.Менеджер);
	
	НаборЗаписей.Прочитать();
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Запись.ДатаОкончания = ?(ЗначениеЗаполнено(ДатаОкончания), ДатаОкончания, ТекущаяДата());
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		
		ШаблонСообщения = НСтр("ru = 'Не удалось записать дату окончания для менеджера %1 по объекту %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, 
			СтрокаТЗ.Менеджер, 
			СтрокаТЗ.Владелец);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьНовуюЗапись(Знач ТаблицаЗагрузки, МенеджерНовый, ДатаНачала)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗагрузки.Владелец КАК Владелец,
	|	&ТекущийМенеджер КАК Менеджер
	|ПОМЕСТИТЬ ВТ_ТаблицаЗагрузки
	|ИЗ
	|	&ТаблицаЗагрузки КАК ТаблицаЗагрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаЗагрузки.Владелец КАК Владелец,
	|	ВТ_ТаблицаЗагрузки.Менеджер КАК Менеджер,
	|	ВЫРАЗИТЬ(ВТ_ТаблицаЗагрузки.Менеджер КАК Справочник.Пользователи).Подразделение КАК Подразделение,
	|	ВЫРАЗИТЬ(ВТ_ТаблицаЗагрузки.Менеджер КАК Справочник.Пользователи).CRM_НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДолжностныеПозиции.Роль КАК Роль
	|ИЗ
	|	ВТ_ТаблицаЗагрузки КАК ВТ_ТаблицаЗагрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.CRM_ДолжностныеПозиции КАК ДолжностныеПозиции
	|		ПО (ВЫРАЗИТЬ(ВТ_ТаблицаЗагрузки.Менеджер КАК Справочник.Пользователи).CRM_ДолжностнаяПозиция = ДолжностныеПозиции.Ссылка)";
	
	Запрос.УстановитьПараметр("ТаблицаЗагрузки", ТаблицаЗагрузки.Выгрузить());
	Запрос.УстановитьПараметр("ТекущийМенеджер", МенеджерНовый);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДанных = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		Подразделение = Справочники.СтруктураПредприятия.ПолучитьОбособленноеПодразделение(ВыборкаДанных.Подразделение);
		
		НаборЗаписей = РегистрыСведений.вогМенеджерыОбъектов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Владелец.Установить(ВыборкаДанных.Владелец);
		НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
		НаборЗаписей.Отбор.Роль.Установить(ВыборкаДанных.Роль);
		НаборЗаписей.Отбор.НаправлениеДеятельности.Установить(ВыборкаДанных.НаправлениеДеятельности);
		НаборЗаписей.Отбор.Менеджер.Установить(ВыборкаДанных.Менеджер);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период 					= ТекущаяДата();
		НоваяЗапись.Владелец 				= ВыборкаДанных.Владелец;
		НоваяЗапись.Подразделение 			= Подразделение;
		НоваяЗапись.Роль 					= ВыборкаДанных.Роль;
		НоваяЗапись.НаправлениеДеятельности = ВыборкаДанных.НаправлениеДеятельности;
		НоваяЗапись.Менеджер 				= ВыборкаДанных.Менеджер;
		НоваяЗапись.ДатаНачала 				= ?(ЗначениеЗаполнено(ДатаНачала), ДатаНачала, ТекущаяДата());
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			
			ШаблонСообщения = НСтр("ru = 'Не удалось создать новую запись для менеджера %1 по объекту %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения, 
				ВыборкаДанных.Менеджер, 
				ВыборкаДанных.Владелец);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
