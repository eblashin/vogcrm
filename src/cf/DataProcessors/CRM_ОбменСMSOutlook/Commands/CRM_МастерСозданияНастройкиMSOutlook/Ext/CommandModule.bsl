
&НаСервере
Функция ПолучитьНовуюНастройку(Компьютер)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	CRM_ПрограммыMSOutlook.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.CRM_ПрограммыMSOutlook КАК CRM_ПрограммыMSOutlook
	               |ГДЕ
	               |	НЕ CRM_ПрограммыMSOutlook.ПометкаУдаления
	               |	И CRM_ПрограммыMSOutlook.Компьютер = &Компьютер
	               |	И CRM_ПрограммыMSOutlook.Пользователь = &Пользователь";
	Запрос.УстановитьПараметр("Компьютер",		?(ЗначениеЗаполнено(Компьютер), Компьютер, ИмяКомпьютера()));
	Запрос.УстановитьПараметр("Пользователь",	Пользователи.АвторизованныйПользователь());
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПрограммаOutlook = Выборка.Ссылка;
	Иначе
		НовыйЭлемент				= Справочники.CRM_ПрограммыMSOutlook.СоздатьЭлемент(); 
		НовыйЭлемент.Компьютер		= ?(ЗначениеЗаполнено(Компьютер), Компьютер, ИмяКомпьютера());
		НовыйЭлемент.Пользователь	= Пользователи.АвторизованныйПользователь();
		НовыйЭлемент.Наименование	= Строка(НовыйЭлемент.Пользователь) + " (" + Строка(НовыйЭлемент.Компьютер) + ")";
		НовыйЭлемент.УстановитьНовыйКод();
		Попытка
			НовыйЭлемент.Записать();
			ПрограммаOutlook = НовыйЭлемент.Ссылка;
		Исключение
			ПрограммаOutlook = Справочники.CRM_ПрограммыMSOutlook.ПустаяСсылка();
		КонецПопытки;	
	КонецЕсли;
	НоваяНастройка = CRM_ОбменСMSOutlookСервер.ДобавитьНовуюНастройку(ПрограммаOutlook);
	Возврат НоваяНастройка;
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	#Если Не ВебКлиент Тогда
		Компьютер = ИмяКомпьютера();
	#Иначе
		Компьютер = "";
	#КонецЕсли
	НоваяНастройка = ПолучитьНовуюНастройку(Компьютер);
	ОткрытьФорму("Обработка.CRM_ОбменСMSOutlook.Форма.ФормаМастерНастройки", Новый Структура("ЗначенияЗаполнения",НоваяНастройка),,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры
