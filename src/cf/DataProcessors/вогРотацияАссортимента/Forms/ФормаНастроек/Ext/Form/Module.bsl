
&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.СписокНаименований.ТекущиеДанные <> Неопределено Тогда
		ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",Элементы.СписокНаименований.ТекущиеДанные.Значение);
		Закрыть(ПараметрыЗакрытия);
	Иначе
		Сообщить("Настройка не выбрана");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокНаименований();
	
	Если Параметры.Свойство("Сохранение") И Параметры.Сохранение Тогда
		ЭтаФорма.Заголовок = "Сохранение настроек";
		Элементы.СписокНаименований.Заголовок = "Ранее сохраненные настройки";
		Элементы.ФормаВыбрать.Видимость = Ложь;
		Элементы.ФормаЗаписать.КнопкаПоУмолчанию = Истина;
		Сохранение = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("Выбор") И Параметры.Выбор Тогда
		ЭтаФорма.Заголовок = "Выбор настроек";
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.СписокНаименований.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.НаименованиеНастройки.Видимость = Ложь;
		Выбор = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокНаименований()
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки КАК ИмяНастройки
		|ИЗ
		|	РегистрСведений.вогНастройкиРасстановкиСтендаВБрендЗоне КАК вогНастройкиРасстановкиСтендаВБрендЗоне
		|ГДЕ
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.Пользователь = &Пользователь
		|
		|СГРУППИРОВАТЬ ПО
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки";
	
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	СписокНаименований.ЗагрузитьЗначения(РезультатЗапроса.ВыгрузитьКолонку("ИмяНастройки"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройку(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		НаименованиеПовторяется = ПроверитьНаименованиеНаДубль(НаименованиеНастройки);
		Если НаименованиеПовторяется Тогда 
			ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьНастройкуЗавершение", ЭтотОбъект), "Наименование настройки совпадает с существующей. Данные будут перезаписаны. Продолжить?",РежимДиалогаВопрос.ДаНет);
		Иначе
			ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",НаименованиеНастройки);
			Закрыть(ПараметрыЗакрытия);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПараметрыЗакрытия = Новый Структура("НаименованиеНастройки",НаименованиеНастройки);
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьНаименованиеНаДубль(ИмяНастройки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки КАК ИмяНастройки
		|ИЗ
		|	РегистрСведений.вогНастройкиРасстановкиСтендаВБрендЗоне КАК вогНастройкиРасстановкиСтендаВБрендЗоне
		|ГДЕ
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.Пользователь = &Пользователь
		|	И вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки = &ИмяНастройки
		|
		|СГРУППИРОВАТЬ ПО
		|	вогНастройкиРасстановкиСтендаВБрендЗоне.ИмяНастройки";
	
	Запрос.УстановитьПараметр("ИмяНастройки", ИмяНастройки);
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СписокНаименованийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		НаименованиеНастройки = Элемент.ТекущиеДанные.Значение;
	КонецЕсли;
	
КонецПроцедуры
