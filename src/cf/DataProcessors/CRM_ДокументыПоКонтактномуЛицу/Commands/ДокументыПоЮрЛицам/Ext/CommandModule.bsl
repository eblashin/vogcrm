
&НаСервере
Функция ПолучитьМассивОтбора(ПараметрКоманды)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КонтактныеЛицаПартнеров.Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Ссылка В
	|			(ВЫБРАТЬ
	|				CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.КонтактноеЛицо
	|			ИЗ
	|				РегистрСведений.CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров КАК CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров
	|			ГДЕ
	|				CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.ФизЛицо = &ФизЛицо)";
	Запрос.УстановитьПараметр("ФизЛицо",ПараметрКоманды);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Массив = ПолучитьМассивОтбора(ПараметрКоманды);
	СписокОтбора = Новый СписокЗначений;
	СписокОтбора.ЗагрузитьЗначения(Массив);
	
	ПараметрыФормы = Новый Структура(
			"Отбор, КлючНастроек, СформироватьПриОткрытии",
			Новый Структура("КонтактноеЛицо", СписокОтбора),
			"КонтактноеЛицо",
			Истина);

	ОткрытьФорму("Обработка.CRM_ДокументыПоКонтактномуЛицу.Форма.ДокументыПоКонтактномуЛицу",
			ПараметрыФормы,
			ПараметрыВыполненияКоманды.Источник,
			ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно);

КонецПроцедуры
