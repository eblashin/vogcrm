
///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьМассивПомеченныхОбъектов(Форма)
	МассивПомеченныхОбъектов = Новый Массив();
	
	Для Каждого СтрокаСписка Из Форма.СписокОбъектов Цикл
		Если СтрокаСписка.Пометка Тогда
			МассивПомеченныхОбъектов.Добавить(СтрокаСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивПомеченныхОбъектов;
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ СЕРВЕР

&НаСервере
Процедура ЗарегистрироватьРазрегистрироватьОбъектыНаСервере(Зарегистрировать)
	Обработки.CRM_РегистрацияОбъектовВУниверсальномЖурналеДокументов.ЗарегистрироватьРазрегистрироватьОбъекты(ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект), Зарегистрировать, ПериодНачало, ПериодОкончание);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ КЛИЕНТ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ

&НаКлиенте
Процедура КомандаЗарегистрировать(Команда)
	МассивПомеченныхОбъектов = ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект);
	Если МассивПомеченныхОбъектов.Количество() = 0 Тогда Возврат; КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗарегистрироватьЗавершение", ЭтотОбъект, Истина);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Выбраные объекты будут зарегистрированы в журнале. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаРазрегистрировать(Команда)
	МассивПомеченныхОбъектов = ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект);
	Если МассивПомеченныхОбъектов.Количество() = 0 Тогда Возврат; КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗарегистрироватьЗавершение", ЭтотОбъект, Ложь);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Выбраные объекты будут удалены из журнала. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗарегистрироватьЗавершение(Ответ, Зарегистрировать) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗарегистрироватьРазрегистрироватьОбъектыНаСервере(Зарегистрировать);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПериод(Команда)
	ДиалогВыбораПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогВыбораПериода.Период.ДатаНачала = ПериодНачало;
	ДиалогВыбораПериода.Период.ДатаОкончания = ПериодОкончание;
	Если ТипЗнч(ОтборТекущийВариантСтандартногоПериода) = Тип("ВариантСтандартногоПериода") Тогда
		ДиалогВыбораПериода.Период.Вариант = ОтборТекущийВариантСтандартногоПериода;
		Если НачалоДня(ДиалогВыбораПериода.Период.ДатаНачала) <> НачалоДня(ПериодНачало) Или НачалоДня(ДиалогВыбораПериода.Период.ДатаОкончания) <> НачалоДня(ПериодОкончание) Тогда
			ДиалогВыбораПериода.Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
		КонецЕсли;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаВыбратьПериодЗавершение", ЭтотОбъект);
	ДиалогВыбораПериода.Показать(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	Если Период <> Неопределено Тогда
		ПериодНачало = Период.ДатаНачала;
		ПериодОкончание = Период.ДатаОкончания;
		ОтборТекущийВариантСтандартногоПериода = Период.Вариант;
	КонецЕсли;
КонецПроцедуры

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СписокОбъектов.ЗагрузитьЗначения(CRM_КлиентыСервер.ПолучитьВсеЭлементыПВХОбъектыЖурнала());
	// Удалим CRM_Событие (убрать после удаления документа CRM_Событие).
	ПВХСобытие = ПланыВидовХарактеристик.CRM_ОбъектыЖурналаДокументов.НайтиПоНаименованию("Событие");
	Если ЗначениеЗаполнено(ПВХСобытие) <> Неопределено Тогда
		ЭлементСобытие = СписокОбъектов.НайтиПоЗначению(ПВХСобытие); 
		СписокОбъектов.Удалить(ЭлементСобытие);
	КонецЕсли;
КонецПроцедуры

#Область Инициализация

#КонецОбласти

#КонецОбласти
