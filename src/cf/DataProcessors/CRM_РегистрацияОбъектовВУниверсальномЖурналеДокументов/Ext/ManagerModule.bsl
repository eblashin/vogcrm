#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ЗарегистрироватьРазрегистрироватьОбъекты(МассивОбъектов, Зарегистрировать, ПериодНачало = Неопределено, ПериодОкончание = Неопределено) Экспорт
	Если МассивОбъектов.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	Если Зарегистрировать Тогда
		ТекстЗапроса = "";
		
		Для нИндекс = 0 По МассивОбъектов.ВГраница() Цикл
			Объект = МассивОбъектов[нИндекс];
			
			ПолноеИмя = CRM_КлиентыСервер.ПолучитьПолноеИмяМетаданныхОбъектаПоСсылкеНаПВХОбъектыЖурнала(Объект);
			Если Не ЗначениеЗаполнено(ПолноеИмя) Тогда Продолжить; КонецЕсли;
			
			ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ Ссылка КАК Ссылка
			|
			|ИЗ " + ПолноеИмя + "
			|";
			
			Если ЗначениеЗаполнено(ПериодНачало) Или ЗначениеЗаполнено(ПериодОкончание) Тогда
				бЕстьДата = Ложь;
				МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмя);
				Попытка
					ТестоваяПеременная = МетаданныеОбъекта.СтандартныеРеквизиты.Дата;
					бЕстьДата = Истина;
				Исключение КонецПопытки;
				Если Не бЕстьДата Тогда
					Попытка
						ТестоваяПеременная = МетаданныеОбъекта.Реквизиты.Дата;
						бЕстьДата = Истина;
					Исключение КонецПопытки;
				КонецЕсли;
				
				Если бЕстьДата Тогда
					Если ЗначениеЗаполнено(ПериодНачало) И ЗначениеЗаполнено(ПериодОкончание) Тогда
						ТекстЗапроса = ТекстЗапроса + "
						| ГДЕ Дата МЕЖДУ &ПериодНачало И &ПериодОкончание"
					ИначеЕсли ЗначениеЗаполнено(ПериодНачало) Тогда
						ТекстЗапроса = ТекстЗапроса + "
						| ГДЕ Дата >= &ПериодНачало"
					ИначеЕсли ЗначениеЗаполнено(ПериодОкончание) Тогда
						ТекстЗапроса = ТекстЗапроса + "
						| ГДЕ Дата <= &ПериодОкончание"
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если нИндекс <> МассивОбъектов.ВГраница() Тогда
				ТекстЗапроса = ТекстЗапроса + "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|"
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		ТекстЗапроса = "ВЫБРАТЬ Объект КАК Ссылка ИЗ РегистрСведений.CRM_ЖурналДокументов ГДЕ ТипОбъекта В (&МассивОбъектов)";
		
		Если ЗначениеЗаполнено(ПериодНачало) И ЗначениеЗаполнено(ПериодОкончание) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|И Дата МЕЖДУ &ПериодНачало И &ПериодОкончание"
		ИначеЕсли ЗначениеЗаполнено(ПериодНачало) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|И Дата >= &ПериодНачало"
		ИначеЕсли ЗначениеЗаполнено(ПериодОкончание) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|И Дата <= &ПериодОкончание"
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ПараметрыЗапроса = Запрос.НайтиПараметры();
	Если ПараметрыЗапроса.Найти("ПериодНачало") <> Неопределено Тогда
		Запрос.УстановитьПараметр("ПериодНачало", ПериодНачало);
	КонецЕсли;
	Если ПараметрыЗапроса.Найти("ПериодОкончание") <> Неопределено Тогда
		Запрос.УстановитьПараметр("ПериодОкончание", ПериодОкончание);
	КонецЕсли;
	Если ПараметрыЗапроса.Найти("МассивОбъектов") <> Неопределено Тогда
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Зарегистрировать Тогда
			CRM_КлиентыСервер.ЗарегистрироватьОбъектВОбщемЖурнале(Выборка.Ссылка.ПолучитьОбъект(), Ложь);
		Иначе
			НаборЗаписей = РегистрыСведений.CRM_ЖурналДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
			НаборЗаписей.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецЕсли