///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Адрес.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		Элементы.Переместить(Элементы.ОК, Элементы.ФормаКоманднаяПанель);
		Элементы.Переместить(Элементы.Справка, Элементы.ФормаКоманднаяПанель);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отмена", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Справка", "ТолькоВоВсехДействиях", Истина);
		
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Объект) = Тип("ДанныеФормыСтруктура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.Объект);
		
		Если Параметры.Объект.ПроверятьПоФИАС Тогда
			ПроверятьПоКлассификатору = 0;
		ИначеЕсли Параметры.Объект.вогПроверятьПоЯндексу Тогда
			ПроверятьПоКлассификатору = 2;
		ИначеЕсли Параметры.Объект.вогПроверятьПоDaDaTa Тогда
			ПроверятьПоКлассификатору = 3;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОтобразитьДоступностьВариантовПроверкиПоКлассификатору();
	ИзменитьОтображениеПриИзмененииТолькоНациональный();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресТолькоНациональныйПриИзменении(Элемент)
	
	ИзменитьРеквизитыПриИзмененииТолькоНациональный();
	ИзменитьОтображениеПриИзмененииТолькоНациональный();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Результат = ОписаниеНастроекАдреса();
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Результат.ПроверятьПоФИАС = ?(ПроверятьПоКлассификатору = 0, Истина, Ложь);
	
	Результат.вогПроверятьПоЯндексу = ?(ПроверятьПоКлассификатору = 2, Истина, Ложь);
	Результат.вогПроверятьПоDaDaTa = ?(ПроверятьПоКлассификатору = 3, Истина, Ложь);
	
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ОписаниеНастроекАдреса()
	Результат = Новый Структура();
	Результат.Вставить("ТолькоНациональныйАдрес");
	Результат.Вставить("ПроверятьКорректность");
	Результат.Вставить("СкрыватьНеактуальныеАдреса");
	Результат.Вставить("УказыватьОКТМО");
	Результат.Вставить("ПроверятьПоФИАС");
	Результат.Вставить("вогПроверятьПоЯндексу");
	Результат.Вставить("вогПроверятьПоDaDaTa");
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОтобразитьДоступностьВариантовПроверкиПоКлассификатору()
	Если ПроверятьКорректность Тогда
		Элементы.ПроверятьПоКлассификатору.Доступность = Истина;
	Иначе
		Элементы.ПроверятьПоКлассификатору.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображениеПриИзмененииТолькоНациональный()
	
	Элементы.ПроверятьКорректностьАдреса.Доступность = ТолькоНациональныйАдрес;
	Элементы.СкрыватьНеактуальныеАдреса.Доступность  = ТолькоНациональныйАдрес;
	Элементы.УказыватьОКТМОВручную.Доступность       = ТолькоНациональныйАдрес;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизитыПриИзмененииТолькоНациональный()
	
	Если Не ТолькоНациональныйАдрес Тогда
		ПроверятьКорректность      = Ложь;
		СкрыватьНеактуальныеАдреса = Ложь;
		УказыватьОКТМО = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти