#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ЗаполнитьДоступныеВидыЖурнала(ДоступныеВидыЖурнала) Экспорт
	ДоступныеВидыЖурнала.Очистить();
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.CRM_ГруппыУниверсальногоЖурнала
	|ГДЕ
	|	НЕ ПометкаУдаления
	|");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДоступныеВидыЖурнала.Добавить(Выборка.Ссылка);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьТаблицуВводимыеНаОснованииОбъекты(ТаблицаВводимыеНаОснованииОбъекты, ПроверятьПрава = Ложь) Экспорт
	ТаблицаВводимыеНаОснованииОбъекты.Очистить();
	
	МассивВидов = Новый Массив();
	МассивВидов.Добавить("Справочники");
	МассивВидов.Добавить("Документы");
	МассивВидов.Добавить("ПланыВидовХарактеристик");
	МассивВидов.Добавить("ПланыСчетов");
	МассивВидов.Добавить("ПланыВидовРасчета");
	МассивВидов.Добавить("БизнесПроцессы");
	МассивВидов.Добавить("Задачи");
	МассивВидов.Добавить("ПланыОбмена");
	
	// В этом соответствии кэшируются данные для ускорения процесса.
	СоответствиеМетаданныхСсылкамНаПВХОбъектыПоКлиенту = Новый Соответствие();
	
	СтруктураПоиска = Новый Структура("Объект");
	Для Каждого МетаданныеВид Из МассивВидов Цикл
		Для Каждого МетаданныеОбъект Из Метаданные[МетаданныеВид] Цикл
			МетаданныеОбъектПолноеИмя = МетаданныеОбъект.ПолноеИмя();
			Если МетаданныеОбъектПолноеИмя = "Документ.Встреча" или МетаданныеОбъектПолноеИмя = "Документ.ЗапланированноеВзаимодействие" Тогда Продолжить КонецЕсли;
			Для Каждого ОбъектВводитсяНаОсновании Из МетаданныеОбъект.ВводитсяНаОсновании Цикл
				СсылочныйТипОбъекта = CRM_ОбщегоНазначенияСервер.ПолучитьСсылочныйТипПоМетаданнымОбъекта(ОбъектВводитсяНаОсновании);
				Если СсылочныйТипОбъекта = Неопределено Тогда Продолжить; КонецЕсли;
				Если Не Метаданные.ПланыВидовХарактеристик.CRM_ОбъектыЖурналаДокументов.Тип.СодержитТип(СсылочныйТипОбъекта) Тогда Продолжить; КонецЕсли;
				
				СсылкаПВХ = СоответствиеМетаданныхСсылкамНаПВХОбъектыПоКлиенту[ОбъектВводитсяНаОсновании];
				Если СсылкаПВХ = ПланыВидовХарактеристик.CRM_ОбъектыЖурналаДокументов.ПустаяСсылка() Тогда Продолжить; КонецЕсли;
				
				Если ПроверятьПрава Тогда
					Если Не ПравоДоступа("Добавление", ОбъектВводитсяНаОсновании) Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				Если СсылкаПВХ = Неопределено Тогда
					СсылкаПВХ = CRM_КлиентыСервер.ПолучитьСсылкуПВХОбъектыЖурналаПоМетаданнымОбъекта(ОбъектВводитсяНаОсновании);
					Если СсылкаПВХ = Неопределено Тогда
						СоответствиеМетаданныхСсылкамНаПВХОбъектыПоКлиенту.Вставить(ОбъектВводитсяНаОсновании, ПланыВидовХарактеристик.CRM_ОбъектыЖурналаДокументов.ПустаяСсылка());
						Продолжить;
					Иначе
						СоответствиеМетаданныхСсылкамНаПВХОбъектыПоКлиенту.Вставить(ОбъектВводитсяНаОсновании, СсылкаПВХ);
					КонецЕсли;
				КонецЕсли;
				
				СтруктураПоиска.Объект = СсылкаПВХ;
				НайденныеСтроки = ТаблицаВводимыеНаОснованииОбъекты.НайтиСтроки(СтруктураПоиска);
				Если НайденныеСтроки.Количество() = 0 Тогда
					СтрокаТаблицы = ТаблицаВводимыеНаОснованииОбъекты.Добавить();
					СтрокаТаблицы.Объект = СсылкаПВХ;
				Иначе
					СтрокаТаблицы = НайденныеСтроки[0];
				КонецЕсли;
				
				Если СтрокаТаблицы.СписокВводимыхНаОснованииОбъектов.НайтиПоЗначению(МетаданныеОбъектПолноеИмя) = Неопределено Тогда
					СтрокаТаблицы.СписокВводимыхНаОснованииОбъектов.Добавить(МетаданныеОбъектПолноеИмя, МетаданныеОбъект.Синоним);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаВводимыеНаОснованииОбъекты Цикл
		СтрокаТаблицы.СписокВводимыхНаОснованииОбъектов.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСоставГруппыЖурнала(ГруппаСсылка, ПроверятьПрава = Ложь) Экспорт
	Массив = Новый Массив();
	Для Каждого СтрокаТаблицы Из ГруппаСсылка.Состав Цикл
		Если ПроверятьПрава Тогда
			Если СтрокаТаблицы.Объект.ТипЗначения.Типы().Количество() > 0 Тогда
				Попытка
					ОбъектМетаданных = Метаданные.НайтиПоТипу(СтрокаТаблицы.Объект.ТипЗначения.Типы()[0]);
				Исключение
					ОбъектМетаданных = Неопределено;
				КонецПопытки;
				Если ОбъектМетаданных <> Неопределено Тогда
					Если Не ПравоДоступа("Чтение", ОбъектМетаданных) Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если Массив.Найти(СтрокаТаблицы.Объект) = Неопределено Тогда
			Массив.Добавить(СтрокаТаблицы.Объект);
		КонецЕсли;
	КонецЦикла;
	Возврат Массив;
КонецФункции


#КонецЕсли