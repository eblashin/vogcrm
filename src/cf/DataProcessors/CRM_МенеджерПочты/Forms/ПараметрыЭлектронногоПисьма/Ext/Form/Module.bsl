&НаКлиенте
Перем ВыполняетсяЗакрытие;

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквзитыФормыИзПараметров(Параметры);
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события формы "ПередЗакрытием".
//
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы Тогда Возврат; КонецЕсли;
	Если НЕ ВыполняетсяЗакрытие И НЕ ВыполненаКомандаЗакрыть И Модифицированность Тогда
		Отказ = Истина;
		ОбратныйВызов = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОбратныйВызов, НСтр("ru = 'Данные были изменены, внесенные изменения будут отменены. Отменить и закрыть?''"), РежимДиалогаВопрос.ОКОтмена);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик ответа на вопрос перед закрытием формы.
//
// Параметры:
//	Результат				- КодВозвратаДиалога	- Ответ на вопрос.
//	ДополнительныеПараметры	- Структура				- Структура дополнительных параметров.
//
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
    Если Результат = КодВозвратаДиалога.ОК Тогда
		Модифицированность	= Ложь;
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
    КонецЕсли;
КонецПроцедуры // ПередЗакрытиемЗавершение()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ВыполненаКомандаЗакрыть = Истина;
	
	Если Папка <> ТекущаяПапка Тогда
		//ВзаимодействияВызовСервера.УстановитьПапкуЭлектронногоПисьма(Письмо,Папка);
	КонецЕсли;
	
	Если ТипПисьма = "ЭлектронноеПисьмоИсходящее" И ОтправленоПолучено = Дата(1,1,1) И Модифицированность Тогда
		Закрыть(Новый Структура("УведомитьОДоставке, УведомитьОПрочтении, ВключатьТелоИсходногоПисьма,Папка",
	                            УведомитьОДоставке,
	                            УведомитьОПрочтении,
	                            ВключатьТелоИсходногоПисьма,
	                            Папка));
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитФормыИзПараметра(Параметры,ИмяПараметра,ИмяРеквизита = "")

	Если Параметры.Свойство(ИмяПараметра) Тогда
		
		ЭтотОбъект[?(ПустаяСтрока(ИмяРеквизита),ИмяПараметра,ИмяРеквизита)] = Параметры[ИмяПараметра];
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквзитыФормыИзПараметров(Параметры)

	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"ВнутреннийНомер");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"ЗаголовкиИнтернета");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"Создано");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"Получено","ОтправленоПолучено");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"Отправлено","ОтправленоПолучено");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"УведомитьОДоставке");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"УведомитьОПрочтении");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"Письмо");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"ТипПисьма");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"ВключатьТелоИсходногоПисьма");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"УчетнаяЗапись");
	ЗаполнитьРеквизитФормыИзПараметра(Параметры,"Папка");	
	
	//Папка = Письмо.CRM_Папка;
	ТекущаяПапка = Папка;

КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()

	Если ТипПисьма = "ЭлектронноеПисьмоИсходящее" Тогда
		Элементы.Заголовки.Заголовок = НСтр("ru='Идентификаторы'");
		Если ОтправленоПолучено = Дата(1,1,1) Тогда
			Элементы.УведомитьОДоставке.ТолькоПросмотр          = Ложь;
			Элементы.УведомитьОПрочтении.ТолькоПросмотр         = Ложь;
			Элементы.ВключатьТелоИсходногоПисьма.ТолькоПросмотр = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ОтправленоПолучено.Заголовок = НСтр("ru='Получено'");
		Элементы.ВключатьТелоИсходногоПисьма.Видимость =Ложь;
	КонецЕсли;
	
	Элементы.Папка.Доступность = ЗначениеЗаполнено(УчетнаяЗапись);
	
КонецПроцедуры

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти
