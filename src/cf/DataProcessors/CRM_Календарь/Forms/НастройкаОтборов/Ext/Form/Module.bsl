
///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура СохранитьНастройкиОтборов()
	МассивВыбранныхОтборов = ТаблицаОтборов.Выгрузить(Новый Структура("Пометка", Истина)).ВыгрузитьКолонку("Имя");
	CRM_ХранилищеНастроек.Сохранить("Обработка_CRM_Календарь_Настройки", "ВыбранныеОтборы", МассивВыбранныхОтборов);
	
	СписокВыбранныхОтборов.ЗагрузитьЗначения(МассивВыбранныхОтборов);
	
	CRM_ХранилищеНастроек.Сохранить("Обработка_CRM_Календарь_Настройки", "ПоказыватьВидыСравненияОтборов", ПризнакПоказыватьРасширенныеНастройкиОтборов);
КонецПроцедуры

&НаКлиенте
Функция ПроверитьСохранить()
	Результат = Истина;
	
	СохранитьНастройкиОтборов();
	
	Возврат Результат;
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	Если ПроверитьСохранить() Тогда
		НастройкиБылиИзменены = Истина;
		Модифицированность = Ложь;
		МассивВыбранныхОтборов = Новый Массив();
		Закрыть(Новый Структура("ПризнакПоказыватьРасширенныеНастройкиОтборов, ВыбранныеОтборы", ПризнакПоказыватьРасширенныеНастройкиОтборов, СписокВыбранныхОтборов.ВыгрузитьЗначения()));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьФлажки(Команда)
	Для Каждого СтрокаТаблицы Из ТаблицаОтборов Цикл
		СтрокаТаблицы.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КомандаСнятьФлажки(Команда)
	Для Каждого СтрокаТаблицы Из ТаблицаОтборов Цикл
		СтрокаТаблицы.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("НастройкиПоУмолчанию") Тогда
		ВыбранныеОтборы = CRM_ХранилищеНастроек.Загрузить("Обработка_CRM_Календарь_Настройки", "ВыбранныеОтборы");
		Если ТипЗнч(ВыбранныеОтборы) <> Тип("Массив") Тогда
			ВыбранныеОтборы = Параметры.НастройкиПоУмолчанию.ОтборыПоУмолчанию;
		КонецЕсли;
			
		Для Каждого ДоступныйОтбор Из Параметры.НастройкиПоУмолчанию.ДоступныеОтборы Цикл
			НоваяСтрока = ТаблицаОтборов.Добавить();
			НоваяСтрока.Имя = ДоступныйОтбор;
			НоваяСтрока.Описание = Параметры.НастройкиПоУмолчанию.ПредставленияОтборов[ДоступныйОтбор];
			НоваяСтрока.Пометка = (ВыбранныеОтборы.Найти(ДоступныйОтбор) <> Неопределено);
		КонецЦикла;
		
		ПоказыватьВидыСравненияОтборов = CRM_ХранилищеНастроек.Загрузить("Обработка_CRM_Календарь_Настройки", "ПоказыватьВидыСравненияОтборов");
		Если ПоказыватьВидыСравненияОтборов = Неопределено Тогда
			ПоказыватьВидыСравненияОтборов = Ложь;
		КонецЕсли;
		
		ПризнакПоказыватьРасширенныеНастройкиОтборов = ПоказыватьВидыСравненияОтборов;
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события формы "ПередЗакрытием".
//
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность Тогда
		Если ЗавершениеРаботы Тогда
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru='При завершении работы в форме ""'") + Заголовок + НСтр("ru='"" все изменения будут утеряны.'");
			Возврат;
		КонецЕсли;
		Отказ = Истина;
		ОбратныйВызов = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОбратныйВызов, НСтр("ru = ''Данные были изменены. Сохранить данные?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик ответа на вопрос перед закрытием формы.
//
// Параметры:
//	Результат				- КодВозвратаДиалога	- Ответ на вопрос.
//	ДополнительныеПараметры	- Структура				- Структура дополнительных параметров.
//
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
		Если ПроверитьСохранить() Тогда
			НастройкиБылиИзменены = Истина;
			Модифицированность = Ложь;
			Закрыть(Новый Структура("ПризнакПоказыватьРасширенныеНастройкиОтборов, ВыбранныеОтборы", ПризнакПоказыватьРасширенныеНастройкиОтборов, СписокВыбранныхОтборов.ВыгрузитьЗначения()));
			Возврат;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
    КонецЕсли;
	Модифицированность	= Ложь;
	Закрыть();
КонецПроцедуры // ПередЗакрытиемЗавершение()

#КонецОбласти
