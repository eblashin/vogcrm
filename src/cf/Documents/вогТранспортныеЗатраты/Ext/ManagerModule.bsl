
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицыТранспортныхЗатрат(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИнициализироватьТаблицыДляДвижений(
		Запрос,
		ТекстыЗапроса,
		ДополнительныеСвойства.ТаблицыДляДвижений,
		Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицыТранспортныхЗатрат(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТранспортныеЗатраты";
	
	ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Период КАК ПериодЗатрат,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Клиент КАК Клиент,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Подразделение КАК Подразделение,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.СтатьяЗатрат КАК СтатьяЗатрат,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.ТранспортныеЗатраты КАК ТранспортныеЗатраты,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Ссылка.СценарийПланирования КАК СценарийПланирования,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Ссылка.ВерсияСценария КАК ВерсияСценария,
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Ссылка.Дата КАК Период
			|ИЗ
			|	Документ.вогТранспортныеЗатраты.ТаблицаТранспортныхЗатрат КАК вогТранспортныеЗатратыТаблицаТранспортныхЗатрат
			|ГДЕ
			|	вогТранспортныеЗатратыТаблицаТранспортныхЗатрат.Ссылка = &Ссылка";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Заполняет движения документа по регистру "вогТранспортныеЗатрты"
//
Процедура ОтразитьДанныеТранспортныхЗатрат(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТранспортныеЗатраты;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.вогТранспортныеЗатраты.Записывать = Истина;
	Движения.вогТранспортныеЗатраты.Загрузить(Таблица);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли