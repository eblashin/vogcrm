&НаКлиентеНаСервереБезКонтекста
Функция НастройкаПоляОтображенияСодержанияПолучитьОграничениеТиповСписка()
	Возврат Новый ОписаниеТипов("ДокументСсылка.CRM_СчетНаОплатуПокупателю");
КонецФункции

&НаСервере
Процедура ЗаполнитьПанельИнформации(Счет)
	
	Если Счет = Неопределено Тогда Возврат КонецЕсли;
	
	// Поле отображения содержания.
	Если ТипЗнч(Счет) = Тип("ДокументСсылка.CRM_СчетНаОплатуПокупателю") Тогда
		Содержание = CRM_ОбщегоНазначенияСервер.НастройкиПолейОтображенияСодержанияПолучитьСодержание(Счет, НастройкаПоляОтображенияСодержанияПолучитьОграничениеТиповСписка());
		Если Содержание <> ПолеОтображениеСодержания Тогда
			ПолеОтображениеСодержания = Содержание;
		КонецЕсли;
	Иначе
		Содержание = CRM_ОбщегоНазначенияКлиентСервер.НастройкиПолейОтображенияСодержанияПолучитьПустоеСодержание();
		Если Содержание <> ПолеОтображениеСодержания Тогда
			ПолеОтображениеСодержания = Содержание;
		КонецЕсли;
	КонецЕсли;
	// +CRM
	ТекБанковскийСчет	= Счет.БанковскийСчет;
	ТекКонтрагент		= Счет.Партнер;
	// -CRM	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеСодержаниеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	// Поле отображения содержания.
	CRM_ОбщегоНазначенияКлиент.НастройкиПолейОтображенияСодержанияПолеСодержаниеПриНажатии(ДанныеСобытия, СтандартнаяОбработка, НастройкаПоляОтображенияСодержанияПолучитьОграничениеТиповСписка());
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Пользователь = Настройки.Получить("ОтборПользователь");
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Ответственный", Пользователь, Истина);
		
	КонецЕсли;	
	
	Оплата = Настройки.Получить("ОтборОплата");
	
	Если Оплата = "Оплаченные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОтгрузки", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОплата", 0, Истина);
		
	ИначеЕсли Оплата = "Неоплаченные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОтгрузки", 0, Истина, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОплата", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);
		
	КонецЕсли;
	
	Отгрузка = Настройки.Получить("ОтборОтгрузка");
	
	Если Отгрузка = "Отгруженные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОтгрузка", 0, Истина);
		
	ИначеЕсли Отгрузка = "Неотгруженные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОтгрузка", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);
		
	КонецЕсли;
	
	Контрагент = Настройки.Получить("ОтборКонтрагент");
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Контрагент", Контрагент, Истина);
		
	КонецЕсли;
// +CRM
	ПоказатьСкрытьДопИнформацию = Настройки.Получить("ПометкаПоказатьСкрытьДопИнформациюСчетНаОплатуПокупателю");
	Если НЕ ПоказатьСкрытьДопИнформацию = Неопределено Тогда
		ПометкаПоказатьСкрытьДопИнформацию	= Настройки["ПометкаПоказатьСкрытьДопИнформациюСчетНаОплатуПокупателю"];
	КонецЕсли;
	
	Элементы.СписокСкрытьДопИнформацию.Пометка		= ПометкаПоказатьСкрытьДопИнформацию;
	Элементы.ГруппаПанельИнформации.Видимость		= ПометкаПоказатьСкрытьДопИнформацию;
// -CRM

КонецПроцедуры

&НаКлиенте
Процедура ОтборОплатаПриИзменении(Элемент)
	
	Если ОтборОплата = "Оплаченные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОплаты", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОплата", 0, Истина);
		
	ИначеЕсли ОтборОплата = "Неоплаченные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОплаты");		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОплата", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);
		
	Иначе
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОплаты");
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОплата");	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОплатаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОплаты");
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОплата");
	
	ОтборОплата = "Все";	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтгрузкаПриИзменении(Элемент)
	
	Если ОтборОтгрузка = "Отгруженные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОтгрузки", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОтгрузка", 0, Истина);
		
	ИначеЕсли ОтборОтгрузка = "Неотгруженные" Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОтгрузки");				
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОтгрузка", 0, Истина, ВидСравненияКомпоновкиДанных.Больше);
		
	Иначе
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОтгрузки");						
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОтгрузка");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтгрузкаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОтгрузки");					
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "СуммаОстатокОтгрузка");
	
	ОтборОтгрузка = "Все";	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПользовательПриИзменении(Элемент)
	
	Если ОтборПользователь.Пустая() Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Ответственный");			
		
	Иначе	
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Ответственный", ОтборПользователь, Истина);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПользовательОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// очистить связанные отборы
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Ответственный");
	
	ОтборПользователь = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПриИзменении(Элемент)
	
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Дата", ОтборПериод, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	Если ОтборКонтрагент.Пустая() Тогда
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Контрагент");
		
	Иначе	
		
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Контрагент", ОтборКонтрагент, Истина);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ЗаполнитьПанельИнформации(Элементы.Список.ТекущаяСтрока);
КонецПроцедуры

// +CRM
&НаКлиенте
Процедура СкрытьДопИнформацию(Команда)
	Элементы.СписокСкрытьДопИнформацию.Пометка	= НЕ Элементы.СписокСкрытьДопИнформацию.Пометка;
	ПометкаПоказатьСкрытьДопИнформацию			= Элементы.СписокСкрытьДопИнформацию.Пометка;
	Элементы.ГруппаПанельИнформации.Видимость	= ПометкаПоказатьСкрытьДопИнформацию;
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	Настройки.Вставить("ПометкаПоказатьСкрытьДопИнформациюСчетНаОплатуПокупателю", ПометкаПоказатьСкрытьДопИнформацию);
КонецПроцедуры

// -CRM