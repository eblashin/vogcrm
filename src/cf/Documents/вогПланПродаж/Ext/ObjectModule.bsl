
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И Планирование.Количество() = 0 Тогда
		
		ШаблонСообщения = НСтр("ru = 'Не заполнена табличная часть ""Планирование"". Проведение невозможно.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, );
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", 	ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", 	РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// +++ VOG Кулаков П.Л. 26.02.2020 CRM-396
	ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Если Статус = Справочники.вогШаблоныСтатусов.Согласован Тогда
		Документы.вогПланПродаж.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	КонецЕсли;
	
	ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Если Статус = Справочники.вогШаблоныСтатусов.Согласован Тогда
		Документы.вогПланПродаж.ОтразитьВогПланыПродаж(ДополнительныеСвойства, Движения, Отказ);
		Документы.вогПланПродаж.ОтразитьПланыПродажПоТорговымТочкам(ДополнительныеСвойства, Движения, Отказ);
		Документы.вогПланПродаж.ОтразитьПланыПродаж(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Документы.вогПланПродаж.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Очистить дополнительные свойства для проведения
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	// --- VOG Кулаков П.Л.
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Документы.вогПланПродаж.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Очистить дополнительные свойства для проведения
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	РеквизитПартнер = ПроверяемыеРеквизиты.Найти("Планирование.Партнер");
	
	Если Не РеквизитПартнер = Неопределено Тогда
		
		ПроверяемыеРеквизиты.Удалить(РеквизитПартнер);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДополнительныеСвойстваДляПроведения(
	ДокументСсылка,
	ДополнительныеСвойства,
	РежимПроведения = Неопределено)
	
	// Дополнительные свойства для проведения
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", 	Новый Структура);
	ДополнительныеСвойства.Вставить("ДляПроведения", 		Новый Структура);
	
	СтруктураВременныеТаблицы = Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", 	СтруктураВременныеТаблицы);
	ДополнительныеСвойства.ДляПроведения.Вставить("РежимПроведения", 			РежимПроведения);
	ДополнительныеСвойства.ДляПроведения.Вставить("МетаданныеДокумента", 		ДокументСсылка.Метаданные());
	ДополнительныеСвойства.ДляПроведения.Вставить("Ссылка", 					ДокументСсылка);
	
КонецПроцедуры

Процедура ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект, ЭтоНовый = Ложь)
	
	Перем ЭтоНовыйДокумент, МетаданныеДвижения;
	
	Для Каждого НаборЗаписей Из Объект.Движения Цикл

		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;

	КонецЦикла;
	
	Если НЕ Объект.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовыйДокумент) Тогда
		ЭтоНовыйДокумент = ЭтоНовый;
	КонецЕсли;
	
	Если НЕ ЭтоНовыйДокумент Тогда

		ТипДокумента = ТипЗнч(Объект.Ссылка);
		
		Если Объект.ДополнительныеСвойства.Свойство("ДляПроведения")
			И Объект.ДополнительныеСвойства.ДляПроведения.Свойство("МетаданныеДокумента") Тогда
			МетаданныеДвижения = Объект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Движения;
		Иначе
			МетаданныеДвижения = Объект.Метаданные().Движения;
		КонецЕсли;
		
		МассивИменРегистров = ПолучитьИспользуемыеРегистры(
			Объект.Ссылка,
			МетаданныеДвижения);
			
		Для Каждого ИмяРегистра Из МассивИменРегистров Цикл
			Объект.Движения[ИмяРегистра].Записывать = Истина;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьИспользуемыеРегистры(Регистратор, Движения)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);

	Результат = Новый Массив;
	МаксимумТаблицВЗапросе = 256;

	СчетчикТаблиц 	= 0;
	СчетчикДвижений = 0;

	ВсегоДвижений = Движения.Количество();
	ТекстЗапроса  = "";
	Для Каждого Движение Из Движения Цикл

		СчетчикДвижений = СчетчикДвижений + 1;

		ПропуститьРегистр = Ложь;

		Если Не ПропуститьРегистр Тогда

			Если СчетчикТаблиц > 0 Тогда

				ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ
				|";

			КонецЕсли;

			СчетчикТаблиц = СчетчикТаблиц + 1;

			ТекстЗапроса = ТекстЗапроса + 
			"
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|""" + Движение.Имя + """ КАК ИмяРегистра
			|
			|ИЗ " + Движение.ПолноеИмя() + "
			|
			|ГДЕ Регистратор = &Регистратор
			|";

		КонецЕсли;

		Если СчетчикТаблиц = МаксимумТаблицВЗапросе Или СчетчикДвижений = ВсегоДвижений Тогда

			Запрос.Текст  = ТекстЗапроса;
			ТекстЗапроса  = "";
			СчетчикТаблиц = 0;

			Если Результат.Количество() = 0 Тогда

				Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");

			Иначе

				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.ИмяРегистра);
				КонецЦикла;

			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения)

	Если ДанныеЗаполнения.Свойство("РеквизитыШапки") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Планирование") Тогда
		
		Для Каждого ТекСтрока Из ДанныеЗаполнения.Планирование Цикл
			
			ЗаполнитьЗначенияСвойств(Планирование.Добавить(), ТекСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли