#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

/////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА ПЕЧАТИ

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт 

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "УтвержденныйВариант";
	КомандаПечати.Представление = "Утвержденный вариант";
	КомандаПечати.СписокФорм    = "ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ВсеВарианты";
	КомандаПечати.Представление = "Все варианты";
	КомандаПечати.СписокФорм    = "ФормаДокумента";
	
КонецПроцедуры 

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УтвержденныйВариант") Тогда
 		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УтвержденныйВариант", "УтвержденныйВариант", 
			СформироватьУтвержденныйВариант(МассивОбъектов));
	КонецЕсли;
	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВсеВарианты") Тогда
 		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ВсеВарианты", "ВсеВарианты", 
			СформироватьВсеВарианты(МассивОбъектов));
	КонецЕсли;
	
КонецПроцедуры 

Функция СформироватьУтвержденныйВариант(МассивОбъектов)
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	Для каждого Ссылка из МассивОбъектов Цикл
		ТабДокументОбъекта = СформироватьПротоколРазногласий(Ссылка, Истина); 
		Если ТабДокумент.ВысотаТаблицы > 0 И ТабДокументОбъекта.ВысотаТаблицы > 0 Тогда 
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц()
		КонецЕсли;
		ТабДокумент.Вывести(ТабДокументОбъекта)
	КонецЦикла;

	Возврат ТабДокумент

КонецФункции 

Функция СформироватьВсеВарианты(МассивОбъектов)
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	Для каждого Ссылка из МассивОбъектов Цикл
		ТабДокументОбъекта = СформироватьПротоколРазногласий(Ссылка); 
		Если ТабДокумент.ВысотаТаблицы > 0 И ТабДокументОбъекта.ВысотаТаблицы > 0 Тогда 
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц()
		КонецЕсли;
		ТабДокумент.Вывести(ТабДокументОбъекта)
	КонецЦикла;

	Возврат ТабДокумент

КонецФункции 

Функция СформироватьПротоколРазногласий(Ссылка, Утвержденный = Ложь)
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("ПФ_MXL_ПротоколРазногласий");
	
	ОблШапка 		= Макет.ПолучитьОбласть("Шапка");
	ОблОрганизация 	= Макет.ПолучитьОбласть("Организация");
	ОблКлиент 		= Макет.ПолучитьОбласть("Клиент");
	ОблДоговор 		= Макет.ПолучитьОбласть("Договор");
	ОблВариант 		= Макет.ПолучитьОбласть("Вариант");
	ОблШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОблСтрока 		= Макет.ПолучитьОбласть("Строка");	
	ОблПодвал 		= Макет.ПолучитьОбласть("Подвал");
	
	ФлагУдалитьПрефикс 	= Константы.CRM_ПечататьНомераДокументовБезПрефиксов.Получить();
	НомерДокумента	 	= ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Ссылка.Номер, ФлагУдалитьПрефикс, ФлагУдалитьПрефикс);
	ДатаДокумента		= Формат(Ссылка.Дата,"ДЛФ=D");
	
	СведенияОбОрганизации	= CRM_ОбщегоНазначенияСервер.СведенияОЮрФизЛице(Ссылка.ДоговорКонтрагента.Организация, ТекущаяДата(), ,);
	ПрдставлениеОрганизации	= CRM_ОбщегоНазначенияСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,");
	
	СведенияОКонтрагенте	= CRM_ОбщегоНазначенияСервер.СведенияОЮрФизЛице(Ссылка.ДоговорКонтрагента.Владелец, ТекущаяДата(), ,);
	ПредставлениеКлиента	= CRM_ОбщегоНазначенияСервер.ОписаниеОрганизации(СведенияОКонтрагенте, "ПолноеНаименование,");
	
	Руководитель = "";
	Запись = РегистрыСведений.ОтветственныеЛица.СоздатьМенеджерЗаписи();
	Запись.Организация = Ссылка.ДоговорКонтрагента.Организация;
	Запись.ТипОтветственногоЛица = Перечисления.ТипыОтветственныхЛиц.Руководитель;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		Руководитель = Запись.Сотрудник.Наименование;
	КонецЕсли;
	
	
	ОблШапка.Параметры.ТекстЗаголовка = "Протокол разногласий №" + НомерДокумента + " от " + ДатаДокумента;
	ОблОрганизация.Параметры.ПрдставлениеОрганизации = ПрдставлениеОрганизации;
	ОблКлиент.Параметры.ПредставлениеКлиента = ПредставлениеКлиента;
	ОблДоговор.Параметры.ПредставлениеДоговор = Ссылка.ДоговорКонтрагента.НомерДоговора + " от " + Формат(Ссылка.ДоговорКонтрагента.ДатаДоговора,"ДЛФ=D");
	
	ТабДок.Вывести(ОблШапка);
	ТабДок.Вывести(ОблОрганизация);
	ТабДок.Вывести(ОблКлиент);
	ТабДок.Вывести(ОблДоговор);

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пункты.ПунктДоговора КАК ПунктДоговора,
		|	Пункты.ИсходныйВариант,
		|	Пункты.НовыйВариант,
		|	Пункты.ИДВарианта КАК ИДВарианта,
		|	Пункты.ИмяВарианта КАК ИмяВарианта,
		|	Пункты.Утвержден КАК Утвержден
		|ИЗ
		|	Документ.вогПротоколРазногласий.Пункты КАК Пункты
		|ГДЕ
		|	Пункты.Ссылка = &Ссылка
		|	И Пункты.Утвержден = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИДВарианта,
		|	ПунктДоговора
		|ИТОГИ
		|	МАКСИМУМ(ИмяВарианта),
		|	МАКСИМУМ(Утвержден)
		|ПО
		|	ИДВарианта";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если НЕ Утвержденный Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Пункты.Утвержден = ИСТИНА", "");	
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИДВарианта = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаИДВарианта.Следующий() Цикл
		
		ВариантТекст = "";
		Если ПустаяСтрока(ВыборкаИДВарианта.ИмяВарианта) Тогда
			ВариантТекст = "Вариант" + ВыборкаИДВарианта.ИДВарианта + ?(ВыборкаИДВарианта.Утвержден, " (Утв.)", "");
		Иначе
			ВариантТекст = ВыборкаИДВарианта.ИмяВарианта + ?(ВыборкаИДВарианта.Утвержден, " (Утв.)", "");
		КонецЕсли; 

		ОблВариант.Параметры.Вариант = ВариантТекст;
		ТабДок.Вывести(ОблВариант);
		ТабДок.Вывести(ОблШапкаТаблицы);
	
		ВыборкаДетальныеЗаписи = ВыборкаИДВарианта.Выбрать();
		
		НомерСтроки = 1;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОблСтрока.Параметры.Номер = НомерСтроки;
			ОблСтрока.Параметры.Пункт = ВыборкаДетальныеЗаписи.ПунктДоговора;
			ОблСтрока.Параметры.ИсходныйВариант = ВыборкаДетальныеЗаписи.ИсходныйВариант;
			ОблСтрока.Параметры.НовыйВариант = ВыборкаДетальныеЗаписи.НовыйВариант;
			
			НомерСтроки = НомерСтроки + 1;
			ТабДок.Вывести(ОблСтрока);
		КонецЦикла;
	КонецЦикла;
	
	ОблПодвал.Параметры.ФИОРуководитель = Руководитель;
	ТабДок.Вывести(ОблПодвал);

	Возврат ТабДок;

КонецФункции
 
#КонецЕсли