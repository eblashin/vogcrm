#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ЗаполнитьПоНазначениюОпросов(ДанныеЗаполнения)
	
	Тема			= "Приглашение к анкетированию";
	//Ответственный	= ДанныеЗаполнения.Ответственный;
	Основание		= ДанныеЗаполнения.Основание;
	
	Если ДанныеЗаполнения.РассылкаТолькоДляНовых Тогда
		
		// Получим список респондентов, которым уже отправлено приглашение.
		ЗапросПоРассылкам = Новый Запрос;
		
		Если ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Партнеры") Тогда
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	CRM_РассылкаЭлектронныхПисемПолучатели.Партнер КАК Респондент
			               |ИЗ
			               |	Документ.CRM_РассылкаЭлектронныхПисем.Получатели КАК CRM_РассылкаЭлектронныхПисемПолучатели
			               |ГДЕ
			               |	CRM_РассылкаЭлектронныхПисемПолучатели.Ссылка.Основание = &Основание";
			
		ИначеЕсли ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	CRM_РассылкаЭлектронныхПисемПолучатели.КонтактноеЛицо КАК Респондент
			               |ИЗ
			               |	Документ.CRM_РассылкаЭлектронныхПисем.Получатели КАК CRM_РассылкаЭлектронныхПисемПолучатели
			               |ГДЕ
			               |	CRM_РассылкаЭлектронныхПисемПолучатели.Ссылка.Основание = &Основание";
			
		Иначе
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	CRM_РассылкаЭлектронныхПисемСвоиЛица.Пользователь КАК Респондент
			               |ИЗ
			               |	Документ.CRM_РассылкаЭлектронныхПисем.СвоиЛица КАК CRM_РассылкаЭлектронныхПисемСвоиЛица
			               |ГДЕ
			               |	CRM_РассылкаЭлектронныхПисемСвоиЛица.Ссылка.Основание = &Основание";
			
		КонецЕсли;	
		
		ЗапросПоРассылкам.Текст = ТекстЗапроса;
		ЗапросПоРассылкам.УстановитьПараметр("Основание", Основание);
		
		ТаблицаРезультатовЗапросаПоРассылкам = ЗапросПоРассылкам.Выполнить().Выгрузить();
		
		МассивРеспондентовИсключений = ТаблицаРезультатовЗапросаПоРассылкам.ВыгрузитьКолонку("Респондент");
		
	Иначе
		
		МассивРеспондентовИсключений = Новый Массив;
		
	КонецЕсли;	
	
	Если Основание.СвободныйОпрос Тогда
		
		ЗапросПоРеспондентам = Новый Запрос;
		
		Если ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Пользователи") Тогда
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ВнешниеПользователи.ОбъектАвторизации КАК Респондент
			               |ИЗ
			               |	Справочник.ВнешниеПользователи КАК ВнешниеПользователи
			               |ГДЕ
			               |	ВнешниеПользователи.ОбъектАвторизации ССЫЛКА Справочник.Пользователи
			               |	И НЕ(ВнешниеПользователи.ОбъектАвторизации В (&РеспондентыИсключения))";
			
		ИначеЕсли ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Партнеры") Тогда
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ВнешниеПользователи.ОбъектАвторизации КАК Респондент
			               |ИЗ
			               |	Справочник.ВнешниеПользователи КАК ВнешниеПользователи
			               |ГДЕ
			               |	ВнешниеПользователи.ОбъектАвторизации ССЫЛКА Справочник.Партнеры
			               |	И НЕ(ВнешниеПользователи.ОбъектАвторизации В (&РеспондентыИсключения))";
			
		ИначеЕсли ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ВнешниеПользователи.ОбъектАвторизации КАК Респондент
			               |ИЗ
			               |	Справочник.ВнешниеПользователи КАК ВнешниеПользователи
			               |ГДЕ
			               |	ВнешниеПользователи.ОбъектАвторизации ССЫЛКА Справочник.КонтактныеЛицаПартнеров
			               |	И НЕ(ВнешниеПользователи.ОбъектАвторизации В (&РеспондентыИсключения))";
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.РассылкаТолькоДляНовых Тогда
			
			ЗапросПоРеспондентам.УстановитьПараметр("РеспондентыИсключения", МассивРеспондентовИсключений);
			
		Иначе
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И НЕ(ВнешниеПользователи.ОбъектАвторизации В (&РеспондентыИсключения))", "");
			
		КонецЕсли;	
				
		ЗапросПоРеспондентам.Текст = ТекстЗапроса;        
		
		ВыборкаПоРеспондентам = ЗапросПоРеспондентам.Выполнить().Выбрать();		
		
		Пока ВыборкаПоРеспондентам.Следующий() Цикл
			
			Если ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Пользователи") Тогда
				
				НоваяСтрока					= СвоиЛица.Добавить();
				НоваяСтрока.Пользователь	= ВыборкаПоРеспондентам.Респондент;
				
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(НоваяСтрока.Пользователь, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
				
				Если Таб.Количество() > 0 Тогда
					НоваяСтрока.Адрес = Таб[0].АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= Строка(НоваяСтрока.Пользователь) + " <" + Таб[0].Представление + ">";
				КонецЕсли;
				
			Иначе
				
				НоваяСтрока = Получатели.Добавить();
				
				Если ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Партнеры") Тогда
					
					НоваяСтрока.Партнер = ВыборкаПоРеспондентам.Респондент;
					НоваяСтрока.КонтактноеЛицо = ВыборкаПоРеспондентам.Респондент.CRM_ОсновноеКонтактноеЛицо;					
				
				Иначе
					
					НоваяСтрока.Партнер = ВыборкаПоРеспондентам.Респондент.Владелец;
					НоваяСтрока.КонтактноеЛицо = ВыборкаПоРеспондентам.Респондент;
					
				КонецЕсли;
				
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(НоваяСтрока.Партнер,
													?(ЗначениеЗаполнено(НоваяСтрока.КонтактноеЛицо),НоваяСтрока.КонтактноеЛицо,Неопределено),
													Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Неопределено);
				Если Таб.Количество() > 0 Тогда
					НоваяСтрока.Адрес = Таб[0].АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= ?(Таб[0].Ссылка = НоваяСтрока.Партнер,Строка(НоваяСтрока.Партнер),Строка(НоваяСтрока.КонтактноеЛицо)) + " <" + Таб[0].Представление + ">";
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ЗапросПоРеспондентам = Новый Запрос;
		
		ЗапросПоРеспондентам.Текст = "ВЫБРАТЬ
		                             |	НазначениеОпросовРеспонденты.Респондент КАК Респондент
		                             |ИЗ
		                             |	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
		                             |ГДЕ
		                             |	НазначениеОпросовРеспонденты.Ссылка = &НазначениеОпросов
		                             |	И НЕ(НазначениеОпросовРеспонденты.Респондент В (&РеспондентыИсключения))";
									 
		
		Если ДанныеЗаполнения.РассылкаТолькоДляНовых Тогда
			
			ЗапросПоРеспондентам.УстановитьПараметр("РеспондентыИсключения", МассивРеспондентовИсключений);
			
		Иначе
			
			ЗапросПоРеспондентам.Текст = СтрЗаменить(ЗапросПоРеспондентам.Текст, "И НЕ(НазначениеОпросовРеспонденты.Респондент В (&РеспондентыИсключения))", "");
			
		КонецЕсли;				
									 
		ЗапросПоРеспондентам.УстановитьПараметр("НазначениеОпросов", Основание);
		
		ВыборкаПоРеспондентам = ЗапросПоРеспондентам.Выполнить().Выбрать();		
		
		Пока ВыборкаПоРеспондентам.Следующий() Цикл
			
			Если ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Пользователи") Тогда
				
				НоваяСтрока					= СвоиЛица.Добавить();
				НоваяСтрока.Пользователь	= ВыборкаПоРеспондентам.Респондент;
				
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(НоваяСтрока.Пользователь, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
				
				Если Таб.Количество() > 0 Тогда
					НоваяСтрока.Адрес = Таб[0].АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= Строка(НоваяСтрока.Пользователь) + " <" + Таб[0].Представление + ">";
				КонецЕсли;
				
			Иначе
				
				НоваяСтрока = Получатели.Добавить();
				
				Если ТипЗнч(Основание.ТипРеспондентов) = Тип("СправочникСсылка.Партнеры") Тогда
					
					НоваяСтрока.Партнер = ВыборкаПоРеспондентам.Респондент;
					НоваяСтрока.КонтактноеЛицо = ВыборкаПоРеспондентам.Респондент.CRM_ОсновноеКонтактноеЛицо;					
				
				Иначе
					
					НоваяСтрока.Партнер = ВыборкаПоРеспондентам.Респондент.Владелец;
					НоваяСтрока.КонтактноеЛицо = ВыборкаПоРеспондентам.Респондент;
					
				КонецЕсли;
				
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(НоваяСтрока.Партнер,
													?(ЗначениеЗаполнено(НоваяСтрока.КонтактноеЛицо),НоваяСтрока.КонтактноеЛицо,Неопределено),
													Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Неопределено);
				Если Таб.Количество() > 0 Тогда
					НоваяСтрока.Адрес = Таб[0].АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= ?(Таб[0].Ссылка = НоваяСтрока.Партнер,Строка(НоваяСтрока.Партнер),Строка(НоваяСтрока.КонтактноеЛицо)) + " <" + Таб[0].Представление + ">";
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	АдресWebДоступа = Константы.CRM_АдресWebДоступа.Получить();
	
	Если ПустаяСтрока(АдресWebДоступа) Тогда
	
		АдресWebДоступа = "{адрес web-доступа в настройках системы не заполнен}";
		
	КонецЕсли;
	
	Организация = CRM_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяОрганизация");
	
	НаименованиеОрганизации = ?(ПустаяСтрока(Организация.НаименованиеПолное), Организация.Наименование, Организация.НаименованиеПолное);
	
	Тема = "Приглашение к анкетированию";
	
	ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTML;
	
	ТекстHTML = Основание.ШаблонАнкеты.CRM_ШаблонРассылки.Текст;
	Текст = Взаимодействия.ПолучитьОбычныйТекстИзHTML(ТекстHTML);
	
КонецПроцедуры

Процедура ЗаполнитьПоМаркетинговойКампании(ДанныеЗаполнения)
	
	ВыбранныеАдресаЭП 	= Неопределено;
	СписокПартнеров		= Неопределено;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("МаркетинговаяКампания") Тогда
		
		Если ДанныеЗаполнения.Свойство("ВыбранныеАдресаЭП") Тогда
		 
		 	ВыбранныеАдресаЭП	= ДанныеЗаполнения.ВыбранныеАдресаЭП;
		 
		ИначеЕсли ДанныеЗаполнения.Свойство("СписокПартнеров") Тогда
			
		 	СписокПартнеров	= ДанныеЗаполнения.СписокПартнеров;
			
		КонецЕсли; 

		 ДанныеЗаполнения = ДанныеЗаполнения.МаркетинговаяКампания;
		 
	КонецЕсли;

	Тема					= ДанныеЗаполнения.Наименование;
	Ответственный			= ДанныеЗаполнения.Ответственный;
	Проект					= ДанныеЗаполнения.CRM_Проект;
	Подразделение			= ДанныеЗаполнения.CRM_Подразделение;
	Основание				= ДанныеЗаполнения;
	
	Если ЗначениеЗаполнено(ВыбранныеАдресаЭП) Тогда
	
		Для Каждого СтрокаТЧ Из ВыбранныеАдресаЭП Цикл
			НоваяСтрока = Получатели.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЧ);
			НоваяСтрока.Партнер			= СтрокаТЧ.Значение;
			НоваяСтрока.КонтактноеЛицо	= ДанныеЗаполнения.КонтактноеЛицо;
			
		КонецЦикла;
	
	ИначеЕсли ЗначениеЗаполнено(СписокПартнеров) Тогда
		
		мДобавленные = Новый Массив;
		Для Каждого элСп Из СписокПартнеров Цикл
			элСп = элСп.Значение;
			Если ЗначениеЗаполнено(элСп.Партнер) Тогда
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(элСп.Партнер, 
					?(ЗначениеЗаполнено(элСп.КонтактноеЛицо), элСп.КонтактноеЛицо, Неопределено),
					Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Неопределено);
				Если Таб.Количество() > 0 Тогда
					АдресЭП = Таб[0].АдресЭП;
					ПредставлениеАдреса	= ?(Таб[0].Ссылка = элСп.Партнер,Строка(элСп.Партнер),Строка(элСп.КонтактноеЛицо)) + " <" + Таб[0].Представление + ">";
					бДоб = мДобавленные.Найти(АдресЭП);									
					Если бДоб = Неопределено И ЗначениеЗаполнено(АдресЭП) Тогда
						мДобавленные.Добавить(АдресЭП);
						НоваяСтрока = Получатели.Добавить();
						НоваяСтрока.Партнер			= элСп.Партнер;
						НоваяСтрока.КонтактноеЛицо	= элСп.КонтактноеЛицо;
						НоваяСтрока.Адрес = АдресЭП;
						НоваяСтрока.ПредставлениеАдреса	= ПредставлениеАдреса;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		
	Иначе
	
		Для Каждого СтрокаТЧ Из ДанныеЗаполнения.ПартнерыИКонтактныеЛица Цикл
			НоваяСтрока = Получатели.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЧ);
			
			Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(НоваяСтрока.Партнер,
												?(ЗначениеЗаполнено(НоваяСтрока.КонтактноеЛицо),НоваяСтрока.КонтактноеЛицо,Неопределено),
												Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Неопределено);
			Если Таб.Количество() > 0 Тогда
				НоваяСтрока.Адрес = Таб[0].АдресЭП;
				НоваяСтрока.ПредставлениеАдреса	= ?(Таб[0].Ссылка = НоваяСтрока.Партнер,Строка(НоваяСтрока.Партнер),Строка(НоваяСтрока.КонтактноеЛицо)) + " <" + Таб[0].Представление + ">";
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаТЧ Из ДанныеЗаполнения.CRM_СвоиУчастники Цикл
			Если ТипЗнч(СтрокаТЧ.Участник) = Тип("СправочникСсылка.Пользователи") Тогда
				
				НоваяСтрока					= СвоиЛица.Добавить();
				НоваяСтрока.Пользователь	= СтрокаТЧ.Участник;
				
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(НоваяСтрока.Пользователь, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
				
				Если Таб.Количество() > 0 Тогда
					НоваяСтрока.Адрес = Таб[0].АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= Строка(НоваяСтрока.Пользователь) + " <" + Таб[0].Представление + ">";
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(СтрокаТЧ.Участник) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				
				НоваяСтрока			= ФизЛица.Добавить();
				НоваяСтрока.ФизЛицо	= СтрокаТЧ.Участник;
				
				Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(НоваяСтрока.ФизЛицо, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
				
				Если Таб.Количество() > 0 Тогда
					НоваяСтрока.Адрес = Таб[0].АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= Строка(НоваяСтрока.ФизЛицо) + " <" + Таб[0].Представление + ">";
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли; 
		
КонецПроцедуры

Процедура ЗаполнитьПоСпискуПартнеров(ДанныеЗаполнения)
	
	ДанныеЗаполнения = ДанныеЗаполнения.СписокПартнеров;
	мДобавленные = Новый Массив;
	Для каждого элСп Из ДанныеЗаполнения Цикл
		элСп = элСп.Значение;
		Если ЗначениеЗаполнено(элСп.Партнер) Тогда

			Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(элСп.Партнер,
												?(ЗначениеЗаполнено(элСп.КонтактноеЛицо),элСп.КонтактноеЛицо,Неопределено),
												Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Неопределено);
												
			Если Таб.Количество() > 0 Тогда
					
				АдресЭП = Таб[0].АдресЭП;
				ПредставлениеАдреса	= ?(Таб[0].Ссылка = элСп.Партнер,Строка(элСп.Партнер),Строка(элСп.КонтактноеЛицо)) + " <" + Таб[0].Представление + ">";
				бДоб = мДобавленные.Найти(АдресЭП);									
				Если бДоб = Неопределено И ЗначениеЗаполнено(АдресЭП) Тогда
					
					мДобавленные.Добавить(АдресЭП);
					НоваяСтрока = Получатели.Добавить();
					НоваяСтрока.Партнер			= элСп.Партнер;
					НоваяСтрока.КонтактноеЛицо	= элСп.КонтактноеЛицо;
					НоваяСтрока.Адрес = АдресЭП;
					НоваяСтрока.ПредставлениеАдреса	= ПредставлениеАдреса;
					
				КонецЕсли;
					
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла; 

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Кодировка) Тогда
		Кодировка = "utf-8";
	КонецЕсли;
	
	CRM_ОбщегоНазначенияСервер.ЗаполнитьШапкуДокумента(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.МаркетинговыеМероприятия") 
		ИЛИ (ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("МаркетинговаяКампания")) Тогда
		ЗаполнитьПоМаркетинговойКампании(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание") 
		И ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.НазначениеОпросов") Тогда
		
		ЗаполнитьПоНазначениюОпросов(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("СписокПартнеров") Тогда
		ЗаполнитьПоСпискуПартнеров(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Партнер") Тогда
		СтруктураПартнера = Новый Структура;
		СтруктураПартнера.Вставить("Партнер",			ДанныеЗаполнения.Партнер);
		СтруктураПартнера.Вставить("КонтактноеЛицо",	Неопределено);
		СписокПартнеров = Новый СписокЗначений;
		СписокПартнеров.Добавить(СтруктураПартнера);
		ДанныеЗаполнения.Вставить("СписокПартнеров", СписокПартнеров);
		ЗаполнитьПоСпискуПартнеров(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс")
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс") Тогда
			CRM_ОбщегоНазначенияСервер.ЗаполнитьОбъектБизнесПроцесса(ЭтотОбъект, ДанныеЗаполнения);
		Иначе
			CRM_ОбщегоНазначенияСервер.ЗаполнитьОбъектБизнесПроцесса(ЭтотОбъект, ДанныеЗаполнения.БизнесПроцесс, ДанныеЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("УчетнаяЗапись") Тогда
		
		УчетнаяЗапись = ДанныеЗаполнения.УчетнаяЗапись;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Проект") Тогда
		
		Проект = ДанныеЗаполнения.Проект;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Ответственный") Тогда
		
		Ответственный = ДанныеЗаполнения.Ответственный;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Тема") Тогда
		
		Тема = ДанныеЗаполнения.Тема;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Важность") Тогда
		
		Важность = ДанныеЗаполнения.Важность;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ЕстьВложения") Тогда
		
		Важность = ДанныеЗаполнения.ЕстьВложения;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипТекста) Тогда
		ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTML;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Прикрепленные файлы при копировании.
	Если ОбъектКопирования.ЕстьВложения Тогда
		Основание = ОбъектКопирования.Ссылка;
	КонецЕсли;	
	
	// Заполним автора и ответственного.
	CRM_ОбщегоНазначенияСервер.ЗаполнитьАвтораИОтветственного(ЭтотОбъект);
	
	Завершена 	  = Ложь;
	ЕстьИзменения = Ложь;
	
КонецПроцедуры

#КонецЕсли