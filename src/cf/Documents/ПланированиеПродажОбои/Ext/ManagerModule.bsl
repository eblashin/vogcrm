//+++ Терпогосян Д.Б. [17.08.2021 11:50:49] № DEV-874
Процедура ПодготовитьДанныеВФоне(Параметры, АдресХранилища) Экспорт
	Результат = СформироватьОтчет(Параметры);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
КонецПроцедуры

Функция СформироватьОтчет(Знач Параметры) Экспорт
	Результат = Новый Структура("ТабличныйДокумент, Расшифровка, ТекстОшибки, Успех");
	
	Результат.Успех = Ложь;
	Результат.ТабличныйДокумент = Новый ТабличныйДокумент;
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	МакетКомпоновкиДанных 	= КомпоновщикМакета.Выполнить(Параметры.Схема,Параметры.Настройки, ДанныеРасшифровки, Параметры.МакетОформления,Тип("ГенераторМакетаКомпоновкиДанных"));
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,ДанныеРасшифровки,Истина,Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат.ТабличныйДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Результат.Расшифровка = ДанныеРасшифровки; 
	
	СохраненныеРасшифровкиЯчеек = Новый ТаблицаЗначений;
	СохраненныеРасшифровкиЯчеек.Колонки.Добавить("ИмяОбласти");
	СохраненныеРасшифровкиЯчеек.Колонки.Добавить("ИДРасшифровки");
	Для НомерСтроки = 1 по Результат.ТабличныйДокумент.ВысотаТаблицы цикл
		Для НомерСтолбца = 1 по Результат.ТабличныйДокумент.ШиринаТаблицы цикл
			ИмяОбласти = "R"+Формат(НомерСтроки,"ЧГ=0")+"C"+Формат(НомерСтолбца,"ЧГ=0");
			СтрокаД = СохраненныеРасшифровкиЯчеек.Добавить();
			СтрокаД.ИДРасшифровки = Результат.ТабличныйДокумент.Область(ИмяОбласти).Расшифровка;
			СтрокаД.ИмяОбласти = ИмяОбласти;
		КонецЦикла;
	КонецЦикла;
	СохраненныеРасшифровкиЯчеек.Индексы.Добавить("ИмяОБласти");
	Результат.Вставить("Расшифровка1", СохраненныеРасшифровкиЯчеек);
	
	Результат.Успех = Истина;
	
	Возврат Результат;
КонецФункции
//--- Терпогосян Д.Б. [17.08.2021 11:50:50] № DEV-874 

