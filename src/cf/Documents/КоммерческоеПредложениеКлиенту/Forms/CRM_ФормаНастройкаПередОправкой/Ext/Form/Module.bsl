
&НаКлиенте
Процедура КнопкаОК(Команда)
	
	Если НЕ ФорматODT И НЕ ФорматDOC Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо указать как минимум один из предложенных форматов.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ЗапаковатьZIP", ЗапаковатьZIP);
	Результат.Вставить("ФорматODT", ФорматODT);
	Результат.Вставить("ФорматDOC", ФорматDOC);
	Результат.Вставить("МакетMS", МакетMS);
	Результат.Вставить("МакетOpenOffice", МакетOpenOffice);
	Результат.Вставить("НомерВарианта", НомерВарианта);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивМакетов = CRM_ОбщегоНазначенияСервер.ПолучитьПользовательскиеМакеты("Документ.КоммерческоеПредложениеКлиенту");
	
	Элементы.МакетMS.СписокВыбора.Добавить("CRM_ПФ_DOC_КоммерческоеПредложение","Коммерческое предложение (Microsoft Word)");
	Элементы.МакетOpenOffice.СписокВыбора.Добавить("CRM_ПФ_ODT_КоммерческоеПредложение","Коммерческое предложение (OpenOffice.org Writer)");
	
	Для Каждого СтруктураМакета Из МассивМакетов Цикл
		Если СтрНайти(СтруктураМакета.ИмяМакета, "ПФ_DOC_") > 0 Тогда
			Элементы.МакетMS.СписокВыбора.Добавить(СтруктураМакета.ИмяМакета, СтруктураМакета.CRM_Представление);
		КонецЕсли;
		
		Если СтрНайти(СтруктураМакета.ИмяМакета, "ПФ_ODT_") > 0 Тогда
			Элементы.МакетOpenOffice.СписокВыбора.Добавить(СтруктураМакета.ИмяМакета, СтруктураМакета.CRM_Представление);
		КонецЕсли;
	КонецЦикла;
	
	МакетMS			= "CRM_ПФ_DOC_КоммерческоеПредложение";
	МакетOpenOffice	= "CRM_ПФ_ODT_КоммерческоеПредложение";
	
	СписокВариантов.Очистить();
	Элементы.ВариантПредставление.СписокВыбора.Очистить();
	ТекВариант = "";
	Для Каждого ЭлементСписка Из Параметры.Варианты Цикл
		СписокВариантов.Добавить(ЭлементСписка.Значение,ЭлементСписка.Представление);
		Элементы.ВариантПредставление.СписокВыбора.Добавить(ЭлементСписка.Представление,ЭлементСписка.Представление);
		Если ТекВариант = "" Тогда
			ТекВариант		= ЭлементСписка.Представление;
			НомерВарианта	= ЭлементСписка.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ВариантПредставление = ТекВариант;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПредставлениеПриИзменении(Элемент)
	
	Для Каждого ЭлементСписка Из СписокВариантов Цикл
		Если НЕ ЭлементСписка.Представление = ВариантПредставление Тогда Продолжить; КонецЕсли;
		
		НомерВарианта	= ЭлементСписка.Значение;
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры
