
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.CRM_Интерес") Тогда
		ДокументОснование	= ДанныеЗаполнения.Ссылка;
		КонтактноеЛицо		= ДанныеЗаполнения.КонтактноеЛицо;
		//ОжидаемаяВыручка	= ДанныеЗаполнения.ОжидаемаяВыручка;
		Описание			= ДанныеЗаполнения.Описание;
		Партнер				= ДанныеЗаполнения.Партнер;
		ПотенциальныйКлиент	= ДанныеЗаполнения.ПотенциальныйКлиент;
		ТипУслуги			= ДанныеЗаполнения.ТипУслуги;
		Тема				= ДанныеЗаполнения.Тема;
		СостояниеИнтереса	= Справочники.CRM_СостоянияИнтересов.ВыявлениеПотребностей;
		ВероятностьСделки	= Справочники.CRM_СостоянияИнтересов.ВыявлениеПотребностей.ВероятностьСделки;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Партнеры") Тогда
		Если ДанныеЗаполнения.Ссылка.ЭтоГруппа Тогда
			СтандартнаяОбработка = Ложь;	
			Возврат;
		КонецЕсли;	
		Партнер				= ДанныеЗаполнения.Ссылка;		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Автор") Тогда
			Автор = ДанныеЗаполнения.Автор;
		КонецЕсли;
		Дата = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса();
		Если ДанныеЗаполнения.Свойство("Партнер") Тогда
			Партнер = ДанныеЗаполнения.Партнер;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("КонтактноеЛицо") Тогда
			Если ЗначениеЗаполнено(ДанныеЗаполнения.КонтактноеЛицо) Тогда
				КонтактноеЛицо = ДанныеЗаполнения.КонтактноеЛицо;
			ИначеЕсли ЗначениеЗаполнено(Партнер) Тогда
				Если  ЗначениеЗаполнено(Партнер.CRM_ОсновноеКонтактноеЛицо) Тогда
					КонтактноеЛицо = Партнер.CRM_ОсновноеКонтактноеЛицо; 
				Иначе
					КонтактноеЛицо = CRM_МетодыМодулейМенеджеровДокументов.ВернутьКЛПартнера(Партнер);;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("ПотенциальныйКлиент") Тогда
			ПотенциальныйКлиент = ДанныеЗаполнения.ПотенциальныйКлиент;
		КонецЕсли;		
		Если ДанныеЗаполнения.Свойство("Описание") Тогда
			Описание = ДанныеЗаполнения.Описание;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Организация") Тогда
			Организация = ДанныеЗаполнения.Организация;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Ответственный") Тогда
			Ответственный = ДанныеЗаполнения.Ответственный;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Подразделение") Тогда
			Подразделение = ДанныеЗаполнения.Подразделение;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Офис") Тогда
			Офис = ДанныеЗаполнения.Офис;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СостояниеИнтереса") Тогда
			СостояниеИнтереса = ДанныеЗаполнения.СостояниеИнтереса;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СостояниеИнтереса") Тогда
			СостояниеИнтереса = ДанныеЗаполнения.СостояниеИнтереса;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Тема") Тогда
			Тема = ДанныеЗаполнения.Тема;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("ТипУслуги") Тогда
			ТипУслуги = ДанныеЗаполнения.ТипУслуги;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Если ЗначениеЗаполнено(ДанныеЗаполнения.БизнесПроцесс) Тогда
			Партнер			= ДанныеЗаполнения.БизнесПроцесс.Партнер;
			КонтактноеЛицо	= ДанныеЗаполнения.БизнесПроцесс.КонтактноеЛицо;
			Организация		= ДанныеЗаполнения.БизнесПроцесс.Организация;
			Ответственный	= ДанныеЗаполнения.БизнесПроцесс.Ответственный;
			Подразделение	= ДанныеЗаполнения.БизнесПроцесс.Подразделение;
		КонецЕсли;
		
		Дата = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса();
		Тема = ДанныеЗаполнения.Наименование;
		Описание = ДанныеЗаполнения.Описание;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	CRM_Взаимодействие.Содержание,
		|	CRM_Взаимодействие.ПлановаяДата
		|ИЗ
		|	Документ.CRM_Взаимодействие КАК CRM_Взаимодействие
		|ГДЕ
		|	CRM_Взаимодействие.ДокументОснование = &Ссылка
		|	И CRM_Взаимодействие.ДатаЗавершенияВзаимодействия = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	CRM_Взаимодействие.ПлановаяДата";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СледующееДействиеПредставление = Формат(Выборка.ПлановаяДата,"ДФ=dd.MM.yyyy") + " | " + Выборка.Содержание;
	КонецЕсли;
	
	Если Ссылка.ПометкаУдаления <> ПометкаУдаления Тогда
		ИзменитьПометкуУдаленияВзаимодействий();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СостояниеИнтереса) И ВероятностьСделки <> СостояниеИнтереса.ВероятностьСделки Тогда
		ВероятностьСделки = СостояниеИнтереса.ВероятностьСделки;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ЭтоНовый = Ложь;
	Если ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый) И ЭтоНовый И Автор <> Ответственный Тогда
		CRM_НапоминанияСервер.СоздатьНапоминаниеОНовомИнтересе(Ссылка);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура проставляет пометку удаления всем связанным с Интересом Взаимодействиям при ее изменении у Интереса.
//
Процедура ИзменитьПометкуУдаленияВзаимодействий()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	CRM_Взаимодействие.Ссылка
		|ИЗ
		|	Документ.CRM_Взаимодействие КАК CRM_Взаимодействие
		|ГДЕ
		|	CRM_Взаимодействие.ДокументОснование = &ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
	
	
	СписокВзаимодействий = Запрос.Выполнить().Выбрать();
	
	Пока СписокВзаимодействий.Следующий() Цикл
		ВзаимодействиеОбъект = СписокВзаимодействий.Ссылка.ПолучитьОбъект();
		ВзаимодействиеОбъект.ПометкаУдаления = ПометкаУдаления;
		ВзаимодействиеОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик при копировании Интереса.
Процедура ПриКопировании(ОбъектКопирования)
	
	Пользователь 					= ПараметрыСеанса.ТекущийПользователь;
 
 	Ответственный 					= CRM_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");  	
	Ответственный 					= ?(ЗначениеЗаполнено(Ответственный), Ответственный, Пользователь);
	 
	Автор 							= Пользователь;
	
	ВероятностьСделки 				= 0;
	ОжидаемаяВыручка   				= 0;
	ДатаСледующегоДействия 			= Дата(1, 1, 1);
	ДатаЗакрытия  					= Дата(1, 1, 1);

	ДокументОснование 				= Неопределено;		
	ПричинаОтказа  					= Неопределено;
	СостояниеИнтереса  				= Неопределено;
	
	СледующееДействиеПредставление 	= "";
	Завершен     					= Ложь;
	
	CRM_Теги.Очистить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли