
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("МодельКейса") Тогда
		МодельКейса = Параметры.МодельКейса;
	КонецЕсли;
	Элементы.Проект.ПодсказкаВвода = Строка(МодельКейса);
	
	Если Параметры.Свойство("МассивКлиентов") Тогда
		Для каждого Клиент Из Параметры.МассивКлиентов Цикл
			СтрокаТЧ = ТаблицаКлиентов.Добавить();
			Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Партнеры") Тогда
				СтрокаТЧ.Партнер = Клиент;
			ИначеЕсли ТипЗнч(Клиент) = Тип("СправочникСсылка.вогТорговыеТочки") Тогда
				СтрокаТЧ.ТорговаяТочка = Клиент;
				СтрокаТЧ.Партнер 	   = Клиент.Партнер;
				
			КонецЕсли;
			
		КонецЦикла;	
	
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТаблицаКлиентов.Количество() > 0 Тогда
		Для каждого СтрокаТЧ Из ТаблицаКлиентов Цикл
			Менеджер = РегистрыСведений.вогПартнерыПроектов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Менеджер, СтрокаТЧ);
			
			Менеджер.Проект = Объект.Проект;
			Менеджер.Этап   = Объект.Ссылка;
			
			Менеджер.Записать();
			
		КонецЦикла;
	
	КонецЕсли;
	
	//Добавление этапа
	CRM_УправлениеПроектамиСервер.ДобавитьЭтапКонтрольнуюТочкуВКейс(Объект.Проект, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ОткрытьЭтап Тогда
		ОткрытьФорму("Документ.CRM_ЭтапКалендарногоПлана.ФормаОбъекта",
			Новый Структура("Ключ", Объект.Ссылка));
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПроектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"       , Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыФормы.Вставить("CRM_ЭтоКейс"       , Истина);
	
	Если ЗначениеЗаполнено(МодельКейса) Тогда
		ПараметрыФормы.Вставить("МодельКейса", МодельКейса);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Проекты.Форма.CRM_ФормаСпискаКейсы", 
		ПараметрыФормы, Элемент, УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура БизнесРегионПриИзменении(Элемент)
	СформироватьНаименование();	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	СформироватьНаименование();	
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗакрытияПриИзменении(Элемент)
	СформироватьНаименование();	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура СформироватьНаименование()
	
	ТекстНаименования = НСтр("ru = '%МодельКейса% - %БизнесРегион%: (%НачалоПериода% - %КонецПериода%)'");
	ТекстНаименования = СтрЗаменить(ТекстНаименования, "%МодельКейса%" , СокрЛП(МодельКейса));
	ТекстНаименования = СтрЗаменить(ТекстНаименования, "%БизнесРегион%", СокрЛП(Строка(Объект.БизнесРегион)));
	ТекстНаименования = СтрЗаменить(ТекстНаименования, "%НачалоПериода%", СокрЛП(Строка(Формат(Объект.Дата, "ДФ=dd.MM.yyyy"))));
	ТекстНаименования = СтрЗаменить(ТекстНаименования, "%КонецПериода%", СокрЛП(Строка(Формат(Объект.ДатаЗакрытия, "ДФ=dd.MM.yyyy"))));
	
	Объект.Тема = ТекстНаименования;
	
КонецПроцедуры // СформироватьНаименование()

#КонецОбласти

