
///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
// Функция возвращает контакт по параметру команды
//
// Параметры:
//	ПараметрКоманды	- Произвольный	- Параметр команды
//
// Возвращаемое значение:
//	СправочникСсылка	- Контакт
//
Функция ПолучитьКонтакт(ПараметрКоманды)
	Контакт = Неопределено;
	Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда		
		Контакт = CRM_УправлениеЭлектроннойПочтой.ЭлектронноеПисьмоИсходящееПолучитьМассивКонтрагентов(ПараметрКоманды);
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда		
		ОтправительПисьма = ПараметрКоманды.ОтправительКонтакт;
		Если ЗначениеЗаполнено(ОтправительПисьма) И ТипЗнч(ОтправительПисьма) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			Контакт = CRM_УправлениеЭлектроннойПочтой.ЭлектронноеПисьмоВходящееПолучитьКонтактноеЛицо(ПараметрКоманды);
		Иначе
			Контакт = CRM_УправлениеЭлектроннойПочтой.ЭлектронноеПисьмоВходящееПолучитьКонтрагента(ПараметрКоманды);
		КонецЕсли;
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Партнеры") Тогда
		Контакт = ПараметрКоманды;
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
		Контакт = ПараметрКоманды;
	КонецЕсли;
	Возврат Контакт;	
КонецФункции // ПолучитьКонтакт()

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДЫ

&НаКлиенте
// Процедура - обработчик команды
//
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если CRM_ОбщегоНазначенияКлиент.смсПроверитьДоступностьSMS4B() Тогда
		Контакт = ПолучитьКонтакт(ПараметрКоманды[0]);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрКоманды",				ПараметрКоманды);
		ДополнительныеПараметры.Вставить("ПараметрыВыполненияКоманды",	ПараметрыВыполненияКоманды);
		Если ЗначениеЗаполнено(Контакт) Тогда
			// Если Открыли из списка КЛ, покажем только тек. КЛ
			ОдноКонтактноеЛицо = ?(ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров"), Истина, Ложь);
			ПараметрыФормы = Новый Структура("Контакт, Тип, Вид, ОдноКонтактноеЛицо", Контакт, ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон"), "СМС", ОдноКонтактноеЛицо);
			ПараметрыФормы.Вставить("КлючНазначенияИспользования",	"СозданиеДокументаСМС");
			ОписаниеВыбора = Новый ОписаниеОповещения("ОткрытьФормуДокументаСМС", CRM_ОбщегоНазначенияКлиент, ДополнительныеПараметры);
			ОткрытьФорму("ОбщаяФорма.CRM_ФормаВыбораКонтактнойИнформации", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеВыбора,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			CRM_ОбщегоНазначенияКлиент.ОткрытьФормуДокументаСМС(Неопределено, ДополнительныеПараметры);
			// В случае ввода на основании письма выводим сообщение
			Если ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") 
				ИЛИ ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru = 'Данное письмо не связано с контактом'"));
			КонецЕсли		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаКоманды()
