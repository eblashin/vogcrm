
////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Процедура устанавливает видимость кнопки "Отправить"
//
// Параметры:
//	Нет.
//
Процедура УстановитьВидимостьОтправить()
	Если ОтправкаSMSЧерезSMS4B.РолиДоступны("ОтправкаSMS, ПолныеПрава") Тогда
		УстановитьПривилегированныйРежим(Истина); 
		Задание	= РегламентныеЗадания.НайтиПредопределенное("ОтправкаSMS");
		Если Задание = Неопределено Тогда
			Элементы.ОтправитьПолучить.Видимость = Истина;
		Иначе	
			Элементы.ОтправитьПолучить.Видимость = НЕ Задание.Использование;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь); 
	Иначе
		Элементы.ОтправитьПолучить.Видимость	= Ложь;
		Элементы.НастройкиSMS4B.Видимость		= Ложь;
	КонецЕсли;	
КонецПроцедуры // УстановитьВидимостьОтправить()

//////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "ПриАктивизацииСтроки" элемента формы "СостоянияСМС"
//
Процедура СостоянияСМСПриАктивизацииСтроки(Элемент)
    ТекущиеДанные =  Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	Состояние = ТекущиеДанные.Состояние;
	ЭлементыСпискаОтбора = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для Каждого ТекущийОтбор Из ЭлементыСпискаОтбора Цикл
        Если ТекущийОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
            ТекущийОтбор.Использование = Ложь;
        КонецЕсли;
    КонецЦикла;        
    Если Состояние = "Входящие"  Тогда
        ЭлементыСпискаОтбора[0].Использование = Истина;
    ИначеЕсли Состояние = "Получено" Тогда
        ЭлементыСпискаОтбора[0].Использование = Истина;
        ЭлементыСпискаОтбора[2].Использование = Истина;
	ИначеЕсли Состояние = "Получено частично"  Тогда
        ЭлементыСпискаОтбора[0].Использование = Истина;
        ЭлементыСпискаОтбора[3].Использование = Истина;
    ИначеЕсли Состояние = "Исходящие"  Тогда
        ЭлементыСпискаОтбора[1].Использование = Истина;
    ИначеЕсли Состояние = "Отправляется"  Тогда
        ЭлементыСпискаОтбора[1].Использование = Истина;
        ЭлементыСпискаОтбора[4].Использование = Истина;
    ИначеЕсли Состояние = "Не отправлено"  Тогда
        ЭлементыСпискаОтбора[1].Использование = Истина;
        ЭлементыСпискаОтбора[5].Использование = Истина;
    ИначеЕсли Состояние = "Доставлено"  Тогда
        ЭлементыСпискаОтбора[1].Использование = Истина;
        ЭлементыСпискаОтбора[6].Использование = Истина;
	ИначеЕсли Состояние = "Не доставлено"  Тогда
        ЭлементыСпискаОтбора[1].Использование = Истина;
        ЭлементыСпискаОтбора[7].Использование = Истина;
    КонецЕсли;
КонецПроцедуры // СостоянияСМСПриАктивизацииСтроки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ КОМАНД ФОРМЫ

&НаКлиенте
// Процедура - обработчик команды формы "ОтправитьПолучитьСМС"
//
Процедура ОтправитьПолучитьСМС(Команда)
	Если НЕ ОтправкаSMSЧерезSMS4B.РолиДоступны("ОтправкаSMS, ПолныеПрава") Тогда
		ОтправкаSMSЧерезSMS4B.ВывестиСообщение(НСтр("ru = 'Недостаточно прав для отправки SMS сообщений!'"), СтатусСообщения.Внимание, , , Истина);
	ИначеЕсли НЕ ОтправкаSMSЧерезSMS4B.НастройкаОтправкиSMSВыполнена() Тогда
		ОтправкаSMSЧерезSMS4B.ВывестиСообщение(НСтр("ru = 'Не заполнены настройки отправки SMS сообщений!'"), СтатусСообщения.Внимание, , , Истина);
	Иначе
		ОтправкаSMSЧерезSMS4B.РегламентноеЗаданиеПроверкаСМС();
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры // ОтправитьПолучитьСМС()

&НаКлиенте
// Процедура - обработчик команды формы "смсНастройкиSMS4B"
//
// Параметры:
//	Элемент	- ЭлементФормы	- Элемент формы
//
Процедура НастройкиSMS4B(Команда)
	ОткрытьФорму("ОбщаяФорма.sms4bФормаНастроек", Новый Структура, ЭтотОбъект);
КонецПроцедуры // НастройкиSMS4B()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    // Заполняем дерево
    ДеревоСостояний = СостоянияСМССообщений.ПолучитьЭлементы();
    ДеревоСостояний.Очистить();
	НоваяСтрока = ДеревоСостояний.Добавить();
	НоваяСтрока.Состояние = "Все";
    НоваяСтрока.ИндексКартинки = 3;
    Входящие = ДеревоСостояний.Добавить();
	Входящие.Состояние = "Входящие";
    Входящие.ИндексКартинки = 0;
    Входящие1 = Входящие.ПолучитьЭлементы();
	НоваяСтрока = Входящие1.Добавить();
	НоваяСтрока.Состояние = Строка(Перечисления.СостоянияДокументаСообщениеSMS.sms4bПолучено);
    НоваяСтрока.ИндексКартинки = 3;
	НоваяСтрока = Входящие1.Добавить();
	НоваяСтрока.Состояние = Строка(Перечисления.СостоянияДокументаСообщениеSMS.sms4bПолученоЧастично);
    НоваяСтрока.ИндексКартинки = 3;
    Исходящие = ДеревоСостояний.Добавить();
	Исходящие.Состояние = "Исходящие";
    Исходящие.ИндексКартинки = 1;
    Исходящие.ПолучитьЭлементы();
    Исходящие1 = Исходящие.ПолучитьЭлементы();
	НоваяСтрока = Исходящие1.Добавить();
	НоваяСтрока.Состояние = "Отправляется";
    НоваяСтрока.ИндексКартинки = 3;
	НоваяСтрока = Исходящие1.Добавить();
	НоваяСтрока.Состояние = Строка(Перечисления.СостоянияДокументаСообщениеSMS.sms4bНеОтправлено);
    НоваяСтрока.ИндексКартинки = 3;
	НоваяСтрока = Исходящие1.Добавить();
	НоваяСтрока.Состояние = Строка(Перечисления.СостоянияДокументаСообщениеSMS.Доставлено);
    НоваяСтрока.ИндексКартинки = 3;
	НоваяСтрока = Исходящие1.Добавить();
	НоваяСтрока.Состояние = Строка(Перечисления.СостоянияДокументаСообщениеSMS.НеДоставлено);
    НоваяСтрока.ИндексКартинки = 3;
	// Устанавливаем видимость кнопки "Отправить"
	УстановитьВидимостьОтправить();
	
	Элементы.НастройкиSMS4B.Видимость = РольДоступна("ПолныеПрава") ИЛИ РольДоступна("CRM_НастройкаПрограммы");

	CRM_МодификацияКонфигурацииПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события формы "ОбработкаОповещения"
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_НастройкиОтправкиSMS" Тогда
		// Устанавливаем видимость кнопки "Отправить"
		УстановитьВидимостьОтправить();
	КонецЕсли;	
КонецПроцедуры // ОбработкаОповещения()
