
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаДанныеПланированияПродажПлиткаКвартал(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПланированиеПосещений(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаКомментарииКПланированиюПродажПлитка(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДанныеПланированияМаркетинг(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИнициализироватьТаблицыДляДвижений(
		Запрос,
		ТекстыЗапроса,
		ДополнительныеСвойства.ТаблицыДляДвижений,
		Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	АвторДокумента 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Автор");
	ПодразделениеАвтора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АвторДокумента, "Подразделение");
	ОбособленноеПодразделение = Справочники.СтруктураПредприятия.ПолучитьОбособленноеПодразделение(ПодразделениеАвтора);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", 		ДокументСсылка);
	Запрос.УстановитьПараметр("Подразделение", 	ОбособленноеПодразделение);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДанныеПланированияПродажПлиткаКвартал(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДанныеПланированияПродажПлиткаКвартал";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.Дата КАК Период,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка КАК Регистратор,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.СценарийПланирования КАК СценарийПланирования,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.ВерсияСценария КАК Версия,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.ПериодПланирования КАК ПериодПланирования,
	|	&Подразделение КАК Подразделение,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.НомерСтроки КАК НомерСтроки,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ТорговаяТочка КАК ТорговаяТочка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КомплектПанелей КАК КомплектПанелей,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Бренд КАК Бренд,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КорректировкаКоличествоВОФактУПП КАК КорректировкаКоличествоВОФактУПП,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ПланВыставленияСледующийКвартал КАК ПланВыставленияСледующийКвартал,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.РотацияПанелей КАК РотацияПанелей,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КоличествоШоуРум КАК КоличествоШоуРум,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.СтоимостьШоуРум КАК СтоимостьШоуРум,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КоличествоПланСледующийКвартал КАК КоличествоПланСледующийКвартал,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КоличествоУценка КАК КоличествоУценка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ПроцентУценка КАК ПроцентУценка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.СтоимостьУценка КАК СтоимостьУценка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингАкция КАК МерчендайзингАкция,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингIndoor КАК МерчендайзингIndoor,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингOutdoor КАК МерчендайзингOutdoor,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.Автор КАК Автор,
	|	ЗНАЧЕНИЕ(Перечисление.видыПотенциальнойТорговойТочки.ПустаяСсылка) КАК ВидПотенциальнойТорговойТочки,
	|	ЗНАЧЕНИЕ(Справочник.вогНаселенныеПункты.ПустаяСсылка) КАК Город,
	|	NULL КАК Клиент
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.ДанныеПланированияКвартал КАК ПланированиеПродажПлиткаДанныеПланированияКвартал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КомплектыПанелейДляПланирования КАК КомплектыПанелейДляПланирования
	|		ПО ПланированиеПродажПлиткаДанныеПланированияКвартал.КомплектПанелей = КомплектыПанелейДляПланирования.Ссылка
	|ГДЕ
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка = &Ссылка
	|	И ПланированиеПродажПлиткаДанныеПланированияКвартал.ТорговаяТочка ССЫЛКА Справочник.вогТорговыеТочки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.Дата,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.СценарийПланирования,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.ВерсияСценария,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.ПериодПланирования,
	|	&Подразделение,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.НомерСтроки,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ТорговаяТочка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КомплектПанелей,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Бренд,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КорректировкаКоличествоВОФактУПП,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ПланВыставленияСледующийКвартал,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.РотацияПанелей,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КоличествоШоуРум,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.СтоимостьШоуРум,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КоличествоПланСледующийКвартал,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.КоличествоУценка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ПроцентУценка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.СтоимостьУценка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингАкция,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингIndoor,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингOutdoor,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.Автор,
	|	ДанныеПланированияПродажПлиткаКвартал.ВидПотенциальнойТорговойТочки,
	|	ДанныеПланированияПродажПлиткаКвартал.Город,
	|	ДанныеПланированияПродажПлиткаКвартал.Клиент
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.ДанныеПланированияКвартал КАК ПланированиеПродажПлиткаДанныеПланированияКвартал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КомплектыПанелейДляПланирования КАК КомплектыПанелейДляПланирования
	|		ПО ПланированиеПродажПлиткаДанныеПланированияКвартал.КомплектПанелей = КомплектыПанелейДляПланирования.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПланированияПродажПлиткаКвартал КАК ДанныеПланированияПродажПлиткаКвартал
	|		ПО ПланированиеПродажПлиткаДанныеПланированияКвартал.ТорговаяТочка = ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка
	|			И (НЕ ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка ССЫЛКА Справочник.вогТорговыеТочки)
	|			И (ДанныеПланированияПродажПлиткаКвартал.Регистратор ССЫЛКА Документ.ПланированиеПродажПоПотенциальнымТорговымТочкамПлитка)
	|ГДЕ
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПланированиеПосещений(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПланированиеПосещений";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.Дата КАК Период,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка) КАК НаправлениеДеятельности,
	|	ПланированиеПродажПлиткаКомандировки.Владелец КАК Партнер,
	|	ЗНАЧЕНИЕ(Справочник.вогТорговыеТочки.ПустаяСсылка) КАК ТорговаяТочка,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.ПериодПланирования КАК ПериодПланирования,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.СценарийПланирования КАК СценарийПланирования,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.ВерсияСценария КАК Версия,
	|	ПланированиеПродажПлиткаКомандировки.КомандировкаМесяц1 КАК Количество1Месяц,
	|	ПланированиеПродажПлиткаКомандировки.КомандировкаМесяц2 КАК Количество2Месяц,
	|	ПланированиеПродажПлиткаКомандировки.КомандировкаМесяц3 КАК Количество3Месяц,
	|	ПланированиеПродажПлиткаКомандировки.ЗадачиПоДостижениюЦели КАК ЗадачиПоДостижениюЦели,
	// +++ VOG Кулаков П.Л. 22.06.2021 DEV-675
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПотенциальнойТорговойТочки.ПустаяСсылка) КАК ВидПотенциальнойТорговойТочки
	// --- VOG Кулаков П.Л.
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.Командировки КАК ПланированиеПродажПлиткаКомандировки
	|ГДЕ
	|	ПланированиеПродажПлиткаКомандировки.Ссылка = &Ссылка
	|	И ПланированиеПродажПлиткаКомандировки.Владелец ССЫЛКА Справочник.Партнеры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.Дата,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка),
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка),
	|	ПланированиеПродажПлиткаКомандировки.Владелец,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.ПериодПланирования,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.СценарийПланирования,
	|	ПланированиеПродажПлиткаКомандировки.Ссылка.ВерсияСценария,
	|	ПланированиеПродажПлиткаКомандировки.КомандировкаМесяц1,
	|	ПланированиеПродажПлиткаКомандировки.КомандировкаМесяц2,
	|	ПланированиеПродажПлиткаКомандировки.КомандировкаМесяц3,
	|	ПланированиеПродажПлиткаКомандировки.ЗадачиПоДостижениюЦели,
	|	ЕСТЬNULL(ДанныеПланированияПродажПлиткаКвартал.ВидПотенциальнойТорговойТочки, ЗНАЧЕНИЕ(Перечисление.ВидыПотенциальнойТорговойТочки.ПустаяСсылка))
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.Командировки КАК ПланированиеПродажПлиткаКомандировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПланированияПродажПлиткаКвартал КАК ДанныеПланированияПродажПлиткаКвартал
	|		ПО ПланированиеПродажПлиткаКомандировки.Владелец = ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка
	|			И (НЕ ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка ССЫЛКА Справочник.вогТорговыеТочки)
	|			И (ДанныеПланированияПродажПлиткаКвартал.Регистратор ССЫЛКА Документ.ПланированиеПродажПоПотенциальнымТорговымТочкамПлитка)
	|ГДЕ
	|	ПланированиеПродажПлиткаКомандировки.Ссылка = &Ссылка
	|	И НЕ ПланированиеПродажПлиткаКомандировки.Владелец ССЫЛКА Справочник.Партнеры";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКомментарииКПланированиюПродажПлитка(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "КомментарииКПланированиюПродажПлитка";
	
	// +++ VOG Кулаков П.Л. 09.06.2021 DEV-605
	// Добавлены поля ВерсияСценария и ПериодПланирования
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.Дата КАК Период,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(Справочник.вогТорговыеТочки.ПустаяСсылка) КАК ТорговаяТочка,
	|	ЗНАЧЕНИЕ(Справочник.вогБренды.ПустаяСсылка) КАК Бренд,
	|	ЗНАЧЕНИЕ(Справочник.КомплектыПанелейДляПланирования.ПустаяСсылка) КАК КомплектПанелей,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Владелец КАК Клиент,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка) КАК НаправлениеДеятельности,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.СценарийПланирования КАК СценарийПланирования,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Выставление КАК Выставление,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Комментарий КАК Комментарий,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПотенциальнойТорговойТочки.ПустаяСсылка) КАК ВидПотенциальнойТТ,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ВерсияСценария КАК ВерсияСценария,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ПериодПланирования КАК ПериодПланирования,
	|	&Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.КомментарииКвартал КАК ПланированиеПродажПлиткаКомментарииКвартал
	|ГДЕ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка = &Ссылка
	|	И ПланированиеПродажПлиткаКомментарииКвартал.Владелец ССЫЛКА Справочник.Партнеры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.Дата,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Владелец,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Бренд,
	|	ПланированиеПродажПлиткаКомментарииКвартал.КомплектПанелей,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка),
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.СценарийПланирования,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Выставление,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Комментарий,
	|	ЕСТЬNULL(ДанныеПланированияПродажПлиткаКвартал.ВидПотенциальнойТорговойТочки, ЗНАЧЕНИЕ(Перечисление.ВидыПотенциальнойТорговойТочки.ПустаяСсылка)),
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ВерсияСценария,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ПериодПланирования,
	|	&Подразделение
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.КомментарииКвартал КАК ПланированиеПродажПлиткаКомментарииКвартал
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.вогБренды КАК СпрБренды
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.Бренд = СпрБренды.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КомплектыПанелейДляПланирования КАК СпрКомплектыПанелейДляПланирования
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.КомплектПанелей = СпрКомплектыПанелейДляПланирования.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПланированияПродажПлиткаКвартал КАК ДанныеПланированияПродажПлиткаКвартал
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.Владелец = ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка
	|			И (НЕ ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка ССЫЛКА Справочник.вогТорговыеТочки)
	|			И (ДанныеПланированияПродажПлиткаКвартал.Регистратор ССЫЛКА Документ.ПланированиеПродажПоПотенциальнымТорговымТочкамПлитка)
	|ГДЕ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка = &Ссылка
	|	И НЕ ПланированиеПродажПлиткаКомментарииКвартал.Владелец ССЫЛКА Справочник.Партнеры
	|	И СпрБренды.Ссылка ЕСТЬ NULL
	|	И СпрКомплектыПанелейДляПланирования.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.Дата,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Владелец,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Бренд,
	|	ПланированиеПродажПлиткаКомментарииКвартал.КомплектПанелей,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка),
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.СценарийПланирования,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Выставление,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Комментарий,
	|	ЕСТЬNULL(ДанныеПланированияПродажПлиткаКвартал.ВидПотенциальнойТорговойТочки, ЗНАЧЕНИЕ(Перечисление.ВидыПотенциальнойТорговойТочки.ПустаяСсылка)),
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ВерсияСценария,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ПериодПланирования,
	|	&Подразделение
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.КомментарииКвартал КАК ПланированиеПродажПлиткаКомментарииКвартал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.вогБренды КАК СпрБренды
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.Бренд = СпрБренды.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КомплектыПанелейДляПланирования КАК СпрКомплектыПанелейДляПланирования
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.КомплектПанелей = СпрКомплектыПанелейДляПланирования.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПланированияПродажПлиткаКвартал КАК ДанныеПланированияПродажПлиткаКвартал
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.Владелец = ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка
	|			И (НЕ ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка ССЫЛКА Справочник.вогТорговыеТочки)
	|			И (ДанныеПланированияПродажПлиткаКвартал.Регистратор ССЫЛКА Документ.ПланированиеПродажПоПотенциальнымТорговымТочкамПлитка)
	|ГДЕ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка = &Ссылка
	|	И СпрКомплектыПанелейДляПланирования.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.Дата,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Владелец,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Бренд,
	|	ПланированиеПродажПлиткаКомментарииКвартал.КомплектПанелей,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка),
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.СценарийПланирования,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Выставление,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Комментарий,
	|	ЕСТЬNULL(ДанныеПланированияПродажПлиткаКвартал.ВидПотенциальнойТорговойТочки, ЗНАЧЕНИЕ(Перечисление.ВидыПотенциальнойТорговойТочки.ПустаяСсылка)),
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ВерсияСценария,
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка.ПериодПланирования,
	|	&Подразделение
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.КомментарииКвартал КАК ПланированиеПродажПлиткаКомментарииКвартал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КомплектыПанелейДляПланирования КАК СпрКомплектыПанелейДляПланирования
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.КомплектПанелей = СпрКомплектыПанелейДляПланирования.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПланированияПродажПлиткаКвартал КАК ДанныеПланированияПродажПлиткаКвартал
	|		ПО ПланированиеПродажПлиткаКомментарииКвартал.Владелец = ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка
	|			И (НЕ ДанныеПланированияПродажПлиткаКвартал.ТорговаяТочка ССЫЛКА Справочник.вогТорговыеТочки)
	|			И (ДанныеПланированияПродажПлиткаКвартал.Регистратор ССЫЛКА Документ.ПланированиеПродажПоПотенциальнымТорговымТочкамПлитка)
	|ГДЕ
	|	ПланированиеПродажПлиткаКомментарииКвартал.Ссылка = &Ссылка";
	// --- VOG Кулаков П.Л.
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Функция ТекстЗапросаТаблицаДанныеПланированияМаркетинг(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДанныеПланированияМаркетинг";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.Дата КАК Период,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Плитка) КАК НаправлениеДеятельности,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.ТорговаяТочка КАК ТорговаяТочка,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Бренд КАК Бренд,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.ПериодПланирования КАК ПериодПланирования,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.СценарийПланирования КАК СценарийПланирования,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка.ВерсияСценария КАК Версия,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингАкция КАК Акция,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингIndoor КАК Indoor,
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингOutdoor КАК Outdoor,
	// +++ VOG Кулаков П.Л. 27.05.2021 DEV-571
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Представительские КАК Представительские
	|ИЗ
	|	Документ.ПланированиеПродажПлитка.ДанныеПланированияКвартал КАК ПланированиеПродажПлиткаДанныеПланированияКвартал
	|ГДЕ
	|	ПланированиеПродажПлиткаДанныеПланированияКвартал.Ссылка = &Ссылка
	|	И (ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингАкция <> 0
	|			ИЛИ ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингIndoor <> 0
	// +++ VOG Кулаков П.Л. 27.05.2021 DEV-571
	|			ИЛИ ПланированиеПродажПлиткаДанныеПланированияКвартал.Представительские <> 0
	// --- VOG Кулаков П.Л.
	|			ИЛИ ПланированиеПродажПлиткаДанныеПланированияКвартал.МерчендайзингOutdoor <> 0)";
	// --- VOG Кулаков П.Л.
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции 

// Заполняет движения документа по регистру "ДанныеПланированияПродажПлиткаКвартал"
//
Процедура ОтразитьДанныеПланированияПродажПлиткаКвартал(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДанныеПланированияПродажПлиткаКвартал;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// ++ VOG Солодов В.В. 02.04.2021
	ПодразделениеДекорК = Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("fcf03b76-6579-11e7-9071-005056acd97c"));
	ПодразделениеКубань = Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("eb3f3e9a-66e5-11e3-9235-00155d66c603"));
	
	Для Каждого Запись Из Таблица Цикл
		
		Если Запись.Подразделение = ПодразделениеДекорК Или Запись.Подразделение = ПодразделениеКубань Тогда
			
			ПодразделениеТорговойТочки = РегистрыСведений.ПодразделенияТорговыхТочек.ПолучитьПодразделениеТорговойТочки(
				Запись.ТорговаяТочка, Справочники.НаправленияДеятельности.Плитка);
			
			Если Не ПодразделениеТорговойТочки = Неопределено Тогда
				Запись.Подразделение = ПодразделениеТорговойТочки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	// -- VOG Солодов В.В. 02.04.2021
	
	Движения.ДанныеПланированияПродажПлиткаКвартал.Записывать = Истина;
	Движения.ДанныеПланированияПродажПлиткаКвартал.Загрузить(Таблица);

КонецПроцедуры

// Заполняет движения документа по регистру "ПланированиеПосещений"
//
Процедура ОтразитьДанныеПланированияПосещений(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПланированиеПосещений;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ПланированиеПосещений.Записывать = Истина;
	Движения.ПланированиеПосещений.Загрузить(Таблица);

КонецПроцедуры

// Заполняет движения документа по регистру "КомментарииКПланированиюПродажПлитка"
//
Процедура ОтразитьДанныеКомментариев(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаКомментарииКПланированиюПродажПлитка;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	// +++ VOG Кулаков П.Л. 09.06.2021 DEV-605
	ПодразделениеДекорК = Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("fcf03b76-6579-11e7-9071-005056acd97c"));
	ПодразделениеКубань = Справочники.СтруктураПредприятия.ПолучитьСсылку(Новый УникальныйИдентификатор("eb3f3e9a-66e5-11e3-9235-00155d66c603"));
	
	Для Каждого Запись Из Таблица Цикл
		
		Если Запись.Подразделение = ПодразделениеДекорК Или Запись.Подразделение = ПодразделениеКубань Тогда
			
			ПодразделениеТорговойТочки = РегистрыСведений.ПодразделенияТорговыхТочек.ПолучитьПодразделениеТорговойТочки(
				Запись.ТорговаяТочка, Справочники.НаправленияДеятельности.Плитка);
			
			Если Не ПодразделениеТорговойТочки = Неопределено Тогда
				Запись.Подразделение = ПодразделениеТорговойТочки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	// --- VOG Кулаков П.Л.
	
	Движения.КомментарииКПланированиюПродажПлитка.Записывать = Истина;
	Движения.КомментарииКПланированиюПродажПлитка.Загрузить(Таблица);

КонецПроцедуры

// Заполняет движения документа по регистру "ДанныеПланированияМаркетинг"
//
Процедура ОтразитьДанныеМаркетинга(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДанныеПланированияМаркетинг;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ДанныеПланированияМаркетинг.Записывать = Истина;
	Движения.ДанныеПланированияМаркетинг.Загрузить(Таблица);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли