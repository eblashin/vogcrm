#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция СформироватьСписокТелефонов(ТекущиеДанные, ВидКИ = Неопределено)

	Таб = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(ТекущиеДанные.Партнер,
	?(ЗначениеЗаполнено(ТекущиеДанные.КонтактноеЛицо),ТекущиеДанные.КонтактноеЛицо,Неопределено),
	Перечисления.ТипыКонтактнойИнформации.Телефон, ВидКИ);
	
	СписокТелефонов = Новый СписокЗначений;
	
	Если НЕ Таб = Неопределено Тогда
		СписокЗнач = Новый СписокЗначений;
		
		ТекСсылка = "";
		Для Каждого СтрокаТаб Из Таб Цикл
			
			Если НЕ ТекСсылка = СтрокаТаб.Ссылка Тогда
				СтруктураНомера = Новый Структура;
				СтруктураНомера.Вставить("Представление"	, СтрокаТаб.Представление);
				СтруктураНомера.Вставить("Объект"	 , СтрокаТаб.Ссылка);
				СписокТелефонов.Добавить(СтруктураНомера, Строка(СтрокаТаб.Ссылка));
				ТекСсылка = СтрокаТаб.Ссылка;
			КонецЕсли;
			
			СтруктураНомера = Новый Структура;
			СтруктураНомера.Вставить("Представление"	, СтрокаТаб.Представление);
			СтруктураНомера.Вставить("Объект"	 , СтрокаТаб.Ссылка);
			СписокТелефонов.Добавить(СтруктураНомера, "   "+СокрЛП(СтрокаТаб.Вид) + ": " + СтрокаТаб.Представление);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокТелефонов;

КонецФункции

Функция ПолучитьКонтактноеЛицоКлиента(Клиент)
	
	Если ЗначениеЗаполнено(Клиент.CRM_ОсновноеКонтактноеЛицо) Тогда
		Возврат Клиент.CRM_ОсновноеКонтактноеЛицо;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтактныеЛицаПартнеров.Ссылка
		|ИЗ
		|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
		|ГДЕ
		|	КонтактныеЛицаПартнеров.Владелец = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец",Клиент);
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		Возврат ВыборкаИзРезультатаЗапроса.Ссылка;
	КонецЕсли;
	
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.ЗаполнитьШапкуДокумента(ЭтотОбъект, ДанныеЗаполнения);
	
	Если (ТипЗнч(ДанныеЗаполнения)=Тип("Структура") И ДанныеЗаполнения.Свойство("МаркетинговаяКампания")) Или 
		  ТипЗнч(ДанныеЗаполнения)=Тип("СправочникСсылка.МаркетинговыеМероприятия") Тогда
		  
		ЗаполнитьПоМаркетинговойКампании(ДанныеЗаполнения);
		 
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("СписокПартнеров") Тогда
		
		ЗаполнитьПоСпискуПартнеров(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Партнер") Тогда
		
		СтруктураПартнера = Новый Структура;
		СтруктураПартнера.Вставить("Партнер",			ДанныеЗаполнения.Партнер);
		СтруктураПартнера.Вставить("КонтактноеЛицо",	ПолучитьКонтактноеЛицоКлиента(ДанныеЗаполнения.Партнер));
		СтруктураПартнера.Вставить("Интерес",			Неопределено);
		СписокПартнеров = Новый СписокЗначений;
		СписокПартнеров.Добавить(СтруктураПартнера);
		ДанныеЗаполнения.Вставить("СписокПартнеров", СписокПартнеров);
		ЗаполнитьПоСпискуПартнеров(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс")
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс") Тогда
			CRM_ОбщегоНазначенияСервер.ЗаполнитьОбъектБизнесПроцесса(ЭтотОбъект, ДанныеЗаполнения);
		Иначе
			CRM_ОбщегоНазначенияСервер.ЗаполнитьОбъектБизнесПроцесса(ЭтотОбъект, ДанныеЗаполнения.БизнесПроцесс, ДанныеЗаполнения);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПоМаркетинговойКампании(ПараметрДанныеЗаполнения)

	СписокПартнеров	= Неопределено;
	Если ТипЗнч(ПараметрДанныеЗаполнения)=Тип("Структура") И ПараметрДанныеЗаполнения.Свойство("МаркетинговаяКампания") Тогда
		Если ПараметрДанныеЗаполнения.Свойство("СписокПартнеров") Тогда
		 	СписокПартнеров	= ПараметрДанныеЗаполнения.СписокПартнеров;
		КонецЕсли; 
		ДанныеЗаполнения = ПараметрДанныеЗаполнения.МаркетинговаяКампания;
	Иначе
		Если Не ТипЗнч(ПараметрДанныеЗаполнения)=Тип("СправочникСсылка.МаркетинговыеМероприятия") Тогда
			Возврат;
		Иначе
			ДанныеЗаполнения = ПараметрДанныеЗаполнения;
		КонецЕсли;
	КонецЕсли;

	ДатаНачала = ДанныеЗаполнения.ДатаНачала;
	ДатаОкончания = ДанныеЗаполнения.ДатаОкончания;
	Ответственный = ДанныеЗаполнения.Ответственный;
	Проект = ДанныеЗаполнения.CRM_Проект;
	Подразделение = ДанныеЗаполнения.CRM_Подразделение;
	МаркетинговоеМероприятие = ДанныеЗаполнения;
	Тема = ДанныеЗаполнения.Наименование;
	
	тзУчастники = ДанныеЗаполнения.ПартнерыИКонтактныеЛица;
	Для Каждого стрУч Из тзУчастники Цикл
		сслПартнер = стрУч.Партнер;
		сслКонтактноеЛицо = стрУч.КонтактноеЛицо;
		спТелефоны = СформироватьСписокТелефонов(Новый Структура("Партнер, КонтактноеЛицо", сслПартнер, сслКонтактноеЛицо));
		стрУчастник = Участники.Добавить();
		стрУчастник.Партнер = сслПартнер;
		стрУчастник.КонтактноеЛицо = сслКонтактноеЛицо;
		Если спТелефоны.Количество() > 0 Тогда
			стрУчастник.Телефон = спТелефоны[0].Значение.Представление;
		КонецЕсли; 
	КонецЦикла;
	Если ЗначениеЗаполнено(СписокПартнеров) Тогда
		Для Каждого элСп Из СписокПартнеров Цикл
			стрУч = элСп.Значение;
			сслПартнер = стрУч.Партнер;
			сслКонтактноеЛицо = стрУч.КонтактноеЛицо;
			спТелефоны = СформироватьСписокТелефонов(Новый Структура("Партнер, КонтактноеЛицо", сслПартнер, сслКонтактноеЛицо));
			стрУчастник = Участники.Добавить();
			стрУчастник.Партнер = сслПартнер;
			стрУчастник.КонтактноеЛицо = сслКонтактноеЛицо;
			Если спТелефоны.Количество() > 0 Тогда
				стрУчастник.Телефон = спТелефоны[0].Значение.Представление;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьПоСпискуПартнеров(ДанныеЗаполнения)

	СписокПартнеров =ДанныеЗаполнения.СписокПартнеров;
	мДобавленные = Новый Массив;
	Для каждого элСп Из СписокПартнеров Цикл
		элСп = элСп.Значение;
		сслПартнер = элСп.Партнер;
		сслКонтактноеЛицо = элСп.КонтактноеЛицо;
		сслИнтерес = элСп.Интерес;
		спТелефоны = СформироватьСписокТелефонов(Новый Структура("Партнер, КонтактноеЛицо",сслПартнер,сслКонтактноеЛицо));
		Если спТелефоны.Количество() > 0 Тогда
			стрТелефон = спТелефоны[0].Значение.Представление;
			бДоб = мДобавленные.Найти(стрТелефон);
			
			Если бДоб = Неопределено Тогда
			
				мДобавленные.Добавить(стрТелефон);
				стрУчастник = Участники.Добавить();
				стрУчастник.Партнер = сслПартнер;
				стрУчастник.КонтактноеЛицо = сслКонтактноеЛицо;
				стрУчастник.Интерес = сслИнтерес;
				стрУчастник.Телефон = стрТелефон;
			
			КонецЕсли; 
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Заполним автора и ответственного.
	CRM_ОбщегоНазначенияСервер.ЗаполнитьАвтораИОтветственного(ЭтотОбъект);
	
КонецПроцедуры

#КонецЕсли