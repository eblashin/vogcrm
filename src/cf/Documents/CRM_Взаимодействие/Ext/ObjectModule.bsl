
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.CRM_Интерес") Тогда
		ЭтотОбъект.ДокументОснование	= ДанныеЗаполнения.Ссылка;
		ЭтотОбъект.КонтактноеЛицо		= ДанныеЗаполнения.КонтактноеЛицо;
		ЭтотОбъект.ОжидаемаяВыручка		= ДанныеЗаполнения.ОжидаемаяВыручка;
		ЭтотОбъект.Ответственный		= ДанныеЗаполнения.Ответственный;
		ЭтотОбъект.Партнер				= ДанныеЗаполнения.Партнер;
		ЭтотОбъект.Подразделение		= ДанныеЗаполнения.Подразделение;
		ЭтотОбъект.ПотенциальныйКлиент	= ДанныеЗаполнения.ПотенциальныйКлиент;
		ЭтотОбъект.Организация			= ДанныеЗаполнения.Организация;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Если ДокументОснование.Пустая() И ВидВзаимодействия <> Справочники.CRM_СостоянияСобытий.Отменено Тогда
	//	Отказ = Истина;
	//	Возврат;
	//КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ТекстОшибки = "";
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.CRM_Интерес")
			И ЗначениеЗаполнено(ДокументОснование.ПотенциальныйКлиент) 
			И ДокументОснование.ПотенциальныйКлиент <> ПотенциальныйКлиент Тогда
			ТекстОшибки = ТекстОшибки + "Не совпадает значение потенциального клиента с документом-основанием";
			Отказ = Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокументОснование) 
			И ОбщегоНазначения.ЕстьРеквизитОбъекта("Партнер", ДокументОснование.Метаданные()) 
			И ЗначениеЗаполнено(ДокументОснование.Партнер) 
			И ДокументОснование.Партнер <> Партнер 
			// Дополнительная проверка при конвертации событий
			И (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.CRM_Телемаркетинг") 
			ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.CRM_Интерес") И НЕ (СтрНайти(ДокументОснование.Описание, НСтр("ru = ' [Конвертирован из '")))) Тогда
			
			ТекстОшибки = ТекстОшибки + "Не совпадает Клиент";
			Отказ = Истина;
		КонецЕсли;
		
		Если ВидВзаимодействия <> Справочники.CRM_ВидыВзаимодействий.ПотеряОбращения И НЕ ЗначениеЗаполнено(Содержание) Тогда
			ТекстОшибки = ТекстОшибки + "Не заполнено содержание взаимодействия";
			Отказ = Истина;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ВидВзаимодействия) Тогда
			ТекстОшибки = ТекстОшибки + "Не заполнен вид взаимодействия";
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ И Не ПустаяСтрока(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		
		МетаданныеДокументаОснования = ДокументОснование.Метаданные();
		
		Если НЕ ЗначениеЗаполнено(КонтактноеЛицо)
			И CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("КонтактноеЛицо", МетаданныеДокументаОснования)
			Тогда
			КонтактноеЛицо = ДокументОснование.КонтактноеЛицо;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Партнер) И НЕ ЗначениеЗаполнено(ПотенциальныйКлиент) И ТипЗнч(ДокументОснование) <> Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			Партнер = ДокументОснование.Партнер;
			Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("ПотенциальныйКлиент", МетаданныеДокументаОснования) Тогда
				ПотенциальныйКлиент = ДокументОснование.ПотенциальныйКлиент;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПлановаяДатаЗавершение) ИЛИ
			ПлановаяДатаЗавершение < ПлановаяДата Тогда
			ПлановаяДатаЗавершение = ПлановаяДата + 60*15;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
