#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТА
#Область ПрограммныйИнтерфейс

// Процедура заполняет список участников мероприятия
//
// Параметры: Нет
//
Процедура ЗаполнитьСписокУчастников() Экспорт
	
	СписокУчастников = "";
	Для Каждого Участник Из ПользователиКТ Цикл
		СписокУчастников = СписокУчастников + ?(СписокУчастников = "","","; ") + Участник.Пользователь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьРеквизитыПоУмолчанию()
	
	Статус 	 = Перечисления.CRM_СтатусыКонтрольныхТочек.Запланирована;
	Дата	 = ТекущаяДатаСеанса();

КонецПроцедуры

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.ЗаполнитьШапкуДокумента(ЭтотОбъект, ДанныеЗаполнения);

	ЗаполнитьРеквизитыПоУмолчанию();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Проекты") Тогда
		Проект = ДанныеЗаполнения;
		Подразделение = Проект.CRM_Подразделение;
		Этап = Проект.CRM_ТекущийЭтап;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.CRM_ЭтапыПроектов") Тогда
		Проект = ДанныеЗаполнения.Владелец;
		Подразделение = Проект.CRM_Подразделение;
		Этап = ДанныеЗаполнения;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Подразделение") Тогда
			Подразделение = ДанныеЗаполнения.Подразделение;
		ИначеЕсли ДанныеЗаполнения.Свойство("Проект") И ЗначениеЗаполнено(ДанныеЗаполнения.Проект) Тогда
			Подразделение = ДанныеЗаполнения.Проект.CRM_Подразделение;
		ИначеЕсли ДанныеЗаполнения.Свойство("Этап") И ЗначениеЗаполнено(ДанныеЗаполнения.Этап) Тогда
			Этап = ДанныеЗаполнения.Этап;
			Проект = Этап.Владелец; 
			Подразделение = Проект.CRM_Подразделение;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Исполнитель") Тогда
			СтрокаПользователь = ПользователиКТ.Добавить();
			СтрокаПользователь.Пользователь = ДанныеЗаполнения.Исполнитель;
			Если  (ДанныеЗаполнения.Свойство("Проект") И ДанныеЗаполнения.Проект.Ответственный = ДанныеЗаполнения.Исполнитель) ИЛИ
				  (ДанныеЗаполнения.Свойство("Этап") И ДанныеЗаполнения.Этап.Ответственный = ДанныеЗаполнения.Исполнитель) Тогда
				СтрокаПользователь.Ответственный = Истина;
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли НЕ ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Подразделение = CRM_ОбщегоНазначенияСервер.ТекущийПользователь().Подразделение;

	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Очистим табличные части и реквизиты.
	Смещения.Очистить();
	Описание	  = "";
	Результат	  = "";
	ДатаЗакрытия  = "";
	Автор 		  = CRM_ОбщегоНазначенияСервер.ТекущийПользователь();

	ЗаполнитьРеквизитыПоУмолчанию();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокУчастников();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли