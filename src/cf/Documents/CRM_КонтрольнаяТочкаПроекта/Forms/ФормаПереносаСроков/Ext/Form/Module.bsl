
&НаКлиенте
Процедура КомандаПрименить(Команда)
	
	ОчиститьСообщения();
	ВсеОК = ПроверитьЗаполнение();
	
	Если НЕ ВсеОК Тогда
		Возврат;
	КонецЕсли;
	
	ДатаВремяСтарая = CRM_ОбщегоНазначенияКлиентСервер.РазделитьДатаНаДатуИВремя(ДатаСтарая);
	// Проверим что новая дата больше старой.
	Если НачалоДня(ДатаНачала) < НачалоДня(ДатаСтарая) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						НСтр("ru = 'Ошибка! Новая дата контрольной точки должна быть больше или равна старой.'"), ,
						, "ДатаНачала",
						);
		Возврат;					
	ИначеЕсли НачалоДня(ДатаНачала) = НачалоДня(ДатаСтарая) Тогда
		Если (Не НаВесьДень) И ВремяНачала < ДатаВремяСтарая.Время Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						НСтр("ru = 'Ошибка! Время новое контрольной точки должно быть больше или равно старой.'"), ,
						, "ВремяНачала"
						);		
		Возврат;
		КонецЕсли;
	КонецЕсли;
	ПередаваемыеДанные = Новый Структура;
	ПередаваемыеДанные.Вставить("ДатаНачала", ДатаНачала);
	ПередаваемыеДанные.Вставить("ДатаОкончания",ДатаОкончания);
	//ПередаваемыеДанные.Вставить("ДатаСтарая",ДатаСтарая);
	//ПередаваемыеДанные.Вставить("ВремяНачала",ВремяНачала);
	ПередаваемыеДанные.Вставить("НаВесьДень",НаВесьДень);
	ПередаваемыеДанные.Вставить("Комментарий", Комментарий);
	Закрыть(ПередаваемыеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СкорректироватьДатуВремя()
	
	ДатаВремяНачала = CRM_ОбщегоНазначенияКлиентСервер.РазделитьДатаНаДатуИВремя(ДатаНачала);
	ДатаВремяСтарая = CRM_ОбщегоНазначенияКлиентСервер.РазделитьДатаНаДатуИВремя(ДатаСтарая);
	Если ДатаВремяНачала.Дата<=ДатаВремяСтарая.Дата Тогда
		ДатаНачала=ДатаСтарая;
		ВремяНачала = ?(ДатаВремяНачала.Время <=ДатаВремяСтарая.Время,ДатаВремяСтарая.Время,ДатаВремяНачала.Время);
	КонецЕсли;
	Если ДатаОкончания < ДатаНачала Тогда
		ДатаОкончания=ДатаНачала;
		ДатаВремяНачала = CRM_ОбщегоНазначенияКлиентСервер.РазделитьДатаНаДатуИВремя(ДатаНачала);
		ВремяОкончания = ДатаВремяНачала.Время;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ДатаСтарая",ДатаСтарая);
	Параметры.Свойство("НаВесьДень",НаВесьДень);
	
	Если НачалоДня(CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса())<НачалоДня(ДатаСтарая) Тогда
		ДатаНачала = НачалоДня(ДатаСтарая)+60*60*24;
		ДатаОкончания = ДатаНачала+60*60*24;
	Иначе
		ДатаНачала = НачалоДня(CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса())+60*60*24;
		ДатаОкончания = ДатаНачала+60*60*24;
	КонецЕсли;
	Если Не НаВесьДень Тогда
		ДатаВремяСтарая = CRM_ОбщегоНазначенияКлиентСервер.РазделитьДатаНаДатуИВремя(ДатаСтарая);
		ВремяНачала = ДатаВремяСтарая.Время;
	Иначе
		ВремяНачала = 0;
		ВремяОкончания = 0;
	КонецЕсли;
	
	// из планировщика
	тКТ = Неопределено;
	Если Параметры.Свойство("Ссылка") Тогда
		тКТ = Параметры.Ссылка;
		ДатаСтарая = тКТ.Дата;
		ДатаВремяСтарая = CRM_ОбщегоНазначенияКлиентСервер.РазделитьДатаНаДатуИВремя(ДатаСтарая);
	КонецЕсли;
	Если Параметры.Свойство("ПериодНачало") Тогда
		ДатаНачала = Параметры.ПериодНачало;
		ВремяНачала = Параметры.ПериодНачало;
		Если ЗначениеЗаполнено(тКТ) Тогда
			Если НачалоДня(ДатаНачала) < НачалоДня(ДатаСтарая) Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Ошибка! Новая дата контрольной точки должна быть больше или равна старой.'"), ,
				, "ДатаНачала",
				);
				Возврат;
			ИначеЕсли НачалоДня(ДатаНачала) = НачалоДня(ДатаСтарая) Тогда
				Если (Не НаВесьДень) И ВремяНачала < ДатаВремяСтарая.Время Тогда
					Отказ = Истина;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Ошибка! Время новой контрольной точки должно быть больше или равно старой.'"), ,
					, "ВремяНачала"
					);		
					Возврат;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("ПериодОкончание") Тогда
		ДатаОкончания = Параметры.ПериодОкончание;
		ВремяОкончания = Параметры.ПериодОкончание;
	КонецЕсли;
	Если Параметры.Свойство("ИзПланировщика") Тогда
		ИзПланировщика = Истина;
		Элементы.ГруппаДатаВремя.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ВремяНачала.Видимость = (НЕ НаВесьДень);
	Элементы.ВремяОкончания.Видимость = (НЕ НаВесьДень);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	СкорректироватьДатуВремя();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	СкорректироватьДатуВремя();
КонецПроцедуры

&НаКлиенте
Процедура ВремяНачалаПриИзменении(Элемент)
	СкорректироватьДатуВремя();
	ДатаНачала = CRM_ОбщегоНазначенияКлиентСервер.СформироватьДатуИзДатыИВремени(ДатаНачала, ВремяНачала);	
КонецПроцедуры

&НаКлиенте
Процедура ВремяОкончанияПриИзменении(Элемент)
	СкорректироватьДатуВремя();
	ДатаОкончания = CRM_ОбщегоНазначенияКлиентСервер.СформироватьДатуИзДатыИВремени(ДатаОкончания, ВремяОкончания);
КонецПроцедуры

&НаКлиенте
Процедура ВремяНачалаНачалоВыбораИзСписка(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ВремяНачалаНачалоВыбораИзСпискаЗавершение", ЭтотОбъект);
	CRM_ОбщегоНазначенияКлиентСервер.ВыбратьВремяИзСписка(ЭтотОбъект, ВремяНачала, Элемент,,, ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВремяНачалаНачалоВыбораИзСпискаЗавершение(ВыбранноеВремя, СтандартнаяОбработка) Экспорт
	Если ВыбранноеВремя <> Неопределено Тогда
		ВремяНачала = ВыбранноеВремя.Значение;
	КонецЕсли;
	ДатаНачала = CRM_ОбщегоНазначенияКлиентСервер.СформироватьДатуИзДатыИВремени(ДатаНачала, ВремяНачала);	
	СкорректироватьДатуВремя();
КонецПроцедуры

&НаКлиенте
Процедура ВремяОкончанияНачалоВыбораИзСписка(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ВремяОкончанияНачалоВыбораИзСпискаЗавершение", ЭтотОбъект);
	CRM_ОбщегоНазначенияКлиентСервер.ВыбратьВремяИзСписка(ЭтотОбъект, ВремяОкончания, Элемент,,, ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВремяОкончанияНачалоВыбораИзСпискаЗавершение(ВыбранноеВремя, СтандартнаяОбработка) Экспорт
	Если ВыбранноеВремя <> Неопределено Тогда
		ВремяОкончания = ВыбранноеВремя.Значение;
	КонецЕсли;
	ДатаОкончания = CRM_ОбщегоНазначенияКлиентСервер.СформироватьДатуИзДатыИВремени(ДатаОкончания, ВремяОкончания);
	СкорректироватьДатуВремя();
КонецПроцедуры

&НаКлиенте
Процедура НаВесьДеньПриИзменении(Элемент)
	
	Элементы.ВремяНачала.Видимость = (Не НаВесьДень);
	Элементы.ВремяОкончания.Видимость = (Не НаВесьДень);
	
КонецПроцедуры


