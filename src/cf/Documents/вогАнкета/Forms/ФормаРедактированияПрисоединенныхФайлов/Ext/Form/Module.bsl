
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	АнкетаСсылка = Параметры.ДокументСсылка;
	Если АнкетаСсылка.Пустая() Тогда
		ВызватьИсключение НСтр("ru = 'Не определен документ анкеты.'");
	КонецЕсли;
	
	Если Параметры.Свойство("МассивФайлов")
	  И Параметры.МассивФайлов.Количество() > 0 Тогда
	  
	  	ПрисоединенныеФайлы.Очистить();
		Для каждого Файл Из Параметры.МассивФайлов Цикл
			НоваяСтрока = ПрисоединенныеФайлы.Добавить();
			НоваяСтрока.Файл 		   = Файл;
			НоваяСтрока.ИндексКартинки = ИндексКартинкиФайла(Файл);
			
		КонецЦикла;
	
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность
	  ИЛИ ЗакрытьБезусловно Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Конекст) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ЗакрытьБезусловно = Истина;
		Закрыть();
	Иначе
		ЗакрытьБезусловно = Истина;
		Закрыть(СформироватьРезультат());
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытиемЗавершение()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_Файл" Тогда
		Если Параметр.ЭтоНовый 
		  И Параметр.ВладелецФайла = АнкетаСсылка Тогда
			Для каждого ПрисоединенныйФайл Из Источник Цикл
				НоваяСтрока = ПрисоединенныеФайлы.Добавить();	
				НоваяСтрока.Файл 		   = ПрисоединенныйФайл;	
				НоваяСтрока.ИндексКартинки = ИндексКартинкиФайла(ПрисоединенныйФайл);
				
			КонецЦикла;
			
			Модифицированность = Истина;
			вогУправлениеПрисоединеннымиФайламиСервер.ВыполнитьДобавлениеВидаФайла(ПредопределенноеЗначение("Справочник.вогВидыПрисоединенныхФайлов.ПустаяСсылка"), Источник);			
			
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	ЗакрытьБезусловно = Истина;
	Закрыть(СформироватьРезультат());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПрисоединенныеФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПрисоединенныеФайлыКлиент.ДобавитьФайлы(АнкетаСсылка, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиенте
Функция СформироватьРезультат()

	Результат = Новый Структура;
	
	МассивФайлов = Новый Массив;
	Для каждого СтрокаФайла Из ПрисоединенныеФайлы Цикл
		МассивФайлов.Добавить(СтрокаФайла.Файл);
		
	КонецЦикла;
	
	Результат.Вставить("ПрисоединенныеФайлы", МассивФайлов);
	
	Возврат Результат;

КонецФункции // СформироватьРезультат()

&НаСервере
Функция ИндексКартинкиФайла(Файл)

	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ПометкаУдаления");
	СтруктураРеквизитов.Вставить("ИндексКартинки");
	
	РеквизитыФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Файл, СтруктураРеквизитов);
	
	Возврат ?(РеквизитыФайла.ПометкаУдаления, РеквизитыФайла.ИндексКартинки + 1, РеквизитыФайла.ИндексКартинки);
	
КонецФункции // ИндексКартинкиФайла()

#КонецОбласти
