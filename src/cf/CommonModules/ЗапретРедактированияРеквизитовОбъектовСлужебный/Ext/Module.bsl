////////////////////////////////////////////////////////////////////////////////
// Подсистема "Запрет редактирования реквизитов объектов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет, что для объекта метаданных предусмотрен запрет редактирования реквизитов.
//
// Параметры:
//  ПолноеИмя - Строка - полное имя объекта метаданных.
//
Функция ЗапретРедактированияПредусмотрен(ПолноеИмя) Экспорт
	
	Объекты = Новый Соответствие;
	ИнтеграцияСтандартныхПодсистем.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами(Объекты);
	ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами(Объекты);
	
	Возврат Объекты[ПолноеИмя] <> Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настраивает форму объекта для работы подсистемы:
// - добавляет реквизит ПараметрыЗапретаРедактированияРеквизитов для хранения внутренних данных
// - добавляет команду и кнопку РазрешитьРедактированиеРеквизитовОбъекта (если есть права).
//
Процедура ПодготовитьФорму(Форма, Ссылка, ГруппаДляКнопкиЗапрета, ЗаголовокКнопкиЗапрета) Экспорт
	
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100));
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	ОписаниеТиповМассив = Новый ОписаниеТипов("СписокЗначений");
	
	РеквизитыФормы = Новый Соответствие;
	Для каждого РеквизитФормы Из Форма.ПолучитьРеквизиты() Цикл
		РеквизитыФормы.Вставить(РеквизитФормы.Имя, РеквизитФормы.Заголовок);
	КонецЦикла;
	
	// Добавление реквизитов на форму.
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПараметрыЗапретаРедактированияРеквизитов", Новый ОписаниеТипов("ТаблицаЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИмяРеквизита",            ОписаниеТиповСтрока100, "ПараметрыЗапретаРедактированияРеквизитов"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Представление",           ОписаниеТиповСтрока100, "ПараметрыЗапретаРедактированияРеквизитов"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РедактированиеРазрешено", ОписаниеТиповБулево,    "ПараметрыЗапретаРедактированияРеквизитов"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("БлокируемыеЭлементы",     ОписаниеТиповМассив,    "ПараметрыЗапретаРедактированияРеквизитов"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПравоРедактирования",     ОписаниеТиповБулево,    "ПараметрыЗапретаРедактированияРеквизитов"));
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	БлокируемыеРеквизиты = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка).ПолучитьБлокируемыеРеквизитыОбъекта();
	ВсеРеквизитыБезПраваРедактирования = Истина;
	
	Для Каждого БлокируемыйРеквизит Из БлокируемыеРеквизиты Цикл
		
		ОписаниеРеквизита = Форма.ПараметрыЗапретаРедактированияРеквизитов.Добавить();
		
		ИнформацияОБР = СтрРазделить(БлокируемыйРеквизит, ";", Ложь);
		ОписаниеРеквизита.ИмяРеквизита = ИнформацияОБР[0];
		
		Если ИнформацияОБР.Количество() > 1 Тогда
			БлокируемыеЭлементы = СтрРазделить(ИнформацияОБР[1], ",", Ложь);
			Для Каждого БлокируемыйЭлемент Из БлокируемыеЭлементы Цикл
				ОписаниеРеквизита.БлокируемыеЭлементы.Добавить(СокрЛП(БлокируемыйЭлемент));
			КонецЦикла;
		КонецЕсли;
		
		МетаданныеОбъекта = Ссылка.Метаданные();
		ЭтоПланСчетов = ОбщегоНазначения.ЭтоПланСчетов(МетаданныеОбъекта);
		ЕстьСтандартныеТабличныеЧасти = ЭтоПланСчетов Или ОбщегоНазначения.ЭтоПланВидовРасчета(МетаданныеОбъекта);
		
		МетаданныеРеквизитаИлиТабличнойЧасти = МетаданныеОбъекта.Реквизиты.Найти(ОписаниеРеквизита.ИмяРеквизита);
		Если МетаданныеРеквизитаИлиТабличнойЧасти = Неопределено И ЭтоПланСчетов Тогда
			МетаданныеРеквизитаИлиТабличнойЧасти = МетаданныеОбъекта.ПризнакиУчета.Найти(ОписаниеРеквизита.ИмяРеквизита);
		КонецЕсли;
		СтандартныйРеквизитИлиСтандартнаяТабличнаяЧасть = Ложь;
		
		Если МетаданныеРеквизитаИлиТабличнойЧасти = Неопределено Тогда
			МетаданныеРеквизитаИлиТабличнойЧасти = МетаданныеОбъекта.ТабличныеЧасти.Найти(ОписаниеРеквизита.ИмяРеквизита);
			
			Если МетаданныеРеквизитаИлиТабличнойЧасти = Неопределено
			   И ЕстьСтандартныеТабличныеЧасти
			   И ОбщегоНазначения.ЭтоСтандартныйРеквизит(МетаданныеОбъекта.СтандартныеТабличныеЧасти, ОписаниеРеквизита.ИмяРеквизита) Тогда
					МетаданныеРеквизитаИлиТабличнойЧасти = МетаданныеОбъекта.СтандартныеТабличныеЧасти[ОписаниеРеквизита.ИмяРеквизита];
					СтандартныйРеквизитИлиСтандартнаяТабличнаяЧасть = Истина;
			КонецЕсли;
			Если МетаданныеРеквизитаИлиТабличнойЧасти = Неопределено Тогда
				Если ОбщегоНазначения.ЭтоСтандартныйРеквизит(МетаданныеОбъекта.СтандартныеРеквизиты, ОписаниеРеквизита.ИмяРеквизита) Тогда
					МетаданныеРеквизитаИлиТабличнойЧасти = МетаданныеОбъекта.СтандартныеРеквизиты[ОписаниеРеквизита.ИмяРеквизита];
					СтандартныйРеквизитИлиСтандартнаяТабличнаяЧасть = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//--> VOG Солодов
		Если МетаданныеРеквизитаИлиТабличнойЧасти = Неопределено Тогда
			ПроверитьЭтоКонтактнаяИнформация(ОписаниеРеквизита, Форма);
		КонецЕсли;
		//<-- VOG Солодов
		
		Если МетаданныеРеквизитаИлиТабличнойЧасти = Неопределено Тогда			
			ОписаниеРеквизита.Представление = РеквизитыФормы[ОписаниеРеквизита.ИмяРеквизита];
			
			ОписаниеРеквизита.ПравоРедактирования = Истина;
			ВсеРеквизитыБезПраваРедактирования = Ложь;
		Иначе
			ОписаниеРеквизита.Представление = МетаданныеРеквизитаИлиТабличнойЧасти.Представление();
			
			Если СтандартныйРеквизитИлиСтандартнаяТабличнаяЧасть Тогда
				ПравоРедактирования = ПравоДоступа("Редактирование", МетаданныеОбъекта, , МетаданныеРеквизитаИлиТабличнойЧасти.Имя);
			Иначе
				ПравоРедактирования = ПравоДоступа("Редактирование", МетаданныеРеквизитаИлиТабличнойЧасти);
			КонецЕсли;
			Если ПравоРедактирования Тогда
				ОписаниеРеквизита.ПравоРедактирования = Истина;
				ВсеРеквизитыБезПраваРедактирования = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьСвязанныеЭлементы(Форма);
	
	// Добавление команды и кнопки, если есть права.
	Если Пользователи.РолиДоступны("РедактированиеРеквизитовОбъектов")
	   И ПравоДоступа("Редактирование", Ссылка.Метаданные())
	   И НЕ ВсеРеквизитыБезПраваРедактирования Тогда
		
		// Добавление команды
		Команда = Форма.Команды.Добавить("РазрешитьРедактированиеРеквизитовОбъекта");
		Команда.Заголовок = ?(ПустаяСтрока(ЗаголовокКнопкиЗапрета), НСтр("ru = 'Разрешить редактирование реквизитов'"), ЗаголовокКнопкиЗапрета);
		Команда.Действие = "Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта";
		Команда.Картинка = БиблиотекаКартинок.РазрешитьРедактированиеРеквизитовОбъекта;
		Команда.ИзменяетСохраняемыеДанные = Истина;
		
		// Добавление кнопки
		РодительскаяГруппа = ?(ГруппаДляКнопкиЗапрета <> Неопределено, ГруппаДляКнопкиЗапрета, Форма.КоманднаяПанель);
		Кнопка = Форма.Элементы.Добавить("РазрешитьРедактированиеРеквизитовОбъекта", Тип("КнопкаФормы"), РодительскаяГруппа);
		Кнопка.ТолькоВоВсехДействиях = Истина;
		Кнопка.ИмяКоманды = "РазрешитьРедактированиеРеквизитовОбъекта";
	КонецЕсли;
	
	//--> VOG Солодов
	ДобавитьБлокируемыеКоманды(Форма);
	//<-- VOG Солодов
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции.

// Для процедуры ПодготовитьФорму.
// Дополняет массив блокируемых элементов формы связанными элементами.
Процедура ЗаполнитьСвязанныеЭлементы(Форма)
	
	Отбор = Новый Структура("ИмяРеквизита", Неопределено);
	
	Для Каждого ЭлементФормы Из Форма.Элементы Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы")
		   И ЭлементФормы.Вид <> ВидПоляФормы.ПолеНадписи
		 Или ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
		
			РазложенныйПутьКДанным = СтрРазделить(ЭлементФормы.ПутьКДанным, ".", Ложь);
			
			Если РазложенныйПутьКДанным.Количество() = 2 Тогда
				Отбор.ИмяРеквизита = РазложенныйПутьКДанным[1];
				Строки = Форма.ПараметрыЗапретаРедактированияРеквизитов.НайтиСтроки(Отбор);
				Если Строки.Количество() > 0 Тогда
					Строки[0].БлокируемыеЭлементы.Добавить(ЭлементФормы.Имя);
				КонецЕсли;
			ИначеЕсли РазложенныйПутьКДанным.Количество() = 1 Тогда // Солодов
				Отбор.ИмяРеквизита = РазложенныйПутьКДанным[0];
				Строки = Форма.ПараметрыЗапретаРедактированияРеквизитов.НайтиСтроки(Отбор);
				Если Строки.Количество() > 0 Тогда
					Строки[0].БлокируемыеЭлементы.Добавить(ЭлементФормы.Имя);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЭтоКонтактнаяИнформация(ОписаниеРеквизита, Форма)
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Форма.ЭтотОбъект.Объект.Ссылка));
	
	Если ОбъектМетаданных <> Неопределено Тогда
		
		ИмяПредопределенныхДанных = "Справочник" + ОбъектМетаданных.Имя;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|ГДЕ
		|	ВидыКонтактнойИнформации.Родитель.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных
		|	И ВидыКонтактнойИнформации.Наименование = &Наименование
		|	И ВидыКонтактнойИнформации.ЭтоГруппа = ЛОЖЬ
		|	И ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ";
		
		Запрос.УстановитьПараметр("Наименование", 				ОписаниеРеквизита.ИмяРеквизита);
		Запрос.УстановитьПараметр("ИмяПредопределенныхДанных", 	ИмяПредопределенныхДанных);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Вид", 		ВыборкаДетальныеЗаписи.Ссылка);
			СтруктураОтбора.Вставить("Выводить", 	Истина);
			
			МассивСтрок = Форма.ЭтотОбъект.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(СтруктураОтбора);
			
			Если МассивСтрок.Количество() > 0 Тогда
				ОписаниеРеквизита.ИмяРеквизита = МассивСтрок[0].ИмяРеквизита;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьИзмененияБлокированныхРеквизитов(Знач Форма) Экспорт
	
	МассивЗаписываемых = Новый Массив;
	
	Ссылка = Форма.Объект.Ссылка;
	
	Для Каждого Реквизит Из Форма.РазрешеныКРедактированию Цикл
		
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("Период", 		Форма.ДатаРедактированияРеквизитов);
		СтруктураЗаписи.Вставить("ОбъектСвязи", Форма.Объект.Ссылка);
		СтруктураЗаписи.Вставить("ТипПоля");
		СтруктураЗаписи.Вставить("Значение");
		
		Если Форма.Объект.Свойство(Реквизит.Значение) Тогда
			
			Если Реквизит.Представление <> Строка(Форма.Объект[Реквизит.Значение]) Тогда
				
				Если Метаданные.Перечисления.вогТипыПолейКонтрагента.ЗначенияПеречисления.Найти(Реквизит.Значение) <> Неопределено Тогда
					ТипПоля = Перечисления.вогТипыПолейКонтрагента[Реквизит.Значение];
				ИначеЕсли Реквизит.Значение = "КПП" И ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.вогТорговыеТочки") Тогда
					ТипПоля = Перечисления.вогТипыПолейКонтрагента.CRM_КПП;
				Иначе
					Продолжить;
				КонецЕсли;
				
				СтруктураЗаписи.ТипПоля 	= ТипПоля;
				СтруктураЗаписи.Значение 	= Форма.Объект[Реквизит.Значение];

				
			КонецЕсли;
		
		ИначеЕсли Форма.Элементы.Найти(Реквизит.Значение) <> Неопределено
			И Реквизит.Представление <> Строка(Форма[Реквизит.Значение]) Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИмяРеквизита", Реквизит.Значение);
			
			НайденныеСтроки = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(ПараметрыОтбора);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				ТипПоля = Перечисления.вогТипыПолейКонтрагента.ОпределитьТипПоляКонтактнойИнформации(НайденныеСтроки[0].Вид);
				
				Если ТипПоля = Неопределено Тогда					
					Продолжить;					
				КонецЕсли;
				
				СтруктураЗаписи.ТипПоля 	= ТипПоля;
				СтруктураЗаписи.Значение 	= Форма[Реквизит.Значение];
				
			КонецЕсли;
			
		КонецЕсли;
		
		МассивЗаписываемых.Добавить(СтруктураЗаписи);
		
	КонецЦикла;
	
	РегистрыСведений.вогИзменениеРеквизитовСДаты.ЗаписатьИзмененияРеквизитов(МассивЗаписываемых);
	
КонецПроцедуры

Процедура ДобавитьБлокируемыеКоманды(Форма)
	
	МассивБлокируемыхКоманд = Новый Массив;
	
	Для Каждого Элемент Из Форма.ПараметрыЗапретаРедактированияРеквизитов Цикл
		
		Если СтрНачинаетсяС(Элемент.ИмяРеквизита, "КонтактнаяИнформация") Тогда
			
			ИмяРеквизита = "Команда" + Элемент.ИмяРеквизита;
			ЭлементКоманда = Форма.Элементы.Найти(ИмяРеквизита);
			
			Если ЭлементКоманда <> Неопределено Тогда
				
				ОписаниеРеквизита = Новый Структура;
				ОписаниеРеквизита.Вставить("ИмяРеквизита", 				ИмяРеквизита);
				ОписаниеРеквизита.Вставить("ПравоРедактирования", 		Истина);
				ОписаниеРеквизита.Вставить("РедактированиеРазрешено", 	Ложь);
				
				БлокируемыеЭлементы = Новый СписокЗначений;
				БлокируемыеЭлементы.Добавить(СокрЛП(ЭлементКоманда.Имя));
				
				ОписаниеРеквизита.Вставить("БлокируемыеЭлементы", БлокируемыеЭлементы);
				
				МассивБлокируемыхКоманд.Добавить(ОписаниеРеквизита);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого БлокируемаяКоманда Из МассивБлокируемыхКоманд Цикл
		
		ОписаниеРеквизита = Форма.ПараметрыЗапретаРедактированияРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(ОписаниеРеквизита, БлокируемаяКоманда);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
