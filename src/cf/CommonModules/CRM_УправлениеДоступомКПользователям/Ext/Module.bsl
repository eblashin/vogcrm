&НаСервере
Функция ПолучитьСписокДоступныхПодразделений(ВозвращатьМассив = Ложь, СтрокаПоиска = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_РасширенныйДоступПоПодразделениям.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ тмпРасширенныйДоступ
	|ИЗ
	|	РегистрСведений.CRM_РасширенныйДоступПоПодразделениям КАК CRM_РасширенныйДоступПоПодразделениям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.CRM_ИспользоватьДолжностныеПозиции КАК CRM_ИспользоватьДолжностныеПозиции
	|		ПО CRM_ИспользоватьДолжностныеПозиции.Значение = Истина
	|ГДЕ
	|	CRM_РасширенныйДоступПоПодразделениям.ДолжностнаяПозиция В ИЕРАРХИИ(&ДолжностнаяПозиция)
	|	И &ДолжностнаяПозиция <> ЗНАЧЕНИЕ(Справочник.CRM_ДолжностныеПозиции.ПустаяСсылка)
	|	И &КОРПВерсия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпТекущееПодразделение
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.ТекущийРуководитель = &Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпДанныеСправочника
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				тмпТекущееПодразделение.Ссылка
	|			ИЗ
	|				тмпТекущееПодразделение КАК тмпТекущееПодразделение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тмпРасширенныйДоступ.Подразделение КАК Подразделение
	|ИЗ
	|	тмпРасширенныйДоступ КАК тмпРасширенныйДоступ
	|ГДЕ тмпРасширенныйДоступ.Подразделение.Наименование ПОДОБНО &СтрокаПоиска
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тмпДанныеСправочника.Ссылка
	|ИЗ
	|	тмпДанныеСправочника КАК тмпДанныеСправочника
	|ГДЕ тмпДанныеСправочника.Ссылка.Наименование ПОДОБНО &СтрокаПоиска
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Подразделение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Пользователи.Подразделение КАК Подразделение
	|	ИЗ
	|		Справочник.Пользователи КАК Пользователи
	|	ГДЕ
	|		Пользователи.Ссылка = &Пользователь) КАК ВложенныйЗапрос
	|ГДЕ ВложенныйЗапрос.Подразделение.Наименование ПОДОБНО &СтрокаПоиска";
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();			   
	Если Метаданные.Имя = "CRM" Тогда
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Иначе	
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь.ФизическоеЛицо);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДолжностнаяПозиция", ТекущийПользователь.CRM_ДолжностнаяПозиция);
	Запрос.УстановитьПараметр("КОРПВерсия", CRM_ЛицензированиеСервер.ВариантПоставкиКОРП());
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска+"%");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ ВложенныйЗапрос.Подразделение.Наименование ПОДОБНО &СтрокаПоиска", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ тмпРасширенныйДоступ.Подразделение.Наименование ПОДОБНО &СтрокаПоиска", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ тмпДанныеСправочника.Ссылка.Наименование ПОДОБНО &СтрокаПоиска", "");
	КонецЕсли;
	ТаблицаПодразделений = Запрос.Выполнить().Выгрузить();
	ТаблицаПодразделений.Свернуть("Подразделение");
	МассивПодразделений = ТаблицаПодразделений.ВыгрузитьКолонку("Подразделение");
	СписокПодразделений = Новый СписокЗначений;
	СписокПодразделений.ЗагрузитьЗначения(МассивПодразделений);
	
	Если ВозвращатьМассив Тогда
		Возврат МассивПодразделений;
	Иначе	
		Возврат СписокПодразделений;	
	КонецЕсли;	
КонецФункции

&НаСервере
Функция ПолучитьСписокДоступныхПользователей(ВозвращатьМассив = Ложь, СтрокаПоиска = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_РасширенныйДоступПоПодразделениям.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ тмпРасширенныйДоступ
	|ИЗ
	|	РегистрСведений.CRM_РасширенныйДоступПоПодразделениям КАК CRM_РасширенныйДоступПоПодразделениям
    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.CRM_ИспользоватьДолжностныеПозиции КАК CRM_ИспользоватьДолжностныеПозиции
    |		ПО CRM_ИспользоватьДолжностныеПозиции.Значение = Истина
	|ГДЕ
	//*** Описание
	//*** 20 июня 2019 г.
	//*** Блашин Евгений Игоревич VOG Начало
	|	CRM_РасширенныйДоступПоПодразделениям.ДолжностнаяПозиция В (&вогДолжностныеПозиции)
	//*** Блашин Евгений Игоревич VOG Окончание 
	//|	CRM_РасширенныйДоступПоПодразделениям.ДолжностнаяПозиция В ИЕРАРХИИ(&ДолжностнаяПозиция)
	|	И &ДолжностнаяПозиция <> ЗНАЧЕНИЕ(Справочник.CRM_ДолжностныеПозиции.ПустаяСсылка)
	|	И &КОРПВерсия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Подразделение
	|ПОМЕСТИТЬ тмпТекущееПодразделение
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.ТекущийРуководитель = &Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпТекущееПодразделениеПользователя
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (Пользователи.Подразделение = СтруктураПредприятия.Ссылка)
	|			И (Пользователи.Ссылка = &Пользователь)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка
	|ПОМЕСТИТЬ тмпДанныеСправочника
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				тмпТекущееПодразделениеПользователя.Ссылка
	|			ИЗ
	|				тмпТекущееПодразделениеПользователя КАК тмпТекущееПодразделениеПользователя)
	|	И НЕ СтруктураПредприятия.Ссылка В
	|				(ВЫБРАТЬ
	|					тмпТекущееПодразделениеПользователя.Ссылка
	|				ИЗ
	|					тмпТекущееПодразделениеПользователя КАК тмпТекущееПодразделениеПользователя)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Подразделение
	|ПОМЕСТИТЬ тмпДоступныеПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		тмпРасширенныйДоступ.Подразделение КАК Подразделение
	|	ИЗ
	|		тмпРасширенныйДоступ КАК тмпРасширенныйДоступ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		тмпДанныеСправочника.Ссылка
	|	ИЗ
	|		тмпДанныеСправочника КАК тмпДанныеСправочника) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь
	|ПОМЕСТИТЬ тмпДляСвертывания
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	(Пользователи.Подразделение В
	|				(ВЫБРАТЬ
	|					тмпДоступныеПодразделения.Подразделение
	|				ИЗ
	|					тмпДоступныеПодразделения КАК тмпДоступныеПодразделения)
	|			ИЛИ Пользователи.Подразделение В
	|				(ВЫБРАТЬ
	|					тмпТекущееПодразделение.Подразделение
	|				ИЗ
	|					тмпТекущееПодразделение КАК тмпТекущееПодразделение))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	//*** Описание
	//*** 20 июня 2019 г.
	//*** Блашин Евгений Игоревич VOG Начало
	|	Пользователи.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Ссылка В(&вогПодчиненныеСотрудники)
	//*** Блашин Евгений Игоревич VOG Окончание 
	//|	&Пользователь
	
	// ++ VOG Солодов В.В. 14.08.2019 task 464
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РуководителиПользователя.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК РуководителиПользователя
	|	ПО Пользователи.Ссылка = &Пользователь
	|		И Пользователи.вогРуководитель = РуководителиПользователя.Ссылка
	// -- VOG Солодов В.В. 14.08.2019
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тмпДляСвертывания.Пользователь
	|ИЗ
	|	тмпДляСвертывания КАК тмпДляСвертывания
	|	ГДЕ тмпДляСвертывания.Пользователь.Наименование ПОДОБНО &СтрокаПоиска";
	
	//*** Описание
	//*** 20 июня 2019 г.
	//*** Блашин Евгений Игоревич VOG Начало
	Запрос.УстановитьПараметр("вогДолжностныеПозиции",ПараметрыСеанса.вогДолжностныеПозиции);
	Запрос.УстановитьПараметр("вогПодчиненныеСотрудники",ПараметрыСеанса.вогПодчиненныеСотрудники);	
	//*** Блашин Евгений Игоревич VOG Окончание 
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();			   
	Если Метаданные.Имя = "CRM" Тогда
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Иначе	
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь.ФизическоеЛицо);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДолжностнаяПозиция", ТекущийПользователь.CRM_ДолжностнаяПозиция);
	Запрос.УстановитьПараметр("КОРПВерсия", CRM_ЛицензированиеСервер.ВариантПоставкиКОРП());
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска+"%");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ тмпДляСвертывания.Пользователь.Наименование ПОДОБНО &СтрокаПоиска", "");
	КонецЕсли;
	
	ТаблицаПользователей = Запрос.Выполнить().Выгрузить();
	ТаблицаПользователей.Свернуть("Пользователь");
	МассивПользователей = ТаблицаПользователей.ВыгрузитьКолонку("Пользователь");
	
	//++ VOG Ульянов И.В. 15.01.2020 CRM-121
	Если НЕ ЗначениеЗаполнено(СтрокаПоиска) Тогда
		МассивПользователей.Добавить(ТекущийПользователь);
	КонецЕсли;	
	//-- VOG Ульянов И.В. 15.01.2020 CRM-121
	
	СписокПользователей = Новый СписокЗначений;
	СписокПользователей.ЗагрузитьЗначения(МассивПользователей);
	
	Если ВозвращатьМассив Тогда
		Возврат МассивПользователей;
	Иначе	
		Возврат СписокПользователей;	
	КонецЕсли;
КонецФункции	
