////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обновление версии CRM".
// Клиентские процедуры и функции для интерактивного обновления информационной базы.
//
////////////////////////////////////////////////////////////////////////////////

// Если указан ПрекратитьРаботу = Истина, то прервать дальнейшее выполнение клиентского кода и прекратить работу.
//
Процедура ОбработатьОшибкуПриЗапускеИлиЗавершении(Параметры, ИнформацияОбОшибке, Событие, ПрекратитьРаботу = Ложь)
	
	Если Событие = "Запуск" Тогда
		Если ПрекратитьРаботу Тогда
			Параметры.Отказ = Истина;
			Параметры.ОбработкаПродолжения = Параметры.ОбработкаЗавершения;
		КонецЕсли;
	Иначе
		ДополнительныеПараметры = Новый Структура(
			"Параметры, ОбработкаПродолжения", Параметры, Параметры.ОбработкаПродолжения);
		
		Параметры.ОбработкаПродолжения = Новый ОписаниеОповещения(
			"ДействияПередЗавершениемРаботыСистемыПослеОбработкиОшибки", ЭтотОбъект, ДополнительныеПараметры);
	КонецЕсли;
	
	НачалоОписанияОшибки = СтандартныеПодсистемыВызовСервера.ЗаписатьОшибкуВЖурналРегистрацииПриЗапускеИлиЗавершении(
		ПрекратитьРаботу, Событие, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));	
	НачалоОписанияОшибки = "";
	
	ТекстПредупреждения = НачалоОписанияОшибки + Символы.ПС
		+ НСтр("ru = 'Техническая информация об ошибке записана в журнал регистрации.'")
		+ Символы.ПС + Символы.ПС
		+ КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ПоказатьПредупреждениеИПродолжить",
		СтандартныеПодсистемыКлиент.ЭтотОбъект,
		ТекстПредупреждения);
	
	Параметры.ИнтерактивнаяОбработка = ИнтерактивнаяОбработка;
	
КонецПроцедуры

// Вызывается перед интерактивным началом работы пользователя с областью данных.
// Соответствует событию ПередНачаломРаботыСистемы модулей приложения.
//
Процедура ПередНачаломРаботыСистемы(Параметры) Экспорт
	
	ИмяПараметра = "CRM_ПараметрыЗапуска";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	
	Если CRM_ОбновлениеИнформационнойБазы.ПервыйЗапускИнформационнойБазы() Тогда
		СтруктураДанных.Вставить("ЭтоПервыйЗапускИнформационнойБазы", Истина);
	Иначе
		СтруктураДанных.Вставить("ЭтоПервыйЗапускИнформационнойБазы", Ложь);
	КонецЕсли;
	
	Если СтрНачинаетсяС(CRM_ОбновлениеИнформационнойБазы.ВерсияCRM(), "2.0") Тогда
		СтруктураДанных.Вставить("ЭтоПереходСВерсии20", Истина);
	Иначе
		СтруктураДанных.Вставить("ЭтоПереходСВерсии20", Ложь);		
	КонецЕсли;
	
	Если CRM_ОбновлениеИнформационнойБазы.ПолучитьРежимОбновленияДанных() = "ПереходСДругойПрограммы" Тогда
		СтруктураДанных.Вставить("ЭтоПереходСДругойПрограммы", Истина);
	Иначе
		СтруктураДанных.Вставить("ЭтоПереходСДругойПрограммы", Ложь);
	КонецЕсли;
	
	Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		СтруктураДанных.Вставить("ЗапускатьМастерНастройкиРешения", CRM_ОбновлениеИнформационнойБазы.ЗапускатьМастерНастройкиРешения());
	КонецЕсли;
	
	Если CRM_ДемонстрационныйРежим.ЭтоДемоРежим() Тогда
		СтруктураДанных.Вставить("ЗакладкиНеОткрываем", Истина);
	Иначе
		СтруктураДанных.Вставить("ЗакладкиНеОткрываем", Ложь);
	КонецЕсли;
	
	ПараметрыПриложения["CRM_ПараметрыЗапуска"] = СтруктураДанных;
	
КонецПроцедуры

// Выполняется при интерактивном начале работы пользователя с областью данных или в локальном режиме.
// Вызывается после завершения действий ПриНачалеРаботыСистемы.
// Используется для подключения обработчиков ожидания, которые не должны вызываться
// в случае интерактивных действий перед и при начале работы системы.
//
Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	CRM_ПараметрыЗапуска = ПараметрыПриложения["CRM_ПараметрыЗапуска"];
	
	Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		// Открывается при первом старте CRM.
		Если CRM_ПараметрыЗапуска.ЭтоПервыйЗапускИнформационнойБазы ИЛИ CRM_ПараметрыЗапуска.ЭтоПереходСДругойПрограммы ИЛИ CRM_ПараметрыЗапуска.ЗапускатьМастерНастройкиРешения Тогда
			ИмяФормыОбработки = "Обработка.CRM_МастерНастройкиРешения.Форма";
			ОткрытьФорму(ИмяФормыОбработки);
		КонецЕсли;
		
	КонецЕсли;
	
	// Открывается после перехода с CRM 2.0 на 3.0.
	Если CRM_ПараметрыЗапуска.ЭтоПереходСВерсии20 Тогда
		ИмяФормыОбработки = "Обработка.CRM_МастерКонвертацииСобытийВИнтересы.Форма";
		ОткрытьФорму(ИмяФормыОбработки);
	КонецЕсли;
	
	ПараметрыПриложения.Удалить("CRM_ПараметрыЗапуска");
	
КонецПроцедуры