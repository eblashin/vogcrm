#Область ПрограммныйИнтерфейс

#Область МоделиКейсов

Процедура ОбработкаКомандыМоделиКейса(Форма, Команда, ПараметрыКоманды) Экспорт
	
	СтрокаМоделиКейса = НайтиСтрокуДанныхМоделиКейса(Форма, Команда.Имя);
	Если СтрокаМоделиКейса = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не определены данные модели кейса'"));	
		Возврат;
		
	КонецЕсли;
	
	ТемаФрагмент = НСтр("ru = 'регион не указан'");
	Если ПараметрыКоманды.Свойство("БизнесРегион")
	  И ЗначениеЗаполнено(ПараметрыКоманды.БизнесРегион) Тогда
		ТемаФрагмент = ": " + Строка(ПараметрыКоманды.БизнесРегион);
	КонецЕсли;
	
	Тема = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1%2'"), СтрокаМоделиКейса.МодельКейса, ТемаФрагмент);	
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Тема", Тема);	
	Если СтрокаМоделиКейса.ИспользоватьКалендарныеГрафики Тогда
		Если ПараметрыКоманды.Свойство("БизнесРегион") Тогда
			ЗначенияЗаполнения.Вставить("БизнесРегион", ПараметрыКоманды.БизнесРегион);
		КонецЕсли;
		ИмяФормы = "Документ.CRM_ЭтапКалендарногоПлана.Форма.ФормаСозданияЭтапа";
		
	Иначе	
		Если ПараметрыКоманды.Свойство("БизнесРегион") Тогда
			ЗначенияЗаполнения.Вставить("CRM_БизнесРегион", ПараметрыКоманды.БизнесРегион);
		КонецЕсли;
		
		ЗначенияЗаполнения.Вставить("CRM_ЭтоКейс"	 , Истина);
		ЗначенияЗаполнения.Вставить("CRM_МодельКейса", СтрокаМоделиКейса.МодельКейса);
		
		ИмяФормы = "Справочник.Проекты.Форма.CRM_ФормаКейса";
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Если ПараметрыКоманды.Свойство("МассивКлиентов") Тогда
		ПараметрыФормы.Вставить("МассивКлиентов" , ПараметрыКоманды.МассивКлиентов);
	КонецЕсли;
	ПараметрыФормы.Вставить("МодельКейса", СтрокаМоделиКейса.МодельКейса);
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, Форма, Форма.УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПрцедурыФункции

Функция НайтиСтрокуДанныхМоделиКейса(Форма, Имя)
	
	Перем Строка;
	
	Строки = Форма.скДанныеМоделейКейса.НайтиСтроки(
		Новый Структура("ИмяКоманды", Имя)
	);
	
	Если Строки.Количество() > 0 Тогда
		Строка = Строки[0];	
	КонецЕсли;	
	
	Возврат Строка;
	
КонецФункции // НайтиСтрокуДанныхМоделиКейса()

#КонецОбласти

#КонецОбласти
