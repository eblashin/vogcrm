
Функция ПолучитьПризнакСтартСистемы() Экспорт
	Данные = CRM_ХранилищеНастроек.Загрузить("СтартСистемыВРежимеЗакладокПризнакСтартСистемы");
	Возврат (Данные = Истина);
КонецФункции

Процедура СохранитьПризнакСтартСистемы(Признак) Экспорт
	CRM_ХранилищеНастроек.Сохранить("СтартСистемыВРежимеЗакладокПризнакСтартСистемы",, Признак);
КонецПроцедуры

Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	Если Не CRM_РежимФормЗакладкиСерверПовтИсп.ИспользуетсяРежимЗакладок() Тогда Возврат; КонецЕсли;
	
	Если Не ПолучитьПризнакСтартСистемы() Тогда Возврат; КонецЕсли;
	
	Попытка		ИмяФормы = ЭтаФорма.ИмяФормы;
	Исключение	Возврат;
	КонецПопытки;
	
	Если ЭтаФорма.Параметры.Свойство("_НачалоРаботыСистемы") И НЕ Отказ Тогда
		// Создание происходит при начале работы системы, обновлять состав открываемых форм не нужно
		// но если передали Отказ, то нужно удалить эту форму из открываемых при запуске.
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура();
	Если ИмяФормы = "ОбщаяФорма.ПанельОтчетов" Тогда
		Попытка Параметры.Вставить("Подсистемы", ЭтаФорма.Параметры.Подсистемы);
		Исключение КонецПопытки;
		Попытка Параметры.Вставить("ИмяФормы", ЭтаФорма.Параметры.ИмяФормы);
		Исключение КонецПопытки;
		//Попытка Параметры.Вставить("КартинкаФормы", ЭтаФорма.Параметры.КартинкаФормы);
		//Исключение КонецПопытки;
	КонецЕсли;
	ОбновитьСоставОткрытыхФорм(ИмяФормы, Параметры);
	
КонецПроцедуры

Процедура ПриЗакрытииНаСервере(ЭтаФорма) Экспорт
	Если Не CRM_РежимФормЗакладкиСерверПовтИсп.ИспользуетсяРежимЗакладок() Тогда Возврат; КонецЕсли;
	
	Попытка		ИмяФормы = ЭтаФорма.ИмяФормы;
	Исключение	Возврат;
	КонецПопытки;
	
	Параметры = Новый Структура();
	Если ИмяФормы = "ОбщаяФорма.ПанельОтчетов" Тогда
		Попытка Параметры.Вставить("Подсистемы", ЭтаФорма.Параметры.Подсистемы);
		Исключение КонецПопытки;
		Попытка Параметры.Вставить("ИмяФормы", ЭтаФорма.Параметры.ИмяФормы);
		Исключение КонецПопытки;
		//Попытка Параметры.Вставить("КартинкаФормы", ЭтаФорма.Параметры.КартинкаФормы);
		//Исключение КонецПопытки;
	КонецЕсли;
	ОбновитьСоставОткрытыхФорм(ИмяФормы, Параметры, Истина);
	
КонецПроцедуры

Функция ФормаВынесенаНаРабочийСтол(ИмяФормы) Экспорт
	
	НастройкиНачальнойСтраницы = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиНачальнойСтраницы");
	
	Если НастройкиНачальнойСтраницы = Неопределено Тогда
		НастройкиНачальнойСтраницы = Новый НастройкиНачальнойСтраницы;
	КонецЕсли;
	
	Если НастройкиНачальнойСтраницы = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СоставФорм = НастройкиНачальнойСтраницы.ПолучитьСоставФорм();
	
	Для Ин = 0 По СоставФорм.ЛеваяКолонка.ВГраница() Цикл
		Если СоставФорм.ЛеваяКолонка[Ин] = ИмяФормы Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла;
	
	Для Ин = 0 По СоставФорм.ПраваяКолонка.ВГраница() Цикл
		Если СоставФорм.ПраваяКолонка[Ин] = ИмяФормы Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Процедура ОбновитьСоставОткрытыхФорм(Знач ИмяФормы, Параметры, ТолькоУдалить = Ложь, Пользователь=Неопределено)
	ТекущийСоставОткрытыхФорм = ПолучитьТекущийСоставОткрытыхФорм(Пользователь);
	
	нИндекс = CRM_РежимФормЗакладкиКлиентСервер.НайтиФормуВСоставеФорм(ИмяФормы, Параметры, ТекущийСоставОткрытыхФорм);
	Если нИндекс <> Неопределено Тогда
		ТекущийСоставОткрытыхФорм.Удалить(нИндекс);
	КонецЕсли;
	
	Если ТолькоУдалить Тогда
		СохранитьТекущийСоставОткрытыхФорм(ТекущийСоставОткрытыхФорм, Пользователь);
		Возврат;
	КонецЕсли;
	
	Если ФормаВынесенаНаРабочийСтол(ИмяФормы) Тогда Возврат; КонецЕсли;
	
	СтруктураОписаниеФормы = CRM_РежимФормЗакладкиКлиентСервер.ПолучитьСтруктуруБланкОписанияФормы();
	СтруктураОписаниеФормы.ИмяФормы = ИмяФормы;
	СтруктураОписаниеФормы.Параметры = Параметры;
	
	ТекущийСоставОткрытыхФорм.Добавить(СтруктураОписаниеФормы);
	СохранитьТекущийСоставОткрытыхФорм(ТекущийСоставОткрытыхФорм, Пользователь);
КонецПроцедуры

Функция ПолучитьТекущийСоставОткрытыхФорм(Пользователь=Неопределено) Экспорт
	Данные = CRM_ХранилищеНастроек.Загрузить("ФормыДляОткрытияПриСтартеСистемыВРежимеЗакладок",,, Пользователь);
	Если ТипЗнч(Данные) <> Тип("Массив") Тогда
		Данные = Новый Массив();
	КонецЕсли;
	Возврат Данные;
КонецФункции

Процедура СохранитьТекущийСоставОткрытыхФорм(ТекущийСоставОткрытыхФорм, Пользователь=Неопределено) Экспорт
	CRM_ХранилищеНастроек.Сохранить("ФормыДляОткрытияПриСтартеСистемыВРежимеЗакладок",, ТекущийСоставОткрытыхФорм,, Пользователь);
КонецПроцедуры

Функция ВосстановлениеФормыПриЗапускеСеанса(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	ВосстанавливатьФормуПриЗапуске = CRM_ХранилищеНастроек.Загрузить(ЭтаФорма.ИмяФормы, "ВосстанавливатьФормуПриОткрытии");
	Если НЕ ЗначениеЗаполнено(ВосстанавливатьФормуПриЗапуске) Тогда
		ВосстанавливатьФормуПриЗапуске = Истина;
	КонецЕсли;		
	
	ЭтаФорма.Элементы.КнопкаВосстанавливатьФормуПриОткрытии.Пометка = ВосстанавливатьФормуПриЗапуске;
	
	// Добавление формы к списку открываемых при запуске приложения в зависимости от настройки.
	Если ВосстанавливатьФормуПриЗапуске Тогда
		// алгоритм по умолчанию
		CRM_РежимФормЗакладкиСервер.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Иначе
		// Форму удаляем из открываемых при запуске.
		CRM_РежимФормЗакладкиСервер.ПриСозданииНаСервере(ЭтаФорма, Истина, СтандартнаяОбработка);
		// Если это начало системы и галочка восстановления снята, то форма не открывается.
		Если ЭтаФорма.Параметры.Свойство("_НачалоРаботыСистемы") Тогда
			Возврат Ложь;
		КонецЕсли;			
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции	


#Область СлужебныеПроцедурыИФункции

Процедура ИсключитьФормыИзЗакладок() Экспорт
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для каждого Пользователь из ПользователиИБ Цикл
		ОбновитьСоставОткрытыхФорм("Обработка.CRM_ВоронкаПродаж.Форма.Форма", Новый Структура, Истина, Пользователь.Имя);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти