
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьДеревоПрисоединенныеФайлы(Форма, Объект) Экспорт
	
	ИмяТаблицы 				   = "Справочник." + Объект.Метаданные().Имя + "ПрисоединенныеФайлы";	
	ЭлементыДерева			   = Форма.ДеревоПрисоединенныеФайлы.ПолучитьЭлементы();	
	ЭлементыДерева.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыПрисоединенныхФайлов.Ссылка КАК ВидФайла
		|ПОМЕСТИТЬ ВТ_ВидыПрисоединенныхФайловВсе
		|ИЗ
		|	Справочник.вогВидыПрисоединенныхФайлов КАК ВидыПрисоединенныхФайлов
		|ГДЕ
		|	ВидыПрисоединенныхФайлов.Принадлежность = &Принадлежность
		|	И НЕ ВидыПрисоединенныхФайлов.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрисоедниненныеФайлыПоВидам.ВидФайла,
		|	ПрисоедниненныеФайлыПоВидам.ПрисоединенныйФайл,
		|	ВЫБОР
		|		КОГДА ПрисоедниненныеФайлыПоВидам.ПрисоединенныйФайл.ПометкаУдаления = ИСТИНА
		|			ТОГДА ПрисоедниненныеФайлыПоВидам.ПрисоединенныйФайл.ИндексКартинки + 1
		|		ИНАЧЕ ПрисоедниненныеФайлыПоВидам.ПрисоединенныйФайл.ИндексКартинки
		|	КОНЕЦ КАК ИндексКартинки
		|ПОМЕСТИТЬ ВТ_ВидыПрисоединенныхФайловОбъекта
		|ИЗ
		|	РегистрСведений.вогПрисоедниненныеФайлыОбъектовПоВидам КАК ПрисоедниненныеФайлыПоВидам
		|ГДЕ
		|	ПрисоедниненныеФайлыПоВидам.ВидФайла.Принадлежность = &Принадлежность
		|	И ПрисоедниненныеФайлыПоВидам.ПрисоединенныйФайл.ВладелецФайла = &Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВидыПрисоединенныхФайловВсе.ВидФайла КАК ВидФайла,
		|	ВТ_ВидыПрисоединенныхФайловОбъекта.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ВТ_ВидыПрисоединенныхФайловОбъекта.ИндексКартинки КАК ИндексКартинки
		|ИЗ
		|	ВТ_ВидыПрисоединенныхФайловВсе КАК ВТ_ВидыПрисоединенныхФайловВсе
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыПрисоединенныхФайловОбъекта КАК ВТ_ВидыПрисоединенныхФайловОбъекта
		|		ПО ВТ_ВидыПрисоединенныхФайловВсе.ВидФайла = ВТ_ВидыПрисоединенныхФайловОбъекта.ВидФайла
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидФайла,
		|	ПрисоединенныйФайл
		|ИТОГИ ПО
		|	ВидФайла
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Объект"        , Объект);
	Запрос.УстановитьПараметр("Принадлежность", ИмяТаблицы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаВидФайла = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидФайла.Следующий() Цикл
		
		СтрокаВидФайла = ЭлементыДерева.Добавить();	
		ЗаполнитьЗначенияСвойств(СтрокаВидФайла, ВыборкаВидФайла);
		
		СтрокаВидФайла.Группировка    = ВыборкаВидФайла.ВидФайла;
		СтрокаВидФайла.ИндексКартинки = -1;
		СтрокаВидФайла.ТолькоПросмотр = Истина;
		
		ВыборкаЗаписи = ВыборкаВидФайла.Выбрать();
		Пока ВыборкаЗаписи.Следующий() Цикл
			Если Не ЗначениеЗаполнено(ВыборкаЗаписи.ПрисоединенныйФайл) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаФайла = СтрокаВидФайла.ПолучитьЭлементы().Добавить();	
			ЗаполнитьЗначенияСвойств(СтрокаФайла, ВыборкаЗаписи);
			
			СтрокаФайла.Группировка = ВыборкаЗаписи.ПрисоединенныйФайл;
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура ВыполнитьДобавлениеВидаФайла(ВидФайла, МассивФайлов) Экспорт

	Для каждого Файл Из МассивФайлов Цикл
		Менеджер = РегистрыСведений.вогПрисоедниненныеФайлыОбъектовПоВидам.СоздатьМенеджерЗаписи();
		Менеджер.ВидФайла			= ВидФайла;
		Менеджер.ПрисоединенныйФайл = Файл;
		
		Менеджер.Записать();
	
	КонецЦикла;
		
КонецПроцедуры // ВыполнитьДобавлениеВидаФайла()

#КонецОбласти

#Область События

Процедура ВыполнитьДействияПриЗаписиПрисоединенногоФайла(ФайлОбъект) Экспорт
	
	Если Не вогУправлениеПрисоединеннымиФайламиКлиентСерверПовтИсп.ИспользоватьРедактированиеПрисоединенныхФайловПоВидам(ФайлОбъект.ВладелецФайла) Тогда
		Возврат;	
	КонецЕсли;	
	
	МассивФайлов = Новый Массив;
	МассивФайлов.Добавить(ФайлОбъект.Ссылка);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивФайлов", МассивФайлов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПрисоедниненныеФайлыОбъектовПоВидам.ВидФайла
		|ИЗ
		|	РегистрСведений.вогПрисоедниненныеФайлыОбъектовПоВидам КАК ПрисоедниненныеФайлыОбъектовПоВидам
		|ГДЕ
		|	ПрисоедниненныеФайлыОбъектовПоВидам.ПрисоединенныйФайл В(&МассивФайлов)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ВыполнитьДобавлениеВидаФайла(Справочники.вогВидыПрисоединенныхФайлов.ПустаяСсылка(), МассивФайлов); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

