
#Область ПрограммныйИнтерфейс

// Помещает во временное хранилище табличный документ-образец отчета по ссылке на вариант
//
Процедура ЗаполнитьОбразцыОтчетов(СтруктураВарианта, АдресХранилища) Экспорт
	
	РезультатФормирования 	= ОбразецВариантаТабличныйДокумент(СтруктураВарианта.Ссылка);
	
	Если РезультатФормирования <> Неопределено Тогда
		СтруктураВарианта.Образец = РезультатФормирования.ТабличныйДокумент;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураВарианта, АдресХранилища);
	
КонецПроцедуры

Функция ОбразецВариантаТабличныйДокумент(ВариантОтчета)
	
	Результат = Неопределено;
	
	Попытка
		
		ПараметрыФормирования = Новый Структура;
		ПараметрыФормирования.Вставить("СсылкаВарианта", 		ВариантОтчета);
		ПараметрыФормирования.Вставить("СсылкаОтчета", 			ВариантОтчета.Отчет);
		ПараметрыФормирования.Вставить("КлючВарианта", 			ВариантОтчета.КлючВарианта);
		ПараметрыФормирования.Вставить("ИдентификаторФормы", 	Неопределено);
		
		Результат = ВариантыОтчетов.СформироватьОтчет(ПараметрыФормирования, Ложь, Ложь);
		
	Исключение
		
		ШаблонСообщения = НСтр("ru = 'Формирование эскиза отчета %1. %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ВариантОтчета, ОписаниеОшибки());
		
		ЗаписьЖурналаРегистрации("ФормированиеЭскизовОтчетов", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

 