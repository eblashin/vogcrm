

Процедура НастроитьЭлементыФормы(Форма, ОтображатьНеДоступныеДляРедактирования = Ложь) Экспорт
	
	
	ПараметрыНастройкиБизнесПроцессов = Форма.ПараметрыНастройкиБизнесПроцессов;
	
	ВариантЗаполненияДанных = Форма.ВариантЗаполненияДанных;
			
	КлиентыВидимость = ПараметрыНастройкиБизнесПроцессов.Клиенты;
		
	НоменклатураВидимость = ПараметрыНастройкиБизнесПроцессов.Номенклатура;
		
	КоллекцииВидимость = ПараметрыНастройкиБизнесПроцессов.Коллекции;	
		
	ТорговыеТочкиВидимость = ПараметрыНастройкиБизнесПроцессов.ТорговыеТочки;
	
	НаселенныйПунктВидимость = ПараметрыНастройкиБизнесПроцессов.ЗаполнятьНаселенныйПункт; //VOG Ульянов И.В. 03.02.2020 CRM-264
	
	КлиентыРедактирование = ПараметрыНастройкиБизнесПроцессов.ВариантЗаполненияДанныхКлиенты = ВариантЗаполненияДанных;
			
	НоменклатураРедактирование = ПараметрыНастройкиБизнесПроцессов.ВариантЗаполненияДанныхНоменклатураКоллекции = ВариантЗаполненияДанных;
		
	КоллекцииРедактирование = ПараметрыНастройкиБизнесПроцессов.ВариантЗаполненияДанныхНоменклатураКоллекции = ВариантЗаполненияДанных;	
		
	ТорговыеТочкиРедактирование = ПараметрыНастройкиБизнесПроцессов.ВариантЗаполненияДанныхТорговыеТочки = ВариантЗаполненияДанных;

	Если ОтображатьНеДоступныеДляРедактирования = Ложь Тогда
		КлиентыВидимость = КлиентыВидимость и КлиентыРедактирование;
		ТорговыеТочкиВидимость = ТорговыеТочкиВидимость и ТорговыеТочкиРедактирование;
		НоменклатураВидимость = НоменклатураВидимость и НоменклатураРедактирование;
		КоллекцииВидимость = КоллекцииВидимость и КоллекцииРедактирование;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "НаселенныйПункт"  , "Видимость", НаселенныйПунктВидимость));  //VOG Ульянов И.В. 03.02.2020 CRM-264
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ГруппаТорговыеТочки"  , "Видимость", ТорговыеТочкиВидимость));
		
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ГруппаКлиенты"		   , "Видимость", КлиентыВидимость));  
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ГруппаНоменклатура"   , "Видимость", НоменклатураВидимость));
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ГруппаКоллекции"      , "Видимость", КоллекцииВидимость));
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ТаблицаТорговыеТочки"  , "ТолькоПросмотр", Не ТорговыеТочкиРедактирование));
		
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ТаблицаКлиенты"		, "ТолькоПросмотр", Не КлиентыРедактирование));  
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ТаблицаНоменклатура"   , "ТолькоПросмотр", Не НоменклатураРедактирование));
	
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ТаблицаКоллекции"      , "ТолькоПросмотр", Не КоллекцииРедактирование));
			
	МассивЭлементов.Добавить(Новый Структура("ИмяЭлемента, Свойство, Значение", "ПовесткаПредставление", "Видимость", ПараметрыНастройкиБизнесПроцессов.Повестка));
		 	 	
	Для каждого ОписаниеЭлемента Из МассивЭлементов Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			ОписаниеЭлемента.ИмяЭлемента, ОписаниеЭлемента.Свойство, ОписаниеЭлемента.Значение); 
			
	КонецЦикла;
		
КонецПроцедуры // НастроитьЭлементыФормы()	

Процедура ПрочитатьСвязанныеДанные(Форма, Процесс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогОбъектыДляПроцесса.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.вогОбъектыДляПроцесса КАК вогОбъектыДляПроцесса
		|ГДЕ
		|	вогОбъектыДляПроцесса.Объект ССЫЛКА Справочник.Партнеры
		|	И вогОбъектыДляПроцесса.Процесс = &Процесс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогОбъектыДляПроцесса.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.вогОбъектыДляПроцесса КАК вогОбъектыДляПроцесса
		|ГДЕ
		|	вогОбъектыДляПроцесса.Объект ССЫЛКА Справочник.вогТорговыеТочки
		|	И вогОбъектыДляПроцесса.Процесс = &Процесс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогОбъектыДляПроцесса.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.вогОбъектыДляПроцесса КАК вогОбъектыДляПроцесса
		|ГДЕ
		|	вогОбъектыДляПроцесса.Объект ССЫЛКА Справочник.Номенклатура
		|	И вогОбъектыДляПроцесса.Процесс = &Процесс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогОбъектыДляПроцесса.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.вогОбъектыДляПроцесса КАК вогОбъектыДляПроцесса
		|ГДЕ
		|	вогОбъектыДляПроцесса.Объект ССЫЛКА Справочник.вогКоллекцииНоменклатуры
		|	И вогОбъектыДляПроцесса.Процесс = &Процесс";
	
	Запрос.УстановитьПараметр("Процесс", Процесс);
 	                       
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Форма.ТаблицаКлиентов.Загрузить(РезультатЗапроса[0].Выгрузить());
	Форма.ТаблицаТорговыхТочек.Загрузить(РезультатЗапроса[1].Выгрузить());
	Форма.ТаблицаНоменклатура.Загрузить(РезультатЗапроса[2].Выгрузить());
	Форма.ТаблицаКоллекции.Загрузить(РезультатЗапроса[3].Выгрузить());	
 	
КонецПроцедуры // ПрочитатьСвязанныеДанные()

Процедура ЗаписатьСвязанныеДанные(Процесс, Форма) Экспорт
	            		
	Набор = РегистрыСведений.вогОбъектыДляПроцесса.СоздатьНаборЗаписей();
	Набор.Отбор.Процесс.Установить(Процесс);	
	Набор.Очистить();
	
	ТаблицаОбъектов = Набор.Выгрузить();
			
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Форма.ТаблицаКлиентов, ТаблицаОбъектов);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Форма.ТаблицаТорговыхТочек, ТаблицаОбъектов);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Форма.ТаблицаНоменклатура, ТаблицаОбъектов);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Форма.ТаблицаКоллекции, ТаблицаОбъектов);
	
	ТаблицаОбъектов.ЗаполнитьЗначения(Процесс, "Процесс");

	Набор.Загрузить(ТаблицаОбъектов);
	
	Набор.Записать();
		
КонецПроцедуры // ЗаписатьСвязанныеДанные()

Процедура ЗаписатьСвязанныеДанныеПоЗадаче(Процесс,ТаблицаОбъектов) Экспорт
	            		
	Набор = РегистрыСведений.вогОбъектыДляПроцесса.СоздатьНаборЗаписей();
	Набор.Отбор.Процесс.Установить(Процесс);	
	Набор.Очистить();
	
	ТаблицаОбъектов.ЗаполнитьЗначения(Процесс, "Процесс");

	Набор.Загрузить(ТаблицаОбъектов);
	
	Набор.Записать();
		
КонецПроцедуры // ЗаписатьСвязанныеДанные()


Функция ПолучитьТаблицуОбъектовПоПроцессу(Процесс) Экспорт
 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогОбъектыДляПроцесса.Процесс КАК Процесс,
		|	вогОбъектыДляПроцесса.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.вогОбъектыДляПроцесса КАК вогОбъектыДляПроцесса
		|ГДЕ
		|	вогОбъектыДляПроцесса.Процесс = &Процесс";
	
	Запрос.УстановитьПараметр("Процесс", Процесс);
	
	Возврат Запрос.Выполнить().Выгрузить();  
	
КонецФункции	

Функция ПолучитьКлиентовМенеджера(Роль, Менеджер, Клиенты) Экспорт
		 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	вогТорговыеТочки.Ссылка КАК Объект,
		|	вогТорговыеТочки.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_ТорговыеТочки
		|ИЗ
		|	Справочник.вогТорговыеТочки КАК вогТорговыеТочки
		|ГДЕ
		|	вогТорговыеТочки.Ссылка В(&Клиенты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вогТорговыеТочки.Партнер,
		|	вогТорговыеТочки.Ссылка
		|ИЗ
		|	Справочник.вогТорговыеТочки КАК вогТорговыеТочки
		|ГДЕ
		|	вогТорговыеТочки.Партнер В(&Клиенты)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВТ_ТорговыеТочки.Объект КАК Объект,
		|	"""" КАК Процесс
		|ИЗ
		|	РегистрСведений.вогМенеджерыОбъектов КАК вогМенеджерыОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТорговыеТочки КАК ВТ_ТорговыеТочки
		|		ПО вогМенеджерыОбъектов.Владелец = ВТ_ТорговыеТочки.Ссылка
		|ГДЕ
		|	вогМенеджерыОбъектов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|	И вогМенеджерыОбъектов.Менеджер = &Менеджер
		|	И вогМенеджерыОбъектов.Роль = &Роль";
	
	Запрос.УстановитьПараметр("Роль", Роль);
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	Запрос.УстановитьПараметр("Клиенты", Клиенты);
	
	Возврат Запрос.Выполнить().Выгрузить();
	       
	
КонецФункции
