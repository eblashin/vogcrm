// В данный модуль вынесены методы подсистемы CRM, вызываемые из модулей типовых объектов. 
// Выносить можно только те методы, которые не вызывают стандартные методы типового модуля или обработчики форм. 
// Т.е. вызывают только те методы, что тоже вынесены из типового или не содержат таких вызовов.

// Для каждого объекта необходимо задать свою #Область с именем объекта и модуля, как он называется в метаданных.

#Область ОпределениеФормОбъектов

Процедура ОбработкаПолученияФормОбъектовCRM(Источник, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	#Область ПолученияФормВопросыДляАнкетирования
	
	Если ТипЗнч(Источник) = Тип("ПланВидовХарактеристикМенеджер.ВопросыДляАнкетирования") Тогда
	
		Если ВидФормы = "ФормаЭлемента" Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ПланВидовХарактеристик.ВопросыДляАнкетирования.Форма.CRM_ФормаЭлемента";
		ИначеЕсли ВидФормы = "ФормаВыбора" Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ПланВидовХарактеристик.ВопросыДляАнкетирования.Форма.CRM_ФормаВыбора";
		КонецЕсли;
	
	#КонецОбласти
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ОпределениеФормОбъектов

#Область ПВХ_ВопросыДляАнкетирования_МодульОбъекта

// Процедура - обработчик подписки на событие "ОбработкаПроверкиЗаполнения" ПВХ "ВопросыДляАнкетирования".
//
Процедура ОбработкаПроверкиЗаполненияВопросовДляАнкетирования(Источник, Отказ, ПроверяемыеРеквизиты) Экспорт
	Если НЕ (Источник.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.CRM_ВидКонтактнойИнформации) Тогда
	
		Если Источник.ЭтоГруппа Тогда Возврат; КонецЕсли;
		МассивНепроверяемыхРеквизитов = Новый Массив();
		МассивНепроверяемыхРеквизитов.Добавить("CRM_ВидКонтактнойИнформации");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик подписки на событие "ПередЗаписью" ПВХ "ВопросыДляАнкетирования".
//
Процедура ОбработкаПередЗаписьюВопросовДляАнкетирования(Источник, Отказ) Экспорт
	
	Если НЕ Источник.ЭтоГруппа Тогда
		ОчиститьНенужныеРеквизиты(Источник);
		УстановкаТипаПВХ(Источник);
	КонецЕсли;
	
КонецПроцедуры

// Процедура очищает значения ненужных реквизитов,
// Такая ситуация возникает, когда пользователь изменяет тип ответа при редактировании.
Процедура ОчиститьНенужныеРеквизиты(Источник)

	Если (Источник.ТипОтвета <> Перечисления.ТипыОтветовНаВопрос.CRM_ВидКонтактнойИнформации)
	   И (НЕ Источник.CRM_ВидКонтактнойИнформации.Пустая()) Тогда
		
		Источник.CRM_ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
		
	КонецЕсли;
	Если ((Источник.ТипОтвета <> Перечисления.ТипыОтветовНаВопрос.Число) И (Источник.ТипОтвета <> Перечисления.ТипыОтветовНаВопрос.Строка)
		И (Источник.ТипОтвета <> Перечисления.ТипыОтветовНаВопрос.Текст)) И (Источник.Длина <> 0)Тогда
		
		Источник.Длина = 0;
		
	КонецЕсли;
	
	Если (Источник.ТипОтвета <> Перечисления.ТипыОтветовНаВопрос.Число) Тогда	
		
		Источник.МинимальноеЗначение       = 0;
		Источник.МаксимальноеЗначение      = 0;
		Источник.АгрегироватьСуммуВОтчетах = Ложь;
		
	КонецЕсли;
	
	Если Источник.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.НесколькоВариантовИз Тогда
		Источник.ТребуетсяКомментарий = Ложь;
		Источник.ПояснениеКомментария = "";
	КонецЕсли;

КонецПроцедуры

// Устанавливает тип значения ПВХ в зависимости от типа ответа.
Процедура УстановкаТипаПВХ(Источник)
	
	ТипыОтветовНаВопрос = Перечисления.ТипыОтветовНаВопрос;
	
	// Квалификаторы
	КЧ = Новый КвалификаторыЧисла(?(Источник.Длина = 0, 15, Источник.Длина), Источник.Точность);
	КС = Новый КвалификаторыСтроки(Источник.Длина);
	КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
	
	// Описание типов
	ОписаниеТиповЧисло  = Новый ОписаниеТипов("Число",,КЧ);
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , КС);
	ОписаниеТиповДата   = Новый ОписаниеТипов("Дата",КД , , );
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	ОписаниеТиповВО     = Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовАнкет");
	
	Если Источник.ТипОтвета = ТипыОтветовНаВопрос.Строка Тогда
		
		Источник.ТипЗначения = ОписаниеТиповСтрока;
		
	ИначеЕсли Источник.ТипОтвета = ТипыОтветовНаВопрос.Текст Тогда
		
		Источник.ТипЗначения = ОписаниеТиповСтрока;
		
	ИначеЕсли Источник.ТипОтвета = ТипыОтветовНаВопрос.Число Тогда
		
		Источник.ТипЗначения = ОписаниеТиповЧисло;
		
	ИначеЕсли Источник.ТипОтвета = ТипыОтветовНаВопрос.Дата Тогда
		
		Источник.ТипЗначения = ОписаниеТиповДата;
		
	ИначеЕсли Источник.ТипОтвета = ТипыОтветовНаВопрос.Булево Тогда
		
		Источник.ТипЗначения = ОписаниеТиповБулево;
			
	ИначеЕсли Источник.ТипОтвета = ТипыОтветовНаВопрос.CRM_ВидКонтактнойИнформации Тогда
	
		Источник.ТипЗначения = ОписаниеТиповСтрока;
		
	ИначеЕсли Источник.ТипОтвета =ТипыОтветовНаВопрос.ОдинВариантИз
		  ИЛИ Источник.ТипОтвета = ТипыОтветовНаВопрос.НесколькоВариантовИз Тогда
		
		Источник.ТипЗначения = ОписаниеТиповВО;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
