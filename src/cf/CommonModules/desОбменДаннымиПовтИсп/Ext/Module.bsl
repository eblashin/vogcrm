
Функция ПодключитьОбработкуДействияПоОтпечатку(ОтпечатокОбработки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	desНастройкиДействийПриОбменеДействия.ХранилищеОбработка
		|ИЗ
		|	Справочник.desНастройкиДействийПриОбмене.Действия КАК desНастройкиДействийПриОбменеДействия
		|ГДЕ
		|	desНастройкиДействийПриОбменеДействия.ОтпечатокОбработки = &ОтпечатокОбработки";
	
	Запрос.УстановитьПараметр("ОтпечатокОбработки", ОтпечатокОбработки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ОбработкаОбменаДанными = Неопределено;	
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		ДанныеОбработки	= ВыборкаДетальныеЗаписи.ХранилищеОбработка.Получить();
		
		Если ДанныеОбработки <> Неопределено Тогда
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".epf");
			ДанныеОбработки.Записать(ИмяВременногоФайла);
			Попытка
				ОбработкаОбменаДанными = ВнешниеОбработки.Создать(ИмяВременногоФайла, Ложь);
			Исключение
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбменаДанными;
	
КонецФункции

Функция ПолучитьОбработкуДействия(Организация,Контрагент,ДействиеОбмена) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	desНастройкиДействийПриОбменеДействия.ОтпечатокОбработки,
	|	1 КАК Приоритет
	|ИЗ
	|	Справочник.desНастройкиДействийПриОбмене.Действия КАК desНастройкиДействийПриОбменеДействия
	|ГДЕ
	|	desНастройкиДействийПриОбменеДействия.Ссылка.Организация = &Организация
	|	И desНастройкиДействийПриОбменеДействия.Ссылка.Контрагент = &Контрагент
	|	И desНастройкиДействийПриОбменеДействия.Действие = &Действие
	|	И НЕ desНастройкиДействийПриОбменеДействия.Ссылка.ПометкаУдаления
	|	И desНастройкиДействийПриОбменеДействия.Включено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	desНастройкиДействийПриОбменеДействия.ОтпечатокОбработки,
	|	2
	|ИЗ
	|	Справочник.desНастройкиДействийПриОбмене.Действия КАК desНастройкиДействийПриОбменеДействия
	|ГДЕ
	|	desНастройкиДействийПриОбменеДействия.Ссылка.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И desНастройкиДействийПриОбменеДействия.Ссылка.Контрагент = &Контрагент
	|	И desНастройкиДействийПриОбменеДействия.Действие = &Действие
	|	И НЕ desНастройкиДействийПриОбменеДействия.Ссылка.ПометкаУдаления
	|	И desНастройкиДействийПриОбменеДействия.Включено
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("Действие"   , ДействиеОбмена);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент" , Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат desОбменДаннымиПовтИсп.ПодключитьОбработкуДействияПоОтпечатку(ВыборкаДетальныеЗаписи.ОтпечатокОбработки);
	КонецЕсли;
	
КонецФункции
