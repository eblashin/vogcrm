
// Заполняет протокол мероприятия на основании программы.
// Если протокол уже заполнен, то он будет очищен.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятия - Мероприятие.
//
Процедура ЗаполнитьПротокол(Мероприятие) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		// Очистка протокола.
		МероприятиеОбъект = Мероприятие.ПолучитьОбъект();
		МероприятиеОбъект.Заблокировать();
		Для Каждого Строка Из МероприятиеОбъект.вогПротокол Цикл
			ПунктПротоколаОбъект = Строка.ПунктПротокола.ПолучитьОбъект();
			ПунктПротоколаОбъект.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		МероприятиеОбъект.вогПротокол.Очистить();
		
		Для Каждого Строка Из МероприятиеОбъект.вогПрограмма Цикл
			
			Если Не Строка.ТребуетПринятияРешения Тогда 
				Продолжить;
			КонецЕсли;
			
			ПунктПротоколаОбъект = Справочники.вогПротоколыМероприятий.СоздатьЭлемент();
			
			ПунктПротоколаОбъект.Мероприятие = Мероприятие;
			ПунктПротоколаОбъект.НомерПунктаПрограммы = Строка.НомерПункта;
			ПунктПротоколаОбъект.Слушали = вогУправлениеМероприятиямиКлиентСервер.СформироватьТекстСлушали(Строка);
			ПунктПротоколаОбъект.Записать();
			
			СтрокаПротокола = МероприятиеОбъект.вогПротокол.Добавить();
			СтрокаПротокола.НомерПунктаПрограммы = Строка.НомерПункта;
			СтрокаПротокола.ПунктПротокола = ПунктПротоколаОбъект.Ссылка;
			
		КонецЦикла;
		
		МероприятиеОбъект.Записать();
		МероприятиеОбъект.Разблокировать();
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Выполняет чтение данных протокола мероприятия из пунктов протокола.
//
// Параметры:
//  Протокол - ДанныеФормыКоллекция - Протокол.
//
Процедура ВывестиПротоколМероприятия(Протокол) Экспорт
	
	ПунктыПротокола = Протокол.Выгрузить(, "ПунктПротокола").ВыгрузитьКолонку("ПунктПротокола");
	// Чтение данных протокола.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПротоколыМероприятий.Ссылка КАК Ссылка,
		|	ПротоколыМероприятий.Выступили КАК Выступили,
		|	ПротоколыМероприятий.Ответственный КАК Ответственный,
		|	ПротоколыМероприятий.Решили КАК Решили,
		|	ПротоколыМероприятий.СрокИсполненияПроцесса КАК СрокИсполнения,
		|	ПротоколыМероприятий.Проверяющий КАК Проверяющий,
		|	ПротоколыМероприятий.СрокОбработкиРезультатов КАК СрокОбработкиРезультатов,
		|	ПротоколыМероприятий.Слушали КАК Слушали,
		|	ЕСТЬNULL(CRM_МероприятиевогПрограмма.ОтКлиента, ЛОЖЬ) КАК ОтКлиента
		|ИЗ
		|	Справочник.вогПротоколыМероприятий КАК ПротоколыМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.CRM_Мероприятие.вогПрограмма КАК CRM_МероприятиевогПрограмма
		|		ПО ПротоколыМероприятий.Мероприятие = CRM_МероприятиевогПрограмма.Ссылка
//		|			И ПротоколыМероприятий.НомерПунктаПрограммы = CRM_МероприятиевогПрограмма.НомерПункта
		|ГДЕ
		|	ПротоколыМероприятий.Ссылка В(&ПунктыПротокола)";

	Запрос.УстановитьПараметр("ПунктыПротокола", ПунктыПротокола);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОтбора = Новый Структура("ПунктПротокола", Выборка.Ссылка);
		СтрокиПунктаПротокола = Протокол.НайтиСтроки(ПараметрыОтбора);
		Если СтрокиПунктаПротокола.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаПунктаПротокола = СтрокиПунктаПротокола[0];
		ЗаполнитьЗначенияСвойств(СтрокаПунктаПротокола, Выборка);
		
		ОтборОбсуждения = Новый ОтборОбсужденийСистемыВзаимодействия;
		ОтборОбсуждения.КонтекстОбсуждения = Новый КонтекстОбсужденияСистемыВзаимодействия(ПолучитьНавигационнуюСсылку(Выборка.Ссылка));
		ОтборОбсуждения.КонтекстноеОбсуждение = Истина;
		Попытка
		СписокОбсуждений = СистемаВзаимодействия.ПолучитьОбсуждения(ОтборОбсуждения);
		Если СписокОбсуждений.Количество()>0 тогда
			СтрокаПунктаПротокола.ЕстьОбсуждение=Истина;
		КонецЕсли;
		Исключение
		КонецПопытки;
		Массив = Новый Массив;
		массив.Добавить(Выборка.Ссылка);
		МассивПоручений = вогУправлениеМероприятиямиВызовСервера.НайтиПорученияПоПротоколам(массив);
		Если МассивПоручений.Количество()>0 тогда
		СтрокаПунктаПротокола.Естьпоручения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Расчет номеров пунктов протокола мероприятия.
	вогУправлениеМероприятиямиКлиентСервер.ВывестиНомераПунктовПротокола(Протокол);
	
КонецПроцедуры

