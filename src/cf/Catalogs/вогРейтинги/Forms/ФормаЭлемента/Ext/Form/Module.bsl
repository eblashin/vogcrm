
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончания) И Объект.ДатаОкончания < Объект.ДатаНачала Тогда
		Отказ = Истина;
		Сообщить("Дата окончания должна быть болльше даты начала");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоРоли(Команда)
	
	Если НЕ Объект.Обои И НЕ Объект.Плитка Тогда
		Сообщить("Выберите хотя бы одно направление деятельности");
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Справочник.РолиИсполнителей.ФормаВыбора",,,,,, Новый ОписаниеОповещения("ДобавитьПоРолиЗавершение", ЭтотОбъект), );
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоРолиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокУчастниковПоРоли(Результат);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокУчастниковПоРоли(ВыбраннаяРоль)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсполнителиЗадач.Исполнитель КАК Участник,
		|	ИсполнителиЗадач.РольИсполнителя КАК Роль,
		|	ИсполнителиЗадач.Исполнитель.CRM_НаправлениеДеятельности КАК НаправлениеДеятельности
		|ИЗ
		|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
		|ГДЕ
		|	НЕ ИсполнителиЗадач.Исполнитель.Недействителен
		|	И НЕ ИсполнителиЗадач.Исполнитель.ПометкаУдаления
		|	И ИсполнителиЗадач.РольИсполнителя = &РольИсполнителя
		|	И ИсполнителиЗадач.Исполнитель.CRM_НаправлениеДеятельности В(&НаправлениеДеятельности)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсполнителиЗадач.Исполнитель,
		|	ИсполнителиЗадач.РольИсполнителя,
		|	ИсполнителиЗадач.Исполнитель.CRM_НаправлениеДеятельности";
	
	Запрос.УстановитьПараметр("РольИсполнителя", ВыбраннаяРоль);
	СписокНаправленийДеятельности = Новый Массив;
	СписокНаправленийДеятельности.Добавить(Справочники.НаправленияДеятельности.ПустаяСсылка());
	Если Объект.Обои Тогда
		СписокНаправленийДеятельности.Добавить(Справочники.НаправленияДеятельности.Обои);
	КонецЕсли;
	Если Объект.Плитка Тогда
		СписокНаправленийДеятельности.Добавить(Справочники.НаправленияДеятельности.Плитка);
	КонецЕсли;
	Запрос.УстановитьПараметр("НаправлениеДеятельности",СписокНаправленийДеятельности);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = Объект.Участники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиУчастникПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Участники.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Участник) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.НаправлениеДеятельности = ОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Участник,"CRM_НаправлениеДеятельности");
	
КонецПроцедуры
