
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипыОтношений = Новый Массив;
	ТипыОтношений.Добавить(Справочники.CRM_ТипыОтношений.Поставщик);
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипОтношений", Новый ФиксированныйМассив(ТипыОтношений));
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	
	Элементы.Производитель.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПроизводительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПустаяСтрока(Текст) Тогда
		ДанныеВыбора = ПолучитьДанныеВыбораНаСервере(Текст);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеВыбораНаСервере(Знач СтрокаПоиска)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ПартнерывогТипыОтношений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Партнеры.вогТипыОтношений КАК ПартнерывогТипыОтношений
		|ГДЕ
		|	ПартнерывогТипыОтношений.ТипОтношений = &ТипОтношений
		|	И ПартнерывогТипыОтношений.Ссылка.Наименование ПОДОБНО &СтрокаПоиска";
	
	Запрос.УстановитьПараметр("ТипОтношений", Справочники.CRM_ТипыОтношений.Поставщик);
	Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеВыбора.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти
