
Функция ПолучитьДействие(Организация, Контрагент, Действие) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	desНастройкиДействийПриОбменеДействия.Ссылка КАК Действие,
		|	desНастройкиДействийПриОбменеДействия.ОтпечатокОбработки,
		|	desНастройкиДействийПриОбменеДействия.ОтпечатокПараметров,
		|	desНастройкиДействийПриОбменеДействия.Включено,
		|	desНастройкиДействийПриОбменеДействия.РегламентноеЗадание,
		|	desНастройкиДействийПриОбменеДействия.ХранилищеОбработка,
		|	desНастройкиДействийПриОбменеДействия.ХранилищеСхемаИсточникаДанных,
		|	desНастройкиДействийПриОбменеДействия.ХранилищеКомпоновщикНастроек,
		|	desНастройкиДействийПриОбменеДействия.ХранилищеПараметры,
		|	desНастройкиДействийПриОбменеДействия.Ссылка.ПрофильОбмена
		|ИЗ
		|	Справочник.desНастройкиДействийПриОбмене.Действия КАК desНастройкиДействийПриОбменеДействия
		|ГДЕ
		|	desНастройкиДействийПриОбменеДействия.Ссылка.Организация = &Организация
		|	И desНастройкиДействийПриОбменеДействия.Ссылка.Контрагент = &Контрагент
		|	И desНастройкиДействийПриОбменеДействия.Действие = &Действие";
	
	Запрос.УстановитьПараметр("Действие", Действие);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе	
		
		СтруктураРезультат = Новый Структура("Действие,ОтпечатокОбработки,ОтпечатокПараметров,Включено,РегламентноеЗадание,ХранилищеОбработка,ХранилищеСхемаИсточникаДанных,ХранилищеКомпоновщикНастроек,ХранилищеПараметры,ПрофильОбмена");
		
		Выборка = Результат.Выбрать();	
		
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураРезультат,Выборка);
		
		Возврат СтруктураРезультат;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИдентификаторыДляОбменаЭДО(Организация, Контрагент, СпособОбменаЭД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	desНастройкиДействийПриОбменеДействия.Ссылка.ИдентификаторЭДО КАК ИдентификаторЭДОКонтрагента,
		|	desНастройкиДействийПриОбменеДействия.Ссылка.ПрофильОбмена.ИдентификаторОрганизации КАК ИдентификаторЭДООрганизации,
		|	desНастройкиДействийПриОбменеДействия.Ссылка.ПрофильОбмена.ОператорЭДО КАК НаименованиеОператора,
		|	desНастройкиДействийПриОбменеДействия.Ссылка.ПрофильОбмена.ОператорЭДОИНН КАК ИННОператора,
		|	desНастройкиДействийПриОбменеДействия.Ссылка.ПрофильОбмена.ОператорЭДОИд КАК ИдентификаторЭДООператора
		|ИЗ
		|	Справочник.desНастройкиДействийПриОбмене.Действия КАК desНастройкиДействийПриОбменеДействия
		|ГДЕ
		|	desНастройкиДействийПриОбменеДействия.Действие = Значение(Справочник.desДействияПриОбмене.ЭДОУПД)
		|	И desНастройкиДействийПриОбменеДействия.Ссылка.Организация = &Организация
		|	И desНастройкиДействийПриОбменеДействия.Ссылка.Контрагент = &Контрагент
		|	И desНастройкиДействийПриОбменеДействия.Ссылка.ПрофильОбмена.СпособОбменаЭД = &СпособОбменаЭД
		|	И НЕ desНастройкиДействийПриОбменеДействия.Ссылка.ПометкаУдаления";

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("СпособОбменаЭД", СпособОбменаЭД);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		СтруктураВозврата = Новый Структура("ИдентификаторЭДОКонтрагента,ИдентификаторЭДООрганизации,ИдентификаторЭДООператора,НаименованиеОператора,ИННОператора");
		
		ЗаполнитьЗначенияСвойств(СтруктураВозврата,ВыборкаДетальныеЗаписи);
		
		Возврат СтруктураВозврата;
		
	Иначе
		
		ВызватьИсключение "Для контрагента " +Строка(Контрагент)+ "не установлена настройка обмена ЭДО - " + Строка(СпособОбменаЭД);
		
	КонецЕсли;
	
КонецФункции
