
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	Если Параметры.Свойство("ПараметрыКонтроля") Тогда
		ЗаполнитьРеквизитыКонтроляОбъекта(Параметры.ПараметрыКонтроля);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ПараметрыКонтроляЗначений = Новый Соответствие;
	
	ЭлементыДерева = РеквизитыКонтроля.ПолучитьЭлементы();
	
	Для Каждого СтрокаДерева Из ЭлементыДерева Цикл
		
		Если СтрокаДерева.ПометкаОбмена Тогда
			
			СтруктураРеквизита = Новый Структура;	
			СтруктураРеквизита.Вставить("ТипРеквизита");
			СтруктураРеквизита.Вставить("СсылкаРеквизита");
			СтруктураРеквизита.Вставить("Представление");
			СтруктураРеквизита.Вставить("ПометкаОбмена");
			
			ЗаполнитьЗначенияСвойств(СтруктураРеквизита, СтрокаДерева);
			
			ПараметрыКонтроляЗначений.Вставить(СтрокаДерева.СсылкаРеквизита, СтруктураРеквизита);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Закрыть(ПараметрыКонтроляЗначений);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыКонтроляОбъекта(ПараметрыКонтроля)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", ПараметрыКонтроля.СсылкаРеквизита);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	CRM_ЗначенияКлассификаторов.Ссылка КАК ЗначениеКлассификатора,
	|	CRM_ЗначенияКлассификаторов.Представление КАК Представление
	|ИЗ
	|	Справочник.CRM_ЗначенияКлассификаторов КАК CRM_ЗначенияКлассификаторов
	|ГДЕ
	|	CRM_ЗначенияКлассификаторов.Владелец = &Владелец";
	
	РезультатЗапроса = Запрос.Выполнить();                                      
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ТипРеквизита 				= "ЗначениеКлассификатораОбъекта";
		ТекущиеПараметрыКонтроля 	= Новый Соответствие;
		ЭлементыДерева 				= РеквизитыКонтроля.ПолучитьЭлементы();
		
		Если ЗначениеЗаполнено(ПараметрыКонтроля.ПараметрыКонтроляЗначений) Тогда
			
			СтрокаХранилища = ЗначениеИзСтрокиВнутр(ПараметрыКонтроля.ПараметрыКонтроляЗначений);
			
			Если ТипЗнч(СтрокаХранилища) = Тип("ХранилищеЗначения") Тогда
				ТекущиеПараметрыКонтроля = СтрокаХранилища.Получить();
			КонецЕсли;
			
		КонецЕсли;
		
		//ТекущиеПараметрыКонтроля = ПолучитьТекущиеПараметрыКонтроля(ТекущиеПараметрыКонтроля, ТипРеквизита);
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ПометкаОбмена = ТекущиеПараметрыКонтроля.Получить(Выборка.ЗначениеКлассификатора) <> Неопределено;
			
			СтруктураРеквизита = Новый Структура;	
			СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
			СтруктураРеквизита.Вставить("СсылкаРеквизита", Выборка.ЗначениеКлассификатора);
			СтруктураРеквизита.Вставить("Представление"  , Выборка.Представление);
			СтруктураРеквизита.Вставить("ИндексКартинки" , 25);
			СтруктураРеквизита.Вставить("ПометкаОбмена"	 , ПометкаОбмена);
			
			СтрокаДерева = ЭлементыДерева.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДерева, СтруктураРеквизита);
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляЗначений, ТипРеквизита)
	
	Если ПараметрыКонтроляЗначений <> Неопределено
		И ПараметрыКонтроляЗначений.Свойство(ТипРеквизита) Тогда
		
		Результат = ПараметрыКонтроляЗначений[ТипРеквизита];	
		
	Иначе
		
		Результат = Новый Массив;
		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

#КонецОбласти
