#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция ПолучитьСоставСпискаРассылки(Ссылка, МассивРезультат = Неопределено, МассивОбработанныхСписков = Неопределено) Экспорт
	Если МассивРезультат = Неопределено Тогда
		МассивРезультат = Новый Массив();
	КонецЕсли;
	Если МассивОбработанныхСписков = Неопределено Тогда
		МассивОбработанныхСписков = Новый Массив();
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Состав.Адрес КАК Адрес,
	|	Состав.Контакт КАК Контакт,
	|	Состав.КонтактСтрокой КАК КонтактСтрокой
	|ИЗ
	|	Справочник.CRM_СпискиРассылок.Состав КАК Состав
	|ГДЕ
	|	Состав.Ссылка = &Ссылка
	|	И НЕ Состав.Ссылка.ПометкаУдаления");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.Адрес) И Не ЗначениеЗаполнено(Выборка.Контакт) Тогда
			Продолжить;
		КонецЕсли;
		
		Если	ТипЗнч(Выборка.Контакт) = Тип("СправочникСсылка.CRM_СпискиРассылок")
			И	МассивОбработанныхСписков.Найти(Выборка.Контакт) = Неопределено Тогда
			//
			МассивОбработанныхСписков.Добавить(Выборка.Контакт);
			ПолучитьСоставСпискаРассылки(Выборка.Контакт, МассивРезультат, МассивОбработанныхСписков);
		Иначе
			МассивРезультат.Добавить(Новый Структура("Адрес,Контакт", Выборка.Адрес, ?(ЗначениеЗаполнено(Выборка.Контакт), Выборка.Контакт, 
				?(ЗначениеЗаполнено(Выборка.КонтактСтрокой),Выборка.КонтактСтрокой,Неопределено))));
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивРезультат;
КонецФункции
#КонецЕсли