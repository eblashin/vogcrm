
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("КлючОтбора") И ЗначениеЗаполнено(Параметры.КлючОтбора) Тогда
		
		Если Параметры.Свойство("ВыбранныеДанные") И ТипЗнч(Параметры.ВыбранныеДанные) = Тип("Массив") Тогда
			МассивВыбранных = Параметры.ВыбранныеДанные;
		Иначе
			МассивВыбранных = Новый Массив;
		КонецЕсли;
		
		ЗаполнитьТаблицуОбъектов(Параметры.КлючОтбора, МассивВыбранных); 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивВыбранных = Новый Массив;
	
	Для Каждого СтрокаТЧ Из ДополнительныеОбъекты Цикл
		Если СтрокаТЧ.Пометка Тогда
			МассивВыбранных.Добавить(СтрокаТЧ.Объект);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(МассивВыбранных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуОбъектов(КлючОтбора, МассивВыбранных)
	
	Если КлючОтбора = Справочники.вогОбластиДелегированияПрав.ПроцессыИЗадачи Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	CRM_КартыМаршрутов.Ссылка КАК Объект,
		|	ВЫБОР
		|		КОГДА CRM_КартыМаршрутов.Ссылка В (&МассивВыбранных)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка
		|ИЗ
		|	Справочник.CRM_КартыМаршрутов КАК CRM_КартыМаршрутов
		|ГДЕ
		|	CRM_КартыМаршрутов.ПометкаУдаления = ЛОЖЬ
		|	И CRM_КартыМаршрутов.ЭтоГруппа = ЛОЖЬ";
		
		Запрос.УстановитьПараметр("МассивВыбранных", МассивВыбранных);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрока = ДополнительныеОбъекты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти