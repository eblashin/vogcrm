// ++ VOG Солодов В.В. 13.09.2021 CRM-1148
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидБрендЗоны();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьВидБрендЗоны()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаКоды.ВидБрендЗоны КАК ВидБрендЗоны,
	|	ТаблицаКоды.ДатаЗакрытияФакт КАК ДатаЗакрытияФакт
	|ПОМЕСТИТЬ ВТ_ВидыБрендЗоны
	|ИЗ
	|	&ТаблицаКоды КАК ТаблицаКоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны КАК ВидБрендЗоны,
	|	1 КАК Приоритет
	|ПОМЕСТИТЬ ВТ_ПриоритетыБрендЗон
	|ИЗ
	|	ВТ_ВидыБрендЗоны КАК ВТ_ВидыБрендЗоны
	|ГДЕ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны = ЗНАЧЕНИЕ(Перечисление.ВидыБрендЗоны.ПалитраВашихСтен)
	|	И &ВидМагазина = ЗНАЧЕНИЕ(Перечисление.ВидМагазина.ФирменныйМагазин)
	|	И ВТ_ВидыБрендЗоны.ДатаЗакрытияФакт = ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны,
	|	2
	|ИЗ
	|	ВТ_ВидыБрендЗоны КАК ВТ_ВидыБрендЗоны
	|ГДЕ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны = ЗНАЧЕНИЕ(Перечисление.ВидыБрендЗоны.ПалитраОК)
	|	И &ВидМагазина = ЗНАЧЕНИЕ(Перечисление.ВидМагазина.ФирменныйМагазин)
	|	И ВТ_ВидыБрендЗоны.ДатаЗакрытияФакт = ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны,
	|	1
	|ИЗ
	|	ВТ_ВидыБрендЗоны КАК ВТ_ВидыБрендЗоны
	|ГДЕ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны = ЗНАЧЕНИЕ(Перечисление.ВидыБрендЗоны.БрендЗонаКлиента)
	|	И &ВидМагазина = ЗНАЧЕНИЕ(Перечисление.ВидМагазина.БрендЗона)
	|	И ВТ_ВидыБрендЗоны.ДатаЗакрытияФакт = ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны,
	|	2
	|ИЗ
	|	ВТ_ВидыБрендЗоны КАК ВТ_ВидыБрендЗоны
	|ГДЕ
	|	ВТ_ВидыБрендЗоны.ВидБрендЗоны = ЗНАЧЕНИЕ(Перечисление.ВидыБрендЗоны.БрендЗонаВОГ)
	|	И &ВидМагазина = ЗНАЧЕНИЕ(Перечисление.ВидМагазина.БрендЗона)
	|	И ВТ_ВидыБрендЗоны.ДатаЗакрытияФакт = ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_ПриоритетыБрендЗон.ВидБрендЗоны КАК ВидБрендЗоны
	|ИЗ
	|	ВТ_ПриоритетыБрендЗон КАК ВТ_ПриоритетыБрендЗон
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(ВТ_ПриоритетыБрендЗон.Приоритет) КАК Приоритет
	|		ИЗ
	|			ВТ_ПриоритетыБрендЗон КАК ВТ_ПриоритетыБрендЗон) КАК ВложенныйЗапрос
	|		ПО ВТ_ПриоритетыБрендЗон.Приоритет = ВложенныйЗапрос.Приоритет";
	
	ТаблицаКоды = КодыУПП.Выгрузить(, "ВидБрендЗоны, ДатаЗакрытияФакт");
	
	Запрос.УстановитьПараметр("ТаблицаКоды", ТаблицаКоды);
	Запрос.УстановитьПараметр("ВидМагазина", ВидМагазина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ВидБрендЗоны = ВыборкаДетальныеЗаписи.ВидБрендЗоны;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
// -- VOG Солодов В.В. 13.09.2021 CRM-1148