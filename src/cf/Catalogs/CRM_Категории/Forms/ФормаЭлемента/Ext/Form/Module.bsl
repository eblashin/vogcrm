
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" элемента формы "Цвет"
//
Процедура ЦветНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("ТекущийЦвет", Объект.ЦветИндекс);
	ОповещениеНовое = Новый ОписаниеОповещения("ЦветНачалоВыбораЗавершение", ЭтотОбъект);
	ФормаВыбораЦвета = ОткрытьФорму("Справочник.CRM_Категории.Форма.ФормаВыбораЦвета", ПараметрыФормы, Элемент,,,,
		ОповещениеНовое, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
КонецПроцедуры // ЦветНачалоВыбора()

&НаКлиенте
// Продолжение процедуры "ЦветНачалоВыбора"
//
Процедура ЦветНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ (Результат = Неопределено) И НЕ (Результат = КодВозвратаДиалога.Отмена) Тогда
		Объект.ЦветПредставление 	= Результат[0].Представление;
		Объект.ЦветИндекс 			= Результат[0].Картинка;
		Объект.ЦветКрасный 			= Результат[0].Красный;
		Объект.ЦветЗеленый 			= Результат[0].Зеленый;
		Объект.ЦветСиний 			= Результат[0].Синий;
		Элементы.Цвет.ЦветФона 		= Новый Цвет(Объект.ЦветКрасный,Объект.ЦветЗеленый,Объект.ЦветСиний);
		Элементы.Цвет.ЦветТекста	= CRM_ОбщегоНазначенияКлиентСервер.ПолучитьКонтрастныйЦвет(Элементы.Цвет.ЦветФона);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" элемента формы "Цвет"
//
Процедура ЦветПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры // ЦветПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМЫ

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Ссылка) ИЛИ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Элементы.Цвет.ЦветФона		= Новый Цвет(Объект.ЦветКрасный, Объект.ЦветЗеленый, Объект.ЦветСиний); 
		Элементы.Цвет.ЦветТекста	= CRM_ОбщегоНазначенияКлиентСервер.ПолучитьКонтрастныйЦвет(Элементы.Цвет.ЦветФона);
	Иначе
		Объект.ЦветИндекс			= 10;
		Объект.ЦветПредставление 	= НСтр("ru = 'Стальной'");
    	Объект.ЦветКрасный  		= 196;	
		Объект.ЦветЗеленый  		= 232; 
		Объект.ЦветСиний  			= 190;
		Элементы.Цвет.ЦветФона		= Новый Цвет(Объект.ЦветКрасный, Объект.ЦветЗеленый, Объект.ЦветСиний); 
		Элементы.Цвет.ЦветТекста	= CRM_ОбщегоНазначенияКлиентСервер.ПолучитьКонтрастныйЦвет(Элементы.Цвет.ЦветФона);
	КонецЕсли;
КонецПроцедуры // ПриСозданииНаСервере()
