
///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ДобавитьПапкиMSExchangeНаСервере(УчетнаяЗаписьMSExchange)
	Если Не ЗначениеЗаполнено(УчетнаяЗаписьMSExchange) Тогда
		Возврат НСтр("ru = 'Не указана учетная запись MS Exchange!'");
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
	|	СтруктурыПапокMSExchange.Ссылка
	|ИЗ
	|	Справочник.CRM_СтруктурыПапокMSExchange КАК СтруктурыПапокMSExchange
	|ГДЕ
	|	СтруктурыПапокMSExchange.СерверMSExchange = &СерверMSExchange
	|	И СтруктурыПапокMSExchange.УчетнаяЗапись = &УчетнаяЗапись
	|");
	Запрос.УстановитьПараметр("СерверMSExchange", УчетнаяЗаписьMSExchange.СерверMSExchange);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗаписьMSExchange);
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат НСтр("ru = 'Для учетной записи электронной почты '") + """" + Строка(Владелец) + """ "
			+ Символы.ПС + НСтр("ru = 'структура папок MS Exchange еще не была загружена с сервера!'");
		//
	КонецЕсли;
	
	Справочники.CRM_СтруктурыПапокMSExchange.ДобавитьПапкиMSExchangeВСправочникПапкиЭлектронныхПисем(Владелец, УчетнаяЗаписьMSExchange, УчетнаяЗаписьMSExchange.СерверMSExchange);
	
	Возврат Неопределено;
КонецФункции

/////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ДобавитьПапкиMSExchange(Команда)
	Если Не ЗначениеЗаполнено(Владелец) Тогда
		Возврат;
	КонецЕсли;
	
	УчетнаяЗаписьMSExchange = CRM_MSExchangeВызовСервера.ПолучитьУчетнуюЗаписьMSExchangeПоУчетнойЗаписиЭлектроннойПочты(Владелец);
	Если Не ЗначениеЗаполнено(УчетнаяЗаписьMSExchange) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для учетной записи электронной почты '") + """" + Строка(Владелец) + """" + Символы.ПС
			+ НСтр("ru = 'не найдена соответствующая учетная запись MS Exchange!'"));
		//
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьПапкиMSExchangeЗавершение", ЭтотОбъект, УчетнаяЗаписьMSExchange);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Текущая структура папок будет дополнена папками из MS Exchange.
													|Продолжить?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПапкиMSExchangeЗавершение(Ответ, УчетнаяЗаписьMSExchange) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СтрОшибка = ДобавитьПапкиMSExchangeНаСервере(УчетнаяЗаписьMSExchange);
		Если ТипЗнч(СтрОшибка) = Тип("Строка") И ЗначениеЗаполнено(СтрОшибка)  Тогда
			ПоказатьПредупреждение(, СтрОшибка);
		КонецЕсли;
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПолучитьФункциональнуюОпцию("CRM_ИспользоватьОбменСMSExchange") Тогда
		УчетнаяЗаписьMSExchange = Неопределено;
		Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец") Тогда
			Владелец = Параметры.Отбор.Владелец;
			УчетнаяЗаписьMSExchange = CRM_MSExchangeВызовСервера.ПолучитьУчетнуюЗаписьMSExchangeПоУчетнойЗаписиЭлектроннойПочты(Владелец);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Владелец) Или Не ЗначениеЗаполнено(УчетнаяЗаписьMSExchange) Или Не ПравоДоступа("Добавление", Метаданные.Справочники.ПапкиЭлектронныхПисем) Тогда
			Элементы.СписокДобавитьПапкиMSExchange.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
