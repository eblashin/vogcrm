#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий


Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ОбъектКопирования.ЭтоГруппа Тогда
		ПлановаяДатаНачала = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса();
		ПлановаяДатаОкончания = Дата(1,1,1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыПоУмолчанию();
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если CRM_ЭтоКейс Тогда 
		
		ПроверяемыеРеквизиты.Добавить("CRM_МодельКейса");
		Если ЗначениеЗаполнено(CRM_МодельКейса) Тогда
			СтруктураМоделиКейса = CRM_УправлениеПроектамиСервер.СтруктураМоделиКейса(CRM_МодельКейса);	
			
			Если СтруктураМоделиКейса.ИспользоватьБизнесРегион Тогда
				ПроверяемыеРеквизиты.Добавить("CRM_БизнесРегион");
			
			КонецЕсли;
			
			Если СтруктураМоделиКейса.ИспользоватьОтветственныеМенеджеры Тогда
				ПроверяемыеРеквизиты.Добавить("CRM_ОтветственныеМенеджеры");
			
			КонецЕсли;
			
			Если СтруктураМоделиКейса.ИспользоватьКонтактныеЛица Тогда
				ПроверяемыеРеквизиты.Добавить("CRM_Партнер");
				ПроверяемыеРеквизиты.Добавить("CRM_КонтактныеЛица");
				
			КонецЕсли;
			
			Если СтруктураМоделиКейса.ИспользоватьКалендарныеГрафики Тогда
				ПроверяемыеРеквизиты.Добавить("ПлановаяДатаНачала");
				ПроверяемыеРеквизиты.Добавить("ПлановаяДатаОкончания");
				
				// Дата окончания действия должна быть не меньше, чем дата начала действия.
				Если ЗначениеЗаполнено(ПлановаяДатаНачала) И ЗначениеЗаполнено(ПлановаяДатаОкончания) Тогда
					Если ПлановаяДатаНачала > ПлановаяДатаОкончания Тогда
						ТекстОшибки = НСтр("ru='Плановая дата окончания должна быть не меньше плановой даты начала'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстОшибки,
							ЭтотОбъект, 
							"ПлановаяДатаОкончания",
							,
							Отказ);
						
					Конецесли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьРеквизитыПоУмолчанию()
	
	Статус 	= Перечисления.CRM_СтатусыПроектов.Планируется;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли
