
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.ИдентификаторОбъекта) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ПостроитьДеревоРеквизитов(Параметры.ИдентификаторОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите реквизит документа'"));
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Деревореквизитов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ОповеститьОВыборе(ТекущаяСтрока.Путь);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Значение = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите реквизит документа'"));
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Деревореквизитов.НайтиПоИдентификатору(Значение);
	ОповеститьОВыборе(ТекущаяСтрока.Путь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПостроитьДеревоРеквизитов(ИдентификаторОбъекта)
	
	ОбъектМетданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторОбъекта);
	ДеревоЭлементов = ДанныеФормыВЗначение(ДеревоРеквизитов,Тип("ДеревоЗначений"));
	
	ПервыйУровень 						= ДеревоЭлементов.Строки.Добавить();
	ПервыйУровень.Имя 					= ОбъектМетданных.Имя;
	ПервыйУровень.СтандартнаяКартинка 	= 2;
	ПервыйУровень.Путь 					= "Объект";
	
	ИмяПервыйУровень = ПервыйУровень.Путь;
	
	Для каждого Реквизит Из ОбъектМетданных.Реквизиты Цикл
		ВторойУровень 						= ПервыйУровень.Строки.Добавить();
		ВторойУровень.Имя 					= Реквизит.Имя;
		ВторойУровень.СтандартнаяКартинка 	= 1;
		ВторойУровень.Путь					= ИмяПервыйУровень+"." + Реквизит.Имя;
	КонецЦикла;
	
	Для каждого ТабличнаяЧасть Из ОбъектМетданных.ТабличныеЧасти Цикл
		ВторойУровень 						= ПервыйУровень.Строки.Добавить();
		ВторойУровень.Имя 					= ТабличнаяЧасть.Имя;
		ВторойУровень.СтандартнаяКартинка 	= 0;
		ВторойУровень.Путь					= ИмяПервыйУровень+"." + ТабличнаяЧасть.Имя;
		ИмяВторойУровень					= ВторойУровень.Путь;
		Для каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
			ТретийУровень 						= ВторойУровень.Строки.Добавить();
			ТретийУровень.Имя 					= РеквизитТЧ.Имя;
			ТретийУровень.СтандартнаяКартинка 	= 1;
			ТретийУровень.Путь 					= ИмяВторойУровень + "." + РеквизитТЧ.Имя;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВДанныеФормы(ДеревоЭлементов,ДеревоРеквизитов);
	
КонецПроцедуры

#КонецОбласти