
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого СтрКартинка Из Метаданные.ОбщиеКартинки Цикл
		Элементы.ИзображениеСтатуса.СписокВыбора.Добавить(СтрКартинка.Имя,СтрКартинка.Синоним, ,БиблиотекаКартинок[СтрКартинка.Имя]);
	КонецЦикла;
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.ИзображениеСтатуса = Элементы.ИзображениеСтатуса.СписокВыбора.НайтиПоЗначению("Информация").Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеВидимостьюНастройкаДоступности();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастроитьДоступностьРеквизитовПриИзменении(Элемент)
	УправлениеВидимостьюНастройкаДоступности();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкаДоступности

&НаКлиенте
Процедура НастройкаДоступностиОбъектМетаданныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	СписокВыбора 			= Новый СписокЗначений;
	ЗаполнитьСписокОбъектыМетаданных(СписокВыбора);
	
	ДанныеВыбора 			= СписокВыбора;
	
	ТекущиеДанные 				= Элементы.НастройкаДоступности.ТекущиеДанные;
	ТекущиеДанные.Свойство 		= "";
	ТекущиеДанные.Доступность 	= Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДоступностиСвойствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	ТекущиеДанные 			= Элементы.НастройкаДоступности.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ОбъектМетаданных) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторОбъекта",ТекущиеДанные.ОбъектМетаданных);
		ОткрытьФорму("Справочник.вогШаблоныСтатусов.Форма.ФормаВыборРеквизитов",ПараметрыФормы,Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеВидимостьюНастройкаДоступности()
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,"ГруппаНастройкаДоступности","Видимость",Объект.НастроитьДоступностьРеквизитов);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокОбъектыМетаданных(ДанныеВыбора)

	ИдентификаторДокументы = Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоНаименованию("Документы");
	
	Запрос = Новый Запрос("Выбрать Спр.Ссылка ИЗ Справочник.ИдентификаторыОбъектовМетаданных КАК Спр Где Спр.Родитель = &Родитель");
	Запрос.УстановитьПараметр("Родитель",ИдентификаторДокументы);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ДанныеВыбора.Добавить(ВыборкаИзРезультатаЗапроса.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти