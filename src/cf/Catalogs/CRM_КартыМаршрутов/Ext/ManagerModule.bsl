
Процедура СкорретироватьСКД_В_КартахМаршрутовИПроцессах() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	CRM_КартыМаршрутов.Ссылка
	               |ИЗ
	               |	Справочник.CRM_КартыМаршрутов КАК CRM_КартыМаршрутов
	               |ГДЕ
	               |	НЕ CRM_КартыМаршрутов.ЭтоГруппа
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	Проекты.Ссылка
	               |ИЗ
	               |	Справочник.Проекты КАК Проекты
	               |ГДЕ
	               |	НЕ Проекты.ЭтоГруппа";
	
	Если НЕ CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ Проекты.ЭтоГруппа", "Истина");
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Если ТипЗнч(Выборка.Ссылка) = Тип("СправочникСсылка.CRM_КартыМаршрутов") Тогда
			РеквизитХранилищаСхемыКомпоновкиДанных = "ХранилищеСхемыКомпоновкиДанных";
		Иначе
			РеквизитХранилищаСхемыКомпоновкиДанных = "bpmХранилищеСхемыКомпоновкиДанных";
		КонецЕсли;
		СКД = Объект[РеквизитХранилищаСхемыКомпоновкиДанных].Получить();
		
		Если СКД = Неопределено Тогда
			
			Продолжить;		
		
		КонецЕсли;		
		
		ЕстьИзменения = Ложь;
		Для Каждого НаборКоллекции ИЗ СКД.НаборыДанных Цикл
			Если НЕ ТипЗнч(НаборКоллекции) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда Продолжить; КонецЕсли;
			
			ТекстЗапроса = НаборКоллекции.Запрос;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"Пользователи.CRM_УровеньДоступа,","");
			
			Если НЕ НаборКоллекции.Запрос = ТекстЗапроса Тогда
				НаборКоллекции.Запрос = ТекстЗапроса;
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьИзменения Тогда
			
			Объект[РеквизитХранилищаСхемыКомпоновкиДанных] = Новый ХранилищеЗначения(СКД);
			Объект.ОбменДанными.Загрузка = Истина;
			Объект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
