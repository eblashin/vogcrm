

Процедура ПолучитьСхемуКомпоновкиРасчетаЗначенийПоказателя(АдресСхемыКомпоновкиДанных,АдресНастроекКомпоновкиДанных, ИнициализироватьСхемуКомпоновки) Экспорт
		
	Если ИнициализироватьСхемуКомпоновки Тогда
		
		СКД 			= ИнициализироватьСхемуКомпоновкиРасчетаЗначенийПоказателя();
		НастройкиСКД 	= СКД.НастройкиПоУмолчанию;
		
	Иначе		
		
		СКД 			= ХранилищеСхемыКомпоновкиДанных.Получить();	
		НастройкиСКД 	= ХранилищеНастроекКомпоновкиДанных.Получить();	
		
	КонецЕсли;
			
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор);
	
	АдресНастроекКомпоновкиДанных = ПоместитьВоВременноеХранилище(НастройкиСКД, Новый УникальныйИдентификатор);
	
КонецПроцедуры

Функция ИнициализироватьСхемуКомпоновкиРасчетаЗначенийПоказателя() 
	
	СКД = ПолучитьМакет("СхемаКомпоновкиРасчетаЗначенийПоказателя");
	               	
	//Если Ссылка.Пустая() Тогда
	//	ПоказательСсылка = Справочники.вогПоказателиЗаполнености.ПолучитьСсылку(Новый УникальныйИдентификатор);
	//	УстановитьСсылкуНового(ПоказательСсылка);	
	//Иначе
	//	ПоказательСсылка = Ссылка;		
	//КонецЕсли;
	//
	//СКД.Параметры.Показатель.Значение = ПоказательСсылка;
		
	Если Не ПустаяСтрока(ИмяОбъектаМетаданных) Тогда
		
		СКД.НаборыДанных.ДанныеОбъекта.Запрос = ПолучитьТекстЗапросаКДаннымОбъектаРасчета();
		
	КонецЕсли;
	
	Возврат СКД;
	
КонецФункции

Функция ПолучитьТекстЗапросаКДаннымОбъектаРасчета()
//START Кайдашов 03/07/19 добавил характеристики в запрос по умолчанию
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДанныеОбъектаРасчета.Ссылка КАК Объект
	               |ИЗ
	               |	Справочник.%1 КАК ДанныеОбъектаРасчета
	               |{ХАРАКТЕРИСТИКИ
	               |	ТИП(Справочник.вогРаспределительныеЦентры, Справочник.вогТорговыеТочки, Справочник.вогЮридическиеЛица, Справочник.Партнеры, Справочник.КонтактныеЛицаПартнеров)
	               |	ВИДЫХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ЗначенияКлассификаторов.ДополнительныйРеквизит КАК ДополнительныйРеквизит,
	               |			CRM_ДополнительныеРеквизитыКлассификаторов.ТипЗначения КАК ТипЗначения,
	               |			CRM_ЗначенияКлассификаторов.Наименование КАК Наименование,
	               |			CRM_ЗначенияКлассификаторов.Ссылка КАК Ссылка
	               |		ИЗ
	               |			Справочник.CRM_ЗначенияКлассификаторов КАК CRM_ЗначенияКлассификаторов
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.CRM_ДополнительныеРеквизитыКлассификаторов КАК CRM_ДополнительныеРеквизитыКлассификаторов
	               |				ПО CRM_ЗначенияКлассификаторов.ДополнительныйРеквизит = CRM_ДополнительныеРеквизитыКлассификаторов.Ссылка)
	               |	ПОЛЕКЛЮЧА Ссылка
	               |	ПОЛЕИМЕНИ Наименование
	               |	ПОЛЕТИПАЗНАЧЕНИЯ ТипЗначения
	               |	ЗНАЧЕНИЯХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ОбъектыЗначенийКлассификаторов.Объект КАК Объект,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора.Владелец КАК ЗначениеКлассификатораВладелец,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеРеквизита КАК ЗначениеРеквизита
	               |		ИЗ
	               |			РегистрСведений.CRM_ОбъектыЗначенийКлассификаторов КАК CRM_ОбъектыЗначенийКлассификаторов)
	               |	ПОЛЕОБЪЕКТА Объект
	               |	ПОЛЕВИДА ЗначениеКлассификатора
	               |	ПОЛЕЗНАЧЕНИЯ ЗначениеРеквизита }
	               |{ХАРАКТЕРИСТИКИ
	               |	ТИП(Справочник.вогРаспределительныеЦентры, Справочник.вогТорговыеТочки, Справочник.вогЮридическиеЛица, Справочник.Партнеры, Справочник.КонтактныеЛицаПартнеров)
	               |	ВИДЫХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_Классификаторы.Ссылка КАК Ссылка,
	               |			CRM_Классификаторы.Наименование КАК Наименование,
	               |			CRM_Классификаторы.ТипЗначения КАК ТипЗначения
	               |		ИЗ
	               |			ПланВидовХарактеристик.CRM_Классификаторы КАК CRM_Классификаторы
	               |		ГДЕ
	               |			CRM_Классификаторы.ВидОтображения = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.CRM_ВидыОтображенияКлассификаторов.ОдиночноеЗначение))
	               |	ПОЛЕКЛЮЧА Ссылка
	               |	ПОЛЕИМЕНИ Наименование
	               |	ЗНАЧЕНИЯХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ОбъектыЗначенийКлассификаторов.Объект КАК Объект,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора.Владелец КАК ЗначениеКлассификатораВладелец,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеРеквизита КАК ЗначениеРеквизита
	               |		ИЗ
	               |			РегистрСведений.CRM_ОбъектыЗначенийКлассификаторов КАК CRM_ОбъектыЗначенийКлассификаторов)
	               |	ПОЛЕОБЪЕКТА Объект
	               |	ПОЛЕВИДА ЗначениеКлассификатораВладелец
	               |	ПОЛЕЗНАЧЕНИЯ ЗначениеКлассификатора }
	               |{ХАРАКТЕРИСТИКИ
	               |	ТИП(Справочник.вогРаспределительныеЦентры, Справочник.вогТорговыеТочки, Справочник.вогЮридическиеЛица, Справочник.Партнеры, Справочник.КонтактныеЛицаПартнеров)
	               |	ВИДЫХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ЗначенияКлассификаторов.Ссылка КАК Ссылка,
	               |			CRM_Классификаторы.Наименование + "" ("" + CRM_ЗначенияКлассификаторов.Наименование + "")"" КАК Наименование,
	               |			CRM_Классификаторы.ТипЗначения КАК ТипЗначения
	               |		ИЗ
	               |			Справочник.CRM_ЗначенияКлассификаторов КАК CRM_ЗначенияКлассификаторов
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.CRM_Классификаторы КАК CRM_Классификаторы
	               |				ПО CRM_ЗначенияКлассификаторов.Владелец = CRM_Классификаторы.Ссылка
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.CRM_ДополнительныеРеквизитыКлассификаторов КАК CRM_ДополнительныеРеквизитыКлассификаторов
	               |				ПО CRM_ЗначенияКлассификаторов.ДополнительныйРеквизит = CRM_ДополнительныеРеквизитыКлассификаторов.Ссылка
	               |		ГДЕ
	               |			CRM_Классификаторы.ВидОтображения = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.CRM_ВидыОтображенияКлассификаторов.Вхождение))
	               |	ПОЛЕКЛЮЧА Ссылка
	               |	ПОЛЕИМЕНИ Наименование
	               |	ПОЛЕТИПАЗНАЧЕНИЯ ТипЗначения
	               |	ЗНАЧЕНИЯХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ОбъектыЗначенийКлассификаторов.Объект КАК Объект,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора КАК ЗначениеКлассификатора,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора.Владелец КАК ЗначениеКлассификатораВладелец,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеРеквизита КАК ЗначениеРеквизита
	               |		ИЗ
	               |			РегистрСведений.CRM_ОбъектыЗначенийКлассификаторов КАК CRM_ОбъектыЗначенийКлассификаторов)
	               |	ПОЛЕОБЪЕКТА Объект
	               |	ПОЛЕВИДА ЗначениеКлассификатора
	               |	ПОЛЕЗНАЧЕНИЯ ЗначениеРеквизита }
	               |{ХАРАКТЕРИСТИКИ
	               |	ТИП(Справочник.вогРаспределительныеЦентры, Справочник.вогТорговыеТочки, Справочник.вогЮридическиеЛица, Справочник.Партнеры, Справочник.КонтактныеЛицаПартнеров)
	               |	ВИДЫХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ЗначенияКлассификаторов.Ссылка КАК Ссылка,
	               |			CRM_Классификаторы.Наименование + "" ["" + CRM_ЗначенияКлассификаторов.Наименование + ""]"" КАК Наименование,
	               |			CRM_Классификаторы.ТипЗначения КАК ТипЗначения
	               |		ИЗ
	               |			Справочник.CRM_ЗначенияКлассификаторов КАК CRM_ЗначенияКлассификаторов
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.CRM_Классификаторы КАК CRM_Классификаторы
	               |				ПО CRM_ЗначенияКлассификаторов.Владелец = CRM_Классификаторы.Ссылка
	               |				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.CRM_ДополнительныеРеквизитыКлассификаторов КАК CRM_ДополнительныеРеквизитыКлассификаторов
	               |				ПО CRM_ЗначенияКлассификаторов.ДополнительныйРеквизит = CRM_ДополнительныеРеквизитыКлассификаторов.Ссылка
	               |		ГДЕ
	               |			CRM_Классификаторы.ВидОтображения = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.CRM_ВидыОтображенияКлассификаторов.Вхождение)
	               |			И CRM_ДополнительныеРеквизитыКлассификаторов.Ссылка ЕСТЬ NULL)
	               |	ПОЛЕКЛЮЧА Ссылка
	               |	ПОЛЕИМЕНИ Наименование
	               |	ЗНАЧЕНИЯХАРАКТЕРИСТИК (ВЫБРАТЬ
	               |			CRM_ОбъектыЗначенийКлассификаторов.Объект КАК Объект,
	               |			ИСТИНА КАК Значение,
	               |			CRM_ОбъектыЗначенийКлассификаторов.ЗначениеКлассификатора КАК ЗначениеКлассификатора
	               |		ИЗ
	               |			РегистрСведений.CRM_ОбъектыЗначенийКлассификаторов КАК CRM_ОбъектыЗначенийКлассификаторов)
	               |	ПОЛЕОБЪЕКТА Объект
	               |	ПОЛЕВИДА ЗначениеКлассификатора }";	

	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса,ИмяОбъектаМетаданных);

	Возврат ТекстЗапроса;

КонецФункции

Процедура ПроверкаКорректностиНастроекСКД(СКД, НастройкиСКД, Отказ) Экспорт
			
	ПользовательскоеПолеЗначение = Новый ПолеКомпоновкиДанных("ПользовательскиеПоля.Поле1");
	
	МассивВыбранныхПолей = Новый Массив;
	
	ВыбранныеПоля = НастройкиСКД.Выбор.Элементы;
	
	Для Каждого ВыбранноеПоле Из ВыбранныеПоля Цикл
		МассивВыбранныхПолей.Добавить(ВыбранноеПоле.Поле);	
	КонецЦикла;
	
	МассивПолей = ПолучитьМассивОбязательныхПолейРезультирующейВыборки();
	
	Для Каждого ПроверяемоеПоле Из МассивПолей Цикл
		
		Если МассивВыбранныхПолей.Найти(ПроверяемоеПоле)=Неопределено Тогда
			
			Если ПроверяемоеПоле = ПользовательскоеПолеЗначение Тогда
				Сообщить("В настройках схемы компоновки данных не задано обязательное пользовательское поле выбора: ""Значение""");
			Иначе
				Сообщить("В настройках схемы компоновки данных не задано обязательное поле выбора: """+Строка(ПроверяемоеПоле)+"""");
			КонецЕсли;
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
					
КонецПроцедуры

Функция ПолучитьМассивОбязательныхПолейРезультирующейВыборки()
	
	МассивОбязательныхПолей = Новый Массив;
	
	Измерения = Метаданные.РегистрыСведений.вогЗначенияРасчетаПоказателейЗаполненности.Измерения;
	
	Для Каждого Поле Из Измерения Цикл
		//Кайдашов 13/06/19 добавил проверку только для  запрета незаполненных значений
		Если Поле.ЗапрещатьНезаполненныеЗначения тогда
			МассивОбязательныхПолей.Добавить(Новый ПолеКомпоновкиДанных(Поле.Имя));	
		КонецЕсли;	
	КонецЦикла;
	
	// в регистре присутствует один единственный ресурс - значение, для которого в компоновке обязательно должно 
	// присутствовать пользовательское поле ПользовательскиеПоля.Поле1
	МассивОбязательныхПолей.Добавить(Новый ПолеКомпоновкиДанных("ПользовательскиеПоля.Поле1"));	
		
	Возврат МассивОбязательныхПолей;
	
КонецФункции
