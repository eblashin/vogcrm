
#Область СобытияИКомандыФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьСхемуКомпоновки = Объект.Ссылка.Пустая();
	
	ПолучитьСхемуКомпоновкиРасчетаЗначенийПоказателя(ИнициализироватьСхемуКомпоновки);
		
	Для каждого СтрТипа Из Метаданные.ОпределяемыеТипы.вогКонтрольЗаполненностиРеквизитов.Тип.Типы() Цикл
		
	    МетаданныеТипа = Метаданные.НайтиПоТипу(СтрТипа);
		
		Элементы.ИмяОбъектаМетаданных.СписокВыбора.Добавить(МетаданныеТипа.Имя,МетаданныеТипа.ПредставлениеОбъекта);	
	
	КонецЦикла; 
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СКД = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	
	НастройкиСКД = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
	
	ТекущийОбъект.ХранилищеСхемыКомпоновкиДанных = Новый ХранилищеЗначения(СКД);
	
	ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиСКД);

	ТекущийОбъект.ПроверкаКорректностиНастроекСКД(СКД,НастройкиСКД, Отказ);
			
КонецПроцедуры

&НаКлиенте
Процедура ИмяОбъектаМетаданныхПриИзменении(Элемент)
	
	ПолучитьСхемуКомпоновкиРасчетаЗначенийПоказателя(Истина);

КонецПроцедуры

#КонецОбласти


#Область РаботаСКД

&НаКлиенте
Процедура РедактироватьСхемуКомпоновкиДанных(Команда)
 	
	// Открыть редактор настроек схемы компоновки данных
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Настройка схемы компоновки данных ""%1""'");
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = СтрЗаменить(ЗаголовокФормыНастройкиСхемыКомпоновкиДанных, "%1", Объект.Наименование);
	
	АдресаНастроек = Неопределено;
	                         	
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
	Новый Структура(
	"НеПомещатьНастройкиВСхемуКомпоновкиДанных,
	|НеРедактироватьСхемуКомпоновкиДанных,
	|НеНастраиватьУсловноеОформление,
	|НеНастраиватьВыбор,
	|НеНастраиватьПорядок,
	|НеНастраиватьПользовательскиеПоля,
	|УникальныйИдентификатор,
	|АдресСхемыКомпоновкиДанных,
	|АдресНастроекКомпоновкиДанных,
	|Заголовок,
	|ИсточникШаблонов,
	|ИмяШаблонаСКД,
	|ВозвращатьИмяТекущегоШаблонаСКД",
	Ложь,
	Ложь,
	Истина,
	Ложь,
	Истина,
	Ложь,
	УникальныйИдентификатор,
	АдресСхемыКомпоновкиДанных,
	АдресНастроекКомпоновкиДанных,
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных,,
	Объект.СхемаКомпоновкиДанных),,,,, Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСхемуКомпоновкиДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    АдресНастроекКомпоновкиДанных = Результат;
     	  
КонецПроцедуры

&НаСервере
Процедура ПолучитьСхемуКомпоновкиРасчетаЗначенийПоказателя(ИнициализироватьСхемуКомпоновки)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	
	ТекущийОбъект.ПолучитьСхемуКомпоновкиРасчетаЗначенийПоказателя(АдресСхемыКомпоновкиДанных,АдресНастроекКомпоновкиДанных,ИнициализироватьСхемуКомпоновки);
	
КонецПроцедуры

#КонецОбласти



