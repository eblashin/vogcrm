
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Групповая.Видимость = (Объект.Вид = Перечисления.CRM_ВидыТочекМаршрута.Действие);
	Элементы.Пояснение.Видимость = (Объект.Вид = Перечисления.CRM_ВидыТочекМаршрута.Действие);
	
	Элементы.ВложенныйПроцесс.Видимость = (Объект.Вид = Перечисления.CRM_ВидыТочекМаршрута.ВложенныйБизнесПроцесс);
	Элементы.ВходящиеТочки.Видимость = (Объект.Вид <> Перечисления.CRM_ВидыТочекМаршрута.Старт);
	Элементы.ИсходящиеТочки.Видимость = (Объект.Вид <> Перечисления.CRM_ВидыТочекМаршрута.Завершение);
	Элементы.ВариантЗавершения.Видимость = (Объект.Вид = Перечисления.CRM_ВидыТочекМаршрута.Завершение);
	
	ЗаполнитьАдресаКартинокКоманд();
	
	// ++ Тищенко В.В. 29.03.2019
	// Видимость тип задачи
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,"вогТипЗадачиИсполнителя","Видимость",Объект.Вид = Перечисления.CRM_ВидыТочекМаршрута.Действие);
	// -- Тищенко В.В. 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикиСобытийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("ТекстОбработки,Заголовок,ШаблонОбработчика", Элемент.ТекущиеДанные.Обработчик,"Обработчик события "+""""+Строка(Элемент.ТекущиеДанные.Событие)+""""
	,Элемент.ТекущиеДанные.ШаблонОбработчика);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикиСобытийВыборЗавершение", ЭтотОбъект, Элемент);
	ОткрытьФорму("Справочник.CRM_ТочкиМаршрутов.Форма.ФормаРедактированияТекста", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикиСобытийВыборЗавершение(Результат, Элемент) экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Элемент.ТекущиеДанные.Обработчик 		= Результат.Обработчик;
		Элемент.ТекущиеДанные.ШаблонОбработчика = Результат.ШаблонОбработчика;
	КонецЕсли;
	
КонецПроцедуры

// ++ Харченко Д.И. №  - 01.11.2018 / 


&НаКлиенте
Процедура КомандыТочкиМаршрутаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "КомандыТочкиМаршрутаОбработчик" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормыОткрытия = Новый Структура("ТекстОбработки, Заголовок", Элемент.ТекущиеДанные.Обработчик, "Обработчик команды "+""""+Строка(Элемент.ТекущиеДанные.Команда)+"""");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандыВопросаВыборЗавершение", ЭтотОбъект, Элемент);
		
		ОткрытьФорму("Справочник.CRM_ТочкиМаршрутов.Форма.ФормаРедактированияТекста", ПараметрыФормыОткрытия, ЭтотОбъект,,,, ОписаниеОповещения);
		
	ИначеЕсли Поле.Имя = "КомандыТочкиМаршрутаИмяКартинки" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Описание = Новый ОписаниеОповещения("ОписаниеОповещенияВыборКартинкиИзСписка", ЭтотОбъект, Элементы.КомандыТочкиМаршрута.ТекущаяСтрока);
		
		СписокКартинок = ПолучитьСписокВыбораКартинок();
		
		ТекДанные = Объект.КомандыТочкиМаршрута.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		ТекЭлемент = Неопределено;
		
		Если ЗначениеЗаполнено(ТекДанные.ИмяКартинки) Тогда
			ТекЭлемент = СписокКартинок.НайтиПоЗначению(ТекДанные.ИмяКартинки);
		КонецЕсли;
		
		ПоказатьВыборИзСписка(Описание, СписокКартинок, , ТекЭлемент);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КомандыВопросаВыборЗавершение(Результат, Элемент) Экспорт
	
	Если Не Результат = Неопределено Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Свойство("Обработчик", Элемент.ТекущиеДанные.Обработчик);
		Иначе 
			Элемент.ТекущиеДанные.Обработчик = Результат;
		КонецЕсли;		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // КомандыВопросаВыборЗавершение()

&НаКлиенте
Процедура ОписаниеОповещенияВыборКартинкиИзСписка(Значение, ИДСтроки) Экспорт 
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоискСтроки = Объект.КомандыТочкиМаршрута.НайтиПоИдентификатору(ИДСтроки);
	
	Если ПоискСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоискСтроки.ИмяКартинки = Значение.Значение;
	
	ЗаполнитьАдресКартинки(ИДСтроки);
	
	Модифицированность = Истина;
	
КонецПроцедуры // ОписаниеОтветаНаВопросГиперссылки()

Процедура ЗаполнитьАдресаКартинокКоманд()
	
	Для Каждого стр Из Объект.КомандыТочкиМаршрута Цикл 
		
		ЗаполнитьАдресКартинки(стр.ПолучитьИдентификатор());
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьАдресКартинки(ИДСтроки)
	
	ПоискСтроки = Объект.КомандыТочкиМаршрута.НайтиПоИдентификатору(ИДСтроки);
	
	Если ПоискСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПоискСтроки.ИмяКартинки) Тогда
		ПоискСтроки.Картинка = ПоместитьВоВременноеХранилище(БиблиотекаКартинок[ПоискСтроки.ИмяКартинки], ЭтаФорма.УникальныйИдентификатор);
	Иначе 
		ПоискСтроки.Картинка = "Нет";
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСписокВыбораКартинок()
	
	Возврат вогУправлениеОпросамиСервер.ПолучитьСписокВыбораКартинок();
	
КонецФункции

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьАдресаКартинокКоманд();
	
КонецПроцедуры

// ++ VOG Солодов В.В. 30.04.2020 CRM-601
&НаКлиенте
Процедура РедактируемыРеквизитыОбработчикПриЗаполненииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.РедактируемыРеквизиты.ТекущиеДанные;
	
	ПараметрыФормыОткрытия = Новый Структура();
	ПараметрыФормыОткрытия.Вставить("ТекстОбработки", 	ТекущиеДанные.ОбработчикПриЗаполнении);
	ПараметрыФормыОткрытия.Вставить("Заголовок", 		Элемент.Заголовок);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"РедактируемыРеквизитыОбработчикПриЗаполненииНачалоВыбораЗавершение",
		ЭтотОбъект,
		ТекущиеДанные);
	
	ОткрытьФорму(
		"Справочник.CRM_ТочкиМаршрутов.Форма.ФормаРедактированияТекста",
		ПараметрыФормыОткрытия,
		ЭтотОбъект,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактируемыРеквизитыОбработчикПриЗаполненииНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Свойство("Обработчик", ДополнительныеПараметры.ОбработчикПриЗаполнении);
	Иначе 
		ДополнительныеПараметры.ОбработчикПриЗаполнении = Результат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры
// -- VOG Солодов В.В. 30.04.2020
