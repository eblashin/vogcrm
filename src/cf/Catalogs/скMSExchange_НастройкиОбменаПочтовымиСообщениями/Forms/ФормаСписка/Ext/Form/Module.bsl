
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтборПоНастройке(ЭтаФорма);
	
	//Добавление формы к списку открываемых при запуске приложения
	CRM_РежимФормЗакладкиСервер.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_СписокПриАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СписокУчетныеЗаписиЭлектроннойПочтыПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИнформацию", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнитьОбмен(Команда)	
	
	ТекущаяУчетнаяЗапись = Элементы.СписокУчетныеЗаписиЭлектроннойПочты.ТекущаяСтрока;
	Если ТекущаяУчетнаяЗапись = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Укажите учетную запись эл. почты в списке. '"));
		Возврат;	
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Обмен электронными сообщениями: '"),, 
		НСтр("ru = 'Выполняется обмен электронными сообщениями...'"), БиблиотекаКартинок.скСинхронизацияДанныхДлительнаяОперация48);
		
	СтрокаОшибки = "";
	скMSExchange_ОбменПочтовымиСообщениямиВызовСервера.СинхронизироватьПоНастройкеОбмена(Элементы.Список.ТекущаяСтрока, 
		ТекущаяУчетнаяЗапись, СтрокаОшибки);
		
	Если ЗначениеЗаполнено(СтрокаОшибки) Тогда
		ПоказатьПредупреждение(, СтрокаОшибки);
		
	КонецЕсли;
		
	Состояние(НСтр("ru = 'Обмен электронными сообщениями: '"),, 
		НСтр("ru = 'Обмен электронными сообщениями завершен'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыПроцедурыФункции

&НаКлиенте
Процедура Подключаемый_СписокПриАктивизацииСтроки()

	УстановитьОтборПоНастройке(ЭтаФорма, Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры // Подключаемый_СписокПриАктивизацииСтроки()

&НаКлиенте
Процедура Подключаемый_ОбновитьИнформацию();
	ТекДанные = Элементы.СписокУчетныеЗаписиЭлектроннойПочты.ТекущиеДанные;
	ОбновитьИнформацию(ТекДанные);
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоНастройке(Форма, Настройка = Неопределено)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.СписокУчетныеЗаписиЭлектроннойПочты,
		"НастройкаОбменаПочтовымиСообщениями",
		Настройка
	);
	
КонецПроцедуры // УстановитьОтборПоНастройке()

&НаКлиенте
Процедура ОбновитьИнформацию(ТекДанные)
	
	Если ТекДанные = Неопределено Тогда
		Элементы.ДекорацияДатаПоследнегоОбменаПочтовыхСообщений.Заголовок	= "";
	Иначе
		
		Элементы.ДекорацияДатаПоследнегоОбменаПочтовыхСообщений.Заголовок = НСтр("ru = 'Дата последнего обмена почтовыми сообщениями: '") +
				Формат(ТекДанные.ДатаПоследнегоОбменаПочтовыхСообщений, "ДФ='dd.MM.yyyy ЧЧ:мм'; ДП='не выполнялся'");
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти
