
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	Если Объект.Ссылка.Пустая() Тогда
		// получаем расписание из самого регламентного задания
		// если РЗ не задано, то расписание = Неопределено и будет создано на клиенте в момент редактирования расписания
		РасписаниеРегламентногоЗадания = Справочники.скMSExchange_НастройкиОбменаПочтовымиСообщениями.РасписаниеРегламентногоЗаданияПоУмолчанию();
		
	Иначе
		
		// получаем расписание из самого регламентного задания
		// если РЗ не задано, то расписание = Неопределено и будет создано на клиенте в момент редактирования расписания
		РасписаниеРегламентногоЗадания = Справочники.скMSExchange_НастройкиОбменаПочтовымиСообщениями.ПолучитьРасписаниеВыполненияОбменаДанными(Объект.Ссылка);
		
	КонецЕсли;

	ЗаполнитьСписокЧасовыхПоясов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПредставлениеРасписания();
	УстановитьДоступностьГиперСсылкиНастройкиРасписания();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Справочники.скMSExchange_НастройкиОбменаПочтовымиСообщениями.ОбновитьДанныеРегламентногоЗадания(Отказ, РасписаниеРегламентногоЗадания, ТекущийОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользоватьРегламентноеЗаданиеПриИзменении(Элемент)
	
	УстановитьДоступностьГиперСсылкиНастройкиРасписания();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьРасписаниеРегламентногоЗадания(Команда)
	
	РедактированиеРасписанияРегламентногоЗадания();
	
	ОбновитьПредставлениеРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	Если Не ЗначениеЗаполнено(Объект.Подключение) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбрано подключение!'"),, "Объект.Подключение");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбрана учетная запись!'"),, "Объект.УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	СтруктураРезультат = ПроверитьПодключениеНаСервере();
	
	Если СтруктураРезультат.Успешно Тогда
		Предупреждение(СтруктураРезультат.СообщениеОшибки);
	Иначе
		Предупреждение(СтруктураРезультат.СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Функция ПроверитьПодключениеНаСервере()
	СтруктураПодключения = скMSExchange_ОбменПочтовымиСообщениямиСервер.СформироватьСтруктуруПодключения(Объект);
	Возврат скMSExchange_ОбменПочтовымиСообщениямиСервер.ПроверитьПодключение(СтруктураПодключения);
КонецФункции

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗадания()
	
	// если расписание не инициализировано в форме на сервере, то создаем новое
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
		
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	
	// открываем диалог для редактирования Расписания
	Если Диалог.ОткрытьМодально() Тогда
		
		РасписаниеРегламентногоЗадания = Диалог.Расписание;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеРасписания()
	
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗадания);
	
	Если ПредставлениеРасписания = Строка(Новый РасписаниеРегламентногоЗадания) Тогда
		
		ПредставлениеРасписания = НСтр("ru = 'Расписание не задано'");
		
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Заголовок = ПредставлениеРасписания;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьГиперСсылкиНастройкиРасписания()
	
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Доступность = Объект.ИспользоватьРегламентноеЗадание;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЧасовыхПоясов()
	Элементы.СмещениеВремениВМинутах.СписокВыбора.Очистить();
	Минуты = -12 * 60;
	Пока Истина Цикл
		Если Минуты = 0 Тогда
			ПредставлениеСмещения = "(GMT 00:00)";
		Иначе
			ПредставлениеСмещения = "(GMT " + CRM_MSExchangeСервер.ПолучитьСмещениеВремениСтрокойДляMSExchange(Минуты) + ")";
		КонецЕсли;
		Элементы.СмещениеВремениВМинутах.СписокВыбора.Добавить(Минуты, ПредставлениеСмещения);
		Минуты = Минуты + 30;
		Если Минуты > 12 * 60 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
