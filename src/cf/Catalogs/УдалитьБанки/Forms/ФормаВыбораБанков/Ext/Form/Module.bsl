
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Процедура заполняет список банков.
//
Процедура ЗаполнитьСписокБанков()

	РезультатЗапроса = Справочники.УдалитьБанки.ПолучитьРезультатЗапросаПоКлассификатору(
		БИК,
		КоррСчет,
		Наименование,
		Город
	);
	
	СписокБанков.Очистить();
    
    Выборка = РезультатЗапроса.Выбрать();
    Пока Выборка.Следующий() Цикл
    
    	НоваяСтрока = СписокБанков.Добавить();
    	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
    
    КонецЦикла;
    
КонецПроцедуры // ЗаполнитьСписокБанков()

&НаСервере
// Процедура добавляет выбранные банки в справочник "Банки".
//
// Возвращаемое значение:
//	Массив - Массив добавленных банков.
//
Функция ДобавитьБанкиВСправочник()

	МассивБанков = Новый Массив;

	МассивНомеровСтрок = Элементы.СписокБанков.ВыделенныеСтроки;
	Для Каждого СтрокаТаблицы Из СписокБанков Цикл
	
		Если СтрокаТаблицы.Выбран Тогда
			
			НайденныйБанк = Справочники.УдалитьБанки.НайтиПоКоду(СтрокаТаблицы.БИК);
			Если Не ЗначениеЗаполнено(НайденныйБанк) Тогда
				
				БанкОбъект = Справочники.УдалитьБанки.СоздатьЭлемент();
				ЗаполнитьЗначенияСвойств(БанкОбъект, СтрокаТаблицы);
				БанкОбъект.Код = СтрокаТаблицы.БИК;
				БанкОбъект.Записать();
				
				МассивБанков.Добавить(БанкОбъект.Ссылка);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивБанков;

КонецФункции // ДобавитьБанкиВСправочник()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
// Процедура вызывается при нажатии кнопки "Подобрать банки".
//
Процедура ПодобратьБанкиВыполнить()

	ЗаполнитьСписокБанков();
	
КонецПроцедуры // ПодобратьБанкиВыполнить()

&НаКлиенте
// Процедура вызывается при нажатии кнопки "Добавить в справочник".
//
Процедура ДобавитьВСправочник(Команда)
	
	МассивБанков = ДобавитьБанкиВСправочник();
	
	Для Каждого Банк Из МассивБанков Цикл
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Банк);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлен новый банк:'"),НавигационнаяСсылка);
	КонецЦикла;
	
	ОповеститьОВыборе("");
	
КонецПроцедуры // ДобавитьВСправочник()

&НаКлиенте
// Процедура вызывается при нажатии на кнопку "Выбрать все".
//
Процедура ВыбратьБанки(Команда)
	
	Для Каждого СтрокаТаблицы Из СписокБанков Цикл
		СтрокаТаблицы.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры // ВыбратьБанки()

&НаКлиенте
// Процедура вызывается при нажатии на кнопку "Исключить все".
//
Процедура ИсключитьБанки(Команда)
	
	Для Каждого СтрокаТаблицы Из СписокБанков Цикл
		СтрокаТаблицы.Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры // ИсключитьБанки()

&НаКлиенте
// Процедура вызывается при нажатии на кнопку "Выбрать выделенные".
//
Процедура ВыбратьВыделенныеБанки(Команда)
	
	МассивСтрок = Элементы.СписокБанков.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СписокБанков.НайтиПоИдентификатору(НомерСтроки).Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры // ВыбратьВыделенныеБанки()

&НаКлиенте
// Процедура вызывается при нажатии на кнопку "Исключить выделенные".
//
Процедура ИсключитьВыделенныеБанки(Команда)
	
	МассивСтрок = Элементы.СписокБанков.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СписокБанков.НайтиПоИдентификатору(НомерСтроки).Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры // ИсключитьВыделенныеБанки()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "Выбор" поля ""СписокБанков".
//
Процедура СписокБанковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = СписокБанков.НайтиПоИдентификатору(ВыбраннаяСтрока);
	СтрокаТаблицы.Выбран = НЕ СтрокаТаблицы.Выбран;
	
КонецПроцедуры // СписокБанковВыбор()



