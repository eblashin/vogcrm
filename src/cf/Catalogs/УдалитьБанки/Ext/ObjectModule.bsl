#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриКопировании".
//
Процедура ПриКопировании(ОбъектКопирования)
	
	Код = "";
	
КонецПроцедуры // ПриКопировании()

// Процедура - обработчик события "ОбработкаПроверкиЗаполнения".
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
			
	Если НЕ ЭтоГруппа Тогда
	
		Если ЗначениеЗаполнено(КоррСчет) И СтрДлина(СокрЛП(КоррСчет)) <> 20 Тогда
			ТекстСообщения = НСтр("ru = 'Корр.счета банка должен иметь 20 знаков.'");
			CRM_ОбщегоНазначенияСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				,
				,
				"КоррСчет",
				Отказ
			);			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КоррСчет) И НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛП(КоррСчет)) Тогда
			ТекстСообщения = НСтр("ru = 'В составе Корр.счета банка должны быть только цифры.'");
			CRM_ОбщегоНазначенияСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				,
				,
				"КоррСчет",
				Отказ
			);
		КонецЕсли;
		
		Если СтрДлина(СокрЛП(Код)) <> 9 Тогда
			ТекстСообщения = НСтр("ru = 'БИК банка должен иметь 9 знаков.'");
			CRM_ОбщегоНазначенияСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				,
				,
				"Код",
				Отказ
			);
		КонецЕсли;

		Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛП(Код)) Тогда
			ТекстСообщения = НСтр("ru = 'В составе БИК банка должны быть только цифры.'");
			CRM_ОбщегоНазначенияСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				,
				,
				"Код",
				Отказ
			);
		КонецЕсли;
		
	Иначе
		
		CRM_ОбщегоНазначенияСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Код");
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти

#КонецЕсли