
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораРазмеры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаправлениеДеятельностиПриИзменении(Элемент)
	
	НастроитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьВидимость()
	
	Элементы.ГруппаРеквизитыОбои.Видимость = Объект.НаправлениеДеятельности = ПредопределенноеЗначение("Справочник.НаправленияДеятельности.Обои");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораРазмеры()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогТипыНоменклатуры.Длина КАК Длина,
		|	вогТипыНоменклатуры.Ширина КАК Ширина
		|ПОМЕСТИТЬ ВТ_Размеры
		|ИЗ
		|	Справочник.вогТипыНоменклатуры КАК вогТипыНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Размеры.Длина КАК Длина
		|ИЗ
		|	ВТ_Размеры КАК ВТ_Размеры
		|ГДЕ
		|	ВТ_Размеры.Длина > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Размеры.Ширина КАК Ширина
		|ИЗ
		|	ВТ_Размеры КАК ВТ_Размеры
		|ГДЕ
		|	ВТ_Размеры.Ширина > 0";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	МассивДлина = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Длина");
	Элементы.Длина.СписокВыбора.ЗагрузитьЗначения(МассивДлина);
	
	МассивШирина = РезультатЗапроса[2].Выгрузить().ВыгрузитьКолонку("Ширина");
	Элементы.Ширина.СписокВыбора.ЗагрузитьЗначения(МассивШирина);
	
КонецПроцедуры

#КонецОбласти
