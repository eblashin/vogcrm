#Область ОбщиеПроцедурыИФункцииФормы

&НаСервере
Функция ПолучитьПериодОбновленияВиджетаНаСервере(Виджет, Индекс)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.CRM_НастройкиВиджетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь 	= ТекущийПользователь;
	МенеджерЗаписи.Виджет 			= Виджет;
	МенеджерЗаписи.ИндексВиджета 	= Индекс;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		Возврат МенеджерЗаписи.ПериодОбновления;
	Иначе
		Возврат Виджет.ИсточникДанных.ПериодОбновления;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ДобавитьВиджетЗавершениеНаСервере(Виджет)
	
	Если Виджет = Неопределено ИЛИ (Виджет.ИсточникДанных.ТипПоказателя = Перечисления.CRM_ТипыПоказателей.Динамика И НЕ ЗначениеЗаполнено(Виджет.ИсточникДанных.ПлановыйПоказатель)) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИндексВиджета = Формат(СписокВиджетовПользователя.Количество()+1, "ЧЦ=2; ЧДЦ=; ЧВН=");
	НовыйЭлемент = СписокВиджетовПользователя.Добавить(Виджет, ИндексВиджета);
	CRM_РаботаСВиджетамиВызовСервера.ЗаполнитьВиджетыТекущихДел(ЭтотОбъект);
	КоличествоВиджетовНаФорме = СписокВиджетовПользователя.Количество();
	
	CRM_ХранилищеНастроек.Сохранить(ЭтотОбъект.ИмяФормы, "ВиджетыПользователя", СписокВиджетовПользователя);
	
	// Записать в регистр настройки нового виджета по умолчанию.
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи = РегистрыСведений.CRM_НастройкиВиджетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь 		= ТекущийПользователь;
	МенеджерЗаписи.Виджет 				= Виджет;
	МенеджерЗаписи.ИндексВиджета 		= ИндексВиджета;
	МенеджерЗаписи.ПериодОбновления 	= Виджет.ИсточникДанных.ПериодОбновления;
	МенеджерЗаписи.ТипАналитики 		= Перечисления.CRM_ВидыРазверткиПоказателей.Аналитика1;
	МенеджерЗаписи.ЗначениеАналитики 	= ТекущийПользователь;
	МенеджерЗаписи.Записать();
	
	Возврат Новый Структура("Виджет, ИндексВиджета, ПериодОбновленияВиджета", Виджет, ИндексВиджета, Виджет.ИсточникДанных.ПериодОбновления);
	
КонецФункции

// Открывает форму настройки виджета формы.
//
// Параметры:
//   Форма - УправляемаяФорма - форма Текущие дела.
//   ИндексВиджета - Строка - индекс настраиваемого виджета в форме.
//
&НаСервере
Процедура УдалитьВиджетФормы(ИндексВиджета)
	
	тЭлемент = CRM_РаботаСВиджетамиКлиентСервер.ПолучитьЭлементСпискаЗначенийПоИндексу(СписокВиджетовПользователя, ИндексВиджета);
	
	Если тЭлемент <> Неопределено Тогда
		// Удаляем из регистра запись с настройками текущего виджета.
		МенеджерЗаписи = РегистрыСведений.CRM_НастройкиВиджетов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь 		= ТекущийПользователь;
		МенеджерЗаписи.Виджет 				= тЭлемент.Значение;
		МенеджерЗаписи.ИндексВиджета 		= ИндексВиджета;
		МенеджерЗаписи.Удалить();
		
		СписокВиджетовПользователя.Удалить(тЭлемент);
		
		// Пересчет порядковых номеров.
		Для Сч = 1 По СписокВиджетовПользователя.Количество() Цикл
			СписокВиджетовПользователя[Сч-1].Представление = Формат(Сч, "ЧЦ=2; ЧДЦ=; ЧВН=");
		КонецЦикла;
		
		CRM_РаботаСВиджетамиВызовСервера.ЗаполнитьВиджетыТекущихДел(ЭтотОбъект);
		КоличествоВиджетовНаФорме = СписокВиджетовПользователя.Количество();
		
		CRM_ХранилищеНастроек.Сохранить(ЭтотОбъект.ИмяФормы, "ВиджетыПользователя", СписокВиджетовПользователя);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает индекс текущего виджета по выделенному элементу форму.
// Если по выделенному элементу невозможно определить индекс виджета, то
// возвращается пустая строка.
//
&НаКлиенте
Функция ИндексТекущегоВиджета()
	
	Если ТипЗнч(ТекущийЭлемент) <> Тип("ПолеФормы")
		И ТипЗнч(ТекущийЭлемент) <> Тип("ДекорацияФормы") Тогда
		
		Возврат "";
	КонецЕсли;
	
	ИндексТекущегоВиджета = Прав(ТекущийЭлемент.Имя,2);
	
	Возврат ИндексТекущегоВиджета;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	СписокВиджетовПользователя = CRM_ХранилищеНастроек.Загрузить(ЭтотОбъект.ИмяФормы, "ВиджетыПользователя");
	КоличествоВиджетовНаФорме = СписокВиджетовПользователя.Количество();
	CRM_РаботаСВиджетамиВызовСервера.ЗаполнитьВиджетыТекущихДел(ЭтотОбъект, "", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для Каждого ЭлементСписка Из СписокВиджетовПользователя Цикл
		Если Число(ЭлементСписка.Представление) <= 4 Тогда
			ПериодОбновленияВиджета = ПолучитьПериодОбновленияВиджетаНаСервере(ЭлементСписка.Значение, ЭлементСписка.Представление);
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьВиджет_" + ЭлементСписка.Представление, ПериодОбновленияВиджета);
		ИначеЕсли Число(ЭлементСписка.Представление) = 5 Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьВиджет_Прочие", 3600);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьВиджеты" Тогда
		CRM_РаботаСВиджетамиВызовСервера.ЗаполнитьВиджетыТекущихДел(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИндексВиджета = ИндексТекущегоВиджета();
	
	Если ЗначениеЗаполнено(ДанныеСобытия.Href) И СтрЗаканчиваетсяНа(ДанныеСобытия.Href, "УдалитьВиджет") Тогда
		УдалитьВиджетФормы(ИндексВиджета);
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.Href) И СтрЗаканчиваетсяНа(ДанныеСобытия.Href, "ОткрытьПоказатель") Тогда
		тЭлемент = CRM_РаботаСВиджетамиКлиентСервер.ПолучитьЭлементСпискаЗначенийПоИндексу(СписокВиджетовПользователя, ИндексВиджета);
		Если тЭлемент <> Неопределено Тогда
			ПоказатьЗначение( ,тЭлемент.Значение);
		КонецЕсли;
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.Href) И СтрЗаканчиваетсяНа(ДанныеСобытия.Href, "НастройкаВиджета") Тогда
		тЭлемент = CRM_РаботаСВиджетамиКлиентСервер.ПолучитьЭлементСпискаЗначенийПоИндексу(СписокВиджетовПользователя, ИндексВиджета);
		Если тЭлемент <> Неопределено Тогда
			ОповещениеНастройкаВиджета = Новый ОписаниеОповещения("НастройкаВиджетаЗавершение", ЭтотОбъект, Новый Структура("Индекс", ИндексВиджета));
			ПараметрыФормы = Новый Структура("Виджет, ИндексВиджета", тЭлемент.Значение, тЭлемент.Представление);
			ОткрытьФорму("Справочник.CRM_Виджеты.Форма.ФормаНастройки", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеНастройкаВиджета, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.Href) И СтрЗаканчиваетсяНа(ДанныеСобытия.Href, "РасшифроватьЗначение") Тогда
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.Href) И СтрЗаканчиваетсяНа(ДанныеСобытия.Href, "Обновить") Тогда
		ОбновитьВиджетыНаСервере();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьНовыйВиджетНажатие(Элемент)
	Если КоличествоВиджетовНаФорме = 99 Тогда
		Возврат;
	КонецЕсли;
		
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДобавитьВиджетЗавершение", ЭтотОбъект);
	
	//ПараметрыФормы = Новый Структура("Виджеты, СписокОткрытыхВиджетов", Истина, СписокВиджетовПользователя.ВыгрузитьЗначения());
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФормаВиджеты", Истина);
	ОткрытьФорму("Справочник.CRM_Виджеты.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область ЭкспортныеПроцедурыФормы

&НаКлиенте
Процедура ДобавитьВиджетЗавершение(Виджет, ДополнительныеПараметры) Экспорт
	
	ДанныеВиджета = ДобавитьВиджетЗавершениеНаСервере(Виджет);
	
	Если ДанныеВиджета <> Неопределено Тогда
		Если Число(ДанныеВиджета.ИндексВиджета) <= 4 Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьВиджет_" + ДанныеВиджета.ИндексВиджета, ДанныеВиджета.ПериодОбновленияВиджета);
		ИначеЕсли Число(ДанныеВиджета.ИндексВиджета) = 5 Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьВиджет_Прочие", 3600);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаВиджетаЗавершение(НовыеНастройки, ДополнительныеПараметры) Экспорт
	
	Если НовыеНастройки <> Неопределено Тогда
		Если НовыеНастройки = Ложь Тогда
			УдалитьВиджетФормы(ДополнительныеПараметры.Индекс);
			Возврат;
		КонецЕсли;
		ОбновитьВиджетНаСервере(ДополнительныеПараметры.Индекс);
		Если Число(ДополнительныеПараметры.Индекс) <= 4 Тогда
			ОтключитьОбработчикОжидания("Подключаемый_ОбновитьВиджет_" + ДополнительныеПараметры.Индекс);
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьВиджет_" + ДополнительныеПараметры.Индекс, НовыеНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеВиджетов

&НаКлиенте
Процедура Подключаемый_ОбновитьВиджет_01()
	ОбновитьВиджетНаСервере("01");
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВиджет_02()
	ОбновитьВиджетНаСервере("02");
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВиджет_03()
	ОбновитьВиджетНаСервере("03");
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВиджет_04()
	ОбновитьВиджетНаСервере("04");
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВиджет_Прочие()
	ОбновитьВиджетНаСервере("Прочие");
КонецПроцедуры

&НаСервере
Процедура ОбновитьВиджетНаСервере(ИндексВиджета)
	CRM_РаботаСВиджетамиВызовСервера.ОбновитьТекущийВиджет(ЭтотОбъект, ИндексВиджета);
КонецПроцедуры

&НаСервере
Процедура ОбновитьВиджетыНаСервере()
	CRM_РаботаСВиджетамиВызовСервера.ЗаполнитьВиджетыТекущихДел(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти