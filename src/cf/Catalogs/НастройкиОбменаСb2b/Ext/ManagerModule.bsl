Функция СоздатьПрокси(НастройкиОбмена, ТекстОшибки = "") Экспорт
	
	Попытка
		
		WSПрокси				= Неопределено;
		
		АдресСервиса 		= НастройкиОбмена.АдресСервиса + "/ws/"+НастройкиОбмена.ИмяСервиса+"?wsdl";;
		Пользователь 		= НастройкиОбмена.ИмяПользователя;
		Пароль 				= НастройкиОбмена.Пароль;
		URIСервис 			= "http://www.B2B.ru";
		ИмяСервиса			= НастройкиОбмена.ИмяСервиса;
		ТочкаПодключения	= НастройкиОбмена.ИмяСервиса+"Soap";
			
		Определение 			= Новый WSОпределения(АдресСервиса,Пользователь,Пароль);
		WSПрокси 				= Новый WSПрокси(Определение,URIСервис,ИмяСервиса,ТочкаПодключения,,0);
		WSПрокси.Пользователь 	= Пользователь;
		WSПрокси.Пароль			= Пароль;
		
		Возврат WSПрокси;
			
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

Процедура ВыполнитьОбмен(НастройкиОбмена) Экспорт
	
	ИмяФайлаОбмена = ПолучитьИмяВременногоФайла("xml");
	ИмяФайлаПравил = ПолучитьИмяВременногоФайла("xml");
	
	НастройкиОбмена.ПравилаОбмена.Получить().Записать(ИмяФайлаПравил);
	
	ОбработкаОбмена = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	ОбработкаОбмена.РежимОбмена = "Выгрузка";	
	ОбработкаОбмена.ИмяФайлаОбмена  = ИмяФайлаОбмена;
	ОбработкаОбмена.ИмяФайлаПравилОбмена  = ИмяФайлаПравил;
	ОбработкаОбмена.ЗагрузитьПравилаОбмена();
	ОбработкаОбмена.ВыполнитьВыгрузку();
	
	ТекстОшибки = "";
	Прокси = СоздатьПрокси(НастройкиОбмена,ТекстОшибки);
	
	Если Прокси = Неопределено Тогда
		Сообщить(ТекстОшибки);
		Возврат;
	КонецЕсли;	
	
	ФайлВыгрузки = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайлаОбмена));
	
	СтрокаПараметров = ПараметрыВJSON(Новый Структура("Режим", "ЗагрузитьФайлОбмена"));
	
	Сообщить(Прокси.AddFile(ФайлВыгрузки, СтрокаПараметров));		
	
КонецПроцедуры	

Функция ПараметрыВJSON(Параметры) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Параметры);
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции
	
