
// ++ VOG Солодов В.В. 23.08.2019 task 577


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ИмяСправочника")
		Или Не Параметры.Свойство("ИмяРеквизита")
		Или Не ЗначениеЗаполнено(Параметры.ИмяРеквизита)
		Или Не ЗначениеЗаполнено(Параметры.ИмяСправочника)Тогда
		Возврат;
	КонецЕсли;
	
	//Параметры.Свойство("УникальныйИдентификатор", УникальныйИдентификаторИсточника);
	
	ЗаполнитьСпискиВыбораНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьИЗакрыть(Команда)
	
	СоответствиеПараметров = СформироватьСоответствиеПараметров();
	
	Адрес = ПоместитьВоВременноеХранилище(СоответствиеПараметров, УникальныйИдентификатор);
	Закрыть(Адрес);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСпискиВыбораНаСервере()
	
	ИмяРеквизита 	= Параметры.ИмяРеквизита;
	ИмяСправочника 	= Параметры.ИмяСправочника;
	ПараметрыВыбора = Неопределено;
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресПараметровВыбора) Тогда
		ПараметрыВыбора = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВыбора);
	КонецЕсли;
	
	РеквизитМетаданных = Неопределено;
	
	// Поиск среди стандартных реквизитов справочника
	Для Каждого Реквизит Из Метаданные.Справочники[ИмяСправочника].СтандартныеРеквизиты Цикл
		Если Врег(Реквизит.Имя) = Врег(ИмяРеквизита) Тогда
			РеквизитМетаданных = Реквизит;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Поиск среди реквизитов справочника по имени реквизита
	Если РеквизитМетаданных = Неопределено Тогда
		РеквизитМетаданных = Метаданные.Справочники[ИмяСправочника].Реквизиты.Найти(ИмяРеквизита);
	КонецЕсли;
	
	// Поиск среди реквизитов справочника по синониму реквизита
	Если РеквизитМетаданных = Неопределено Тогда
		Для Каждого Реквизит Из Метаданные.Справочники[ИмяСправочника].Реквизиты Цикл
			Если Врег(Реквизит.Синоним) = Врег(ИмяРеквизита) Тогда
				РеквизитМетаданных = Реквизит;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не РеквизитМетаданных = Неопределено Тогда
		
		Ссылка = РеквизитМетаданных.Тип.ПривестиЗначение();
		
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Ссылка)) Тогда
			
			ИмяМетаданных = Ссылка.Метаданные().Имя;
			
			Для Каждого Реквизит Из Метаданные.Справочники[ИмяМетаданных].Реквизиты Цикл
				
				Если ТипЗнч(ПараметрыВыбора) = Тип("Соответствие")
					И ПараметрыВыбора.Получить(Реквизит.Имя) <> Неопределено Тогда
					
					ТипРеквизита 		= ПараметрыВыбора.Получить(Реквизит.Имя);
				Иначе
					ТипРеквизита 		= Реквизит.Тип.ПривестиЗначение();
				КонецЕсли;
				
				Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ТипРеквизита)) Тогда
					
					НоваяСтрока 			= ТаблицаПараметрыВыбора.Добавить();
					НоваяСтрока.Имя 		= Реквизит.Имя;
					НоваяСтрока.Реквизит 	= ТипРеквизита;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого ТабличнаяЧасть Из Метаданные.Справочники[ИмяМетаданных].ТабличныеЧасти Цикл
				
				Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
					
					//ИмяРеквизитаТЧ = Строка(ТабличнаяЧасть) + "." + РеквизитТЧ.Имя;
					ИмяРеквизитаТЧ = РеквизитТЧ.Имя;
					
					Если ТипЗнч(ПараметрыВыбора) = Тип("Соответствие")
						И ПараметрыВыбора.Получить(ИмяРеквизитаТЧ) <> Неопределено Тогда
						
						ТипРеквизита 		= ПараметрыВыбора.Получить(ИмяРеквизитаТЧ);
					Иначе
						ТипРеквизита 		= РеквизитТЧ.Тип.ПривестиЗначение();
					КонецЕсли;
					
					Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ТипРеквизита))
						Или ТипЗнч(ТипРеквизита) = Тип("Массив") Тогда
						
						НоваяСтрока 					= ТаблицаПараметрыВыбора.Добавить();
						НоваяСтрока.Имя 				= ИмяРеквизитаТЧ;
						НоваяСтрока.ЭтоТЧ 				= Истина;
						
						Если ТипЗнч(ТипРеквизита) = Тип("Массив") Тогда
						
							Для Каждого ЭлементМассива Из ТипРеквизита Цикл
								НоваяСтрока.Реквизит 	= ЭлементМассива;
							КонецЦикла;
							
						Иначе
							НоваяСтрока.Реквизит 		= ТипРеквизита;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьСоответствиеПараметров()
	
	СоответствиеПараметров = Новый Соответствие;
	
	Для Каждого СтрокаПараметр Из ТаблицаПараметрыВыбора Цикл
		Если ЗначениеЗаполнено(СтрокаПараметр.Реквизит) Тогда
			Если СтрокаПараметр.ЭтоТЧ Тогда
				
				МассивЗначений = Новый Массив;
				МассивЗначений.Добавить(СтрокаПараметр.Реквизит);
				
				СоответствиеПараметров.Вставить(СтрокаПараметр.Имя, МассивЗначений);
				
			Иначе
				СоответствиеПараметров.Вставить(СтрокаПараметр.Имя, СтрокаПараметр.Реквизит);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоответствиеПараметров;
	
КонецФункции

#КонецОбласти

// -- VOG Солодов В.В. 23.08.2019