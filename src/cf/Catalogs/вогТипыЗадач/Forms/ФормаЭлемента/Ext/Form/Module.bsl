
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокВыбораТипыПредметов();
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТипыПредметов

&НаКлиенте
Процедура ТипыПредметовТипПредметаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТипыПредметов.ТекущиеДанные;
	ТекущиеДанные.ТипПредметаСтрокой = Строка(ТипЗнч(ТекущиеДанные.ТипПредмета));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораТипыПредметов() 
	
	Элементы.ТипыПредметовТипПредмета.СписокВыбора.Добавить(Неопределено, "Не указано");
	
	Для Каждого ТипПредмета Из Метаданные.ОпределяемыеТипы.вогТипыПредметовПоручений.Тип.Типы() Цикл 
		
		МассивТипов 	= Новый Массив;
		МассивТипов.Добавить(ТипПредмета);
		
		Описание 		= Новый ОписаниеТипов(МассивТипов);
		ПустаяСсылка 	= Описание.ПривестиЗначение(Неопределено);
		
		Элементы.ТипыПредметовТипПредмета.СписокВыбора.Добавить(ПустаяСсылка, СокрЛП(ТипПредмета));
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Для Каждого ЭлементСпискаВыбора Из Элементы.ТипыПредметовТипПредмета.СписокВыбора Цикл
		
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
		ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТипыПредметовТипПредмета.Имя);
		
		ЭлементУсловия = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементУсловия.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элементы.ТипыПредметовТипПредмета.ПутьКДанным);
		ЭлементУсловия.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементУсловия.ПравоеЗначение = ЭлементСпискаВыбора.Значение;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", ЭлементСпискаВыбора.Представление);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
