
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеПодсчета = Новый Соответствие;
	СоответствиеПодсчета.Вставить(1, 0);
	СоответствиеПодсчета.Вставить(2, 0);
	СоответствиеПодсчета.Вставить(3, 0);
	СоответствиеПодсчета.Вставить(4, 0);
	СоответствиеПодсчета.Вставить(5, 0);
	СоответствиеПодсчета.Вставить(6, 0);
	СоответствиеПодсчета.Вставить(7, 0);
	
	Для Каждого Стр из Периоды Цикл 
		ДанныеДня = СоответствиеПодсчета.Получить(стр.НомерДня);
		Стр.НомерПериода = ДанныеДня + 1;
		СоответствиеПодсчета.Вставить(стр.НомерДня, Стр.НомерПериода);
	КонецЦикла;
			
КонецПроцедуры

Процедура ПроверитьНаличиеДубля(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вогГрафикиРаботыПериоды.Ссылка КАК Ссылка,
		|	вогГрафикиРаботыПериоды.НомерДня КАК НомерДня,
		|	вогГрафикиРаботыПериоды.ВремяНачала КАК ВремяНачала,
		|	вогГрафикиРаботыПериоды.ВремяОкончания КАК ВремяОкончания
		|ПОМЕСТИТЬ ТекущийСписок
		|ИЗ
		|	Справочник.вогГрафикиРаботы.Периоды КАК вогГрафикиРаботыПериоды
		|ГДЕ
		|	вогГрафикиРаботыПериоды.Ссылка = &Ссылка
		|	И НЕ вогГрафикиРаботыПериоды.ВремяНачала = ДАТАВРЕМЯ(1, 1, 1)
		|	И НЕ вогГрафикиРаботыПериоды.ВремяОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вогГрафикиРаботыПериоды.Ссылка КАК Ссылка,
		|	вогГрафикиРаботыПериоды.НомерДня КАК НомерДня,
		|	вогГрафикиРаботыПериоды.ВремяНачала КАК ВремяНачала,
		|	вогГрафикиРаботыПериоды.ВремяОкончания КАК ВремяОкончания
		|ПОМЕСТИТЬ ПроверяемыйСписок
		|ИЗ
		|	Справочник.вогГрафикиРаботы.Периоды КАК вогГрафикиРаботыПериоды
		|ГДЕ
		|	НЕ вогГрафикиРаботыПериоды.Ссылка = &Ссылка
		|	И НЕ вогГрафикиРаботыПериоды.ВремяНачала = ДАТАВРЕМЯ(1, 1, 1)
		|	И НЕ вогГрафикиРаботыПериоды.ВремяОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПроверяемыйСписок.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ТекущийСписок.Ссылка ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК КоличествоСовпадений,
		|	1 КАК ВсегоСтрок
		|ПОМЕСТИТЬ Совпадений
		|ИЗ
		|	ПроверяемыйСписок КАК ПроверяемыйСписок
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущийСписок КАК ТекущийСписок
		|		ПО (ТекущийСписок.НомерДня = ПроверяемыйСписок.НомерДня)
		|			И (ТекущийСписок.ВремяНачала = ПроверяемыйСписок.ВремяНачала)
		|			И (ТекущийСписок.ВремяОкончания = ПроверяемыйСписок.ВремяОкончания)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Совпадений.Ссылка КАК Ссылка
		|ИЗ
		|	(ВЫБРАТЬ
		|		Совпадений.Ссылка КАК Ссылка,
		|		СУММА(Совпадений.КоличествоСовпадений) КАК КоличествоСовпадений,
		|		СУММА(Совпадений.ВсегоСтрок) КАК ВсегоСтрок
		|	ИЗ
		|		Совпадений КАК Совпадений
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Совпадений.Ссылка) КАК Совпадений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ТекущийСписок.Ссылка КАК Ссылка,
		|			КОЛИЧЕСТВО(ТекущийСписок.Ссылка) КАК КоличествоСтрок
		|		ИЗ
		|			ТекущийСписок КАК ТекущийСписок
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ТекущийСписок.Ссылка) КАК Текущий
		|		ПО Совпадений.КоличествоСовпадений = Текущий.КоличествоСтрок
		|			И Совпадений.ВсегоСтрок = Текущий.КоличествоСтрок";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Такой график уже указан в %1", ВыборкаДетальныеЗаписи.Ссылка));
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПроверитьНаличиеДубля(Отказ);
	
КонецПроцедуры
