
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Автор", Автор);
	
	Доступ = Новый ТаблицаЗначений;
	Доступ.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	Для Каждого ЭлементСписка Из Параметры.СписокДоступа Цикл
		Доступ.Добавить().Пользователь = ЭлементСписка.Значение;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Доступ", Доступ);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Доступ.Пользователь
		|ПОМЕСТИТЬ втДоступ
		|ИЗ
		|	&Доступ КАК Доступ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СправочникПользователи.Ссылка КАК Пользователь,
		|	ВЫБОР
		|		КОГДА Доступ.Пользователь ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Пометка
		|ИЗ
		|	Справочник.Пользователи КАК СправочникПользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втДоступ КАК Доступ
		|		ПО СправочникПользователи.Ссылка = Доступ.Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Пользователь
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	УстановитьПривилегированныйРежим(Истина);
	Пользователи.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность
	  ИЛИ ЗакрытьБезусловно Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Конекст) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ЗакрытьБезусловно = Истина;
		Закрыть();
	Иначе
		ЗакрытьБезусловно = Истина;
		Закрыть(СформироватьРезультат());
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытиемЗавершение()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Принять(Команда)
	
	ЗакрытьБезусловно = Истина;
	Закрыть(СформироватьРезультат());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьСнятьОтметки(Истина);		
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	УстановитьСнятьОтметки(Ложь);		
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьСнятьОтметки(Пометка)
	
	Для Каждого СтрокаПользователь Из Пользователи Цикл
		Если СтрокаПользователь.Пользователь = Автор Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаПользователь.Пометка = Пометка;
		
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры // УстановитьСнятьОтметки()

&НаКлиенте
Функция СформироватьРезультат()
	
	Результат = Новый Структура("СписокДоступа", Новый СписокЗначений);
	
	СтрокиДоступа = Пользователи.НайтиСтроки(Новый Структура("Пометка", Истина));
	Для Каждого СтрокаПользователь Из СтрокиДоступа Цикл
		Результат.СписокДоступа.Добавить(СтрокаПользователь.Пользователь);
		
	КонецЦикла;

	Возврат Результат;
	
КонецФункции // СформироватьРезультат()

#КонецОбласти

