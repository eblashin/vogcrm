
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	CRM_КлассификаторыВызовСервера.ЗаполнитьИменаСписковУправленияКлассификацией(Элементы.ИмяСписка.СписокВыбора);	
	ДоступПредставление = ПредставлениеНастроекДоступа(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ИмяСпискаПредставление = Элементы.ИмяСписка.СписокВыбора.НайтиПоЗначению(ТекущийОбъект.ИмяСписка).Представление;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписьНастройкиКлассификации", Объект.Ссылка, Объект.ИмяСписка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВариантыОтчетовКлиент.РедактироватьМногострочныйТекст(ЭтотОбъект, Элемент.ТекстРедактирования, Объект, "Описание", НСтр("ru = 'Описание'"));
КонецПроцедуры

&НаКлиенте
Процедура ДоступПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокДоступа = Новый СписокЗначений;
	Для каждого СтрокаПользователь из Объект.Доступ Цикл
		СписокДоступа.Добавить(СтрокаПользователь.Пользователь);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Автор"		   , Объект.Автор);
	ПараметрыФормы.Вставить("СписокДоступа", СписокДоступа);
	
	Оповещение = Новый ОписаниеОповещения("ПослеРедактированияДоступа", ЭтотОбъект);
	ОткрытьФорму("Справочник.вогНастройкиУправленияКлассификацией.Форма.НастройкаДоступа", 
		ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРедактированияДоступа(Структура, Контекст) Экспорт
	
	Если Структура = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	Объект.Доступ.Очистить();
	Для Каждого ЭлементСписка Из Структура.СписокДоступа Цикл
		Объект.Доступ.Добавить().Пользователь = ЭлементСписка.Значение;
	КонецЦикла;
	
	ДоступПредставление = ПредставлениеНастроекДоступа(ЭтотОбъект);
	Модифицированность = Истина;

КонецПроцедуры // ПослеРедактированияДоступа()

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеНастроекДоступа(Форма)
	
	Результат = "";
	Для Каждого СтрокаТаблицы Из Форма.Объект.Доступ Цикл
		ПредставлениеСтроки = СокрЛП(Строка(СтрокаТаблицы.Пользователь));
		Если ПредставлениеСтроки <> "" Тогда
			Если Результат = "" Тогда
				Результат = Результат + ПредставлениеСтроки;
			Иначе
				Результат = Результат +", "+ ПредставлениеСтроки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Результат = "" Тогда
		Результат = НСтр("ru = 'Пользователи не выбраны'");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
