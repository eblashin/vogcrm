
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьСКД();
	
	Если Не ЗначениеЗаполнено(Объект.УникальныйИдентификатор) 
		Или ЗначениеЗаполнено(ЭтаФорма.Параметры.ЗначениеКопирования.УникальныйИдентификатор) Тогда
		
		Объект.УникальныйИдентификатор = Новый УникальныйИдентификатор;
		
	КонецЕсли;

	ЗаполнитьСписокВыбораИсточника();
	ЗаполнитьАдресКартинки();
	
	ИнициализироватьПараметрыЗапроса();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(Объект.СхемаКомпоновкиДанных) Тогда
		
		Если Не ПустаяСтрока(АдресСхемыКомпоновкиДанных) Тогда
			
			ТекущийОбъект.ХранилищеСхемыКомпоновкиДанных = 
				Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных));
				
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Необходимо настроить схему компоновки данных.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, 
				Объект.Ссылка, 
				"Объект.СхемаКомпоновкиДанных",
				, 
				Отказ);
				
		КонецЕсли;
		
	Иначе
		ТекущийОбъект.ХранилищеСхемыКомпоновкиДанных = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
	Если Не ПустаяСтрока(АдресНастроекКомпоновкиДанных) Тогда
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = 
			Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных));
	Иначе
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
	// ++ VOG Солодов В.В. 15.08.2019 task 553
	Если ПараметрыЗапроса.Количество() > 0 Тогда
		ТекущийОбъект.ХранилищеПараметровЗапроса = Новый ХранилищеЗначения(ПараметрыЗапроса.Выгрузить());
	КонецЕсли;
	// -- VOG Солодов В.В. 15.08.2019
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИмяКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещенияВыбора = Новый ОписаниеОповещения("ОписаниеОповещенияВыборКартинкиИзСписка", ЭтотОбъект);
	
	СписокКартинок = ПолучитьСписокВыбораКартинок();
	
	Если ЗначениеЗаполнено(Объект.ИмяКартинки) Тогда
		ТекЭлемент = СписокКартинок.НайтиПоЗначению(Объект.ИмяКартинки);
	КонецЕсли;
	
	ПоказатьВыборИзСписка(ОписаниеОповещенияВыбора, СписокКартинок,, ТекЭлемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеОповещенияВыборКартинкиИзСписка(Результат, ДопПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ИмяКартинки = Результат.Значение;
	
	ЗаполнитьАдресКартинки();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТекстОбработки", Объект[Элемент.Имя]);
	ПараметрыОткрытия.Вставить("Заголовок", "Обработчик команды " + """" + Строка(Объект.Наименование) + """");
	
	ОписаниеОповещенияЗавершения = Новый ОписаниеОповещения("ОбработчикВыборЗавершение", ЭтотОбъект, Элемент);
	
	ОткрытьФорму("Справочник.CRM_ТочкиМаршрутов.Форма.ФормаРедактированияТекста", 
		ПараметрыОткрытия, 
		ЭтотОбъект,
		,
		,
		, 
		ОписаниеОповещенияЗавершения);
				

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыборЗавершение(Результат, Элемент) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Свойство("Обработчик", Объект[Элемент.Имя]);
		Иначе 
			Объект[Элемент.Имя] = Результат;
		КонецЕсли;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ИсточникПриИзмененииЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Настройки компоновки будут сброшены. Продолжить?'");
	
	ПоказатьВопрос(ОповещениеЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);	
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСКДСервер();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьСхемуКомпоновкиДанных(Команда)
	
	ОписаниеОповещенияЗавершения = Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение", ЭтотОбъект);
	
	// Открыть редактор настроек схемы компоновки данных
	ШаблонЗаголовка = НСтр("ru = 'Настройка схемы компоновки данных ""%1""'");
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = СтрЗаменить(ШаблонЗаголовка, "%1", Объект.Наименование);
	
	АдресаНастроек = Неопределено;

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных", Истина);
	ПараметрыОткрытия.Вставить("НеРедактироватьСхемуКомпоновкиДанных", 		Ложь);
	ПараметрыОткрытия.Вставить("НеНастраиватьУсловноеОформление", 			Истина);
	ПараметрыОткрытия.Вставить("НеНастраиватьВыбор", 						Истина);
	ПараметрыОткрытия.Вставить("НеНастраиватьПорядок", 						Ложь);
	ПараметрыОткрытия.Вставить("УникальныйИдентификатор", 					УникальныйИдентификатор);
	ПараметрыОткрытия.Вставить("АдресСхемыКомпоновкиДанных", 				АдресСхемыКомпоновкиДанных);
	ПараметрыОткрытия.Вставить("АдресНастроекКомпоновкиДанных", 			АдресНастроекКомпоновкиДанных);
	ПараметрыОткрытия.Вставить("Заголовок", 								ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	ПараметрыОткрытия.Вставить("ИсточникШаблонов", 							Объект.Ссылка);
	ПараметрыОткрытия.Вставить("ИмяШаблонаСКД", 							Объект.СхемаКомпоновкиДанных);
	ПараметрыОткрытия.Вставить("ВозвращатьИмяТекущегоШаблонаСКД", 			Истина);
	
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
		ПараметрыОткрытия,
		,
		,
		,
		, 
		ОписаниеОповещенияЗавершения, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РедактироватьСхемуКомпоновкиДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Если ПустаяСтрока(Результат.ИмяТекущегоШаблонаСКД) 
			И Элементы.СхемаКомпоновкиДанных.СписокВыбора.НайтиПоЗначению("") = Неопределено Тогда
			
			Элементы.СхемаКомпоновкиДанных.СписокВыбора.Добавить("", НСтр("ru = 'Произвольный'"));
			
		КонецЕсли;
		
		Объект.СхемаКомпоновкиДанных = Результат.ИмяТекущегоШаблонаСКД;
		
		Если Результат.Свойство("АдресХранилищаНастройкиКомпоновщика") Тогда
			АдресНастроекКомпоновкиДанных = Результат.АдресХранилищаНастройкиКомпоновщика;
			ПерезаполнитьИсточникСервер(АдресНастроекКомпоновкиДанных);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

// ++ VOG Солодов В.В. 15.08.2019 task 553
&НаКлиенте
Процедура ЗаполнитьПараметрыЗапроса(Команда)
	
	ЗначениеВозврата = ЗаполнитьПараметрыЗапросаНаСервере(Объект.ТекстЗапроса);
	
	Если ТипЗнч(ЗначениеВозврата) = Тип("Структура") Тогда
		
		ПараметрыЗапроса.Очистить();
		
		Для Каждого ПараметрЗапроса Из ЗначениеВозврата Цикл
			НовыйПараметр 					= ПараметрыЗапроса.Добавить();
			НовыйПараметр.ИмяПараметра 		= ПараметрЗапроса.Ключ;
			НовыйПараметр.ЗначениеПараметра = ПараметрЗапроса.Значение.ПривестиЗначение(НовыйПараметр.ЗначениеПараметра);
		КонецЦикла;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ЗначениеВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторЗапроса(Команда)
	
	КонструкторЗапроса = Новый КонструкторЗапроса(Объект.ТекстЗапроса);
	ОписаниеОповщенеия = Новый ОписаниеОповещения("ПриЗакрытииКонструктораЗапроса", ЭтотОбъект);
	
	КонструкторЗапроса.Показать(ОписаниеОповщенеия);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииКонструктораЗапроса(ТекстЗапроса, ДополонительныеПараметры) Экспорт
	
	Если ТекстЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ТекстЗапроса = ТекстЗапроса;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзСхемыКомпоновки(Команда)
	
	ПолучитьТекстЗапросаИзСхемыКомпоновкиСервер();
	
КонецПроцедуры
// -- VOG Солодов В.В. 15.08.2019

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПерезаполнитьИсточникСервер(АдресНастроекКомпоновкиДанных)
	
	НастройкиКомпоновки = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
	
	Если ТипЗнч(НастройкиКомпоновки) = Тип("НастройкиКомпоновкиДанных") Тогда
		
		ЗначениеПараметра = НастройкиКомпоновки.ПараметрыДанных.НайтиЗначениеПараметра(
			Новый ПараметрКомпоновкиДанных("Ссылка"));
		
		Если ЗначениеПараметра <> Неопределено Тогда
			Объект.Источник = ЗначениеПараметра.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСКД()
	
	ПризнакПредопределенногоМакета 			= Врег("Предопределенный");
	ДлинаПризнакаПредопределенногоМакета 	= СтрДлина(ПризнакПредопределенногоМакета);
	
	Для Каждого Макет Из Метаданные.НайтиПоТипу(ТипЗнч(Объект.Ссылка)).Макеты Цикл
		
		Если Макет.ТипМакета = Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных Тогда
			
			Если ВРег(Прав(Макет.Имя, ДлинаПризнакаПредопределенногоМакета)) = ПризнакПредопределенногоМакета Тогда
				Элементы.СхемаКомпоновкиДанных.СписокВыбора.Добавить(Макет.Имя, Макет.Синоним);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.СхемаКомпоновкиДанных.СписокВыбора.Добавить("", НСтр("ru = 'Произвольная'"));
	
	Если Параметры.ЗначениеКопирования.Пустая() Тогда
		
		СхемаИНастройки = Справочники.вогДополнительныеКоманды.ОписаниеИСхемаКомпоновкиДанныхНастройкиПоИмениМакета(
			Объект.Ссылка, Объект.СхемаКомпоновкиДанных);
			
	Иначе
		СхемаИНастройки = Справочники.вогДополнительныеКоманды.ОписаниеИСхемаКомпоновкиДанныхНастройкиПоИмениМакета(
			Параметры.ЗначениеКопирования, Параметры.ЗначениеКопирования.СхемаКомпоновкиДанных);
	КонецЕсли;
	
	Если ПустаяСтрока(СхемаИНастройки.Описание) Тогда
		Объект.СхемаКомпоновкиДанных = "";
	КонецЕсли;
	
	Адреса = Справочники.вогДополнительныеКоманды.АдресаСхемыКомпоновкиДанныхИНастроекВоВременномХранилище(Объект);
	
	АдресСхемыКомпоновкиДанных 		= Адреса.СхемаКомпоновкиДанных;
	АдресНастроекКомпоновкиДанных 	= Адреса.НастройкиКомпоновкиДанных;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСКДСервер()
	
	Адреса = Справочники.вогДополнительныеКоманды.АдресаСхемыКомпоновкиДанныхИНастроекВоВременномХранилище(
		Справочники.вогДополнительныеКоманды.ПустаяСсылка());
	
	АдресСхемыКомпоновкиДанных 		= Адреса.СхемаКомпоновкиДанных;
	АдресНастроекКомпоновкиДанных 	= Адреса.НастройкиКомпоновкиДанных;
	Объект.СхемаКомпоновкиДанных 	= "";
	Элементы.СхемаКомпоновкиДанных.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораИсточника() 
	
	Элементы.Источник.СписокВыбора.Добавить(Неопределено, "Не указано");
	
	Для Каждого Стр Из Метаданные.ОпределяемыеТипы.вогИсточникДополнительныхКомандФормы.Тип.Типы() Цикл 
		
		МассивТек = Новый Массив;
		МассивТек.Добавить(Стр);
		
		Описание = Новый ОписаниеТипов(МассивТек);
		
		ПустаяСсылка = Описание.ПривестиЗначение(Неопределено);
		
		Элементы.Источник.СписокВыбора.Добавить(ПустаяСсылка, СокрЛП(Стр));
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАдресКартинки()

	Если ЗначениеЗаполнено(Объект.ИмяКартинки) Тогда
		Картинка = ПоместитьВоВременноеХранилище(БиблиотекаКартинок[Объект.ИмяКартинки], ЭтаФорма.УникальныйИдентификатор);
	Иначе 
		Картинка = "Нет";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокВыбораКартинок()
	
	Возврат вогУправлениеОпросамиСервер.ПолучитьСписокВыбораКартинок();
	
КонецФункции

// ++ VOG Солодов В.В. 15.08.2019 task 553
&НаСервереБезКонтекста
Функция ЗаполнитьПараметрыЗапросаНаСервере(ТекстЗапроса) Экспорт
	
	СтруктураПараметров = Новый Структура();

	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		
		Запрос = Новый Запрос(ТекстЗапроса);
		
		Попытка
			НайденныеПараметры = Запрос.НайтиПараметры();
		Исключение
			Возврат ОписаниеОшибки();
		КонецПопытки;
		
		Для Каждого ПараметрЗапроса Из НайденныеПараметры Цикл
			СтруктураПараметров.Вставить(ПараметрЗапроса.Имя, ПараметрЗапроса.ТипЗначения);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьПараметрыЗапроса()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	вогДополнительныеКоманды.ХранилищеПараметровЗапроса КАК ХранилищеПараметровЗапроса
	|ИЗ
	|	Справочник.вогДополнительныеКоманды КАК вогДополнительныеКоманды
	|ГДЕ
	|	вогДополнительныеКоманды.Ссылка = &КомандаСсылка";
	
	Запрос.УстановитьПараметр("КомандаСсылка", Объект.Ссылка);
	
	Результат 	= Запрос.Выполнить();
	Выборка 	= Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ТаблицаПараметровЗапроса = Выборка.ХранилищеПараметровЗапроса.Получить();
		
		Если ТипЗнч(ТаблицаПараметровЗапроса) = Тип("ТаблицаЗначений") Тогда
			ПараметрыЗапроса.Загрузить(ТаблицаПараметровЗапроса);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТекстЗапросаИзСхемыКомпоновкиСервер()
	
	СхемаИНастройки 		= Справочники.вогДополнительныеКоманды.ОписаниеИСхемаКомпоновкиДанныхНастройкиПоИмениМакета(
		Объект.Ссылка, Объект.СхемаКомпоновкиДанных);
	СхемаКомпоновкиДанных 	= СхемаИНастройки.СхемаКомпоновкиДанных;
	
	Если ТипЗнч(СхемаКомпоновкиДанных) = Тип("СхемаКомпоновкиДанных") Тогда
		НаборыДанных = СхемаКомпоновкиДанных.НаборыДанных;
		
		Для Каждого НаборДанных Из НаборыДанных Цикл
			Объект.ТекстЗапроса = НаборДанных.Запрос;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
// -- VOG Солодов В.В. 15.08.2019

#КонецОбласти
