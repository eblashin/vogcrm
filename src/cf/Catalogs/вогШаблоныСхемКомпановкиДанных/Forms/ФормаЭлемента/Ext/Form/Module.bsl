
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СКД = Объект.Ссылка.ХранилищеСхемыКомпоновкиДанных.Получить();
	НастройкиСКД = Объект.Ссылка.ХранилищеНастроекКомпоновкиДанных.Получить();
	
	Если СКД= Неопределено ТОгда
		СКД = Новый СхемаКомпоновкиДанных;
	КонецЕсли;
	Если НастройкиСКД = Неопределено Тогда		
		НастройкиСКД = Новый НастройкиКомпоновкиДанных;
	КонецЕсли;
	
	СтруктураНастроекСКД = Новый Структура();
	
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СКД,УникальныйИдентификатор);
	АдресНастроекКомпоновкиДанных = ПоместитьВоВременноеХранилище(НастройкиСКД,УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность Тогда
		Схема = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		ТекущийОбъект.ХранилищеСхемыКомпоновкиДанных = Новый ХранилищеЗначения(Схема);
		НастройкиСКД = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиСКД);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьСхемуПолученияДанных(Команда)
	
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Настройки схемы получения произвольных данных'");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных",				АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных",			АдресНастроекКомпоновкиДанных);
	ПараметрыФормы.Вставить("Заголовок",								ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("УникальныйИдентификатор",					УникальныйИдентификатор);
	ПараметрыФормы.Вставить("НеНастраиватьУсловноеОформление",			Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПорядок",						Истина);
	ПараметрыФормы.Вставить("НеНастраиватьОтбор",						Ложь);
	ПараметрыФормы.Вставить("НеНастраиватьВыбор",						Ложь);
	ПараметрыФормы.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных",Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзмененитьСхемуКомпоновкиДанных", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных", 
		ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзмененитьСхемуКомпоновкиДанных(Результат, Параметры) Экспорт
	
	Модифицированность = Истина;
			
	Если Не ПустаяСтрока(Результат) Тогда
		АдресНастроекКомпоновкиДанных 	= Результат;
	КонецЕсли;
				
КонецПроцедуры

#КонецОбласти




