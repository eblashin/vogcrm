#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
// Возвращаемое значение:
//   Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("СпособФормирования");
	Результат.Добавить("ПроверятьНаВхождениеПриСозданииНового");
	Результат.Добавить("РегламентноеЗаданиеИспользуется; РегламентноеЗаданиеАктивно");
	Результат.Добавить("ПредставлениеШаблонаСКД; Настройки");
	Результат.Добавить("Расписание; НастроитьРасписание");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Выполняет заполнение предопределенного элемента при записи.
Процедура ЗаполнитьПредопределенныйСегмент() Экспорт
	
	СправочникОбъект = Справочники.СегментыПартнеров.ПродажиЗапрещены.ПолучитьОбъект();
	СправочникОбъект.СпособФормирования = Перечисления.СпособыФормированияСегментов.ФормироватьВручную;
	СправочникОбъект.СхемаКомпоновкиДанных  = Новый ХранилищеЗначения(Справочники.СегментыПартнеров.ПолучитьМакет("ОсновнаяСхема"));
	CRM_ПараметрыЗапретаРедактирования = Новый Соответствие;
	CRM_ПараметрыЗапретаРедактирования.Вставить("СпособФормирования",Истина);	 
	СправочникОбъект.ДополнительныеСвойства.Вставить("CRM_ПараметрыЗапретаРедактирования",CRM_ПараметрыЗапретаРедактирования);
	СправочникОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ШаблоныСхемыКомпоновкиДанных() Экспорт
	
	Шаблоны = Новый Массив;
	
	Для каждого Макет Из Метаданные.Справочники.СегментыПартнеров.Макеты Цикл
		
		Если Макет.ТипМакета <> Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Шаблоны.Добавить(Новый Структура("Имя, Синоним", Макет.Имя, Макет.Синоним));
		
	КонецЦикла;
	
	Возврат Шаблоны;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли