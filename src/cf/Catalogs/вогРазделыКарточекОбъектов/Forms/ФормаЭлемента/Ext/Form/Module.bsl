
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	//Владельцы карточек
	МассивОбъектов = вогОбщегоНазначенияКлиентСерверПовтИсп.МассивОбъектовРасширеннойКарточки();
	Для каждого ИмяОбъекта Из МассивОбъектов Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
		Если МетаданныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		Элементы.ВладелецКарточки.СписокВыбора.Добавить(ИмяОбъекта, МетаданныеОбъекта.Представление());
		
	КонецЦикла;
	
	Элементы.ПоложениеРаздела.СписокВыбора.Добавить("Лево" , НСтр("ru = 'Лево'"));
	Элементы.ПоложениеРаздела.СписокВыбора.Добавить("Право", НСтр("ru = 'Право'"));
	
	ЗаполнитьПараметрыВыбора(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецКарточкиПриИзменении(Элемент)
	
	ОчиститьСвязанныеДанные(ЭтаФорма);
	ЗаполнитьПараметрыВыбора(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыВыбора(Форма)
	
	СписокПринадлежность = Новый СписокЗначений;
	СписокПринадлежность.Добавить(Форма.Объект.ВладелецКарточки);
	
	ПараметрыВыбора = Новый Массив;
	
	//Классификаторы
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Принадлежность.ИмяТаблиц", СписокПринадлежность));
	
	//Доп. реквизиты
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Ложь));
	Если Форма.Объект.ВладелецКарточки = "Справочник.Партнеры" Тогда
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Общие"));
		МассивОтбора.Добавить(ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Компании_CRM"));
		
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", Новый ФиксированныйМассив(МассивОтбора)));
		
	Иначе	
		ИмяНабора = СтрЗаменить(Форма.Объект.ВладелецКарточки, ".", "_");
		Если ЗначениеЗаполнено(ИмяНабора) Тогда
			ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств", 
				ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений." + ИмяНабора)));
			
		КонецЕсли;
			
	КонецЕсли;
	
	
	Форма.Элементы.СоставРазделаДанныеРаздела.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры // ЗаполнитьПараметрыВыбора()

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьСвязанныеДанные(Форма)

	Форма.Объект.СоставРаздела.Очистить();
	
КонецПроцедуры // ОчиститьСвязанныеДанные()


#КонецОбласти
