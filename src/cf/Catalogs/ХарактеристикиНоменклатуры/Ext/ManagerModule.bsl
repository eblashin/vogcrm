#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Процедура заполняет данные выбора.
//
Процедура ЗаполнитьДанныеВыбора(ДанныеВыбора, Параметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|	(ХарактеристикиНоменклатуры.Владелец = &ВладелецНоменклатура
	|			ИЛИ ХарактеристикиНоменклатуры.Владелец = &ВладелецНоменклатурнаяГруппа)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХарактеристикиНоменклатуры.Ссылка
	|
	|ИМЕЮЩИЕ
	|	ПОДСТРОКА(ХарактеристикиНоменклатуры.Наименование, 1, &ДлинаПодстроки) ПОДОБНО &СтрокаПоиска
	|
	|УПОРЯДОЧИТЬ ПО
	|	ХарактеристикиНоменклатуры.Наименование";
	
	Запрос.УстановитьПараметр("ВладелецНоменклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("ВладелецНоменклатурнаяГруппа", Параметры.Номенклатура.НоменклатурнаяГруппа);
	Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска);
	Запрос.УстановитьПараметр("ДлинаПодстроки", СтрДлина(Параметры.СтрокаПоиска));
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДанныеВыбора = Новый СписокЗначений;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Характеристика);	
		КонецЦикла;
	КонецЕсли;	
		
КонецПроцедуры // ЗаполнитьДанныеВыбора()	

// Процедура обработчик события ОбработкаПолученияДанныхВыбора.
//
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.Свойство("Номенклатура")
		И ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		
		Если НЕ Параметры.Номенклатура.ИспользоватьХарактеристики Тогда
		
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Для номенклатуры не ведется учет по характеристикам!'");
			Сообщение.Сообщить();
						
		Иначе	
						
			ЗаполнитьДанныеВыбора(ДанныеВыбора, Параметры);
						
		КонецЕсли;	
				
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПолученияДанныхВыбора()

#КонецЕсли
