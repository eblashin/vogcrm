
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда 
		Возврат; 
	КонецЕсли;
	
	
	НоменклатураКоллекции = Объект.Номенклатура ИЛИ Объект.Коллекции;
	ИспользованиеНоменклатураКоллекци = ?(Объект.Коллекции, "Коллекции", "Номенклатура");
	
	ЗаполнитьСписокПравилРасчета(ЭтотОбъект);
	УстановитьВидимостьДоступность(ЭтотОбъект);
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.ПравилоРасчетаПроцентаВыполненияЗадач)
	  И Элементы.ПравилоРасчетаПроцентаВыполненияЗадач.СписокВыбора.НайтиПоЗначению(ТекущийОбъект.ПравилоРасчетаПроцентаВыполненияЗадач) = Неопределено Тогда
		ТекущийОбъект.ПравилоРасчетаПроцентаВыполненияЗадач = Неопределено;
		
	КонецЕсли;
	
	Для каждого ЭлементСписка Из Элементы.ИспользованиеНоменклатураКоллекци.СписокВыбора Цикл
		ТекущийОбъект[ЭлементСписка.Значение] = Ложь;		
	КонецЦикла;
	
	// VOG Солодов
	Если НоменклатураКоллекции Тогда
		ТекущийОбъект[ИспользованиеНоменклатураКоллекци] = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПараметрИспользованияПриИзменении(Элемент)
	
	ЗаполнитьСписокПравилРасчета(ЭтотОбъект);
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры // ПараметрИспользованияПриИзменении()

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокПравилРасчета(Форма)

	Форма.Элементы.ПравилоРасчетаПроцентаВыполненияЗадач.СписокВыбора.Очистить();
	
	Если Форма.Объект.Клиенты
	  И Форма.Объект.ВариантЗаполненияДанныхКлиенты = ПредопределенноеЗначение("Перечисление.вогВариантыЗаполненияДанныхПроцессов.ПриПостановкеЗадач") Тогда
		Форма.Элементы.ПравилоРасчетаПроцентаВыполненияЗадач.СписокВыбора.Добавить("ТаблицаКлиентов", НСтр("ru = 'По клиентам'"));
	КонецЕсли;
	
	Если Форма.НоменклатураКоллекции 
	  И Форма.Объект.ВариантЗаполненияДанныхНоменклатураКоллекции = ПредопределенноеЗначение("Перечисление.вогВариантыЗаполненияДанныхПроцессов.ПриПостановкеЗадач") Тогда
		Форма.Элементы.ПравилоРасчетаПроцентаВыполненияЗадач.СписокВыбора.Добавить("ТаблицаНоменклатураКоллекции", НСтр("ru = 'По номенклатуре/коллекциям'"));
	КонецЕсли;
	
	Если Форма.Объект.Повестка Тогда
		Форма.Элементы.ПравилоРасчетаПроцентаВыполненияЗадач.СписокВыбора.Добавить("Повестка", НСтр("ru = 'По повестке'"));
	КонецЕсли;
		
КонецПроцедуры // ЗаполнитьСписокПравилРасчета()

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)

	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ВариантЗаполненияДанныхКлиенты.Доступность = Объект.Клиенты;
	
	Элементы.ИспользованиеНоменклатураКоллекци.Доступность = Форма.НоменклатураКоллекции;
	Элементы.ВариантЗаполненияДанныхНоменклатураКоллекции.Доступность = Форма.НоменклатураКоллекции;
	
КонецПроцедуры // УстановитьВидимостьДоступность()

#КонецОбласти
