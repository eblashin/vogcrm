
///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
// Процедура добавляет категорию в таблицу.
//
// Параметры:
//	Категория	- СправочникСсылка	- Категория
//
Процедура ДобавитьСтрокуКатегории(Категория)
	НоваяСтрока = ТаблицаКатегории.Добавить();
	НоваяСтрока.Категория	= Категория;
	НоваяСтрока.ЦветИндекс	= Категория.ЦветИндекс;
КонецПроцедуры // ДобавитьСтрокуКатегории()

&НаСервере
// Процедура устанавливает пометку удаления категории.
//
// Параметры:
//	Категория	- СправочникСсылка	- Категория
//
Процедура УстановитьПометкуУдаленияКатегории(Категория)
	КатегорияОбъект = Категория.ПолучитьОбъект();
	КатегорияОбъект.УстановитьПометкуУдаления(Истина);
	КатегорияОбъект.Записать();
КонецПроцедуры // УстановитьПометкуУдаленияКатегории()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "ПриАктивизацииСтроки" таблицы формы "ТаблицаКатегории".
//
// Параметры:
//	Нет.
//
Процедура ТаблицаКатегорииПриАктивизацииСтроки(Элемент)
	ТД = Элементы.ТаблицаКатегории.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;
	Элементы.ТаблицаКатегорииКнопкаОсновная.Пометка = (ТД.Основная);
КонецПроцедуры // ТаблицаКатегорииПриАктивизацииСтроки()

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" таблицы формы "ТаблицаКатегории".
//
// Параметры:
//	Нет.
//
Процедура ТаблицаКатегорииПометкаПриИзменении(Элемент)
	ТД = Элементы.ТаблицаКатегории.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;
	Если ТД.Пометка Тогда
		// Проверяем установлена ли основная категория.
		МассивСтрок = ТаблицаКатегории.НайтиСтроки(Новый Структура("Основная", Истина));
		// Если нет, то устанавливаем текущую.
		Если МассивСтрок.Количество() = 0 Тогда
			ТД.Основная = Истина;
		КонецЕсли;	
	ИначеЕсли ТД.Основная Тогда 	
		ТД.Основная = Ложь;
		// Устанавливаем основной любую выбранную ранее категорию.
		МассивСтрок = ТаблицаКатегории.НайтиСтроки(Новый Структура("Пометка", Истина));
		Если МассивСтрок.Количество() > 0 Тогда
			МассивСтрок[0].Основная = Истина;
		КонецЕсли;
	КонецЕсли;	
	ТаблицаКатегорииПриАктивизацииСтроки(Элемент);
КонецПроцедуры // ТаблицаКатегорииПометкаПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ КОМАНД ФОРМЫ

&НаКлиенте
// Процедура - обработчик команды формы "ДобавитьКатегорию".
//
Процедура КомандаДобавитьКатегорию(Команда)
	ПараметрыФормы = Новый Структура;
	ОткрытьФорму("Справочник.CRM_КатегорииРешений.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры // КомандаДобавитьКатегорию()

&НаКлиенте
// Процедура - обработчик команды формы "ОК".
//
Процедура КомандаОК(Команда)
	СписокКатегорий = Новый СписокЗначений;
	Для Каждого СтрокаТаблицы Из ТаблицаКатегории Цикл
		Если НЕ СтрокаТаблицы.Пометка Тогда Продолжить; КонецЕсли;
		СписокКатегорий.Добавить(СтрокаТаблицы.Категория, , СтрокаТаблицы.Основная);
	КонецЦикла;	
	Закрыть(СписокКатегорий);
КонецПроцедуры // КомандаОК()

&НаКлиенте
// Процедура - обработчик команды формы "ОсновнаяКатегория".
//
Процедура КомандаОсновнаяКатегория(Команда)
	ТД = Элементы.ТаблицаКатегории.ТекущиеДанные;
	Если ТД = Неопределено Тогда Возврат; КонецЕсли;
	Элементы.ТаблицаКатегорииКнопкаОсновная.Пометка = НЕ Элементы.ТаблицаКатегорииКнопкаОсновная.Пометка;
	Если Элементы.ТаблицаКатегорииКнопкаОсновная.Пометка Тогда
		// Снимаем пометки "Основная"
		МассивСтрок = ТаблицаКатегории.НайтиСтроки(Новый Структура("Основная", Истина));
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			СтрокаМассива.Основная = Ложь;
		КонецЦикла;
		// Устанавливаем пометку "Основная".
		ТД.Основная = Истина;
	Иначе
		// Устанавливаем пометку "Основная" на первой категории в списке отмеченных.
		МассивСтрок = ТаблицаКатегории.НайтиСтроки(Новый Структура("Пометка", Истина));
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			Если НЕ (СтрокаМассива.Категория = ТД.Категория) Тогда
				СтрокаМассива.Основная = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		// Снимаем пометку "Основная"
		ТД.Основная = Ложь;
	КонецЕсли;	
КонецПроцедуры // КомандаОсновнаяКатегория()

&НаКлиенте
// Процедура - обработчик команды формы "Отмена".
//
Процедура КомандаОтмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры // КомандаОтмена()

&НаКлиенте
// Процедура - обработчик команды формы "СнятьФлажки".
//
Процедура КомандаСнятьФлажки(Команда)
	Для Каждого СтрокаТаблицы Из ТаблицаКатегории Цикл
		СтрокаТаблицы.Пометка		= Ложь;
		СтрокаТаблицы.Основная	= Ложь;
	КонецЦикла;
	Элементы.ТаблицаКатегорииКнопкаОсновная.Пометка = Ложь;	
КонецПроцедуры // КомандаСнятьФлажки()

&НаКлиенте
// Процедура - обработчик команды формы "УдалитьКатегорию".
//
Процедура КомандаУдалитьКатегорию(Команда)
	ТекущиеДанные = Элементы.ТаблицаКатегории.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	УстановитьПометкуУдаленияКатегории(ТекущиеДанные.Категория);
	ТаблицаКатегории.Удалить(ТаблицаКатегории.Индекс(ТаблицаКатегории.НайтиПоИдентификатору(Элементы.ТаблицаКатегории.ТекущаяСтрока)));
КонецПроцедуры // КомандаУдалитьКатегорию()

&НаКлиенте
// Процедура - обработчик команды формы "УстановитьФлажки".
//
Процедура КомандаУстановитьФлажки(Команда)
	Для Каждого СтрокаТаблицы Из ТаблицаКатегории Цикл
		СтрокаТаблицы.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры // КомандаУстановитьФлажки()

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("МассивКатегорий") Тогда
		Отказ = Истина;
		Возврат;
	ИначеЕсли НЕ Параметры.Свойство("ОсновнаяКатегория") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВыбранныеКатегории",	Параметры.МассивКатегорий);
	Запрос.УстановитьПараметр("ОсновнаяКатегория",	Параметры.ОсновнаяКатегория);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Категории.Ссылка КАК Категория,
	               |	Категории.ЦветИндекс КАК ЦветИндекс,
	               |	ВЫБОР
	               |		КОГДА Категории.Ссылка В (&ВыбранныеКатегории)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Пометка,
	               |	ВЫБОР
	               |		КОГДА Категории.Ссылка = &ОсновнаяКатегория
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Основная
	               |ИЗ
	               |	Справочник.CRM_КатегорииРешений КАК Категории
	               |ГДЕ
	               |	НЕ Категории.ПометкаУдаления";
	Таблица = Запрос.Выполнить().Выгрузить();
	ЗначениеВРеквизитФормы(Таблица, "ТаблицаКатегории");
	Элементы.ГруппаТаблицаКатегорииДобавитьУдалитьКатегорию.Видимость = Пользователи.РолиДоступны("CRM_АдминистрированиеБазыЗнаний, ПолныеПрава");
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события формы "ОбработкаЗаписиНового".
//
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	ДобавитьСтрокуКатегории(НовыйОбъект);
КонецПроцедуры // ОбработкаЗаписиНового()

#КонецОбласти
