
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	ВариантОтвета = Параметры.ВариантОтвета;
		
	// Заполнение таблицы значений свойств.
	ЗаполнитьТаблицуЗначенийСвойств();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВариантыОтветов" Тогда
		Если ВариантОтвета = Параметр Тогда
			ЗаполнитьТаблицуЗначенийСвойств();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаЗначенийСвойств

&НаКлиенте
Процедура ТаблицаЗначенийСвойствПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийСвойствПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийСвойствПередУдалением(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные.НомерКартинки = -1 Тогда
		Отказ = Истина;
		Элемент.ТекущиеДанные.Значение = Элемент.ТекущиеДанные.ТипЗначения.ПривестиЗначение(Неопределено);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийСвойствПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Элемент.ПодчиненныеЭлементы.ТаблицаЗначенийСвойствЗначение.ОграничениеТипа
		= Элемент.ТекущиеДанные.ТипЗначения;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийСвойствПередНачаломИзменения(Элемент, Отказ)
	
	Если Элементы.ТаблицаЗначенийСвойств.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Строка = Элементы.ТаблицаЗначенийСвойств.ТекущиеДанные;
	
	ПараметрыВыбораМассив = Новый Массив;
	Если Строка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Строка.Свойство));
		
	КонецЕсли;
	
	Элементы.ТаблицаЗначенийСвойствЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	ПрименитьИЗакрытьЗавершение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПрименитьИЗакрытьЗавершение(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Модифицированность = Ложь;
	Закрыть(СформироватьЗначенияСвойств());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗначенийСвойств()
	
	ТекущиеЗначенияСвойств = Параметры.ТаблицаДополнительнаяИнформация.Выгрузить();
	ТаблицаЗначенийСвойств.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВариантОтвета"		  , ВариантОтвета);
	Запрос.УстановитьПараметр("ТекущиеЗначенияСвойств", ТекущиеЗначенияСвойств);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущиеЗначенияСвойств.Свойство КАК Свойство,
		|	ТекущиеЗначенияСвойств.Значение
		|ПОМЕСТИТЬ ТекущиеЗначенияСвойств
		|ИЗ
		|	&ТекущиеЗначенияСвойств КАК ТекущиеЗначенияСвойств
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Свойство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НаборДополнительнойИнформации.Свойство,
		|	НаборДополнительнойИнформации.Свойство.ТипЗначения КАК ТипЗначения,
		|	ТекущиеЗначенияСвойств.Значение,
		|	ВЫБОР
		|		КОГДА НаборДополнительнойИнформации.Свойство.ПометкаУдаления
		|			ТОГДА 0
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК НомерКартинки,
		|	НаборДополнительнойИнформации.Свойство.Наименование КАК Наименование
		|ИЗ
		|	Справочник.вогВариантыОтветов.НаборДополнительнойИнформации КАК НаборДополнительнойИнформации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеЗначенияСвойств КАК ТекущиеЗначенияСвойств
		|		ПО НаборДополнительнойИнформации.Свойство = ТекущиеЗначенияСвойств.Свойство
		|ГДЕ
		|	НаборДополнительнойИнформации.Ссылка = &ВариантОтвета
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаборДополнительнойИнформации.НомерСтроки";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаЗначенийСвойств.Добавить(), Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьЗначенияСвойств()
	
	ЗначенияСвойств = Новый Соответствие;
	Для каждого СтрокаТЧ Из ТаблицаЗначенийСвойств Цикл
		ЗначенияСвойств.Вставить(СтрокаТЧ.Свойство, СтрокаТЧ.Значение);
		
	КонецЦикла;
	
	Возврат ЗначенияСвойств;
	
КонецФункции

#КонецОбласти
