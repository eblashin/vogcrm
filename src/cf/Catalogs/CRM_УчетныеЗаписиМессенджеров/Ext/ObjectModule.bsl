
Процедура ПередЗаписью(Отказ)
	Если ТипМессенджера = Перечисления.CRM_ТипыМессенджеров.ВКонтакте Тогда
		РегламентноеЗадание = Неопределено;
		Если ЗначениеЗаполнено(УникальныйИдентификаторЗадания) Тогда
			GUIDЗадания = Новый УникальныйИдентификатор(УникальныйИдентификаторЗадания);
			РегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(GUIDЗадания);
		КонецЕсли;
		Если РегламентноеЗадание = Неопределено Тогда
			Если ЗначениеЗаполнено(ТокенГруппы) Тогда
				РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.CRM_ОжиданиеСобытийМессенджера);
				УникальныйИдентификаторЗадания = РегламентноеЗадание.УникальныйИдентификатор;
				ПараметрыЗаданияМассив = Новый Массив;
				ПараметрыСтруктура = Новый Структура;
				ПараметрыСтруктура.Вставить("УникальныйИдентификаторЗадания", УникальныйИдентификаторЗадания);
				ПараметрыСтруктура.Вставить("ТипМессенджера", ТипМессенджера);
				ПараметрыЗаданияМассив.Добавить(ПараметрыСтруктура);
				РегламентноеЗадание.Параметры = ПараметрыЗаданияМассив;
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не получен токен группы. Обмен данными не доступен!'"));
			КонецЕсли;
		КонецЕсли;
		Если РегламентноеЗадание<>Неопределено Тогда
			РегламентноеЗадание.Наименование = "Ожидание событий: "+ТипМессенджера+" "+Наименование;
			РегламентноеЗадание.Использование = ЗначениеЗаполнено(ТокенГруппы);
			РегламентноеЗадание.Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	УникальныйИдентификаторЗадания = "";
КонецПроцедуры
