////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция получает список сайтов для анализа
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Булево - Удалось ли получить список сайтов
//
&НаСервере
Функция ПолучитьСписокСайтовДляЗаполнения()
	
	//GET http://api.comagic.ru/api/v1/site/?session_key=05e765dc1ac3901fe1b57b865924271d
	КлючСессии	= сфпСофтФонПроСервер.сфпПолучитьКлючСессииCoMagic();
	Если НЕ ЗначениеЗаполнено(КлючСессии) Тогда Возврат Ложь; КонецЕсли;
	Соединение 			= Новый HTTPСоединение("api.comagic.ru", , , , , ,
		Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено));
	Запрос 				= Новый HTTPЗапрос;
	Запрос.АдресРесурса = "/api/v1/site/?session_key=" + СокрЛП(КлючСессии);
	ФайлРезультата		= ПолучитьИмяВременногоФайла("txt");
	
	Попытка
		Ответ = Соединение.Получить(Запрос, ФайлРезультата);
		Если Ответ.КодСостояния = 200 Тогда
			ТекстовыйФайл = Новый ТекстовыйДокумент;
			ТекстовыйФайл.Прочитать(ФайлРезультата,"UTF-8");
			РезультатСоответствие = CRM_GoogleИнтеграция.UnJSON(ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыXML(ТекстовыйФайл.ПолучитьТекст()));
			Если РезультатСоответствие.Получить("data").Количество() > 0 Тогда
				Для каждого ЭлементСоответствия Из РезультатСоответствие.Получить("data") Цикл
					Элементы.Сайт.СписокВыбора.Добавить(Строка(ЭлементСоответствия.Получить("id")), Строка(ЭлементСоответствия.Получить("domain")));
				КонецЦикла;
				Отчет.АнализируемыйСайт = РезультатСоответствие.Получить("data")[0].Получить("id");
			Иначе
				Возврат Ложь;
			КонецЕсли;			
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Соединение = Неопределено;
	Возврат Истина;

КонецФункции

&НаКлиенте
// Процедура - обработчик выбра периода отчета
//
// Параметры:
//	Период		- СтандартныйПериод	- Период отчета
//	Параметры	- Структура			- Структура дополнительных параметров
//
Процедура ОбработкаВыбораПериода(Период, Параметры) Экспорт
	Если Период = Неопределено Тогда Возврат; КонецЕсли;
	Отчет.ПериодОтчета = Период;
КонецПроцедуры // ОбработкаВыбораПериода()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ КОМАНД ФОРМЫ

&НаКлиенте
// Процедура - обработчик команды формы "УстановитьИнтервал"
//
Процедура УстановитьИнтервал(Команда)
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Отчет.ПериодОтчета;
	ОписаниеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораПериода", ЭтотОбъект);
	Диалог.Показать(ОписаниеВыбора);
КонецПроцедуры // УстановитьИнтервал()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМЫ

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ сфпСофтФонПроСервер.сфпИспользоватьCoMagic() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Использование CoMagic отключено в настройках системы!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ сфпСофтФонПроСервер.сфпРолиДоступны("ПолныеПрава, сфпУправлениеМаршрутизацией") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Недостаточно прав для просмотра отчета!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ПолучитьСписокСайтовДляЗаполнения() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось получить список сайтов для анализа! '"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Процедура - обработчик события формы "ПриЗагрузкеВариантаНаСервере"
//
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	Отчет.ВариантОтчета = КлючТекущегоВарианта;
	Отчет.ПериодОтчета.Вариант 	= ВариантСтандартногоПериода.ЭтотМесяц;
КонецПроцедуры
