
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиКомпоновки, "ТекущийПользователь", ТекущийПользователь);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КоличествоСтрок = ДокументРезультат.ВысотаТаблицы;
	КоличествоКолонок = ДокументРезультат.ШиринаТаблицы;
	
	Для СтрокаДокумента = 1 По КоличествоСтрок Цикл
	
		Для КолонкаДокумента = 1 По КоличествоКолонок Цикл
		
			Область = ДокументРезультат.Область(СтрокаДокумента, КолонкаДокумента);
			Расшифровка = Область.Расшифровка;
			Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
			
				 ПоляРасшифровки = ДанныеРасшифровки.Элементы.Получить(Расшифровка).ПолучитьПоля();
				 НайденноеПоле = ПоляРасшифровки.Найти("ИзменениеРейтинга");
				 Если НЕ НайденноеПоле = Неопределено Тогда
				 
				 	ИзменениеРейтинга = НайденноеПоле.Значение;
					Если ТипЗнч(ИзменениеРейтинга) = Тип("Число") Тогда
						
						Если ИзменениеРейтинга = 0 Тогда
							ТекКартинка = БиблиотекаКартинок.CRM_ВиджетыДинамикаНет;
						ИначеЕсли ИзменениеРейтинга = 1 Тогда	
							ТекКартинка = БиблиотекаКартинок.CRM_ВиджетыДинамикаПоложительнаяВверх;
						ИначеЕсли ИзменениеРейтинга = -1 Тогда	
							ТекКартинка = БиблиотекаКартинок.CRM_ВиджетыДинамикаОтрицательнаяВниз;
						КонецЕсли;
						
						КартинкаВДокументе = ДокументРезультат.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
						КартинкаВДокументе.Картинка = ТекКартинка;
						КартинкаВДокументе.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
						КартинкаВДокументе.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 1);
						КартинкаВДокументе.ЦветЛинии = Новый Цвет(230, 230, 230);
						КартинкаВДокументе.Расположить(Область);
						
						КартинкаВДокументе.Расшифровка = ТекКартинка;
					КонецЕсли;
					
				 
				 КонецЕсли;
			
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЦикла;
	
КонецПроцедуры
