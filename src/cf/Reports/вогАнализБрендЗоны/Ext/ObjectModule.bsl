
// ++ VOG Солодов В.В. 22.09.2021 CRM-1174
// Рефакторинг
// +
// Вместо формы отчета добавлены вызовы ОпределитьНастройкиФормы и ПриЗагрузкеПользовательскихНастроекНаСервере


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задать настройки формы отчета.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения, Неопределено - 
//   КлючВарианта - Строка, Неопределено - 
//   Настройки - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных -
//       Пользовательские настройки для загрузки в компоновщик настроек.
//
// См. синтакс-помощник "Расширение управляемой формы для отчета.ПриЗагрузкеПользовательскихНастроекНаСервере"
//    в синтакс-помощнике.
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	// Идентификаторы
	РольМенеджер_УИД				= Новый УникальныйИдентификатор("2403e82e-cad9-11e8-a684-005056bc3fe8");
	РольКоординатор_УИД 			= Новый УникальныйИдентификатор("21bee87e-caef-11e8-a684-005056bc3fe8");
	ПодразделениеВог_УИД 			= Новый УникальныйИдентификатор("e040cc1f-0c11-11e8-92c3-005056bcf152");
	
	// 
	РольМенеджер					= Справочники.РолиИсполнителей.ПолучитьСсылку(РольМенеджер_УИД);
	РольКоординатор 				= Справочники.РолиИсполнителей.ПолучитьСсылку(РольКоординатор_УИД);
	ПодразделениеВог	 			= Справочники.СтруктураПредприятия.ПолучитьСсылку(ПодразделениеВог_УИД);
	
	ТекущийПользователь 			= Пользователи.ТекущийПользователь();
	ПодразделениеПользователя 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Подразделение", Истина);
	ОбособленноеПодразделение 		= Справочники.СтруктураПредприятия.ПолучитьОбособленноеПодразделение(ПодразделениеПользователя);
	
	УстановитьПривилегированныйРежим(Истина);
	РолиПользователя 				= ПараметрыСеанса.вогРолиИсполнителя;
	УстановитьПривилегированныйРежим(Ложь);
	
	// Отборы
	Если Не РолиПользователя.Найти(РольМенеджер) = Неопределено
		Или Не РолиПользователя.Найти(РольКоординатор) = Неопределено Тогда
		
		КомпоновкаДанныхКлиентСервер.УдалитьОтбор(КомпоновщикНастроек, "Подразделение");
		КомпоновкаДанныхКлиентСервер.УдалитьОтбор(КомпоновщикНастроек, "Компания");
		
		ЭлементОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек.ФиксированныеНастройки,
			"Подразделение",
			ОбособленноеПодразделение,
			ВидСравненияКомпоновкиДанных.Равно,
			Истина);
			
	ИначеЕсли ОбособленноеПодразделение <> ПодразделениеВог Тогда
		
		ДополнительныеПараметры = Новый Структура("ВПользовательскиеНастройки, ЗаменятьСуществующий", Истина, Истина);
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек,
			"Подразделение",
			ОбособленноеПодразделение,
			ВидСравненияКомпоновкиДанных.Равно,
			Истина,
			ДополнительныеПараметры);
		
	КонецЕсли;
	
	Форма.ВариантМодифицирован = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомпоновщикМакет 	= Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет 				= КомпоновщикМакет.Выполнить(
		СхемаКомпоновкиДанных,
		КомпоновщикНастроек.ПолучитьНастройки(),
		ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет, , ДанныеРасшифровки);
	ПроцессорВывода 	= Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ДокументРезультат.ПоказатьУровеньГруппировокКолонок(0); //Уровень 1
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
