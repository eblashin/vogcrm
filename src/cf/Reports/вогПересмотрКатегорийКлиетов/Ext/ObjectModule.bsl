
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	КатегорияКлиентаКлассификатор = ПланыВидовХарактеристик.CRM_Классификаторы.ПолучитьСсылку(Новый УникальныйИдентификатор("165f1d7d-b614-11ea-8f2a-005056bcd3e3"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиКомпоновки, "КлассификаторКатегория", КатегорияКлиентаКлассификатор);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КоличествоСтрок = ДокументРезультат.ВысотаТаблицы;
	КоличествоКолонок = ДокументРезультат.ШиринаТаблицы;
	
	Для СтрокаДокумента = 1 По КоличествоСтрок Цикл
	
		Для КолонкаДокумента = 1 По КоличествоКолонок Цикл
		
			Область = ДокументРезультат.Область(СтрокаДокумента, КолонкаДокумента);
			Расшифровка = Область.Расшифровка;
			Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
			
				 ПоляРасшифровки = ДанныеРасшифровки.Элементы.Получить(Расшифровка).ПолучитьПоля();
				 НайденноеПоле = ПоляРасшифровки.Найти("ИзменениеКатегории");
				 Если НЕ НайденноеПоле = Неопределено Тогда
				 
				 	ИзменениеКатегории = НайденноеПоле.Значение;
					Если ТипЗнч(ИзменениеКатегории) = Тип("Число") Тогда
						
						Если ИзменениеКатегории = 0 Тогда
							ТекКартинка = Новый Картинка;
						ИначеЕсли ИзменениеКатегории = 1 Тогда	
							ТекКартинка = БиблиотекаКартинок.CRM_ВиджетыДинамикаПоложительнаяВверх;
						ИначеЕсли ИзменениеКатегории = -1 Тогда	
							ТекКартинка = БиблиотекаКартинок.CRM_ВиджетыДинамикаОтрицательнаяВниз;
						КонецЕсли;
						
						КартинкаВДокументе = ДокументРезультат.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
						КартинкаВДокументе.Картинка = ТекКартинка;
						КартинкаВДокументе.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
						КартинкаВДокументе.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 1);
						КартинкаВДокументе.ЦветЛинии = Новый Цвет(230, 230, 230);
						КартинкаВДокументе.Расположить(Область);
						
						КартинкаВДокументе.Расшифровка = ТекКартинка;
					КонецЕсли;
					
				 
				 КонецЕсли;
			
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЦикла;
	
КонецПроцедуры
