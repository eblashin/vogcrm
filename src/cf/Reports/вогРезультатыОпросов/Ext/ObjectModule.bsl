#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет, , ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПолеПоКоторомуНеобходимоПолучитьЗначение = "Выбран";
	ПроцессорВывода.НачатьВывод();
	
	Пока Истина Цикл
		
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();
		
		Если ЭлементРезультата = Неопределено Тогда
			Прервать;
		КонецЕсли;	
		
		ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		
		Если ЭлементРезультата.ЗначенияПараметров.Количество() > 0 Тогда
			
			ЗначениеВыводимыхДанных = Неопределено;
			
			Для каждого ЗначениеПараметра Из ЭлементРезультата.ЗначенияПараметров Цикл
				
				Если ЗначениеПараметра.Значение = "%КартинкаСнять%"
					Или ЗначениеПараметра.Значение = "%КартинкаУстановить%" Тогда
					
					ЗначениеВыводимыхДанных = ЗначениеПараметра.Значение;
					
				КонецЕсли;	
				
			КонецЦикла;	
			
			Если ЗначениеЗаполнено(ЗначениеВыводимыхДанных) Тогда
				
				Рис = ДокументРезультат.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
				Рис.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
				
				Индекс = ДокументРезультат.Рисунки.Индекс(Рис);
				
				Если ЗначениеВыводимыхДанных = "%КартинкаУстановить%" Тогда
					ДокументРезультат.Рисунки[Индекс].Картинка = БиблиотекаКартинок.вог_УстановитьФлажки_16;
				ИначеЕсли ЗначениеВыводимыхДанных = "%КартинкаСнять%" Тогда
					ДокументРезультат.Рисунки[Индекс].Картинка = БиблиотекаКартинок.вог_СнятьФлажки_16;
				КонецЕсли;
				
				ДокументРезультат.Рисунки[Индекс].ЦветЛинии = ЦветаСтиля.ЦветРамки;
				
				ОбластьДляКартинки = ДокументРезультат.НайтиТекст("%КартинкаСнять%");
				
				Если ОбластьДляКартинки = Неопределено Тогда
					ОбластьДляКартинки = ДокументРезультат.НайтиТекст("%КартинкаУстановить%");
				КонецЕсли;
				
				Если ОбластьДляКартинки <> Неопределено Тогда
					
					ДокументРезультат.Рисунки[Индекс].Расположить(ОбластьДляКартинки);	
					ОбластьДляКартинки.Текст = "%КартинкаДобавлена%";
				
				КонецЕсли;
				
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;
	
	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
